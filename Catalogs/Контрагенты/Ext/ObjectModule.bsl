
Процедура ПередЗаписью(Отказ)
	
	Если НЕ ЭтоГруппа И ЗначениеЗаполнено(Префикс) Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Префикс", Префикс);
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Контрагенты.Ссылка
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ГДЕ
		|	Контрагенты.Префикс = &Префикс
		|	И Контрагенты.Ссылка <> &Ссылка";
		
		РезультатЗапроса = Запрос.Выполнить();
		Если НЕ РезультатЗапроса.Пустой() Тогда
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Такой префикс уже существует", ,"Объект.Префикс");
			Отказ = Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	Ошибки = Неопределено;
	
	Если (Поставщик ИЛИ Дилер) И НЕ ЗначениеЗаполнено(Город) Тогда
		ТекстСообщения = "Заполните город";
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "Объект.Город", ТекстСообщения);
	КонецЕсли;
	
	Если Дилер И НЕ ЗначениеЗаполнено(Префикс) Тогда
		ТекстСообщения = "Заполните префикс";
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "Объект.Префикс", ТекстСообщения);
	КонецЕсли;
	
	Если Ошибки <> Неопределено Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки, Отказ);
		Возврат;
		
	КонецЕсли;
	
	
КонецПроцедуры
