
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Пользователь = ПользователиКлиентСервер.ТекущийПользователь();
	Если ТипЗнч(Пользователь) = Тип("СправочникСсылка.ВнешниеПользователи") Тогда
		
		Отказ = Истина;
		
	Иначе
		
		Должность = Пользователь.ФизическоеЛицо.ОсновнаяДолжность;
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Должность", Должность);
		Запрос.УстановитьПараметр("Сотрудник", Пользователь);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ФайлыВБазе.Объект,
		|	МАКСИМУМ(ФайлыВБазе.НомерВерсии) КАК НомерВерсии
		|ПОМЕСТИТЬ ФайлыДолжности
		|ИЗ
		|	РегистрСведений.ФайлыВБазе КАК ФайлыВБазе
		|ГДЕ
		|	ФайлыВБазе.Объект.Должности.Должность В(&Должность)
		|
		|СГРУППИРОВАТЬ ПО
		|	ФайлыВБазе.Объект
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ФайлыДолжности.Объект КАК Значение
		|ИЗ
		|	ФайлыДолжности КАК ФайлыДолжности
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОзнакомлениеСПравовойБазой КАК ОзнакомлениеСПравовойБазой
		|		ПО (ОзнакомлениеСПравовойБазой.Сотрудник = &Сотрудник)
		|			И (ФайлыДолжности.Объект = ВЫБОР
		|				КОГДА ОзнакомлениеСПравовойБазой.Объект = ЗНАЧЕНИЕ(Справочник.ПравоваяБаза.ПустаяСсылка)
		|					ТОГДА ФайлыДолжности.Объект
		|				ИНАЧЕ ОзнакомлениеСПравовойБазой.Объект
		|			КОНЕЦ)
		|			И (ФайлыДолжности.НомерВерсии = ВЫБОР
		|				КОГДА ОзнакомлениеСПравовойБазой.НомерВерсии = 0
		|					ТОГДА ФайлыДолжности.НомерВерсии
		|				ИНАЧЕ ОзнакомлениеСПравовойБазой.НомерВерсии
		|			КОНЕЦ)
		|ГДЕ
		|	ВЫБОР
		|			КОГДА ОзнакомлениеСПравовойБазой.Ознакомлен ЕСТЬ NULL 
		|				ТОГДА ЛОЖЬ
		|			ИНАЧЕ ОзнакомлениеСПравовойБазой.Ознакомлен
		|		КОНЕЦ = ЛОЖЬ";
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Список.Добавить(Выборка.Значение);
			
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Список.Количество() = 0 Тогда
		
		Отказ = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СсылкаНаПравовуюБазу = Элемент.ТекущиеДанные.Значение;
	ОткрытьФорму("Справочник.ПравоваяБаза.ФормаОбъекта", Новый Структура("Ключ", СсылкаНаПравовуюБазу));
	
КонецПроцедуры

