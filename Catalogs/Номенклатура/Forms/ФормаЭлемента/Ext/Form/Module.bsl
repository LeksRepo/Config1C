
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Базовый", Объект.Базовый);
	СтруктураПараметров.Вставить("Комплект", Объект.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Комплект);
	СтруктураПараметров.Вставить("НаличиеТекстуры", Объект.НаличиеТекстуры);
	ОбновитьЭлементыФормы(ЭтаФорма, СтруктураПараметров);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ОбновитьЭлементыФормы(лФорма, лПараметры)
	
	Если ТипЗнч(лПараметры) <> Тип("Структура") Тогда
		Возврат Истина;
	КонецЕсли;
	
	Элементы = лФорма.Элементы;
	
	Если лПараметры.Свойство("Комплект") Тогда
		Элементы.Состав.Доступность = лПараметры.Комплект;
	КонецЕсли;
	
	Если лПараметры.Свойство("НаличиеТекстуры") Тогда
		Элементы.ПоперечнаяТекстура.Доступность = лПараметры.НаличиеТекстуры;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура БазовыйПриИзменении(Элемент)
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Базовый", Объект.Базовый);
	ОбновитьЭлементыФормы(ЭтаФорма, СтруктураПараметров);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидНоменклатурыПриИзменении(Элемент)
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Комплект", Объект.ВидНоменклатуры = ПредопределенноеЗначение("Перечисление.ВидыНоменклатуры.Комплект"));
	ОбновитьЭлементыФормы(ЭтаФорма, СтруктураПараметров);
	
КонецПроцедуры

&НаКлиенте
Процедура НаличиеТекстурыПриИзменении(Элемент)
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("НаличиеТекстуры", Объект.НаличиеТекстуры);
	ОбновитьЭлементыФормы(ЭтаФорма, СтруктураПараметров);
	
	Если НЕ Объект.НаличиеТекстуры Тогда
		Объект.ПоперечнаяТекстура = Ложь;
	КонецЕсли;
	
КонецПроцедуры
