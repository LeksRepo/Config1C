Перем НоваяНоменклатура;

Процедура ПередЗаписью(Отказ)
	
	Если ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Услуга Тогда
		// { Васильев Александр Леонидович [02.07.2015]
		// Базовый = ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Услуга
		// Так делать нельзя, ибо не услуги бывают базовыми, а бывают нет. :)
		// } Васильев Александр Леонидович [02.07.2015]
		Базовый = Истина;
	КонецЕсли;
	
	Если НЕ ЭтоГруппа И Базовый Тогда
		КоэффициентБазовых = 1;
	КонецЕсли;
	
	НоваяНоменклатура = ЭтоНовый();
	
	Если НЕ ЭтоГруппа Тогда
		ЕстьОписаниеСостава = ОписаниеСостава.Количество() > 0;
	КонецЕсли;
	
	ПодпискиНаСобытия.ПередЗаписьюДокументаИлиСправочника(ЭтотОбъект);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Ошибки = Неопределено;
	
	Если ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Материал И НЕ Базовый И БазоваяНоменклатура.Пустая() Тогда
		
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "БазоваяНоменклатура", "Заполните базовую номенклатуру");
		
	КонецЕсли;
	
	Если НЕ ЭтоГруппа И НЕ Базовый И НЕ БазоваяНоменклатура.Базовый Тогда
		
		ТекстОшибки = "Номенклатура '%1' не является базовой";
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, БазоваяНоменклатура);
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, ЭтотОбъект, ТекстОшибки);
		
	КонецЕсли;
	
	Если НЕ ЭтоГруппа И ЗначениеЗаполнено(НоменклатурнаяГруппа) Тогда
		
		Если НоменклатурнаяГруппа.ПринадлежитЭлементу(Справочники.НоменклатурныеГруппы.Кромка)
			ИЛИ НоменклатурнаяГруппа.ПринадлежитЭлементу(Справочники.НоменклатурныеГруппы.Кант) Тогда
			
			СтруктураКромок = Новый Структура;
			СтруктураКромок.Вставить("КантАлюминиевый", "AL-");
			СтруктураКромок.Вставить("КантП", "P-");
			СтруктураКромок.Вставить("КантТ", "Т-");
			СтруктураКромок.Вставить("Кромка045_19", "V-");
			СтруктураКромок.Вставить("Кромка2_19", "W-");
			СтруктураКромок.Вставить("Кромка2_35", "Z-");
			СтруктураКромок.Вставить("КромкаМДФ", "ABS-");
			СтруктураКромок.Вставить("Кромка2_42", "Q-");
			СтруктураКромок.Вставить("Кромка2_45", "F-");
			
			ИмяГруппы = Справочники.НоменклатурныеГруппы.ПолучитьИмяПредопределенного(НоменклатурнаяГруппа);
			ПрефиксПлан = СтруктураКромок[ИмяГруппы];
			ДлинаПрефикса = СтрДлина(ПрефиксПлан);
			ПрефиксФакт = Лев(КраткоеНаименование, ДлинаПрефикса);
			
			Если ПрефиксПлан <> ПрефиксФакт Тогда
				
				ТекстСообщения = "Краткое наименование у '%1' должно начинаться с '%2'";
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, НоменклатурнаяГруппа, ПрефиксПлан);
				
				ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "Объект.КраткоеНаименование", ТекстСообщения);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки, Отказ);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если НоваяНоменклатура И НЕ ЭтоГруппа И НоменклатурнаяГруппа = Справочники.НоменклатурныеГруппы.Гравировка Тогда
		
		ДоступностьПодразделению = РегистрыСведений.НоменклатураПодразделений;
		НаборПодразделение = ДоступностьПодразделению.СоздатьНаборЗаписей();
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ВидПодразделения", Перечисления.ВидыПодразделений.Производство);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Подразделения.Ссылка
		|ИЗ
		|	Справочник.Подразделения КАК Подразделения
		|ГДЕ
		|	Подразделения.ВидПодразделения = &ВидПодразделения";
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = НаборПодразделение.Добавить();
			НоваяСтрока.Номенклатура = Ссылка;
			НоваяСтрока.Подразделение = Выборка.Ссылка;
			НоваяСтрока.ПодЗаказ = Истина;
			НоваяСтрока.ОкруглятьДоЛистов = Истина;
		КонецЦикла;
		
		УстановитьПривилегированныйРежим(Истина);
		НаборПодразделение.Записать(Ложь);
		
	КонецЕсли;
	
КонецПроцедуры