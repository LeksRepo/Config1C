Перем НоваяНоменклатура;

Процедура ПередЗаписью(Отказ)
	
	Если ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Услуга Тогда
		Базовый = Истина;
	КонецЕсли;
	
	Если НЕ ЭтоГруппа И Базовый Тогда
		КоэффициентБазовых = 1;
	КонецЕсли;
	
	НоваяНоменклатура = ЭтоНовый();
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Материал И НЕ Базовый И БазоваяНоменклатура.Пустая() Тогда
		
		Отказ = Истина;
		ТекстСообщения = "Заполните базовую номенклатуру";
		Поле = "БазоваяНоменклатура";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если НоваяНоменклатура И НЕ ЭтоГруппа И НоменклатурнаяГруппа = Справочники.НоменклатурныеГруппы.Гравировка Тогда
		
		ДоступностьПодразделению = РегистрыСведений.НоменклатураПодразделений;
		НаборПодразделение = ДоступностьПодразделению.СоздатьНаборЗаписей();
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ВидПодразделения", Перечисления.ВидыПодразделений.Производство);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Подразделения.Ссылка
		|ИЗ
		|	Справочник.Подразделения КАК Подразделения
		|ГДЕ
		|	Подразделения.ВидПодразделения = &ВидПодразделения";
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = НаборПодразделение.Добавить();
			НоваяСтрока.Номенклатура = Ссылка;
			НоваяСтрока.Подразделение = Выборка.Ссылка;
			НоваяСтрока.Доступность = Истина;
		КонецЦикла;
		
		НаборПодразделение.Записать(Ложь);
		
	КонецЕсли;
	
КонецПроцедуры
