
&НаКлиенте
Процедура ОбновитьИзФайла(Команда)
		
	СтруктураФайла = ЛексКлиент.ВыбратьФайл(ЭтаФорма);
	
	Если ТипЗнч(СтруктураФайла) = Тип("Структура") Тогда
		
		ОбновитьФайл(Объект.Ссылка, СтруктураФайла);
		Прочитать();

	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОбновитьФайл(ФайлСсылка, СтруктураФайла)
	
	Файл = ФайлСсылка.ПолучитьОбъект();
	
	Файл.Наименование = СтруктураФайла.Наименование;
	Файл.Данные = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(СтруктураФайла.АдресВременногоХранилищаФайла));
	Файл.Дата = СтруктураФайла.Дата;
	Файл.Размер = СтруктураФайла.Размер;
	Файл.Автор = СтруктураФайла.Автор;
	Файл.Хэш = ЛексСервер.ПолучитьХэшСумму(Файл.Данные.Получить());
	Файл.Расширение = СтруктураФайла.Расширение;
	Файл.Пиктограмма = СтруктураФайла.Пиктограмма;
	
	Файл.Записать();
	
КонецПроцедуры

&НаКлиенте
Процедура Посмотреть(Команда)
	
	ЛексКлиент.ОткрытьФайл(Объект.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Перем Ошибки;
	
	Если НЕ ЗначениеЗаполнено(Объект.Вид) Тогда		
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "Объект.Вид", "Заполните поле ""Вид""");	
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки, Отказ);

КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.Предопределенный Тогда
		Элементы.Владелец.ТолькоПросмотр = Ложь;
	КонецЕсли;
	
	ЗаполнитьВидыФайла();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВидыФайла()
			
	МассивДоступныхВидовФайла = Новый Массив;
	
	Если ЗначениеЗаполнено(Объект.ВладелецФайла) Тогда
	
		ТипВладельца = ТипЗнч(Объект.ВладелецФайла);
	
		Если ТипВладельца = Тип("ДокументСсылка.Спецификация") Тогда
			
			МассивДоступныхВидовФайла.Добавить(Перечисления.ВидыПрисоединенныхФайлов.Эскиз);
			МассивДоступныхВидовФайла.Добавить(Перечисления.ВидыПрисоединенныхФайлов.Макет3D);
			МассивДоступныхВидовФайла.Добавить(Перечисления.ВидыПрисоединенныхФайлов.МонтажнаяСхема);
			МассивДоступныхВидовФайла.Добавить(Перечисления.ВидыПрисоединенныхФайлов.ЦеховаяСхема);
			МассивДоступныхВидовФайла.Добавить(Перечисления.ВидыПрисоединенныхФайлов.ЗамерныйЛист);
			
		ИначеЕсли ТипВладельца = Тип("ДокументСсылка.Замер") Тогда
			
			МассивДоступныхВидовФайла.Добавить(Перечисления.ВидыПрисоединенныхФайлов.ЗамерныйЛист);
			МассивДоступныхВидовФайла.Добавить(Перечисления.ВидыПрисоединенныхФайлов.ФотоЗамера);
			МассивДоступныхВидовФайла.Добавить(Перечисления.ВидыПрисоединенныхФайлов.БытоваяТехника);

		ИначеЕсли ТипВладельца = Тип("СправочникСсылка.Номенклатура") Тогда
			
			МассивДоступныхВидовФайла.Добавить(Перечисления.ВидыПрисоединенныхФайлов.ТекстураНоменклатуры);
			МассивДоступныхВидовФайла.Добавить(Перечисления.ВидыПрисоединенныхФайлов.ВекторныйФайл);
			
		ИначеЕсли ТипВладельца = Тип("ДокументСсылка.СлужебнаяЗаписка") Тогда
			
			МассивДоступныхВидовФайла.Добавить(Перечисления.ВидыПрисоединенныхФайлов.СканДокумента);
			
		ИначеЕсли ТипВладельца = Тип("СправочникСсылка.КаталогИзделий") Тогда
			
			МассивДоступныхВидовФайла.Добавить(Перечисления.ВидыПрисоединенныхФайлов.ОсновнаяКартинка);
			МассивДоступныхВидовФайла.Добавить(Перечисления.ВидыПрисоединенныхФайлов.КартинкаЛевая);
			МассивДоступныхВидовФайла.Добавить(Перечисления.ВидыПрисоединенныхФайлов.КартинкаПравая);
			МассивДоступныхВидовФайла.Добавить(Перечисления.ВидыПрисоединенныхФайлов.Макет3D);
			МассивДоступныхВидовФайла.Добавить(Перечисления.ВидыПрисоединенныхФайлов.ЦеховаяСхема);
			МассивДоступныхВидовФайла.Добавить(Перечисления.ВидыПрисоединенныхФайлов.МонтажнаяСхема);
			МассивДоступныхВидовФайла.Добавить(Перечисления.ВидыПрисоединенныхФайлов.КрышаЛевая);
			МассивДоступныхВидовФайла.Добавить(Перечисления.ВидыПрисоединенныхФайлов.КрышаПравая);
			МассивДоступныхВидовФайла.Добавить(Перечисления.ВидыПрисоединенныхФайлов.ВзрывСхемаЛевая);
			МассивДоступныхВидовФайла.Добавить(Перечисления.ВидыПрисоединенныхФайлов.ВзрывСхемаОсновная);
			МассивДоступныхВидовФайла.Добавить(Перечисления.ВидыПрисоединенныхФайлов.ВзрывСхемаПравая);
			
		ИначеЕсли ТипВладельца = Тип("СправочникСсылка.Контрагенты") Тогда
			
			МассивДоступныхВидовФайла.Добавить(Перечисления.ВидыПрисоединенныхФайлов.Логотип);	
			
		КонецЕсли;
		
	Иначе
		
		МассивДоступныхВидовФайла.Добавить(Перечисления.ВидыПрисоединенныхФайлов.СистемныйФайл);
		
	КонецЕсли;
	
	Если МассивДоступныхВидовФайла.Количество() > 0 Тогда
		
		Элементы.Вид.СписокВыбора.Очистить();
		Элементы.Вид.СписокВыбора.ЗагрузитьЗначения(МассивДоступныхВидовФайла);
		
	Иначе
		
		Элементы.Вид.РежимВыбораИзСписка = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если ТекущийОбъект.Вид = Перечисления.ВидыПрисоединенныхФайлов.Логотип
		И ТипЗнч(ТекущийОбъект.ВладелецФайла) = Тип("СправочникСсылка.Контрагенты") Тогда
		
		ВладелецОб = ТекущийОбъект.ВладелецФайла.ПолучитьОбъект();
		ВладелецОб.Логотип = Объект.Ссылка;
		ВладелецОб.Записать();
		
	КонецЕсли;
	
КонецПроцедуры
