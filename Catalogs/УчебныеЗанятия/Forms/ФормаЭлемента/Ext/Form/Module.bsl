
&НаКлиенте
Процедура Добавить(Команда)
	
	ИмяФайла = "";
	Если ПоместитьФайл(АдресВХранилище, , ИмяФайла, , УникальныйИдентификатор) Тогда
		Файл = Новый Файл(ИмяФайла);
		Объект.ИмяФайла = Файл.Имя;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ЗаписатьФайлВРегистр(ТекущийОбъект.Ссылка, АдресВХранилище, НомерВерсии, Хеш);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаписатьФайлВРегистр(Объект, АдресВХранилище, НомерВерсии, Хеш)
	
	Если ЭтоАдресВременногоХранилища(АдресВХранилище) Тогда
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресВХранилище);
		
		ОбъектХеш = Новый ХешированиеДанных(ХешФункция.CRC32);
		ОбъектХеш.Добавить(ДвоичныеДанные);
		ХешНовый = ОбъектХеш.ХешСумма;
		
		Если ХешНовый <> Хеш Тогда
			НомерВерсии = НомерВерсии + 1;
			НаборЗаписей = РегистрыСведений.ФайлыВБазе.СоздатьНаборЗаписей();
			Запись = НаборЗаписей.Добавить();
			Запись.Объект = Объект;
			Запись.НомерВерсии = НомерВерсии;
			Запись.Файл = Новый ХранилищеЗначения(ДвоичныеДанные);
			НаборЗаписей.Записать(Ложь);
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		Структура = ПолучитьАдресНомерФайла(Объект.Ссылка, УникальныйИдентификатор);
		АдресВХранилище = Структура.АдресВХранилище;
		НомерВерсии = Структура.НомерВерсии;
		Хеш = Структура.Хеш;
		РазмерФайла = Структура.Размер;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьАдресНомерФайла(Объект, ИдентификаторФормы)
	
	Структура = Новый Структура;
	Структура.Вставить("НомерВерсии", Неопределено);
	Структура.Вставить("АдресВХранилище", Неопределено);
	Структура.Вставить("Хеш", Неопределено);
	Структура.Вставить("Размер");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Объект", Объект);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ФайлыВБазе.Объект,
	|	ФайлыВБазе.НомерВерсии КАК НомерВерсии,
	|	ФайлыВБазе.Файл
	|ИЗ
	|	РегистрСведений.ФайлыВБазе КАК ФайлыВБазе
	|ГДЕ
	|	ФайлыВБазе.Объект = &Объект
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерВерсии УБЫВ";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Выборка.Следующий();
	Структура.Вставить("НомерВерсии", Выборка.НомерВерсии);
	Попытка
		
		Данные = Выборка.Файл.Получить();
		Если Данные <> Неопределено Тогда
			Структура.АдресВХранилище = ПоместитьВоВременноеХранилище(Данные, ИдентификаторФормы);
			ОбъектХеш = Новый ХешированиеДанных(ХешФункция.CRC32);
			ОбъектХеш.Добавить(Данные);
			Структура.Хеш = ОбъектХеш.ХешСумма;
			Структура.Размер = Данные.Размер() / 1024;
		КонецЕсли;
		
		Выборка.Сбросить();
	Исключение
	КонецПопытки;
	
	Возврат Структура;
	
КонецФункции

&НаКлиенте
Процедура ИмяФайлаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Если ЭтоАдресВременногоХранилища(АдресВХранилище) Тогда
			ПолучитьФайл(АдресВХранилище, Объект.ИмяФайла);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Перезаполнить(Команда)
	ПерезаполнитьНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Должности.Ссылка КАК Должность
	|ИЗ
	|	Справочник.Должности КАК Должности
	|
	|УПОРЯДОЧИТЬ ПО
	|	Должности.Наименование";
	
	Объект.Должности.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если НЕ ДоступноРедактированиеФайла() Тогда
		
		Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
			Если ЭтоАдресВременногоХранилища(АдресВХранилище) Тогда
				Отказ = Истина;
				ПолучитьФайл(АдресВХранилище, Объект.ИмяФайла);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДоступноРедактированиеФайла()
	
	Возврат РольДоступна("ПолныеПрава") ИЛИ РольДоступна("ДобавлениеИзменениеПравовойБазы");
	
КонецФункции



