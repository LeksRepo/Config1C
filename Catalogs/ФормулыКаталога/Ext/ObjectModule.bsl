
Процедура ПередЗаписью(Отказ)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Наименование", Наименование);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ФормулыКаталога.Ссылка
	|ИЗ
	|	Справочник.ФормулыКаталога КАК ФормулыКаталога
	|ГДЕ
	|	ФормулыКаталога.Наименование = &Наименование
	|	И ФормулыКаталога.Ссылка <> &Ссылка";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Наименование формулы уже существует", ,"Объект.Наименование");
		Отказ = Истина;
		
	КонецЕсли;
	
	//Проверка наименования в структуре
	Попытка
		Структура = Новый Структура;
		Структура.Вставить(Наименование, Наименование);
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Неправильное наименование формулы", ,"Объект.Наименование");
		Отказ = Истина;
	КонецПопытки;
	
	РазвернутаяФормула = РазвернутьФормулу(Формула, 0);
	Активность = Активность И ЗначениеЗаполнено(РазвернутаяФормула);
	
КонецПроцедуры

Функция РазвернутьФормулу(нФормула, НомерЦикла) Экспорт
	
	НашаФормула = нФормула;
	
	Если НомерЦикла = 3 Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Слишком большая вложенность формулы", ,"Объект.Формула");
		Возврат Неопределено;
		
	КонецЕсли;
	
	ЧислоФормул = СтрЧислоВхождений(НашаФормула, "%%") / 2;
	
	Если ЧислоФормул > 0 Тогда
		
		МассивНаименований = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(НашаФормула, "%%");
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Наименование", МассивНаименований);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ФормулыКаталога.Наименование,
		|	ФормулыКаталога.Формула
		|ИЗ
		|	Справочник.ФормулыКаталога КАК ФормулыКаталога
		|ГДЕ
		|	ФормулыКаталога.Активность
		|	И НЕ ФормулыКаталога.ПометкаУдаления
		|	И ФормулыКаталога.Наименование В(&Наименование)";
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Количество() <> ЧислоФормул Тогда
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Некоторые формулы не найдены", ,"Объект.Формула");
			Возврат Неопределено;
			
		КонецЕсли;
		
		Пока Выборка.Следующий() Цикл
			
			НашаФормула = СтрЗаменить(НашаФормула, "%%" + Выборка.Наименование + "%%", Выборка.Формула); 
			
		КонецЦикла;
		
		НашаФормула = РазвернутьФормулу(НашаФормула, НомерЦикла + 1);
		
	КонецЕсли;
	
	Возврат НашаФормула;
	
КонецФункции


