#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ 

Процедура ПередЗаписью(Отказ)
	
	Если ЕстьЗеметка(Ссылка, Предмет) И НЕ ПометкаУдаления Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Заметка к предмету уже существует.'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;                    
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Родитель) И Родитель.Автор <> Автор Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Нельзя указывать группу другого пользователя.'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Не ЭтоГруппа Тогда 
		ДатаИзменения = ТекущаяДатаСеанса();
		ПредставлениеПредмета = ОбщегоНазначения.ПредметСтрокой(Предмет);
		
		Позиция = Найти(ТекстСодержания, Символы.ПС);
		Если Позиция > 0 Тогда
			Наименование = Сред(ТекстСодержания, 1, Позиция - 1);
		Иначе
			Наименование = ТекстСодержания;
		КонецЕсли;
		
		Если ПустаяСтрока(Наименование) Тогда 
			Наименование = "<" + НСтр("ru = 'Пустая заметка'") + ">";
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Функция ЕстьЗеметка(Ссылка, Предмет) 
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Предмет", Предмет);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Заметки.Ссылка
	|ИЗ
	|	Справочник.Заметки КАК Заметки
	|ГДЕ
	|	Заметки.Предмет = &Предмет
	|	И Заметки.Ссылка <> &Ссылка";
		
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции

Процедура ПриЗаписи(Отказ)

	Если ОбменДанными.Загрузка Тогда 
		Возврат; 
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(Предмет) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Заметки.Ссылка
	|ИЗ
	|	Справочник.Заметки КАК Заметки
	|ГДЕ
	|	Заметки.Предмет = &Предмет
	|	И Заметки.ПометкаУдаления = ЛОЖЬ";
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Предмет", Предмет);
	Выборка = Запрос.Выполнить().Выбрать();
	ЕстьЗаметки = Выборка.Количество() > 0;

	НаборЗаписей = РегистрыСведений.НаличиеЗаметокПоПредмету.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Предмет.Установить(Предмет);
	
	Если ЕстьЗаметки Тогда 
		Если НаборЗаписей.Количество() = 0 Тогда
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.Предмет = Предмет;
			НоваяЗапись.ЕстьЗаметка = Истина;				
		Иначе
			Для Каждого Запись Из НаборЗаписей Цикл
				Запись.ЕстьЗаметка = Истина;
			КонецЦикла;
		КонецЕсли;
	Иначе
		НаборЗаписей.Очистить();
	КонецЕсли;

	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецЕсли


