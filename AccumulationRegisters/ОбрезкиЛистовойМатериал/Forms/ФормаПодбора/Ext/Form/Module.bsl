
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВидМатериала", Перечисления.ВидыМатериалов.Листовой);
	Запрос.УстановитьПараметр("Подразделение", Параметры.Подразделение);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Обрезки.Номенклатура КАК Номенклатура,
	|	Обрезки.Номенклатура.Наименование КАК Наименование,
	|	Обрезки.Номенклатура.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	Обрезки.Высота КАК Высота,
	|	Обрезки.Ширина,
	|	Обрезки.КоличествоОстаток КАК Количество,
	|	Обрезки.КоличествоРезервОстаток КАК КоличествоРезерв,
	|	Обрезки.Спецификация КАК Спецификация
	|ИЗ
	|	РегистрНакопления.ОбрезкиЛистовойМатериал.Остатки(
	|			,
	|			Подразделение = &Подразделение
	|				И Номенклатура.НоменклатурнаяГруппа.ВидМатериала = &ВидМатериала) КАК Обрезки
	|ГДЕ
	|	Обрезки.КоличествоОстаток >= 0
	|	И Обрезки.КоличествоРезервОстаток >= 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	НоменклатурнаяГруппа,
	|	Наименование,
	|	Высота
	|ИТОГИ ПО
	|	НоменклатурнаяГруппа,
	|	Номенклатура";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока Выборка.Следующий() Цикл
		
		ЭлГруппа = Дерево.ПолучитьЭлементы().Добавить();
		ЭлГруппа.Номенклатура = Выборка.НоменклатурнаяГруппа; 
		
		ВыборкаНомГруппа = Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаНомГруппа.Следующий() Цикл
			
			ЭлНом = ЭлГруппа.ПолучитьЭлементы().Добавить();
			ЭлНом.Номенклатура = ВыборкаНомГруппа.Номенклатура;
			
			ВыборкаНоменклатура = ВыборкаНомГруппа.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам); 
			
			Пока ВыборкаНоменклатура.Следующий() Цикл
			
				Для Счетчик = 1 По ВыборкаНоменклатура.Количество Цикл
					
					Эл = ЭлНом.ПолучитьЭлементы().Добавить();
					ЗаполнитьЗначенияСвойств(Эл, ВыборкаНоменклатура);
					Эл.Резерв = Документы.Спецификация.ПустаяСсылка();
					
				КонецЦикла;
				
				Для Счетчик = 1 По ВыборкаНоменклатура.КоличествоРезерв Цикл
					
					Эл = ЭлНом.ПолучитьЭлементы().Добавить();
					ЗаполнитьЗначенияСвойств(Эл, ВыборкаНоменклатура);
					Эл.Резерв = ВыборкаНоменклатура.Спецификация;
					
				КонецЦикла;
				
			КонецЦикла;
			
		 КонецЦикла;
		
	КонецЦикла;
	
	ОтметитьСуществующиеВДокументе(Параметры.АдресТаблицыОбрезков);
	
КонецПроцедуры

&НаСервере
Функция ОтметитьСуществующиеВДокументе(АдресХранилища)
	
	ТаблицаОбрезков = ПолучитьИзВременногоХранилища(АдресХранилища);
	
	Если ТаблицаОбрезков.Количество() > 0 Тогда
		
		Для каждого Строка Из ТаблицаОбрезков Цикл
			
			Эл1 = Дерево.ПолучитьЭлементы();
			
			Для Каждого Стр ИЗ Эл1 Цикл
				
				Эл2 = Стр.ПолучитьЭлементы();
				
				Для Каждого Стр2 ИЗ Эл2 Цикл
					
					Эл3 = Стр2.ПолучитьЭлементы();
					
					Для Каждого Стр3 ИЗ Эл3 Цикл
						
						Если НЕ Стр3.Выбрать
							  И Стр3.Номенклатура = Строка.Номенклатура
							  И Стр3.Высота = Строка.Высота
							  И Стр3.Ширина = Строка.Ширина
							  И Стр3.Резерв = Строка.Резерв Тогда
							
							Стр3.Выбрать = Истина;
							Прервать;
							
						КонецЕсли;
						
					КонецЦикла;
					
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ПеренестиВДокумент(Команда)
	
	Структура = Новый Структура;
	Структура.Вставить("АдресХранилища", ПолучитьАдресХранилища());
	ОповеститьОВыборе(Структура);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьАдресХранилища()
	
	ТаблицаОстатков.Очистить();
	
	Эл1 = Дерево.ПолучитьЭлементы();
	
	Для Каждого Стр ИЗ Эл1 Цикл
		
		Эл2 = Стр.ПолучитьЭлементы();
		
		Для Каждого Стр2 ИЗ Эл2 Цикл
			
			Эл3 = Стр2.ПолучитьЭлементы();
			
			Для Каждого Стр3 ИЗ Эл3 Цикл
				
				Если Стр3.Выбрать Тогда
					
					Эл = ТаблицаОстатков.Добавить();
					Эл.Номенклатура = Стр3.Номенклатура;
					Эл.Высота = Стр3.Высота;
					Эл.Ширина = Стр3.Ширина;
					Эл.Резерв = Стр3.Резерв;
					Эл.Выбрать = Истина;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
		
	СтруктураДляОстатков = Новый Структура;
	СтруктураДляОстатков.Вставить("Выбрать", Истина);
	МассивОстатков = ТаблицаОстатков.НайтиСтроки(СтруктураДляОстатков);
	
	Возврат ПоместитьВоВременноеХранИЛИще(ТаблицаОстатков.Выгрузить(МассивОстатков));
	
КонецФункции

&НаКлиенте
Процедура ДеревоПередНачаломИзменения(Элемент, Отказ)
	
	Данные = Элементы.Дерево.ТекущиеДанные;
	
	Если Данные.Высота = 0 ИЛИ Данные.Ширина = 0 Тогда
		 Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДеревоВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.Дерево.ТекущиеДанные;
	
	Если ТекущиеДанные.Высота > 0 И ТекущиеДанные.Ширина > 0 Тогда
		ТекущиеДанные.Выбрать = НЕ ТекущиеДанные.Выбрать;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсеФлажки(Команда)
	
	Эл1 = Дерево.ПолучитьЭлементы();
	
	Для Каждого Стр ИЗ Эл1 Цикл
		
		Эл2 = Стр.ПолучитьЭлементы();
		
		Для Каждого Стр2 ИЗ Эл2 Цикл
			
			Эл3 = Стр2.ПолучитьЭлементы();
			
			Для Каждого Стр3 ИЗ Эл3 Цикл
				
				Стр3.Выбрать = Ложь;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

