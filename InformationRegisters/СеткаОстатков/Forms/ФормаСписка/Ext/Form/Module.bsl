
&НаКлиенте
Процедура НоменклатурнаяГруппаПриИзменении(Элемент)
	
	ОтобразитьНаСервере(); 	
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьНаСервере()
	
	ТабДок.Очистить();
	
	Макет = РегистрыСведений.СеткаОстатков.ПолучитьМакет("Макет");
	ОбластьТело = Макет.ПолучитьОбласть("Тело");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НоменклатурнаяГруппа", НоменклатурнаяГруппа);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СеткаОстатков.НоменклатурнаяГруппа,
	|	СеткаОстатков.Длина,
	|	СеткаОстатков.Ширина,
	|	СеткаОстатков.Использовать
	|ИЗ
	|	РегистрСведений.СеткаОстатков КАК СеткаОстатков
	|ГДЕ
	|	СеткаОстатков.НоменклатурнаяГруппа = &НоменклатурнаяГруппа";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Положение = "" + СтрЗаменить(Выборка.Ширина, Символы.НПП, "") + "_" + СтрЗаменить(Выборка.Длина, Символы.НПП, "");
		Параметр = "п" + Положение;
		Расшифровка = "р" + Положение;
		ОбластьТело.Параметры[Параметр] = Выборка.Использовать;
		ОбластьТело.Параметры[Расшифровка] = Новый Структура("Длина, Ширина, Использовать", Выборка.Длина, Выборка.Ширина, НЕ Выборка.Использовать);
		
	КонецЦикла;	
	
	ТабДок.Вывести(ОбластьТело);
	 
КонецПроцедуры

&НаКлиенте
Процедура ТабДокОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РасшифровкаНаСервере(Расшифровка);
		
КонецПроцедуры

&НаСервере
Процедура РасшифровкаНаСервере(Расшифровка)
	
	
	НаборЗаписей = РегистрыСведений.СеткаОстатков.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.НоменклатурнаяГруппа.Установить(НоменклатурнаяГруппа);
	НаборЗаписей.Отбор.Длина.Установить(Расшифровка.Длина);
	НаборЗаписей.Отбор.Ширина.Установить(Расшифровка.Ширина);
	
	НаборЗаписей.Прочитать();
	
	Если НаборЗаписей.Количество() = 0 Тогда                  
		Запись = НаборЗаписей.Добавить();
	Иначе
		Запись = НаборЗаписей[0];
	КонецЕсли;                                              
	
	Запись.Использовать = Расшифровка.Использовать;
	Запись.Длина = Расшифровка.Длина;
	Запись.Ширина = Расшифровка.Ширина;
	Запись.НоменклатурнаяГруппа = НоменклатурнаяГруппа;
	
	НаборЗаписей.Записать();
	
	ОтобразитьНаСервере();
	 
КонецПроцедуры
