
&НаКлиенте
Процедура НоменклатурнаяГруппаПодразделениеПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Подразделение) И ЗначениеЗаполнено(НоменклатурнаяГруппа) Тогда
		ОтобразитьНаСервере(); 	
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОтобразитьНаСервере()
	
	ТабДок.Очистить();
	
	Макет = РегистрыСведений.СеткаОстатков.ПолучитьМакет("Макет");
	ОбластьТело = Макет.ПолучитьОбласть("Тело");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НоменклатурнаяГруппа", НоменклатурнаяГруппа);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СеткаОстатков.НоменклатурнаяГруппа,
	|	СеткаОстатков.Длина,
	|	СеткаОстатков.Ширина,
	|	СеткаОстатков.Подразделение,
	|	СеткаОстатков.Использовать
	|ИЗ
	|	РегистрСведений.СеткаОстатков КАК СеткаОстатков
	|ГДЕ
	|	СеткаОстатков.НоменклатурнаяГруппа = &НоменклатурнаяГруппа
	|	И СеткаОстатков.Подразделение = &Подразделение";
	
	Результат = Запрос.Выполнить();
	
	Если НЕ Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Положение = "" + СтрЗаменить(Выборка.Ширина, Символы.НПП, "") + "_" + СтрЗаменить(Выборка.Длина, Символы.НПП, "");
			Параметр = "п" + Положение;
			Расшифровка = "р" + Положение;
			ОбластьТело.Параметры[Параметр] = Выборка.Использовать;
			ОбластьТело.Параметры[Расшифровка] = Новый Структура("Длина, Ширина, Использовать, Подразделение", Выборка.Длина, Выборка.Ширина, НЕ Выборка.Использовать, Выборка.Подразделение);
			
		КонецЦикла;	
		
		ТабДок.Вывести(ОбластьТело);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ТабДокОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РасшифровкаНаСервере(Расшифровка);
		
КонецПроцедуры

&НаСервере
Процедура РасшифровкаНаСервере(Расшифровка)
	
	
	НаборЗаписей = РегистрыСведений.СеткаОстатков.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.НоменклатурнаяГруппа.Установить(НоменклатурнаяГруппа);
	НаборЗаписей.Отбор.Подразделение.Установить(Подразделение);
	НаборЗаписей.Отбор.Длина.Установить(Расшифровка.Длина);
	НаборЗаписей.Отбор.Ширина.Установить(Расшифровка.Ширина);
	
	НаборЗаписей.Прочитать();
	
	Если НаборЗаписей.Количество() = 0 Тогда                  
		Запись = НаборЗаписей.Добавить();
	Иначе
		Запись = НаборЗаписей[0];
	КонецЕсли;                                              
	
	Запись.Использовать = Расшифровка.Использовать;
	Запись.Длина = Расшифровка.Длина;
	Запись.Ширина = Расшифровка.Ширина;
	Запись.НоменклатурнаяГруппа = НоменклатурнаяГруппа;
	Запись.Подразделение = Подразделение;

	НаборЗаписей.Записать();
	
	ОтобразитьНаСервере();
	 
КонецПроцедуры

&НаКлиенте
Процедура КопироватьДанные(Команда)
	
	Если ЗначениеЗаполнено(ИсточникНоменклатурнаяГруппа) И ЗначениеЗаполнено(ИсточникПодразделение) Тогда
		
		Режим = РежимДиалогаВопрос.ДаНет;
		Текст = "Текущие данные будут заменены данными номеклатурной группы: "+ИсточникНоменклатурнаяГруппа + Символы.ПС + "Продолжить?";
			
		Если Вопрос(Текст, Режим, 0) = КодВозвратаДиалога.Да Тогда
			 КопироватьДанныеСервер();
		КонецЕсли;
	
	Иначе
		Сообщить("Выберите подразделение и номенклатурную группу для заполнения");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура КопироватьДанныеСервер()
	
	НаборИсточник = РегистрыСведений.СеткаОстатков.СоздатьНаборЗаписей();
	НаборИсточник.Отбор.НоменклатурнаяГруппа.Установить(ИсточникНоменклатурнаяГруппа);
	НаборИсточник.Отбор.Подразделение.Установить(ИсточникПодразделение);
	
	НаборИсточник.Прочитать();
	
	Если НаборИсточник.Количество() > 0 Тогда                  
		
		 Таб = НаборИсточник.Выгрузить();
		 
		 Для Каждого Строка Из Таб Цикл
			 
			 Строка.Подразделение = Подразделение;
			 Строка.НоменклатурнаяГруппа = НоменклатурнаяГруппа;

		 КонецЦикла;
		 
		 НаборПриемник = РегистрыСведений.СеткаОстатков.СоздатьНаборЗаписей();
		 НаборПриемник.Отбор.НоменклатурнаяГруппа.Установить(НоменклатурнаяГруппа);
		 НаборПриемник.Отбор.Подразделение.Установить(Подразделение);

		 НаборПриемник.Загрузить(Таб);
		 НаборПриемник.Записать();
	
	КонецЕсли; 
	
	ОтобразитьНаСервере();
	
КонецПроцедуры


