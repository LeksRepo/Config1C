
Функция ПолучитьСтрокуРаскроя(Спецификация) Экспорт
	
	ТолщинаПила = 5;
	
	СписокДеталей = Спецификация.СписокМатериалы.Выгрузить(,"НомерСтроки, Материал, Номенклатура, ВысотаДетали, ШиринаДетали, Количество, 
	|Идентификатор, НоменклатураДляСклеивания, НомерИзделия, СоблюдениеТекстуры, НеТорцевать, СтруктураОтверстий, Комментарий,
	|РадиусЛевоВерх, РадиусЛевоНиз, РадиусПравоВерх, РадиусПравоНиз, КривойПилСверху, КривойПилСлева, КривойПилСнизу, КривойПилСправа,      
	|ВыборМебельнойКромкиСверху, ВыборМебельнойКромкиСлева, ВыборМебельнойКромкиСнизу, ВыборМебельнойКромкиСправа");
	СписокДеталей.Колонки.Добавить("ШиринаЛиста", Новый ОписаниеТипов("Число"));
	СписокДеталей.Колонки.Добавить("ВысотаЛиста", Новый ОписаниеТипов("Число"));
	СписокДеталей.Колонки.Добавить("НомерЛиста", Новый ОписаниеТипов("Число"));
	СписокДеталей.Колонки.Добавить("КоординатаХ", Новый ОписаниеТипов("Число"));
	СписокДеталей.Колонки.Добавить("КоординатаУ", Новый ОписаниеТипов("Число"));
	СписокДеталей.Колонки.Добавить("ПоворотДетали", Новый ОписаниеТипов("Число"));
	СписокДеталей.Колонки.Добавить("Порядок", Новый ОписаниеТипов("Число"));
	
	#Область Преобразование_ящиков_в_детали
	СписокЯщиков = Спецификация.СписокЯщики;                            
	                                                              
	Для Каждого Элемент Из СписокЯщиков Цикл                      
		                                                          
		//ДНО ЯЩИКА                                               
		НоваяСтрока = СписокДеталей.Добавить();                   
		НоваяСтрока.Номенклатура = Элемент.ДноНоменклатура;       
		НоваяСтрока.ВысотаДетали = Элемент.ДлинаДно;              
		НоваяСтрока.ШиринаДетали = Элемент.ШиринаДно;             
		НоваяСтрока.Количество = Элемент.КоличествоЯщиков;
		НоваяСтрока.НомерИзделия = Элемент.НомерИзделия;
		НоваяСтрока.НомерСтроки = Элемент.НомерСтроки;
		НоваяСтрока.СоблюдениеТекстуры = 1;
		
		Если Элемент.ВидЯщика = Перечисления.ВидыЯщика.Обычный Тогда
			
			Если ЗначениеЗаполнено(Элемент.ДлинаРебро) Тогда
				
				//РЕБРО ЯЩИКА
				НоваяСтрока = СписокДеталей.Добавить();
				НоваяСтрока.Номенклатура = Элемент.Номенклатура;
				НоваяСтрока.ВысотаДетали = Элемент.ДлинаРебро;
				НоваяСтрока.ШиринаДетали = Элемент.ВысотаЯщика;
				НоваяСтрока.Количество = Элемент.КоличествоЯщиков;
				НоваяСтрока.НомерИзделия = Элемент.НомерИзделия;
				НоваяСтрока.НомерСтроки = Элемент.НомерСтроки;
				НоваяСтрока.СоблюдениеТекстуры = 1;	
				НоваяСтрока.ВыборМебельнойКромкиСлева = Элемент.КромкаНоменклатура;
				
			КонецЕсли;
			
			//БОКОВАЯ СТОРОНА1
			НоваяСтрока = СписокДеталей.Добавить();
			НоваяСтрока.Номенклатура = Элемент.Номенклатура;
			НоваяСтрока.ВысотаДетали = Элемент.ШиринаБоковойСтороны;
			НоваяСтрока.ШиринаДетали = Элемент.ВысотаБоковойСтороны;
			НоваяСтрока.Количество = Элемент.КоличествоЯщиков;
			НоваяСтрока.НомерИзделия = Элемент.НомерИзделия;
			НоваяСтрока.НомерСтроки = Элемент.НомерСтроки;
			НоваяСтрока.СоблюдениеТекстуры = 1;
			НоваяСтрока.ВыборМебельнойКромкиСлева = Элемент.КромкаНоменклатура;
			
			//БОКОВАЯ СТОРОНА2
			НоваяСтрока = СписокДеталей.Добавить();
			НоваяСтрока.Номенклатура = Элемент.Номенклатура;
			НоваяСтрока.ВысотаДетали = Элемент.ДлинаБоковойСтороны;
			НоваяСтрока.ШиринаДетали = Элемент.ВысотаБоковойСтороны;
			НоваяСтрока.Количество = Элемент.КоличествоЯщиков;
			НоваяСтрока.НомерИзделия = Элемент.НомерИзделия;
			НоваяСтрока.НомерСтроки = Элемент.НомерСтроки;
			НоваяСтрока.СоблюдениеТекстуры = 1;
			НоваяСтрока.ВыборМебельнойКромкиСверху = Элемент.КромкаНоменклатура;
			НоваяСтрока.ВыборМебельнойКромкиСнизу = Элемент.КромкаНоменклатура;
			НоваяСтрока.ВыборМебельнойКромкиСлева = Элемент.КромкаНоменклатура;
			
		Иначе
			
			//МТБОКСЫ
			НоваяСтрока = СписокДеталей.Добавить();
			НоваяСтрока.Номенклатура = Элемент.Номенклатура;
			НоваяСтрока.ВысотаДетали = Элемент.ШиринаБоковойСтороны;
			НоваяСтрока.ШиринаДетали = Элемент.ВысотаБоковойСтороны;
			НоваяСтрока.Количество = Элемент.КоличествоЯщиков;
			НоваяСтрока.НомерИзделия = Элемент.НомерИзделия;
			НоваяСтрока.НомерСтроки = Элемент.НомерСтроки;
			НоваяСтрока.СоблюдениеТекстуры = 1;
			НоваяСтрока.ВыборМебельнойКромкиСверху = Элемент.КромкаНоменклатура;
			НоваяСтрока.ВыборМебельнойКромкиСнизу = Элемент.КромкаНоменклатура;
			НоваяСтрока.ВыборМебельнойКромкиСлева = Элемент.КромкаНоменклатура;
			НоваяСтрока.ВыборМебельнойКромкиСправа = Элемент.КромкаНоменклатура;
			
		КонецЕсли;
		
		Если Элемент.ВидФасада <> "Нет" Тогда
			Если ЗначениеЗаполнено(Элемент.ФасадНоменклатура) Тогда
				
				//ФАСАД ЯЩИКА
				НоваяСтрока = СписокДеталей.Добавить();
				НоваяСтрока.Номенклатура = Элемент.ФасадНоменклатура;
				НоваяСтрока.ВысотаДетали = Элемент.ВысотаФасад - 2 * Элемент.КромкаФасадНоменклатура.ГлубинаДетали;
				НоваяСтрока.ШиринаДетали = Элемент.ШиринаФасад - 2 * Элемент.КромкаФасадНоменклатура.ГлубинаДетали;
				НоваяСтрока.Количество = Элемент.КоличествоЯщиков;
				НоваяСтрока.НомерИзделия = Элемент.НомерИзделия;
				НоваяСтрока.НомерСтроки = Элемент.НомерСтроки;
				НоваяСтрока.СоблюдениеТекстуры = 1;
				НоваяСтрока.ВыборМебельнойКромкиСверху = Элемент.КромкаФасадНоменклатура;
				НоваяСтрока.ВыборМебельнойКромкиСнизу = Элемент.КромкаФасадНоменклатура;
				НоваяСтрока.ВыборМебельнойКромкиСлева = Элемент.КромкаФасадНоменклатура;
				НоваяСтрока.ВыборМебельнойКромкиСправа = Элемент.КромкаФасадНоменклатура;
				
			КонецЕсли;
		КонецЕсли;			
	КонецЦикла;
	#КонецОбласти 
	
	Для Каждого Элемент Из СписокДеталей Цикл
		
		Количество = Элемент.Количество;
		Материал = Элемент.Материал;
		
		//Разбивка клееной детали 
		Если Материал = "10 ЛДСП+10 ЛДСП" ИЛИ Материал = "16 ЛДСП+10 ЛДСП" ИЛИ Материал = "16 ЛДСП+16 ЛДСП" Тогда
			
			Элемент.Материал = Неопределено;
			НоваяСтрока = СписокДеталей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Элемент);
			НоваяСтрока.Номенклатура = Элемент.НоменклатураДляСклеивания;
			НоваяСтрока.ВысотаДетали = НоваяСтрока.ВысотаДетали + 10;
			НоваяСтрока.ШиринаДетали = НоваяСтрока.ШиринаДетали + 10;

		КонецЕсли;
		
		Если Количество > 1 Тогда
			//разбиваем детали
			Элемент.Количество = 1;
			Для ы = 2 По Количество Цикл
				НоваяСтрока = СписокДеталей.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Элемент);
			КонецЦикла;
			
		КонецЕсли;
		
		Элемент.ШиринаЛиста = Элемент.Номенклатура.ШиринаДетали;
		Элемент.ВысотаЛиста = Элемент.Номенклатура.ДлинаДетали;
		
	КонецЦикла;
	
	СписокДеталей.Сортировать("Номенклатура, ШиринаДетали Убыв, ВысотаДетали Убыв");
	
	//Для Каждого Элемент Из СписокДеталей Цикл
	//	
	//	
	//	
	//КонецЦикла;
	
	НоменклатураДетали = Неопределено;
	НомерЛиста = 0;
    	
	Для Каждого Элемент Из СписокДеталей Цикл
		
		Если Элемент.Номенклатура <> Справочники.Номенклатура.ПустаяСсылка() Тогда
			
			#Область Параметры_нового_листа
			Если НоменклатураДетали <> Элемент.Номенклатура Тогда
				НомерЛиста = НомерЛиста + 1;
				НоменклатураДетали = Элемент.Номенклатура;
				КоординатаХ = 0;
				КоординатаУ = 0;
				
				//Заполнение начальный остаток
				МассивОстатков = СписокДеталей.НайтиСтроки(Новый Структура("Номенклатура, НомерЛиста", Справочники.Номенклатура.ПустаяСсылка(), НомерЛиста));
				Если МассивОстатков.Количество() = 0 Тогда
					НоваяСтрока = СписокДеталей.Добавить();
					НоваяСтрока.Номенклатура = Справочники.Номенклатура.ПустаяСсылка();
					НоваяСтрока.Количество = 1;
					НоваяСтрока.НомерЛиста = НомерЛиста;
					НоваяСтрока.ВысотаДетали = Элемент.ВысотаЛиста;
					НоваяСтрока.ШиринаДетали = Элемент.ШиринаЛиста;
					НоваяСтрока.ВысотаЛиста = Элемент.ВысотаЛиста;
					НоваяСтрока.ШиринаЛиста = Элемент.ШиринаЛиста;
					НоваяСтрока.КоординатаУ = 0;
					НоваяСтрока.КоординатаХ = 0;
				КонецЕсли;
			КонецЕсли;
			#КонецОбласти
			
			
			МассивОстатков = СписокДеталей.НайтиСтроки(Новый Структура("Номенклатура, НомерЛиста", Справочники.Номенклатура.ПустаяСсылка(), НомерЛиста));
			Для Каждого ЭлементМассива Из МассивОстатков Цикл
				
				ОстатокПоВысоте = ЭлементМассива.ВысотаДетали - Элемент.ВысотаДетали;
				ОстатокПоШирине = ЭлементМассива.ШиринаДетали - Элемент.ШиринаДетали;
				
				Если ОстатокПоВысоте >= 0  И ОстатокПоШирине >= 0 Тогда
					
					Элемент.КоординатаУ = ЭлементМассива.КоординатаУ;
					Элемент.КоординатаХ = ЭлементМассива.КоординатаХ;
					Элемент.НомерЛиста = НомерЛиста;
					Элемент.ПоворотДетали = 0;
			
					ИндексОстатка = СписокДеталей.Индекс(ЭлементМассива);										
					
					Если ОстатокПоШирине > 40 Тогда
						
						НоваяСтрока = СписокДеталей.Вставить(ИндексОстатка);
						НоваяСтрока.Номенклатура = Справочники.Номенклатура.ПустаяСсылка();
						НоваяСтрока.Количество = 1;
						НоваяСтрока.НомерЛиста = НомерЛиста;
						НоваяСтрока.ВысотаДетали = ЭлементМассива.ВысотаДетали;
						НоваяСтрока.ШиринаДетали = ОстатокПоШирине - ТолщинаПила;
						НоваяСтрока.ВысотаЛиста = Элемент.ВысотаЛиста;
						НоваяСтрока.ШиринаЛиста = Элемент.ШиринаЛиста;
						НоваяСтрока.КоординатаУ = Элемент.КоординатаУ;
						НоваяСтрока.КоординатаХ = Элемент.КоординатаХ + Элемент.ШиринаДетали + ТолщинаПила;
						
					КонецЕсли;
					
					
					Если ОстатокПоВысоте > 40 Тогда
						
						НоваяСтрока = СписокДеталей.Вставить(ИндексОстатка);
						НоваяСтрока.Номенклатура = Справочники.Номенклатура.ПустаяСсылка();
						НоваяСтрока.Количество = 1;
						НоваяСтрока.НомерЛиста = НомерЛиста;
						НоваяСтрока.ВысотаДетали = ОстатокПоВысоте - ТолщинаПила;
						НоваяСтрока.ШиринаДетали = Элемент.ШиринаДетали;
						НоваяСтрока.ВысотаЛиста = Элемент.ВысотаЛиста;
						НоваяСтрока.ШиринаЛиста = Элемент.ШиринаЛиста;
						НоваяСтрока.КоординатаУ = Элемент.КоординатаУ + Элемент.ВысотаДетали + ТолщинаПила;
						НоваяСтрока.КоординатаХ = Элемент.КоординатаХ;
						
					КонецЕсли;
					
					СписокДеталей.Удалить(ЭлементМассива);
					Прервать;
					
				ИначеЕсли (МассивОстатков.Найти(ЭлементМассива) + 1) = МассивОстатков.Количество() Тогда
					
					НомерЛиста = НомерЛиста + 1;	
					КоординатаХ = 0;
					КоординатаУ = 0;
					
					НоваяСтрока = СписокДеталей.Добавить();
					НоваяСтрока.Номенклатура = Справочники.Номенклатура.ПустаяСсылка();
					НоваяСтрока.Количество = 1;
					НоваяСтрока.НомерЛиста = НомерЛиста;
					НоваяСтрока.ВысотаДетали = Элемент.ВысотаЛиста;
					НоваяСтрока.ШиринаДетали = Элемент.ШиринаЛиста;
					НоваяСтрока.ВысотаЛиста = Элемент.ВысотаЛиста;
					НоваяСтрока.ШиринаЛиста = Элемент.ШиринаЛиста;
					НоваяСтрока.КоординатаУ = 0;
					НоваяСтрока.КоординатаХ = 0;
					
					МассивОстатков.Добавить(НоваяСтрока);
					
				КонецЕсли;
				
			КонецЦикла;	
			
		КонецЕсли; //Элемент.Номенклатура <> Справочники.Номенклатура.ПустаяСсылка()
		
	КонецЦикла;
	
	ДанныеДляРаскроя = "";
	
	Для Каждого Элемент Из СписокДеталей Цикл
		
		Номенклатура = ?(Элемент.Номенклатура = Справочники.Номенклатура.ПустаяСсылка(), "Остаток", Элемент.Номенклатура);
		
		#Область Формирование_строки_раскроя
		ДанныеДляРаскроя = ДанныеДляРаскроя 
		+ Номенклатура + "☺" // 1 - Номенклатура
		+ "" + "☺" //2 - КлеенаяНоменклатура??? 
		+ Элемент.ВысотаДетали + "☺" //3 - Высота детали
		+ Элемент.ШиринаДетали + "☺" //4 - Ширина детали
		+ "" + "☺" //5
		+ "" + "☺" //6
		+ "" + "☺" //7
		+ "" + "☺" //8
		+ "" + "☺" //9
		+ "" + "☺" //10
		+ "" + "☺" //11
		+ "" + "☺" //12
		+ "" + "☺" //13
		+ "" + "☺" //14
		+ "" + "☺" //15
		+ "" + "☺" //16
		+ Элемент.СоблюдениеТекстуры + "☺" //17 - СоблюдениеТекстуры номенклатуры
		+ "" + "☺" //18 - СоблюдениеТекстуры Клееной номенклатуры???
		+ Элемент.Количество + "☺" //19 - Количество
		+ Элемент.Комментарий + "☺" //20 - Комментарий
		+ "" + "☺" //21
		+ Элемент.ШиринаЛиста + "☺" //22 - Ширина листа раскроя
		+ Элемент.ВысотаЛиста + "☺" //23 - Высота листа раскроя
		+ Элемент.ВыборМебельнойКромкиСверху.КраткоеНаименование + "☺" //24
		+ Элемент.ВыборМебельнойКромкиСнизу.КраткоеНаименование + "☺" //25
		+ Элемент.ВыборМебельнойКромкиСлева.КраткоеНаименование + "☺" //26
		+ Элемент.ВыборМебельнойКромкиСправа.КраткоеНаименование + "☺" //27
		+ "" + "☺" //28
		+ "" + "☺" //29
		+ "" + "☺" //30
		+ "" + "☺" //31
		+ "" + "☺" //32
		+ "" + "☺" //33
		+ "" + "☺" //34
		+ "" + "☺" //35
		+ "" + "☺" //36
		+ "" + "☺" //37
		+ Элемент.НомерЛиста + "☺" //38 - Номер листа
		+ Элемент.КоординатаУ + "☺" //39 - координата по у
		+ Элемент.КоординатаХ + "☺" //40 - координата по х
		+ Элемент.ПоворотДетали + "☺" //41 - признак поворота детали
		+ "☻";
		#КонецОбласти
		
	КонецЦикла;
	
	Возврат ДанныеДляРаскроя;
	
КонецФункции // ПолучитьСтрокуРаскроя()

