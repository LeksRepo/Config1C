
&НаКлиенте
Процедура ЗаписатьИзменения(Команда)
	
	СтруктураНоменклатур = ПроверитьЗаполнениеНоменклатуры();
	
	Если СтруктураНоменклатур.НетОшибок Тогда
		
		ЗаписатьНаСервере();
		Модифицированность = Ложь;
		
		Оповестить("ЗаписаноСоответствие");
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНаСервере()
	
	Для Каждого Строка Из ТаблицаСоответсвий Цикл
		
		ПодставляемаяНоменклатура = Строка.Номенклатура;
		НоменклатурнаяГруппа = ПодставляемаяНоменклатура.НоменклатурнаяГруппа;
		НаборЗаписей = РегистрыСведений.СоответствиеНоменклатуры.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Номенклатура.Установить(Номенклатура);
		НаборЗаписей.Отбор.НоменклатурнаяГруппа.Установить(НоменклатурнаяГруппа);
		НаборЗаписей.Отбор.Подразделение.Установить(Подразделение);
		НаборЗаписей.Прочитать();
		
		Если НаборЗаписей.Количество() = 0 Тогда
			
			НоваяЗапись = НаборЗаписей.Добавить();
			
		Иначе
			
			НоваяЗапись = НаборЗаписей[0];
			
		КонецЕсли;
		
		НоваяЗапись.Подразделение = Подразделение;
		НоваяЗапись.Номенклатура = Номенклатура;
		НоваяЗапись.ПодставляемаяНоменклатура = ПодставляемаяНоменклатура;
		НоваяЗапись.НоменклатурнаяГруппа = НоменклатурнаяГруппа;
		
		НаборЗаписей.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьЗаполнениеНоменклатуры()
	
	СтруктураНоменклатурДляПроверки = Новый Структура;
	СтруктураНоменклатурДляПроверки.Вставить("НетОшибок", Истина);
	
	Ошибки = Неопределено;
	НомерСтроки = 0;
	Для каждого Строка Из ТаблицаСоответсвий Цикл
		
		Если Не ЗначениеЗаполнено(Строка.Номенклатура) Тогда
			
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "" + "ТаблицаСоответсвий[" + НомерСтроки + "].Номенклатура", "Номенклатура не заполнена");
			СтруктураНоменклатурДляПроверки.Вставить("НетОшибок", Ложь);
			НомерСтроки = НомерСтроки + 1;
			
		КонецЕсли; 
		
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки, Ложь);
	
	Возврат СтруктураНоменклатурДляПроверки;
	
КонецФункции // ПроверитьЗаполнение()

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Подразделение = Параметры.Подразделение;
	Номенклатура = Параметры.Номенклатура;
	
	ПерезаполнитьТаблицу();
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСоответсвийПередУдалением(Элемент, Отказ)
	
	Если Элемент.ТекущиеДанные.ОбязательноСоответствие Тогда
		
		Сообщить("Этот элемент нельзя удалить");
		Отказ = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСоответсвийНоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	НоваяНоменклатурнаяГруппа = ТаблицаСоответсвийНоменклатураОбработкаВыбораНаСервере(ВыбранноеЗначение);
	
	//Ошибки = Неопределено;
	
	Если Элементы.ТаблицаСоответсвий.ТекущиеДанные.ОбязательноСоответствие Тогда
		
		Если Элементы.ТаблицаСоответсвий.ТекущиеДанные.НоменклатурнаяГруппа <> НоваяНоменклатурнаяГруппа Тогда
			
			СтандартнаяОбработка = Ложь;
			//ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "Элементы.ТаблицаСоответсвий.ТекущиеДанные.Номенклатура", "Номенклатурная группа не совпадает с выбранной");
			Сообщить("Номенклатурная группа не совпадает с выбранной");
			
		КонецЕсли;
		
		//ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки);
		
	Иначе
		
		Элементы.ТаблицаСоответсвий.ТекущиеДанные.НоменклатурнаяГруппа = НоваяНоменклатурнаяГруппа;
		
	КонецЕсли; 
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция  ТаблицаСоответсвийНоменклатураОбработкаВыбораНаСервере(Номенклатура)
	Возврат Номенклатура.НоменклатурнаяГруппа;
КонецФункции

&НаКлиенте
Процедура ТаблицаСоответсвийНоменклатураНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НомГруппа = Элементы.ТаблицаСоответсвий.ТекущиеДанные.НоменклатурнаяГруппа;
	
	Если ЗначениеЗаполнено(НомГруппа) Тогда
		
		СтандартнаяОбработка = Ложь;
		ЗначениеОтбора = Новый Структура;
		ЗначениеОтбора.Вставить("НоменклатурнаяГруппа", НомГруппа);
		ЗначениеОтбора.Вставить("ЭтоГруппа", Ложь);
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("Отбор", ЗначениеОтбора);
		
		
		Форма = ПолучитьФорму("Справочник.Номенклатура.ФормаВыбора", ПараметрыОтбора, Элемент);
		Форма.Элементы.Список.Отображение=ОтображениеТаблицы.Список;
		Форма.Открыть();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если НЕ РазрешитьПерезаполнение() Тогда
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураПриИзменении(Элемент)
	
	ПерезаполнитьТаблицу();
	
КонецПроцедуры

&НаСервере
Функция ПерезаполнитьТаблицу()
	
	// { Васильев Александр Леонидович [21.04.2015]
	// Костыль. Добавить проверку на подразделение
	// } Васильев Александр Леонидович [21.04.2015]
	
	ТаблицаСоответсвий.Очистить();
	
	Если ЗначениеЗаполнено(Номенклатура) Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
		Запрос.УстановитьПараметр("Подразделение", Подразделение);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СправочникНоменклатура.Ссылка КАК Номенклатура,
		|	НоменклатурныеГруппы.Ссылка КАК НоменклатурнаяГруппа
		|ПОМЕСТИТЬ Предварительная
		|ИЗ
		|	Справочник.Номенклатура КАК СправочникНоменклатура,
		|	Справочник.НоменклатурныеГруппы КАК НоменклатурныеГруппы
		|ГДЕ
		|	СправочникНоменклатура.Ссылка = &Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Предварительная.Номенклатура,
		|	Предварительная.НоменклатурнаяГруппа,
		|	СоответствиеНоменклатуры.ПодставляемаяНоменклатура,
		|	Предварительная.НоменклатурнаяГруппа.ОбязательноСоответсвие КАК ОбязательноСоответсвие
		|ИЗ
		|	Предварительная КАК Предварительная
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатуры КАК СоответствиеНоменклатуры
		|		ПО Предварительная.Номенклатура = СоответствиеНоменклатуры.Номенклатура
		|			И Предварительная.НоменклатурнаяГруппа = СоответствиеНоменклатуры.НоменклатурнаяГруппа
		|			И (СоответствиеНоменклатуры.Подразделение = &Подразделение)
		|ГДЕ
		|	(Предварительная.НоменклатурнаяГруппа.ОбязательноСоответсвие
		|			ИЛИ СоответствиеНоменклатуры.ПодставляемаяНоменклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка))";
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			НоваяСтрока = ТаблицаСоответсвий.Добавить();
			НоваяСтрока.НоменклатурнаяГруппа = ВыборкаДетальныеЗаписи.НоменклатурнаяГруппа;
			НоваяСтрока.ОбязательноСоответствие = ВыборкаДетальныеЗаписи.ОбязательноСоответсвие;
			НоваяСтрока.Номенклатура = ВыборкаДетальныеЗаписи.ПодставляемаяНоменклатура;
			
		КонецЦикла;
		
	Иначе
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	НоменклатурныеГруппы.Ссылка КАК НоменклатурнаяГруппа,
		|	НоменклатурныеГруппы.ОбязательноСоответсвие
		|ИЗ
		|	Справочник.НоменклатурныеГруппы КАК НоменклатурныеГруппы
		|ГДЕ
		|	НоменклатурныеГруппы.ОбязательноСоответсвие";
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			НоваяСтрока = ТаблицаСоответсвий.Добавить();
			НоваяСтрока.НоменклатурнаяГруппа = ВыборкаДетальныеЗаписи.НоменклатурнаяГруппа;
			НоваяСтрока.ОбязательноСоответствие = ВыборкаДетальныеЗаписи.ОбязательноСоответсвие;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ПодразделениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если НЕ РазрешитьПерезаполнение() Тогда
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеПриИзменении(Элемент)
	
	ПерезаполнитьТаблицу();
	
КонецПроцедуры

&НаКлиенте
Функция РазрешитьПерезаполнение()
	
	Ответ = Истина;
	
	Если Модифицированность И 
		Вопрос("Текущие настройки не будут сохранены." + Символы.ПС + "Продолжить?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет Тогда
		
		Ответ = Ложь;
		
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции
