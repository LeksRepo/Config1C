
Функция ПолучитьСтруктуруСоответствий(Номенклатура, Подразделение) Экспорт
	
	СтруктураСоответствий = Новый Структура();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СоответствиеНоменклатуры.НоменклатурнаяГруппа,
	|	НоменклатураПодразделений.Номенклатура
	|ПОМЕСТИТЬ втНоменклатура
	|ИЗ
	|	РегистрСведений.СоответствиеНоменклатуры КАК СоответствиеНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатураПодразделений.СрезПоследних(, Подразделение = &Подразделение) КАК НоменклатураПодразделений
	|		ПО СоответствиеНоменклатуры.ПодставляемаяНоменклатура = НоменклатураПодразделений.Номенклатура
	|ГДЕ
	|	СоответствиеНоменклатуры.Номенклатура = &Номенклатура
	|	И СоответствиеНоменклатуры.Подразделение = &Подразделение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НоменклатурныеГруппы.Ссылка КАК НоменклатурнаяГруппа,
	|	ЕСТЬNULL(втНоменклатура.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК Номенклатура
	|ИЗ
	|	Справочник.НоменклатурныеГруппы КАК НоменклатурныеГруппы
	|		ЛЕВОЕ СОЕДИНЕНИЕ втНоменклатура КАК втНоменклатура
	|		ПО (втНоменклатура.НоменклатурнаяГруппа = НоменклатурныеГруппы.Ссылка)
	|ГДЕ
	|	НЕ НоменклатурныеГруппы.ПометкаУдаления
	|	И НоменклатурныеГруппы.Предопределенный";
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Имя = Справочники.НоменклатурныеГруппы.ПолучитьИмяПредопределенного(Выборка.НоменклатурнаяГруппа);
		СтруктураСоответствий.Вставить(Имя, Выборка.Номенклатура);
		
	КонецЦикла;
	
	Возврат СтруктураСоответствий;
	
КонецФункции // ПолучитьКромкуПоЦвету()

Функция ПолучитьСоответствие(Номенклатура, НоменклатурнаяГруппа, Подразделение) Экспорт
	
	СтруктураСоответствий = ПолучитьСтруктуруСоответствий(Номенклатура, Подразделение);
	Имя = Справочники.НоменклатурныеГруппы.ПолучитьИмяПредопределенного(НоменклатурнаяГруппа);
	
	Возврат СтруктураСоответствий[Имя];
	
КонецФункции // ПолучитьКромкуПоЦвету()
