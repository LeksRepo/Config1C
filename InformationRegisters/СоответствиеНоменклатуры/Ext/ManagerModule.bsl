
Функция ПолучитьСтруктуруСоответствий(Номенклатура, Подразделение) Экспорт
	
	СтруктураСоответствий = Новый Структура();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.УстановитьПараметр("ПустоеПодразделение", Справочники.Подразделения.ПустаяСсылка());
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СоответствиеНоменклатуры.НоменклатурнаяГруппа,
	|	СоответствиеНоменклатуры.Подразделение,
	|	СоответствиеНоменклатуры.ПодставляемаяНоменклатура
	|ПОМЕСТИТЬ втНоменклатура
	|ИЗ
	|	РегистрСведений.СоответствиеНоменклатуры КАК СоответствиеНоменклатуры
	|ГДЕ
	|	СоответствиеНоменклатуры.Номенклатура = &Номенклатура
	|	И СоответствиеНоменклатуры.Подразделение = &Подразделение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СоответствиеНоменклатуры.НоменклатурнаяГруппа,
	|	СоответствиеНоменклатуры.Подразделение,
	|	СоответствиеНоменклатуры.ПодставляемаяНоменклатура
	|ПОМЕСТИТЬ втНоменклатураПустогоПодразделения
	|ИЗ
	|	РегистрСведений.СоответствиеНоменклатуры КАК СоответствиеНоменклатуры
	|ГДЕ
	|	СоответствиеНоменклатуры.Номенклатура = &Номенклатура
	|	И СоответствиеНоменклатуры.Подразделение = &ПустоеПодразделение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НоменклатурныеГруппы.Ссылка КАК НоменклатурнаяГруппа,
	|	втНоменклатура.Подразделение,
	|	втНоменклатура.ПодставляемаяНоменклатура Как Номенклатура
	|ИЗ
	|	Справочник.НоменклатурныеГруппы КАК НоменклатурныеГруппы
	|		ЛЕВОЕ СОЕДИНЕНИЕ втНоменклатура КАК втНоменклатура
	|		ПО (втНоменклатура.НоменклатурнаяГруппа = НоменклатурныеГруппы.Ссылка)
	|ГДЕ
	|	НЕ НоменклатурныеГруппы.ПометкаУдаления
	|	И НоменклатурныеГруппы.Предопределенный
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НоменклатурныеГруппы.Ссылка КАК НоменклатурнаяГруппа,
	|	втНоменклатураПустогоПодразделения.Подразделение,
	|	втНоменклатураПустогоПодразделения.ПодставляемаяНоменклатура КАК Номенклатура
	|ИЗ
	|	Справочник.НоменклатурныеГруппы КАК НоменклатурныеГруппы
	|		ЛЕВОЕ СОЕДИНЕНИЕ втНоменклатураПустогоПодразделения КАК втНоменклатураПустогоПодразделения
	|		ПО (втНоменклатураПустогоПодразделения.НоменклатурнаяГруппа = НоменклатурныеГруппы.Ссылка)
	|ГДЕ
	|	НЕ НоменклатурныеГруппы.ПометкаУдаления
	|	И НоменклатурныеГруппы.Предопределенный";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	ВыборкаСПодразделением = РезультатЗапроса[2].Выбрать();
	ВыборкаБезПодразделения = РезультатЗапроса[3].Выбрать();
	
	Пока ВыборкаСПодразделением.Следующий() Цикл
		
		Имя = Справочники.НоменклатурныеГруппы.ПолучитьИмяПредопределенного(ВыборкаСПодразделением.НоменклатурнаяГруппа);
		СтруктураСоответствий.Вставить(Имя, ВыборкаСПодразделением.Номенклатура);
		
	КонецЦикла;
	
	Пока ВыборкаБезПодразделения.Следующий() Цикл
		
		Имя = Справочники.НоменклатурныеГруппы.ПолучитьИмяПредопределенного(ВыборкаБезПодразделения.НоменклатурнаяГруппа);
		ЗначениеДляПроверки = Неопределено;
		СтруктураСоответствий.Свойство(Имя, ЗначениеДляПроверки);
		Если НЕ ЗначениеЗаполнено(ЗначениеДляПроверки) и ЗначениеЗаполнено(ВыборкаБезПодразделения.Номенклатура) Тогда
			СтруктураСоответствий.Вставить(Имя, ВыборкаБезПодразделения.Номенклатура);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СтруктураСоответствий;
	
КонецФункции // ПолучитьКромкуПоЦвету()

Функция ПолучитьСоответствие(Номенклатура, НоменклатурнаяГруппа, Подразделение) Экспорт
	
	СтруктураСоответствий = ПолучитьСтруктуруСоответствий(Номенклатура, Подразделение);
	Имя = Справочники.НоменклатурныеГруппы.ПолучитьИмяПредопределенного(НоменклатурнаяГруппа);
	
	Возврат СтруктураСоответствий[Имя];
	
КонецФункции // ПолучитьКромкуПоЦвету()
