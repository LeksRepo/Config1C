
Функция ПолучитьСвойстваФайла(СсылкаНаОбъект, ИдентификаторФормы) Экспорт
	
	Структура = Новый Структура;
	Структура.Вставить("НомерВерсии", Неопределено);
	Структура.Вставить("АдресВХранилище", Неопределено);
	Структура.Вставить("Хеш", Неопределено);
	Структура.Вставить("Размер", 0);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Объект", СсылкаНаОбъект);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ФайлыВБазе.Объект,
	|	ФайлыВБазе.Файл,
	|	ФайлыВБазе.Хеш
	|ИЗ
	|	РегистрСведений.ФайлыВБазе КАК ФайлыВБазе
	|ГДЕ
	|	ФайлыВБазе.Объект = &Объект";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		Данные = Выборка.Файл.Получить();
		Если Данные <> Неопределено Тогда
			Структура.АдресВХранилище = ПоместитьВоВременноеХранилище(Данные, ИдентификаторФормы);
			Структура.Хеш = Выборка.Хеш;
			Структура.Размер = Данные.Размер() / 1024;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Структура;
	
КонецФункции

Функция ЗаписатьФайлВРегистр(СсылкаНаОбъект, АдресВХРанилище) Экспорт
	
	Если ЭтоАдресВременногоХранилища(АдресВХранилище) Тогда
		
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресВХранилище);
		
		ОбъектХеш = Новый ХешированиеДанных(ХешФункция.CRC32);
		ОбъектХеш.Добавить(ДвоичныеДанные);
		ХешНовый = ОбъектХеш.ХешСумма;
		
	Иначе
		
		ХешНовый = 0;
		
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.ФайлыВБазе.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Объект.Значение = СсылкаНаОбъект;
	НаборЗаписей.Отбор.Объект.Использование = Истина;
	НаборЗаписей.Прочитать();
	
	Если НаборЗаписей.Количество() = 1 Тогда
		Запись = НаборЗаписей[0];
	Иначе
		Запись = НаборЗаписей.Добавить();
	КонецЕсли;
	
	Если ХешНовый <> Запись.Хеш Тогда
		
		Запись.Объект = СсылкаНаОбъект;
		Запись.Файл = Новый ХранилищеЗначения(ДвоичныеДанные);
		Запись.Хеш = ХешНовый;
		НаборЗаписей.Записать();
		
	КонецЕсли;
	
КонецФункции




