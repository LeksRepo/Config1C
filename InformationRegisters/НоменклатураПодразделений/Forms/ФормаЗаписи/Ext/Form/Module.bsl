
&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Перем Ошибки;
	
	Если НЕ ТекущийОбъект.ОкруглятьДоЛистов Тогда
		
		ТребоватьПроцентОтхода = ТекущийОбъект.Номенклатура.НоменклатурнаяГруппа.ТребоватьПроцентОтхода;
		
		Если ТребоватьПроцентОтхода
			И ТекущийОбъект.ПроцентОтхода = 0 Тогда
			
			ТекстСообщения = "Установите процент отхода или включите округление до листов";
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, , ТекстСообщения);
			
		КонецЕсли;
		
	КонецЕсли; // НЕ ТекущийОбъект.ОкруглятьДоЛистов
	
	Если НЕ ТекущийОбъект.ПодЗаказ Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("МоментВремени", ТекущаяДата());
		Запрос.УстановитьПараметр("Подразделение",ТекущийОбъект.Подразделение);
		Запрос.УстановитьПараметр("Номенклатура", ТекущийОбъект.Номенклатура);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ЦеныНоменклатурыПоПодразделениямСрезПоследних.Розничная КАК Цена
		|ИЗ
		|	РегистрСведений.ЦеныНоменклатурыПоПодразделениям.СрезПоследних(
		|			&МоментВремени,
		|			Подразделение = &Подразделение
		|				И Номенклатура = &Номенклатура) КАК ЦеныНоменклатурыПоПодразделениямСрезПоследних";
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Если НЕ РезультатЗапроса.Пустой() Тогда
			Выборка = РезультатЗапроса.Выбрать();
			Выборка.Следующий();
			Цена = Выборка.Цена;
			
		Иначе
			
			Цена = 0;
			
		КонецЕсли;
		
		Если Цена = 0 Тогда
			
			ТекстСообщения = "Флаг 'Под заказ' можно убрать только после установки цены на материал.";
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, , ТекстСообщения);
			
		КонецЕсли;
		
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ОсновнаяПоСкладуНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = ПолучитьСписокНоменклатуры(Запись.Номенклатура);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСписокНоменклатуры(фнНоменклатура)
	
	СписокЗначений = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("БазоваяНоменклатура", фнНоменклатура);
	Запрос.Текст = "ВЫБРАТЬ
	|	Номенклатура.Ссылка КАК Номенклатура
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	(Номенклатура.БазоваяНоменклатура = &БазоваяНоменклатура
	|			ИЛИ Номенклатура.Ссылка = &БазоваяНоменклатура)";
	
	РезультатЗапроса = Запрос.Выполнить();
	Таблица = РезультатЗапроса.Выгрузить();
	Массив = Таблица.ВыгрузитьКолонку("Номенклатура");
	СписокЗначений.ЗагрузитьЗначения(Массив);
	
	Возврат СписокЗначений;
	
КонецФункции

&НаКлиенте
Функция ОбновитьРозничнуюЦену()
	
	РозничнаяЦена = ПолучитьРозничнуюЦену(Запись.Подразделение, Запись.Номенклатура);
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьРозничнуюЦену(фнПодразделение, фнНоменклатура)
	
	Результат = 0;
	Если НЕ ЗначениеЗаполнено(фнПодразделение)
		ИЛИ НЕ ЗначениеЗаполнено(фнНоменклатура) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Номенклатура", фнНоменклатура);
	Запрос.УстановитьПараметр("Подразделение", фнПодразделение);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЦеныНоменклатурыПоПодразделениямСрезПоследних.Розничная
	|ИЗ
	|	РегистрСведений.ЦеныНоменклатурыПоПодразделениям.СрезПоследних(
	|			,
	|			Номенклатура = &Номенклатура
	|				И Подразделение = &Подразделение) КАК ЦеныНоменклатурыПоПодразделениямСрезПоследних";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Результат = Выборка.Розничная;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ПодразделениеПриИзменении(Элемент)
	
	ОбновитьРозничнуюЦену();
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураПриИзменении(Элемент)
	
	ОбновитьРозничнуюЦену();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РозничнаяЦена = ПолучитьРозничнуюЦену(Запись.Подразделение, Запись.Номенклатура);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	ОповеститьОВыборе("ок");
	
КонецПроцедуры
