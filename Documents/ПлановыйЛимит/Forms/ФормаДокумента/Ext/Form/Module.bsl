&НаКлиенте
Процедура ЗаполнитьТаблицуДней()
	
    Объект.ТаблицаДней.Очистить();
	
	КоличествоДней = День(КонецМесяца(Объект.Период));
	ТекущаяДатаСтроки = НачалоМесяца(Объект.Период);
	
	Сч = 1;
	
	Пока Сч <= КоличествоДней Цикл
		
		НоваяСтрока = Объект.ТаблицаДней.Добавить();
		НоваяСтрока.Дата = ТекущаяДатаСтроки;
		НоваяСтрока.Норматив = 0;
		
		ТекущаяДатаСтроки = ТекущаяДатаСтроки + 60*60*24;
		Сч = Сч + 1;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДиаграмму()
	
	Диаграмма.Очистить();
	Диаграмма.ТипДиаграммы=ТипДиаграммы.График;
	Диаграмма.РежимСглаживания=РежимСглаживанияДиаграммы.ГладкаяКривая;
	Диаграмма.НатяжениеСглаживания=70;
	Диаграмма.РежимПолупрозрачности=РежимПолупрозрачностиДиаграммы.НеИспользовать;
	
	Серия=Диаграмма.Серии.Добавить("Норматив");
	Серия.Цвет=WebЦвета.ЗеленыйЛес;
	Серия.Маркер=ТипМаркераДиаграммы.Круг;
	Сч = 0;
	
	Для Каждого Строка Из Объект.ТаблицаДней Цикл
				  
		Диаграмма.Точки.Добавить(Формат(Строка.Дата,"ДФ=dd.MM"));
		Диаграмма.УстановитьЗначение(Сч,0,Строка.Норматив);
		
		Если (НЕ(Элементы.ТаблицаДней.ТекущиеДанные = Неопределено)) И Строка.Дата = Элементы.ТаблицаДней.ТекущиеДанные.Дата Тогда

			Диаграмма.Точки[Сч].Цвет = Новый Цвет(250,1,1);
			Диаграмма.Точки[Сч].ПриоритетЦвета = Истина;
			
		КонецЕсли;

		Сч = Сч + 1;
	
	КонецЦикла;
	
	Диаграмма.Обновление = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	
	ЗаполнитьТаблицуДней();
	ОбновитьДиаграмму();
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДнейНормативПриИзменении(Элемент)
	ОбновитьДиаграмму();
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДнейПриАктивизацииСтроки(Элемент)
	ОбновитьДиаграмму();
КонецПроцедуры
