&НаКлиенте
Процедура ЗаполнитьТаблицуДней()
	
	Если ЗначениеЗаполнено(Объект.Подразделение) И ЗначениеЗаполнено(Объект.Дата) Тогда
	
		Объект.ТаблицаДней.Очистить();
		
		КоличествоДней = День(КонецМесяца(Объект.Дата));
		ТекущаяДатаСтроки = НачалоМесяца(Объект.Дата);
		
		Сч = 1;
		
		Пока Сч <= КоличествоДней Цикл
			
			НоваяСтрока = Объект.ТаблицаДней.Добавить();
			НоваяСтрока.Дата = ТекущаяДатаСтроки;
			НоваяСтрока.Норматив = 0;
			
			Если ДеньНедели(ТекущаяДатаСтроки) = 7 Тогда
				НоваяСтрока.Выходной = Истина;	
			КонецЕсли;
			
			ТекущаяДатаСтроки = ТекущаяДатаСтроки + 60*60*24;
			Сч = Сч + 1;
			
		КонецЦикла;
		
		ОбновитьДиаграмму();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДиаграмму()
	
	Диаграмма.Очистить();
	Диаграмма.ОтображатьЛегенду = Ложь;
	Диаграмма.ТипДиаграммы=ТипДиаграммы.График;
	Диаграмма.РежимСглаживания=РежимСглаживанияДиаграммы.ГладкаяКривая;
	Диаграмма.НатяжениеСглаживания=70;
	Диаграмма.РежимПолупрозрачности=РежимПолупрозрачностиДиаграммы.НеИспользовать;
	
	Диаграмма.ПропускатьБазовоеЗначение = Истина;
	Диаграмма.БазовоеЗначение = 0;
	
	Серия1 = Диаграмма.Серии.Добавить("Норматив");
	Серия1.Цвет = WebЦвета.ЗеленыйЛес;
	Серия1.Маркер = ТипМаркераДиаграммы.Круг;
	
	Серия2 = Диаграмма.Серии.Добавить("Выходные");
	Серия2.Цвет = WebЦвета.Черный;
	Серия2.Маркер = ТипМаркераДиаграммы.Круг;
	Серия2.Линия = Новый Линия(ТипЛинииДиаграммы.Сплошная,3);
	
	Серия3 = Диаграмма.Серии.Добавить("");
	Серия3.Цвет = WebЦвета.Красный;
	Серия3.Маркер = ТипМаркераДиаграммы.Круг;
	Серия3.Линия = Новый Линия(ТипЛинииДиаграммы.Сплошная,3);
		
	Сч = 0;
	
	Для Каждого Строка Из Объект.ТаблицаДней Цикл
		
		ТекущееЗначение = Строка.Норматив;
		Если ТекущееЗначение = 0 Тогда 
			ТекущееЗначение = 0.001;	
		КонецЕсли;
		
		Диаграмма.Точки.Добавить(Формат(Строка.Дата,"ДФ=dd.MM"));
		Диаграмма.УстановитьЗначение(Сч,Серия1,ТекущееЗначение);
		
		Если ДеньНедели(Строка.Дата) = 7 Тогда
			
			Диаграмма.УстановитьЗначение(Сч,Серия2,ТекущееЗначение);
			
		КонецЕсли;
		
		Если (НЕ(Элементы.ТаблицаДней.ТекущиеДанные = Неопределено)) И Строка.Дата = Элементы.ТаблицаДней.ТекущиеДанные.Дата Тогда
			
			Диаграмма.УстановитьЗначение(Сч,Серия3,ТекущееЗначение);			
			
		КонецЕсли;

		Сч = Сч + 1;
	
	КонецЦикла;
	
	Диаграмма.Обновление = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДнейНормативПриИзменении(Элемент)
	ОбновитьДиаграмму();
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДнейПриАктивизацииСтроки(Элемент)
	ОбновитьДиаграмму();
КонецПроцедуры

&НаКлиенте
Функция ПередИзменениемДанных()
	
	Знч = Истина;
	
	Если ЗначениеЗаполнено(Объект.Подразделение) И ЗначениеЗаполнено(Объект.Дата) Тогда
	
		Для Каждого Строка Из Объект.ТаблицаДней Цикл
			Если Строка.Норматив > 0 ИЛИ Строка.КоличествоКоробов > 0 Тогда
				
				 Результат = Вопрос("Текущие данные будут удалены. Продолжить?", РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
			     Если Результат = КодВозвратаДиалога.Нет Тогда
				     Знч = Ложь;
			     КонецЕсли;
				
				 Прервать;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	 
	Возврат Знч;
	
КонецФункции

&НаКлиенте
Процедура ПериодОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Объект.Подразделение) Тогда
		СтандартнаяОбработка = ПроверитьДублированиеДокумента(НачалоМесяца(ВыбранноеЗначение), Объект.Подразделение, Объект.Ссылка);
	КонецЕсли;	
		
	Если НЕ СтандартнаяОбработка Тогда
		
		Предупреждение("Документ уже существует. Выберите другой период или подразделение.");
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПроверитьДублированиеДокумента(Дата, Подразделение, Документ)
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("Дата", Дата);
	Запрос.Параметры.Вставить("Подразделение", Подразделение);
	Запрос.Параметры.Вставить("Документ", Документ);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПлановыйЛимит.Ссылка
	|ИЗ
	|	Документ.ПлановыйЛимит КАК ПлановыйЛимит
	|ГДЕ
	|	ПлановыйЛимит.Дата = &Дата
	|	И ПлановыйЛимит.Подразделение = &Подразделение
	|	И ПлановыйЛимит.Ссылка <> &Документ
	|	И НЕ ПлановыйЛимит.ПометкаУдаления";
	
	Результат = Запрос.Выполнить().Выбрать();
	
	Знч = Истина;
	
	Если Результат.Количество() > 0 Тогда
		
		Знч = Ложь;
		
	КонецЕсли;
	
	Возврат Знч;
	
КонецФункции

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	
	ЗаполнитьТаблицуДней();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Объект.Дата) Тогда
		СтандартнаяОбработка = ПроверитьДублированиеДокумента(НачалоМесяца(Объект.Дата), ВыбранноеЗначение, Объект.Ссылка);
	КонецЕсли;
	
	Если НЕ СтандартнаяОбработка Тогда
		
		Предупреждение("Документ уже существует. Выберите другой период или подразделение.");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеПриИзменении(Элемент)
	
	ЗаполнитьТаблицуДней();
		
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДнейПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДнейНачалоПеретаскивания(Элемент, ПараметрыПеретаскивания, Выполнение)
	Выполнение = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДнейПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	
	Если ЗначениеЗаполнено(Объект.Дата) И ЗначениеЗаполнено(Объект.Подразделение) Тогда
	
		ОткрытьФорму("Документ.ПлановыйЛимит.Форма.ФормаЗаполнитьДанными", Новый Структура, ЭтаФорма, Неопределено, Неопределено, Неопределено, Неопределено, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	Иначе
		
		Предупреждение("Заполните Подразделение и Период");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДанными(КоличествоКоробов, Норматив) Экспорт
	
	Для Каждого Строка Из Объект.ТаблицаДней Цикл
		Если Строка.Норматив > 0 ИЛИ Строка.КоличествоКоробов > 0 Тогда
			
			 Результат = Вопрос("Текущие данные будут удалены. Продолжить?", РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
			 Если Результат = КодВозвратаДиалога.Нет Тогда
				  Возврат;
			 КонецЕсли;
			
			 Прервать;
		КонецЕсли;
	КонецЦикла;
		
	Для Каждого Строка Из Объект.ТаблицаДней Цикл
		
		Если НЕ (ДеньНедели(Строка.Дата) = 7) Тогда
			
			Если ДеньНедели(Строка.Дата) = 6 Тогда
				
				Строка.Норматив = Окр(Норматив/2);
				Строка.КоличествоКоробов = Окр(КоличествоКоробов/2);
				
			Иначе
				
				Строка.Норматив = Норматив;
				Строка.КоличествоКоробов = КоличествоКоробов;
				
			КонецЕсли;
			
		Иначе
			
			Строка.Норматив = 0;
			Строка.КоличествоКоробов = 0;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ОбновитьДиаграмму();
		
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = ПередИзменениемДанных();
	Если НЕ СтандартнаяОбработка Тогда
		Возврат;	
	КонецЕсли;
	
	Мас = Новый Массив();
	Мас.Добавить(Новый ПараметрВыбора("Отбор.Активность", Истина));
	Мас.Добавить(Новый ПараметрВыбора("Отбор.ВидПодразделения", ПредопределенноеЗначение("Перечисление.ВидыПодразделений.Производство")));
	Элемент.ПараметрыВыбора = Новый ФиксированныйМассив(Мас);
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = ПередИзменениемДанных();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если НЕ ЗначениеЗаполнено(Объект.Номер) Тогда
		 Объект.Дата = Дата("00000000000000");	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	Если ЗначениеЗаполнено(Объект.Дата) И ЗначениеЗаполнено(Объект.Подразделение) Тогда
		Отказ = НЕ ПроверитьДублированиеДокумента(НачалоМесяца(Объект.Дата), Объект.Подразделение, Объект.Ссылка);
	Иначе
		Предупреждение("Заполните Подразделение и Период");
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры
