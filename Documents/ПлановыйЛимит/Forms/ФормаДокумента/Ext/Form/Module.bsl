&НаКлиенте
Процедура ЗаполнитьТаблицуДней()
		
    Объект.ТаблицаДней.Очистить();
	
	КоличествоДней = День(КонецМесяца(Объект.Период));
	ТекущаяДатаСтроки = НачалоМесяца(Объект.Период);
	
	Сч = 1;
	
	Пока Сч <= КоличествоДней Цикл
		
		НоваяСтрока = Объект.ТаблицаДней.Добавить();
		НоваяСтрока.Дата = ТекущаяДатаСтроки;
		НоваяСтрока.Норматив = 0;
		
		Если ДеньНедели(ТекущаяДатаСтроки) = 7 Тогда
			НоваяСтрока.Выходной = Истина;	
		КонецЕсли;
		
		ТекущаяДатаСтроки = ТекущаяДатаСтроки + 60*60*24;
		Сч = Сч + 1;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДиаграмму()
	
	Диаграмма.Очистить();
	Диаграмма.ТипДиаграммы=ТипДиаграммы.График;
	Диаграмма.РежимСглаживания=РежимСглаживанияДиаграммы.ГладкаяКривая;
	Диаграмма.НатяжениеСглаживания=70;
	Диаграмма.РежимПолупрозрачности=РежимПолупрозрачностиДиаграммы.НеИспользовать;
	
	Диаграмма.ПропускатьБазовоеЗначение = Истина;
	Диаграмма.БазовоеЗначение = 0;
	
	Серия1 = Диаграмма.Серии.Добавить("Норматив");
	Серия1.Цвет = WebЦвета.ЗеленыйЛес;
	Серия1.Маркер = ТипМаркераДиаграммы.Круг;
	
	Серия2 = Диаграмма.Серии.Добавить("Выходные");
	Серия2.Цвет = WebЦвета.Черный;
	Серия2.Маркер = ТипМаркераДиаграммы.Круг;
	Серия2.Линия = Новый Линия(ТипЛинииДиаграммы.Сплошная,3);
	
	Серия3 = Диаграмма.Серии.Добавить("");
	Серия3.Цвет = WebЦвета.Красный;
	Серия3.Маркер = ТипМаркераДиаграммы.Круг;
	Серия3.Линия = Новый Линия(ТипЛинииДиаграммы.Сплошная,3);
		
	Сч = 0;
	
	Для Каждого Строка Из Объект.ТаблицаДней Цикл
		
		ТекущееЗначение = Строка.Норматив;
		Если ТекущееЗначение = 0 Тогда 
			ТекущееЗначение = 0.001;	
		КонецЕсли;
		
		Диаграмма.Точки.Добавить(Формат(Строка.Дата,"ДФ=dd.MM"));
		Диаграмма.УстановитьЗначение(Сч,Серия1,ТекущееЗначение);
		
		Если ДеньНедели(Строка.Дата) = 7 Тогда
			
			Диаграмма.УстановитьЗначение(Сч,Серия2,ТекущееЗначение);
			
		КонецЕсли;
		
		Если (НЕ(Элементы.ТаблицаДней.ТекущиеДанные = Неопределено)) И Строка.Дата = Элементы.ТаблицаДней.ТекущиеДанные.Дата Тогда
			
			Диаграмма.УстановитьЗначение(Сч,Серия3,ТекущееЗначение);			
			
		КонецЕсли;

		Сч = Сч + 1;
	
	КонецЦикла;
	
	Диаграмма.Обновление = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДнейНормативПриИзменении(Элемент)
	ОбновитьДиаграмму();
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДнейПриАктивизацииСтроки(Элемент)
	ОбновитьДиаграмму();
КонецПроцедуры

&НаКлиенте
Функция ПередИзменениемДанных()
	
	Знч = Истина;
	
	Если Объект.ТаблицаДней.Количество() > 0 Тогда
		
		 Результат = Вопрос("Текущие данные будут удалены. Продолжить?", РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
		 Если Результат = КодВозвратаДиалога.Нет Тогда
			   Знч = Ложь;
		 КонецЕсли;		 
			 
	 КонецЕсли;
	 
	 Возврат Знч;
	
КонецФункции

&НаКлиенте
Процедура ПериодОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
		
	СтандартнаяОбработка = ПередИзменениемДанных();

КонецПроцедуры

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	
	ЗаполнитьТаблицуДней();
	ОбновитьДиаграмму();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = ПередИзменениемДанных();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеПриИзменении(Элемент)
	
	ЗаполнитьТаблицуДней();
	ОбновитьДиаграмму();
		
КонецПроцедуры
