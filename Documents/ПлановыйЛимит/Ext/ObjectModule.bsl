
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.ЦеховойЛимит.Записывать = Истина;
	
	Для Каждого День Из ТаблицаДней Цикл
		
		Запись = Движения.ЦеховойЛимит.Добавить();
		Запись.Подразделение = Подразделение;
		Запись.Период = НачалоДня(День.Дата);
		Запись.КоличествоКоробовПлан = День.КоличествоКоробов;
		Запись.КоличествоДеталейПлан = День.КоличествоДеталей;
		Запись.КоличествоДоставокПлан = День.КоличествоДоставок;
		Запись.СтоимостьУслугПлан = День.Норматив;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДатыЗапретаИзменения.ПроверитьДатуЗапретаИзмененияПередЗаписьюДокумента(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	Дата = КонецМесяца(Дата);
	Отказ = НЕ ПроверитьЗаполнение();
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("НачалоМесяца", НачалоМесяца(Дата));
	Запрос.Параметры.Вставить("КонецМесяца", КонецМесяца(Дата));
	Запрос.Параметры.Вставить("Подразделение", Подразделение);
	Запрос.Параметры.Вставить("Документ", Ссылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПлановыйЛимит.Ссылка
	|ИЗ
	|	Документ.ПлановыйЛимит КАК ПлановыйЛимит
	|ГДЕ
	|	ПлановыйЛимит.Дата МЕЖДУ &НачалоМесяца И &КонецМесяца
	|	И ПлановыйЛимит.Подразделение = &Подразделение
	|	И ПлановыйЛимит.Ссылка <> &Документ";
	
	Результат = Запрос.Выполнить();
	
	Если НЕ Результат.Пустой() Тогда
		Отказ = Истина;
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ТекстСообщения = "Для этого периода уже существует документ " + Выборка.Ссылка;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры
