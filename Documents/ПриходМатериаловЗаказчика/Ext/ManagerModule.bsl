
Функция ПолучитьПриходМатериаловЗаказчика(СсылкаСпецификация) Экспорт
	
	МассивДокументов = ЛексСервер.НайтиПодчиненныеДокументы(СсылкаСпецификация, "Документ.ПриходМатериаловЗаказчика", "Спецификация");
	Если МассивДокументов.Количество() = 1 Тогда
		Возврат МассивДокументов[0];
	ИначеЕсли МассивДокументов.Количество() = 0 Тогда
		Возврат Документы.Договор.ПустаяСсылка();
	Иначе
		ВызватьИсключение "Ошибка 795: Нарушена связь ""Спецификации"" и документа ""Приход материалов заказчика""";
	КонецЕсли;
	
КонецФункции

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	ЛексСервер.ПолучитьПредставлениеДокумента(Данные, Представление, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура ЗаполнитьМатериалыЗаказчика(Таблица, Спецификация) Экспорт
	
	Таблица.Очистить();
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Спецификация", Спецификация);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СпецификацияСписокНоменклатуры.Номенклатура,
	|	СУММА(СпецификацияСписокНоменклатуры.Количество) КАК Количество,
	|   Истина КАК Предоставлен
	|ИЗ
	|	Документ.Спецификация.СписокНоменклатуры КАК СпецификацияСписокНоменклатуры
	|ГДЕ
	|	СпецификацияСписокНоменклатуры.ПредоставитЗаказчик
	|	И СпецификацияСписокНоменклатуры.Ссылка = &Спецификация
	|
	|СГРУППИРОВАТЬ ПО
	|	СпецификацияСписокНоменклатуры.Номенклатура";
	
	РезультатЗапроса = Запрос.Выполнить();
	Таблица.Загрузить(РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // ЗаполнитьМатериалыЗаказчика()

Процедура Печать(Ссылка, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПриходМатериаловЗаказчика") Тогда
		ПодготовитьПечатнуюФорму("ПриходМатериаловЗаказчика", "Приход материалов заказчика", "Документ.ПриходМатериаловЗаказчика.МатериалЗаказчика",
		Ссылка, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);	
	КонецЕсли;                                                                            
	                                                                 
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;                                            
	
КонецПроцедуры

Процедура ПодготовитьПечатнуюФорму(Знач ИмяМакета, ПредставлениеМакета, ПолныйПутьКМакету = "", Ссылка, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода)
			
	Если ИмяМакета = "ПриходМатериаловЗаказчика" Тогда
		
		ТабДок = ПечатьПриходМатериаловЗаказчика(Ссылка, ОбъектыПечати);
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, ИмяМакета,
		ПредставлениеМакета, ТабДок,, ПолныйПутьКМакету);
		
	КонецЕсли;
		
КонецПроцедуры

Функция ПечатьПриходМатериаловЗаказчика(МассивДокументов, ОбъектыПечати) Экспорт
	
	Макет = Документы.ПриходМатериаловЗаказчика.ПолучитьМакет("МатериалЗаказчика");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьСписокНоменклатурыШапка = Макет.ПолучитьОбласть("ШапкаНоменклатуры");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.ОтображатьЗаголовки = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Список.Ссылка.Спецификация.ДатаИзготовления КАК ДатаИзготовления,
	|	Список.Ссылка.Номер КАК ПриходНомер,
	|	Список.Ссылка.Спецификация.Номер КАК СпецификацияНомер,
	|	Список.Ссылка.Спецификация.Контрагент КАК Контрагент,
	|	Список.Номенклатура КАК Номенклатура,
	|	Список.Количество КАК Количество,
	|	Список.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	МатЗаказчика.Комментарий КАК Комментарий
	|ИЗ
	|	Документ.ПриходМатериаловЗаказчика.Материалы КАК Список
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Спецификация.СписокМатериаловЗаказчика КАК МатЗаказчика
	|		ПО Список.Номенклатура = МатЗаказчика.Номенклатура
	|			И Список.Ссылка.Спецификация = МатЗаказчика.Ссылка
	|ГДЕ
	|	Список.Ссылка В(&МассивДокументов)
	|ИТОГИ
	|	МАКСИМУМ(ДатаИзготовления),
	|	МАКСИМУМ(СпецификацияНомер),
	|	МАКСИМУМ(Контрагент)
	|ПО
	|	ПриходНомер";
	
	Запрос.Параметры.Вставить("МассивДокументов", МассивДокументов);
	
	ВставлятьРазделительСтраниц = Ложь;
	
	Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1;
	
	Пока Выборка.Следующий() Цикл
		
		Если ВставлятьРазделительСтраниц Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();			
		КонецЕсли;
		
		ОбластьШапка.Параметры.ПриходНомер = Формат(Число(Выборка.ПриходНомер), "ЧГ=");
		ОбластьШапка.Параметры.СпецификацияНомер = Формат(Число(Выборка.СпецификацияНомер), "ЧГ=");
		ОбластьШапка.Параметры.ДатаИзготовления = Формат(Выборка.ДатаИзготовления, "ДЛФ=DD");
		ТабДок.Вывести(ОбластьШапка, Выборка.Уровень());
		
		ТабДок.Вывести(ОбластьСписокНоменклатурыШапка);

		НомерСтроки = 0;
		
		ВыборкаНоменклатура = Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам); 
		
		Пока ВыборкаНоменклатура.Следующий() Цикл
		
			НомерСтроки = НомерСтроки + 1;

			ОбластьСтрока.Параметры.Заполнить(ВыборкаНоменклатура);
			ОбластьСтрока.Параметры.НомерСтроки = НомерСтроки;
			ТабДок.Вывести(ОбластьСтрока);
			
		КонецЦикла;
		
		ОбластьПодвал.Параметры.Заполнить(Выборка);
		ТабДок.Вывести(ОбластьПодвал);
		
		ВставлятьРазделительСтраниц = Истина;
	
	КонецЦикла;
	
	УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, НомерСтрокиНачало, ОбъектыПечати, МассивДокументов);
	
	Возврат ТабДок;
	
КонецФункции
