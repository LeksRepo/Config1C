
Функция ПолучитьПриходМатериаловЗаказчика(СсылкаСпецификация) Экспорт
	
	МассивДокументов = ЛексСервер.НайтиПодчиненныеДокументы(СсылкаСпецификация, "Документ.ПриходМатериаловЗаказчика", "Спецификация");
	Если МассивДокументов.Количество() = 1 Тогда
		Возврат МассивДокументов[0];
	ИначеЕсли МассивДокументов.Количество() = 0 Тогда
		Возврат Документы.Договор.ПустаяСсылка();
	Иначе
		ВызватьИсключение "Ошибка 795: Нарушена связь ""Спецификации"" и документа ""Приход материалов заказчика""";
	КонецЕсли;
	
КонецФункции

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	ЛексСервер.ПолучитьПредставлениеДокумента(Данные, Представление, СтандартнаяОбработка);
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "МатериалЗаказчика") Тогда
		
		ПодготовитьПечатнуюФорму("МатериалЗаказчика", "ПечатьМатериалЗаказчика", "Документ.ПриходМатериаловЗаказчика.ПечатьМатериалЗаказчика",
		МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
		
	КонецЕсли;
	
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
КонецПроцедуры

Процедура ПодготовитьПечатнуюФорму(Знач ИмяМакета, ПредставлениеМакета, ПолныйПутьКМакету = "", МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода)
	
	Если ИмяМакета = "МатериалЗаказчика" Тогда
		
		ТабДок = ПечатьМатериалЗаказчика(МассивОбъектов, ОбъектыПечати);
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, ИмяМакета, 
		ПредставлениеМакета, ТабДок, , ПолныйПутьКМакету);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПечатьМатериалЗаказчика(МассивДокументов, ОбъектыПечати) Экспорт
	
	Макет = Документы.ПриходМатериаловЗаказчика.ПолучитьМакет("МатериалЗаказчика");
	//ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьСписокНоменклатурыШапка = Макет.ПолучитьОбласть("ШапкаНоменклатуры");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал"); 
	
	ТабДок = Новый ТабличныйДокумент;
	//ТабДок.ИмяПараметровПечати = "ПараметрыПечати_РеализацияМатериалов";
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Истина;
	ТабДок.ТолькоПросмотр = Истина;
	ТабДок.ОтображатьЗаголовки = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПриходМатериаловЗаказчика.Автор,
	|	ПриходМатериаловЗаказчика.Дата,
	|	ПриходМатериаловЗаказчика.Номер,
	|	ПриходМатериаловЗаказчика.Подразделение,
	|	ПриходМатериаловЗаказчика.Подразделение.Организация КАК Организация,
	|	ПриходМатериаловЗаказчика.Материалы.(
	|		НомерСтроки,
	|		Количество,
	|		Комментарий,
	|		Номенклатура,
	|		Предоставлен,
	|		Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения
	|	),
	|	ПриходМатериаловЗаказчика.Кладовщик,
	|	ПриходМатериаловЗаказчика.Спецификация.Контрагент КАК Контрагент,
	|	ПриходМатериаловЗаказчика.Спецификация.Номер,
	|	ПриходМатериаловЗаказчика.Спецификация.ДатаИзготовления КАК ДатаИзготовления
	|ИЗ
	|	Документ.ПриходМатериаловЗаказчика КАК ПриходМатериаловЗаказчика
	|ГДЕ
	|	ПриходМатериаловЗаказчика.Ссылка В(&МассивДокументов)";
	Запрос.Параметры.Вставить("МассивДокументов", МассивДокументов);
	
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ВставлятьРазделительСтраниц = Ложь;
	
	Пока Выборка.Следующий() Цикл
		
		НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1;
		
		Если ВставлятьРазделительСтраниц Тогда
			
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
			
		КонецЕсли; 
		
		ОбластьШапка.Параметры.Заполнить(Выборка);
		ОбластьШапка.Параметры.ДатаИзготовления = Формат(Выборка.ДатаИзготовления, "ДЛФ=DD");
		ТабДок.Вывести(ОбластьШапка, Выборка.Уровень());
		
		ТабДок.Вывести(ОбластьСписокНоменклатурыШапка);
		ВыборкаСписокНоменклатуры = Выборка.Материалы.Выбрать();
		
		Пока ВыборкаСписокНоменклатуры.Следующий() Цикл
			
			ОбластьСтрока.Параметры.Заполнить(ВыборкаСписокНоменклатуры);
			ТабДок.Вывести(ОбластьСтрока, ВыборкаСписокНоменклатуры.Уровень());
			
		КонецЦикла;
		
		ОбластьПодвал.Параметры.Заполнить(Выборка);
		ТабДок.Вывести(ОбластьПодвал); 
		
		ВставлятьРазделительСтраниц = Истина;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, НомерСтрокиНачало, ОбъектыПечати, МассивДокументов);
		
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции
