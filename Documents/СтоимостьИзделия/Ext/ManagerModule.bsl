Функция ПолучитьСуммуДокумента (Ссылка, ДатаДоговора) Экспорт
	
	Если (ДатаДоговора - Ссылка.Дата)/86400 < 30 Тогда
		Возврат Ссылка.СуммаДокумента;
	КонецЕсли;
	
КонецФункции

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм,
	ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ЭскизнаяЗаявка") Тогда
		ПодготовитьПечатнуюФорму("ЭскизнаяЗаявка", "Эскизная заявка", "Документ.СтоимостьИзделия.ЭскизнаяЗаявка",
		МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ИнструкцияПоСборкеШкаф") Тогда
		ПодготовитьПечатнуюФорму("ИнструкцияПоСборкеШкаф", "Инструкция По Сборке Шкаф", "Документ.СтоимостьИзделия.ИнструкцияПоСборкеШкаф",
		МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	КонецЕсли;
	
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
КонецПроцедуры

Процедура ПодготовитьПечатнуюФорму(Знач ИмяМакета, ПредставлениеМакета, ПолныйПутьКМакету = "", МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода)
	
	НужноПечататьМакет = УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, ИмяМакета);
	Если НужноПечататьМакет Тогда
		Если ИмяМакета = "ЭскизнаяЗаявка" Тогда
			ТабДок = ПечатьЭскизнойЗаявки(МассивОбъектов, ОбъектыПечати); 
		Иначе
			ТабДок = ПечатьИнструкцииПоСборке(МассивОбъектов, ОбъектыПечати);
		КонецЕсли;
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, ИмяМакета,
		ПредставлениеМакета, ТабДок,, ПолныйПутьКМакету);
	КонецЕсли;
	
КонецПроцедуры

Функция ПечатьЭскизнойЗаявки(МассивОбъектов, ОбъектыПечати)
	
	МассивРасширений = Новый Массив;
	МассивРасширений.Добавить("png");
	МассивРасширений.Добавить("bmp");
	МассивРасширений.Добавить("gif");
	МассивРасширений.Добавить("jpeg");
	МассивРасширений.Добавить("jpg");
	
	// Создаем табличный документ и устанавливаем имя параметров печати.
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.ИмяПараметровПечати = "ПараметрыПечати_ЭскизнаяЗаявка";
	ТабДок.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабДок.Автомасштаб = Истина;
	ТабДок.ТолькоПросмотр = Истина;
	
	// Получаем запросом необходимые данные.
	Запрос = Новый Запрос();
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СтоимостьИзделия.Дата КАК Дата,
	|	СтоимостьИзделия.Изделие КАК Изделие,
	|	СтоимостьИзделия.Комментарий КАК Комментарий,
	|	СтоимостьИзделия.Номер КАК Номер,
	|	СтоимостьИзделия.Номенклатура.(
	|		Номенклатура,
	|		Деталь
	|	),
	|	ХранимыеФайлыВерсий.ХранимыйФайл,
	|	СтоимостьИзделия.Двери.(
	|		Дверь.Профиль КАК Профиль
	|	),
	|	СтоимостьИзделия.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.СтоимостьИзделия КАК СтоимостьИзделия
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ХранимыеФайлыВерсий КАК ХранимыеФайлыВерсий
	|		ПО СтоимостьИзделия.Ссылка = ХранимыеФайлыВерсий.ВерсияФайла.Владелец.ВладелецФайла
	|ГДЕ
	|	СтоимостьИзделия.Ссылка В(&МассивОбъектов)
	|	И (СтоимостьИзделия.Номенклатура.Деталь = ЗНАЧЕНИЕ(Перечисление.ВидыДеталей.ЛДСПосн)
	|			ИЛИ СтоимостьИзделия.Номенклатура.Деталь = ЗНАЧЕНИЕ(Перечисление.ВидыДеталей.ЛДСПдоп)
	|			ИЛИ СтоимостьИзделия.Номенклатура.Деталь = ЗНАЧЕНИЕ(Перечисление.ВидыДеталей.Кант)
	|			ИЛИ СтоимостьИзделия.Номенклатура.Деталь = ЗНАЧЕНИЕ(Перечисление.ВидыДеталей.Ручка)
	|			ИЛИ СтоимостьИзделия.Номенклатура.Деталь = ЗНАЧЕНИЕ(Перечисление.ВидыДеталей.Направляющие)
	|			ИЛИ СтоимостьИзделия.Номенклатура.Деталь = ЗНАЧЕНИЕ(Перечисление.ВидыДеталей.Софиты)
	|			ИЛИ СтоимостьИзделия.Номенклатура.Деталь = ЗНАЧЕНИЕ(Перечисление.ВидыДеталей.Корзина))
	|	И ХранимыеФайлыВерсий.ВерсияФайла = ХранимыеФайлыВерсий.ВерсияФайла.Владелец.ТекущаяВерсия
	|	И НЕ ХранимыеФайлыВерсий.ВерсияФайла.Владелец.ПометкаУдаления
	|	И ХранимыеФайлыВерсий.ВерсияФайла.Владелец.ТекущаяВерсияРасширение В(&МассивРасширений)
	|ИТОГИ ПО
	|	Ссылка";
	
	Если ТипЗнч(МассивОбъектов[0]) = Тип("ДокументСсылка.Договор") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "СтоимостьИзделия.Ссылка КАК", "СтоимостьИзделия.Договор.Ссылка КАК");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "СтоимостьИзделия.Ссылка В", "СтоимостьИзделия.Договор.Ссылка В");
	КонецЕсли;	
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Параметры.Вставить("МассивРасширений", МассивРасширений);
	
	ВыборкаИтог = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Макет = Документы.СтоимостьИзделия.ПолучитьМакет("ЭскизнаяЗаявка");
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	Шапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьНоменклатураШапка = Макет.ПолучитьОбласть("НоменклатураШапка");
	ОбластьНоменклатура = Макет.ПолучитьОбласть("Номенклатура");
	ОбластьКартинка = Макет.ПолучитьОбласть("Картинка");
	Подвал = Макет.ПолучитьОбласть("Подвал");
	Согласование = Макет.ПолучитьОбласть("Согласование");
	ОбластьРамка = Макет.ПолучитьОбласть("Рамка");
	
	ТабДок.Очистить();
	
	ВставлятьРазделительСтраниц = Ложь;
	
	Пока ВыборкаИтог.Следующий() Цикл
		Выборка = ВыборкаИтог.Выбрать();
		
		Если ВставлятьРазделительСтраниц Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1;
		
		
		Пока Выборка.Следующий() Цикл
			Если ВставлятьРазделительСтраниц Тогда
				ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			
			ОбластьЗаголовок.Параметры.Заполнить(Выборка);
			ТабДок.Вывести(ОбластьЗаголовок);
			
			Шапка.Параметры.Заполнить(Выборка);
			ТабДок.Вывести(Шапка, Выборка.Уровень());
			
			ТабДок.Вывести(ОбластьНоменклатураШапка);
			
			ВыборкаНоменклатура = Выборка.Номенклатура.Выбрать();
			Пока ВыборкаНоменклатура.Следующий() Цикл
				Если ЗначениеЗаполнено(ВыборкаНоменклатура.Номенклатура) Тогда
					ОбластьНоменклатура.Параметры.Заполнить(ВыборкаНоменклатура);
					ТабДок.Вывести(ОбластьНоменклатура, ВыборкаНоменклатура.Уровень());
				КонецЕсли;
			КонецЦикла;
			
			ВыборкаДвери = Выборка.Двери.Выбрать();
			ВыборкаДвери.Следующий();
			Если ЗначениеЗаполнено(ВыборкаДвери.Профиль) Тогда
				ОбластьРамка.Параметры.Заполнить(ВыборкаДвери);
				ТабДок.Вывести(ОбластьРамка, ВыборкаДвери.Уровень());
			КонецЕсли;
			ВыборкаДвери.Сбросить();
			
			Подвал.Параметры.Заполнить(Выборка);
			ТабДок.Вывести(Подвал);
			
			ТабДок.Вывести(Согласование);
			
			ДанныеФайла = Выборка.ХранимыйФайл.Получить();
			Р = ТабДок.Области.НоменклатураШапка.Верх;
			С =	ТабДок.Области.Согласование.Низ;
			МояКартинка = Новый Картинка(ДанныеФайла);
			Рис = ТабДок.Рисунки.Добавить(ТипРисункаТабличногоДокумента.Картинка);
			Индекс = ТабДок.Рисунки.Индекс(Рис);
			ТабДок.Рисунки[Индекс].Картинка = МояКартинка;
			//Картинка растянута от шапки номенклатуры до согласования
			ТабДок.Рисунки[Индекс].Расположить(ТабДок.Область("R"+Р+"C5:R"+С+"C17"));
			
			ВставлятьРазделительСтраниц = Истина;
		КонецЦикла;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, 
		НомерСтрокиНачало, ОбъектыПечати, ВыборкаИтог.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабДок;
КонецФункции

Функция ПечатьИнструкцииПоСборке(МассивОбъектов, ОбъектыПечати) Экспорт
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.ИмяПараметровПечати = "ПараметрыПечати_ИнструкцияПоСборкеШкаф";
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СтоимостьИзделия.Изделие,
	|	СтоимостьИзделия.Изделие.ИмяМакетаПаспорта КАК ИмяМакета,
	|	СтоимостьИзделия.Ссылка
	|ИЗ
	|	Документ.СтоимостьИзделия КАК СтоимостьИзделия
	|ГДЕ
	|	СтоимостьИзделия.Ссылка В(&Ссылка)";
	Запрос.Параметры.Вставить("Ссылка", МассивОбъектов);
	Выборка = Запрос.Выполнить().Выбрать();
	
	ТабДок.Очистить();
	
	ВставлятьРазделительСтраниц = Ложь;
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Выборка.ИмяМакета) Тогда		
			Если ВставлятьРазделительСтраниц Тогда
				ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;		
			
			НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1;
			
			Макет = Документы.СтоимостьИзделия.ПолучитьМакет(Выборка.ИмяМакета);
			ОбластьТело = Макет.ПолучитьОбласть("Тело");
			ТабДок.Вывести(ОбластьТело);
			
			ВставлятьРазделительСтраниц = Истина;
			
			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, 
			НомерСтрокиНачало, ОбъектыПечати, Выборка.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции