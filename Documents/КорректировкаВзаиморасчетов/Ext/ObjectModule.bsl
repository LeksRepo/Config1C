
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДатыЗапретаИзменения.ПроверитьДатуЗапретаИзмененияПередЗаписьюДокумента(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Запись = Движения.Управленческий.Добавить();
	Запись.Подразделение = Подразделение;
	Запись.Период = Дата;
	Запись.Сумма = СуммаДокумента;
	
	Если ВидКорректировки = Перечисления.ВидКорректировкиВзаиморасчетов.НачислитьНаСчетДилера Тогда
		
		Если НЕ ЗначениеЗаполнено(Статья) Тогда
			Статья = Справочники.СтатьиДоходовРасходов.ПрочиеХозяйственныеРасходы; 
		КонецЕсли;
		
		Запись.СчетДт = ПланыСчетов.Управленческий.Расходы;
		Запись.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.СтатьиДР] = Статья;
		
		Запись.СчетКт = ПланыСчетов.Управленческий.ВзаиморасчетыСПокупателями;
		Запись.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Контрагенты] = Контрагент;
		
	ИначеЕсли ВидКорректировки = Перечисления.ВидКорректировкиВзаиморасчетов.СписатьСоСчетаДилера Тогда
		
		Если НЕ ЗначениеЗаполнено(Статья) Тогда
			Статья = Справочники.СтатьиДоходовРасходов.ДоходыПрочие; 
		КонецЕсли;
		
		Запись.СчетКт = ПланыСчетов.Управленческий.Доходы;
		Запись.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.СтатьиДР] = Статья;
		
		Запись.СчетДт = ПланыСчетов.Управленческий.ВзаиморасчетыСПокупателями;
		Запись.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Контрагенты] = Контрагент;
		
	КонецЕсли;
	
КонецПроцедуры
