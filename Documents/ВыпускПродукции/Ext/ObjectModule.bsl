Перем
СчетОсновноеПроизводство,
СчетГотоваяПродукция,
СчетДополнительныйЛимитЦеха,
ПВХНоменклатура,
ПВХСпецификация;

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	МассивСпецификаций = СписокСпецификаций.ВыгрузитьКолонку("Спецификация");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("МассивСпецификаций", МассивСпецификаций);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВыпускПродукцииСписокСпецификаций.Спецификация
	|ИЗ
	|	Документ.ВыпускПродукции.СписокСпецификаций КАК ВыпускПродукцииСписокСпецификаций
	|ГДЕ
	|	НЕ ВыпускПродукцииСписокСпецификаций.Спецификация В (&МассивСпецификаций)
	|	И ВыпускПродукцииСписокСпецификаций.Ссылка = &Ссылка";
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Документы.Спецификация.ПолучитьСтатусСпецификации(Выборка.Спецификация) = Перечисления.СтатусыСпецификации.Изготовлен Тогда
			Документы.Спецификация.УстановитьСтатусСпецификации(Выборка.Спецификация, Перечисления.СтатусыСпецификации.ПереданВЦех);
		КонецЕсли;
	КонецЦикла;
	
	// проверка повторного ввода спецификаций
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивСпецификаций", МассивСпецификаций);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВыпускПродукцииСписокСпецификаций.Ссылка
	|ИЗ
	|	Документ.ВыпускПродукции.СписокСпецификаций КАК ВыпускПродукцииСписокСпецификаций
	|ГДЕ
	|	ВыпускПродукцииСписокСпецификаций.Спецификация В(&МассивСпецификаций)
	|	И ВыпускПродукцииСписокСпецификаций.Ссылка <> &Ссылка";
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Отказ = Истина;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Уже есть Выпуск продукции к некоторым спецификациям", ЭтотОбъект, "Номер");
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ИнициализироватьПеременные();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВыпускПродукцииСписокСпецификаций.Спецификация КАК Спецификация
	|ПОМЕСТИТЬ СписокСпецификаций
	|ИЗ
	|	Документ.ВыпускПродукции.СписокСпецификаций КАК ВыпускПродукцииСписокСпецификаций
	|ГДЕ
	|	ВыпускПродукцииСписокСпецификаций.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СпецификацияСписокНоменклатуры.Ссылка КАК Спецификация,
	|	СпецификацияСписокНоменклатуры.Номенклатура КАК Номенклатура,
	|	СпецификацияСписокНоменклатуры.Количество
	|ИЗ
	|	Документ.Спецификация.СписокНоменклатуры КАК СпецификацияСписокНоменклатуры
	|ГДЕ
	|	СпецификацияСписокНоменклатуры.Ссылка В
	|			(ВЫБРАТЬ
	|				СписокСпецификаций.Спецификация
	|			ИЗ
	|				СписокСпецификаций)
	|	И СпецификацияСписокНоменклатуры.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Материал)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура,
	|	Спецификация";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Проводка = Движения.Управленческий.Добавить();
		Проводка.Период = Дата;
		Проводка.Подразделение = Подразделение;
		Проводка.Сумма = 0;
		Проводка.СчетДт = СчетГотоваяПродукция;
		Проводка.СубконтоДт[ПВХСпецификация] = Выборка.Спецификация;
		
		Проводка.СчетКт = СчетОсновноеПроизводство;
		Проводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
		Проводка.КоличествоКт = Выборка.Количество;
		
		Проводка = Движения.Управленческий.Добавить();
		Проводка.Период = Дата;
		Проводка.Подразделение = Подразделение;
		Проводка.СчетКт = СчетДополнительныйЛимитЦеха;
		Проводка.СубконтоКт[ПВХНоменклатура] = Выборка.Номенклатура;
		Проводка.КоличествоКт = Выборка.Количество;
		
	КонецЦикла;
	
	// оприходовать количество готовой продукции
	Для Каждого Строка Из СписокСпецификаций Цикл
		
		// начислить заработную плату цеховым ???
		Проводка = Движения.Управленческий.Добавить();
		Проводка.Период = Дата;
		Проводка.Подразделение = Подразделение;
		Проводка.Сумма = ОбщегоНазначения.ПолучитьЗначениеРеквизита(Строка.Спецификация, "СуммаНарядаСпецификации");
		Проводка.СчетДт = ПланыСчетов.Управленческий.ЗарплатаЦеха;
		Проводка.СубконтоДт[ПВХСпецификация] = Строка.Спецификация;
		Проводка.КоличествоДт = 1;
		
		// забалансовый счет СкладГотовойПродукции
		Проводка = Движения.Управленческий.Добавить();
		Проводка.Период = Дата;
		Проводка.Подразделение = Подразделение;
		Проводка.Сумма = ОбщегоНазначения.ПолучитьЗначениеРеквизита(Строка.Спецификация, "ВнутренняяСтоимостьСпецификации");
		Проводка.СчетДт = ПланыСчетов.Управленческий.СкладГотовойПродукции;
		Проводка.СубконтоДт[ПВХСпецификация] = Строка.Спецификация;
		Проводка.КоличествоДт = 1;
		
		// Изменение статуса спецификации
		Если Документы.Спецификация.ПолучитьСтатусСпецификации(Строка.Спецификация) = Перечисления.СтатусыСпецификации.ПереданВЦех Тогда
			Документы.Спецификация.УстановитьСтатусСпецификации(Строка.Спецификация, Перечисления.СтатусыСпецификации.Изготовлен);
		КонецЕсли;
		
	КонецЦикла;
	
	Движения.Управленческий.Записывать = Истина;
	
КонецПроцедуры

Функция ИнициализироватьПеременные()
	
	СчетОсновноеПроизводство = ПланыСчетов.Управленческий.ОсновноеПроизводство;
	СчетГотоваяПродукция = ПланыСчетов.Управленческий.ГотоваяПродукция;
	ПВХНоменклатура = ПланыВидовХарактеристик.ВидыСубконто.Номенклатура;
	ПВХСпецификация = ПланыВидовХарактеристик.ВидыСубконто.СпецификацияДоговор;
	СчетДополнительныйЛимитЦеха = ПланыСчетов.Управленческий.ДополнительныйЛимитЦеха;
	
КонецФункции

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.Спецификация") Тогда
		
		Подразделение 	= ДанныеЗаполнения.Производство;
		СписокСпецификаций.Загрузить(ЛексСервер.ПолучитьСпецификацииПоСтатусу(Подразделение, Перечисления.СтатусыСпецификации.ПереданВЦех));
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	// Изменение статуса спецификации
	Для Каждого Элемент Из СписокСпецификаций Цикл
		
		Если Документы.Спецификация.ПолучитьСтатусСпецификации(Элемент.Спецификация) = Перечисления.СтатусыСпецификации.Изготовлен Тогда
			
			Документы.Спецификация.УстановитьСтатусСпецификации(Элемент.Спецификация, Перечисления.СтатусыСпецификации.ПереданВЦех);
			
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Функция ПроведениеПропорциональное()
	
	//пропорционально списать материалы и оприходовать изделие на склад готовой продукции
	// Дт Готовая продукция Кт Основное производство
	
	МассивСпецификаций = СписокСпецификаций.ВыгрузитьКолонку("Спецификация");
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", МассивСпецификаций);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СпецификацияСписокНоменклатуры.Номенклатура,
	|	СУММА(СпецификацияСписокНоменклатуры.Количество) КАК Количество,
	|	СпецификацияСписокНоменклатуры.Ссылка КАК Спецификация
	|ПОМЕСТИТЬ втНоменклатура
	|ИЗ
	|	Документ.Спецификация.СписокНоменклатуры КАК СпецификацияСписокНоменклатуры
	|ГДЕ
	|	СпецификацияСписокНоменклатуры.Ссылка В(&Ссылка)
	|	И СпецификацияСписокНоменклатуры.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Материал)
	|	И НЕ СпецификацияСписокНоменклатуры.ЧерезСклад
	|
	|СГРУППИРОВАТЬ ПО
	|	СпецификацияСписокНоменклатуры.Номенклатура,
	|	СпецификацияСписокНоменклатуры.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втНоменклатура.Номенклатура
	|ИЗ
	|	втНоменклатура КАК втНоменклатура";
	
	//	блокировка
	
	ТЗНоменклатура = Запрос.Выполнить().Выгрузить();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;	
	ЭлементБлокировки.УстановитьЗначение("Подразделение", Подразделение);
	ЭлементБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.ОсновноеПроизводство);
	ЭлементБлокировки.ИсточникДанных = ТЗНоменклатура;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура, "Номенклатура");
	Блокировка.Заблокировать();
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	втНоменклатура.Номенклатура КАК Номенклатура,
	|	втНоменклатура.Количество КАК Количество,
	|	ЕСТЬNULL(УправленческийОстатки.КоличествоОстатокДт, 0) КАК КоличествоОстаток,
	|	втНоменклатура.Спецификация,
	|	ЕСТЬNULL(УправленческийОстатки.СуммаОстатокДт, 0) КАК СуммаОстаток
	|ИЗ
	|	втНоменклатура КАК втНоменклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
	|				&МоментВремени,
	|				Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ОсновноеПроизводство),
	|				ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконто.Номенклатура),
	|				Подразделение = &Подразделение
	|					И Субконто1 В
	|						(ВЫБРАТЬ
	|							втНоменклатура.Номенклатура
	|						ИЗ
	|							втНоменклатура)) КАК УправленческийОстатки
	|		ПО втНоменклатура.Номенклатура = УправленческийОстатки.Субконто1
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура
	|ИТОГИ
	|	СУММА(Количество),
	|	МАКСИМУМ(КоличествоОстаток)
	|ПО
	|	Номенклатура";
	
	Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.КоличествоОстаток < Выборка.Количество Тогда
			УменьшитьДополнительныйЛимит = Выборка.КоличествоОстаток;
			СписываемПропорционально = Истина;
		Иначе
			СписываемПропорционально = Ложь;
			УменьшитьДополнительныйЛимит = Выборка.Количество;
			// здесь можно накопить экономию
		КонецЕсли;
		
		Проводка = Движения.Управленческий.Добавить();
		Проводка.Период = Дата;
		Проводка.Подразделение = Подразделение;
		Проводка.СчетКт = СчетДополнительныйЛимитЦеха;
		Проводка.СубконтоКт[ПВХНоменклатура] = Выборка.Номенклатура;
		Проводка.КоличествоКт = УменьшитьДополнительныйЛимит;
		
		ВыборкаПоСпецификациям = Выборка.Выбрать();
		
		Пока ВыборкаПоСпецификациям.Следующий() Цикл
			
			Если ВыборкаПоСпецификациям.КоличествоОстаток > 0 Тогда
				
				Если СписываемПропорционально Тогда
					КоличествоСписываемое = ВыборкаПоСпецификациям.КоличествоОстаток / Выборка.Количество * ВыборкаПоСпецификациям.Количество;
				Иначе
					КоличествоСписываемое = ВыборкаПоСпецификациям.Количество;
				КонецЕсли;
				
				СуммаСписания = КоличествоСписываемое / ВыборкаПоСпецификациям.КоличествоОстаток * ВыборкаПоСпецификациям.СуммаОстаток;
				
				Проводка = Движения.Управленческий.Добавить();
				Проводка.Период = Дата;
				Проводка.Подразделение = Подразделение;
				Проводка.СчетДт = СчетГотоваяПродукция;
				Проводка.СубконтоДт[ПВХСпецификация] = ВыборкаПоСпецификациям.Спецификация;
				Проводка.СчетКт = СчетОсновноеПроизводство;
				Проводка.СубконтоКт[ПВХНоменклатура] = ВыборкаПоСпецификациям.Номенклатура;
				Проводка.КоличествоКт = КоличествоСписываемое;
				Проводка.Сумма = СуммаСписания;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецФункции
