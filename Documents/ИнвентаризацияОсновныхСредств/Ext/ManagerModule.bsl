Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка) Экспорт
	
	ЛексСервер.ПолучитьПредставлениеДокумента(Данные, Представление, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура ПечатьИнвентаризацияОС(ТабДок, Ссылка) Экспорт
		
	Макет = Документы.ИнвентаризацияОсновныхСредств.ПолучитьМакет("МакетИнв1");
	
	ОбластьРасписка = Макет.ПолучитьОбласть("Расписка");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьРасписка = Макет.ПолучитьОбласть("Расписка");
	ОбластьПримечание = Макет.ПолучитьОбласть("Примечание");
	ОбластьПодпись = Макет.ПолучитьОбласть("Подпись");
	ОбластьПолеДаты = Макет.ПолучитьОбласть("ПолеДаты");
	ОбластьЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ОбластьСтрокаТаблицы = Макет.ПолучитьОбласть("СтрокаТаблицы");
	ОбластьИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	ОбластьИтогоПоОписи = Макет.ПолучитьОбласть("ИтогоПоОписи");
	ОбластьИтоги = Макет.ПолучитьОбласть("Итоги");
	ОбластьПодсчеты = Макет.ПолучитьОбласть("Подсчеты");
	ОбластьПретензии = Макет.ПолучитьОбласть("Претензии");
		
	ТабДок.Очистить();
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ПолеСлева = 5;
	ТабДок.ПолеСправа = 1;
	ТабДок.ПолеСверху = 5;
	ТабДок.ПолеСнизу = 5;
	
	ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
	
	ПараметрыШапка = Новый Структура;
	ПараметрыШапка.Вставить("Подразделение",Ссылка.Подразделение);
	ПараметрыШапка.Вставить("НомерДокумента",Ссылка.Номер);
	ПараметрыШапка.Вставить("ДатаДокумента",Формат(Ссылка.Дата,"ДФ=dd.MM.yyyy"));

	ОбластьШапка.Параметры.Заполнить(ПараметрыШапка);
	ТабДок.Вывести(ОбластьШапка);
	
	ТабДок.Вывести(ОбластьЗаголовокТаблицы);
	
	Список = Ссылка.Список;
	НомерСтроки = 1;
	КоличествоВсего = 0;
	СуммаВсего = 0;
	
	Для Каждого Строка Из Список Цикл
		
		ПараметрыСтрокаТаблицы = Новый Структура;
		ПараметрыСтрокаТаблицы.Вставить("НомерСтроки",НомерСтроки);
		ПараметрыСтрокаТаблицы.Вставить("ОсновноеСредствоНаименованиеПолное",Строка.ОсновноеСредство);
		ПараметрыСтрокаТаблицы.Вставить("ИнвентарныйНомер",Строка.ИнвентарныйНомер);
		ПараметрыСтрокаТаблицы.Вставить("НомерПаспорта",Строка.ОсновноеСредство.НомерПаспорта);
		ПараметрыСтрокаТаблицы.Вставить("ДатаВыпуска",Строка.ОсновноеСредство.ДатаВыпуска);
		ПараметрыСтрокаТаблицы.Вставить("ЗаводскойНомер",Строка.ОсновноеСредство.ЗаводскойНомер);
		ПараметрыСтрокаТаблицы.Вставить("СтоимостьФактическая",Строка.Стоимость);
		ПараметрыСтрокаТаблицы.Вставить("СтоимостьПоДаннымУчета",Строка.Стоимость);
		ПараметрыСтрокаТаблицы.Вставить("НаличиеФактическое",1);
		ПараметрыСтрокаТаблицы.Вставить("НаличиеПоДаннымУчета",1);
		
		ОбластьСтрокаТаблицы.Параметры.Заполнить(ПараметрыСтрокаТаблицы);
		
		Пока НЕ ТабДок.ПроверитьВывод(ОбластьСтрокаТаблицы) Цикл
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЦикла;
		
		ТабДок.Вывести(ОбластьСтрокаТаблицы);

		НомерСтроки = НомерСтроки + 1;
		КоличествоВсего = КоличествоВсего + 1;
		СуммаВсего = СуммаВсего + Строка.Стоимость;
		
	КонецЦикла;
	
	 ПараметрыИтогоПоСтранице = Новый Структура;
	 ПараметрыИтогоПоСтранице.Вставить("ИтогНаличиеФактическое",КоличествоВсего);
	 ПараметрыИтогоПоСтранице.Вставить("ИтогНаличиеПоДаннымУчета",КоличествоВсего);
	 ПараметрыИтогоПоСтранице.Вставить("ИтогСтоимостьФактическая",СуммаВсего);
	 ПараметрыИтогоПоСтранице.Вставить("ИтогСтоимостьПоДаннымУчета",СуммаВсего);

	 ОбластьИтогоПоСтранице.Параметры.Заполнить(ПараметрыИтогоПоСтранице);
	 ТабДок.Вывести(ОбластьИтогоПоСтранице);
	 ТабДок.Вывести(ОбластьИтогоПоОписи);

	 ПараметрыОбластьИтоги = Новый Структура;
	 ПараметрыОбластьИтоги.Вставить("КоличествоПорядковыхНомеровПрописью",ЧислоПрописью(КоличествоВсего,"Л=ru_RU; ДП=Ложь",",,,,,,,,0"));
	 ПараметрыОбластьИтоги.Вставить("КоличествоФактическоеПрописью",ЧислоПрописью(КоличествоВсего,"Л=ru_RU; ДП=Ложь",",,,,,,,,0"));
	 ПараметрыОбластьИтоги.Вставить("СтоимостьФактическаяПрописью",ЧислоПрописью(СуммаВсего,"Л=ru_RU; ДП=Ложь","рубль, рубля, рублей, м, копейка, копейки, копеек, ж"));

	 ОбластьИтоги.Параметры.Заполнить(ПараметрыОбластьИтоги);
	 
	 
	 ДляПроверкиВывода = Новый Массив;
	 ДляПроверкиВывода.Добавить(ОбластьИтоги); 
	 ДляПроверкиВывода.Добавить(ОбластьРасписка);
	 ДляПроверкиВывода.Добавить(ОбластьПодпись);
	 
	 Пока НЕ ТабДок.ПроверитьВывод(ДляПроверкиВывода) Цикл
		ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
	 КонецЦикла; 
	 
	 ТабДок.Вывести(ОбластьИтоги);
	 ТабДок.Вывести(ОбластьРасписка); 
	 ТабДок.Вывести(ОбластьПодпись);
		
КонецПроцедуры