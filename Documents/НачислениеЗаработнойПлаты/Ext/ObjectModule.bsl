
Процедура ОбработкаПроведения(Отказ, Режим)
	
	ЗатратныйСчет = ПланыСчетов.Управленческий.Расходы;
	
	Для Каждого ТекСтрокаСписокФизлиц Из СписокФизлиц Цикл
		
		Движение = Движения.Управленческий.Добавить();
		Движение.Период = Дата;
		Движение.Подразделение = Подразделение;
		Движение.Сумма = ТекСтрокаСписокФизлиц.Сумма;
		Движение.Содержание = ТекСтрокаСписокФизлиц.Комментарий;
		
		Движение.СчетДт = ЗатратныйСчет;
		Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.СтатьиДР] = ТекСтрокаСписокФизлиц.Статья;
		Движение.СчетКт = ПланыСчетов.Управленческий.ВзаиморасчетыССотрудниками;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.ФизическиеЛица] = ТекСтрокаСписокФизлиц.ФизЛицо;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.ВидыНачисленийУдержаний] = ТекСтрокаСписокФизлиц.ВидыНачисленийУдержаний;
		
	КонецЦикла;
	
	// Для цеховых кредитуем забалансовый счет по зарплате
	Если ВариантРасчета = Перечисления.ВидыНачисленийЗаработнойПлаты.Цеховые Тогда
		Движение = Движения.Управленческий.Добавить();
		
		Движение.Период = Дата;
		Движение.Подразделение = Подразделение;
		Движение.Сумма = СуммаДокумента;
		Движение.СчетКт = ПланыСчетов.Управленческий.ЗарплатаЦеха;
		
	КонецЕсли;
	
	Движения.Управленческий.Записывать = Истина;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДатыЗапретаИзменения.ПроверитьДатуЗапретаИзмененияПередЗаписьюДокумента(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	СуммаДокумента = СписокФизлиц.Итог("Сумма");
	
КонецПроцедуры
