
Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка) Экспорт
	
	ЛексСервер.ПолучитьПредставлениеДокумента(Данные, Представление, СтандартнаяОбработка);
	
КонецПроцедуры

Функция ПечатьПеремещениеДенежныхСредств(МассивДокументов, ОбъектыПечати) Экспорт
	
	//ФормСтрока 		= "Л = ru_RU; ДП = Истина";
	//ПарПредмета 		= "рубль, рубля, рублей, м, копейка, копейки, копеек, ж, 2";
	
	ТабДок 										= Новый ТабличныйДокумент;
	ТабДок.ИмяПараметровПечати 	= "ПараметрыПечати_ПеремещениеДенежныхСредств";
	ТабДок.АвтоМасштаб 					= Истина;
	ТабДок.ОриентацияСтраницы 		= ОриентацияСтраницы.Ландшафт;
	ТабДок.ОтображатьСетку				= Ложь;
	ТабДок.Защита 							= Истина;
	ТабДок.ТолькоПросмотр 				= Истина;
	ТабДок.ОтображатьЗаголовки 		= Ложь;
	
	Макет 				= Документы.ПеремещениеДенежныхСредств.ПолучитьМакет("ПеремещениеДенежныхСредств");
	Запрос 				= Новый Запрос;
	Запрос.Текст 		=
	"ВЫБРАТЬ
	|	ПеремещениеДенежныхСредств.Номер КАК НомерДокумента,
	|	ПеремещениеДенежныхСредств.Дата КАК ДатаДокумента,
	|	ПеремещениеДенежныхСредств.Автор,
	|	ПеремещениеДенежныхСредств.Подразделение КАК ПодразделениеОтправитель,
	|	ПеремещениеДенежныхСредств.ПодразделениеПолучатель,
	|	ПеремещениеДенежныхСредств.СтатьяДДС КАК СтатьяДДСОтправитель,
	|	ПеремещениеДенежныхСредств.СтатьяДДСПолучателя КАК СтатьяДДСПолучатель,
	|	ПеремещениеДенежныхСредств.Сумма КАК СуммаДокумента,
	|	ПеремещениеДенежныхСредств.Счет КАК СчетОтправитель,
	|	ПеремещениеДенежныхСредств.СчетПолучателя КАК СчетПолучатель,
	|	ПеремещениеДенежныхСредств.Комментарий
	|ИЗ
	|	Документ.ПеремещениеДенежныхСредств КАК ПеремещениеДенежныхСредств
	|ГДЕ
	|	ПеремещениеДенежныхСредств.Ссылка В(&МассивДокументов)";
	Запрос.Параметры.Вставить("МассивДокументов", МассивДокументов);
	Выборка = Запрос.Выполнить().Выбрать();
	
	ОбластьЗаголовок 		= Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапка 			= Макет.ПолучитьОбласть("Шапка");
	ОбластьТело 				= Макет.ПолучитьОбласть("Тело");
	областьПустаяСтрока 	= Макет.ПолучитьОбласть("ПустаяСтрока");
	//ОбластьПодвал 	= Макет.ПолучитьОбласть("Подвал"); 
	
	ВставлятьРазделительСтраниц = Ложь;
	
	Пока Выборка.Следующий() Цикл
		
		НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1;
		
		Если ВставлятьРазделительСтраниц Тогда
			
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
			
		КонецЕсли; 
		
		ТабДок.Вывести(ОбластьЗаголовок);
		
		ОбластьШапка.Параметры.Заполнить(Выборка);
		ОбластьШапка.Параметры.ДатаДокумента = Формат(Выборка.ДатаДокумента, "ДЛФ=DD");
		ТабДок.Вывести(ОбластьШапка);
		ТабДок.Вывести(областьПустаяСтрока);
		ОбластьТело.Параметры.Заполнить(Выборка);
		ТабДок.Вывести(ОбластьТело);
		
		//ОбластьПодвал.Параметры.Заполнить(Выборка);
		//ОбластьПодвал.Параметры.СуммаДокументаПрописью = ЧислоПрописью(Выборка.СуммаДокумента, ФормСтрока, ПарПредмета);
		//ТабДок.Вывести(ОбластьПодвал); 
		
		ВставлятьРазделительСтраниц = Истина;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, НомерСтрокиНачало, ОбъектыПечати, МассивДокументов);
		
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции

Функция ПечатьКассовыйОрдер(МассивДокументов, ОбъектыПечати, ИмяМакета) Экспорт
	
	ТабДок 										= Новый ТабличныйДокумент;
	ТабДок.ИмяПараметровПечати 	= "ПараметрыПечати_РасходныйКассовыйОрдер";
	ТабДок.АвтоМасштаб 					= Истина;
	ТабДок.ОтображатьСетку 			= Ложь;
	ТабДок.Защита 							= Ложь;
	ТабДок.ТолькоПросмотр 				= Истина;
	ТабДок.ОтображатьЗаголовки 		= Ложь;
	ФормСтрока 								= "Л = ru_RU; ДП = Истина";
	ПарПредмета 								= "рубль, рубля, рублей, м, копейка, копейки, копеек, ж, 2";
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПеремещениеДенежныхСредств.Номер КАК НомерДокумента,
	|	ПеремещениеДенежныхСредств.Дата КАК ДатаДокумента,
	|	ПеремещениеДенежныхСредств.Подразделение КАК ПодразделениеОтправитель,
	|	ПеремещениеДенежныхСредств.ПодразделениеПолучатель,
	|	ПеремещениеДенежныхСредств.Сумма КАК Сумма,
	|	ПеремещениеДенежныхСредств.Комментарий,
	|	ПеремещениеДенежныхСредств.Подразделение.Организация КАК ОрганизацияОтправитель,
	|	ПеремещениеДенежныхСредств.ПодразделениеПолучатель.Организация КАК ОрганизацияПолучатель,
	|	ПеремещениеДенежныхСредств.Ссылка КАК Основание,
	|	ПеремещениеДенежныхСредств.Подразделение.Руководитель КАК ФИОРуководителя,
	|	ПеремещениеДенежныхСредств.ПодразделениеПолучатель.Руководитель КАК ФИОРуководителяПодразделения
	|ИЗ
	|	Документ.ПеремещениеДенежныхСредств КАК ПеремещениеДенежныхСредств
	|ГДЕ
	|	ПеремещениеДенежныхСредств.Ссылка В(&МассивДокументов)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если ИмяМакета = "РасходныйКассовыйОрдер" Тогда
		
		Макет 	= Документы.РасходДенежныхСредств.ПолучитьМакет("РасходныйКассовыйОрдер");
		Шапка 	= Макет.ПолучитьОбласть("Шапка");
		
		ТабДок.Очистить();
		
		ВставлятьРазделительСтраниц = Ложь;
		
		Пока Выборка.Следующий() Цикл
			
			Если ВставлятьРазделительСтраниц Тогда
				
				ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
				
			КонецЕсли;
			
			НомерСтрокиНачало 					= ТабДок.ВысотаТаблицы + 1;
			СуммаПрописью 							= ЧислоПрописью(Выборка.Сумма, ФормСтрока, ПарПредмета);
			ДатаПрописью 							= ЛексКлиентСервер.ДатаПрописью(Выборка.ДатаДокумента);
			ПредставлениеПодразделения 	= Выборка.ПодразделениеОтправитель;
			ПредставлениеОрганизации 		= Выборка.ОрганизацияОтправитель;
			
			Шапка.Параметры.Заполнить(Выборка);
			Шапка.Параметры.СуммаПрописью 						= СуммаПрописью;
			Шапка.Параметры.ПредставлениеОрганизации 		= ПредставлениеОрганизации;
			Шапка.Параметры.ПредставлениеПодразделения 	= ПредставлениеПодразделения;
			Шапка.Параметры.ДатаДокументаПрописью 			= ДатаПрописью;
			Шапка.Параметры.ФИОГлавногоБухгалтера 			= Константы.ГлавныйБухгалтер.Получить();
			Шапка.Параметры.Подразделение 						= ПредставлениеПодразделения;
			
			ТабДок.Вывести(Шапка);
			
			ВставлятьРазделительСтраниц = Истина;
			
			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, НомерСтрокиНачало, ОбъектыПечати, Выборка.Основание);
			
		КонецЦикла;
		
	Иначе
		
		Макет 	= Документы.ПриходДенежныхСредств.ПолучитьМакет("ПриходныйКассовыйОрдер");
		Шапка 	= Макет.ПолучитьОбласть("Шапка");
		
		ТабДок.Очистить();
		
		ВставлятьРазделительСтраниц = Ложь;
		
		Пока Выборка.Следующий() Цикл
			
			Если ВставлятьРазделительСтраниц Тогда
				
				ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
				
			КонецЕсли;
			
			НомерСтрокиНачало 					= ТабДок.ВысотаТаблицы + 1;
			СуммаПрописью 							= ЧислоПрописью(Выборка.Сумма, ФормСтрока, ПарПредмета);
			ДатаПрописью 							= ЛексКлиентСервер.ДатаПрописью(Выборка.ДатаДокумента);
			ПредставлениеПодразделения 	= Выборка.ПодразделениеПолучатель;
			ПредставлениеОрганизации 		= Выборка.ОрганизацияПолучатель;
			
			Шапка.Параметры.Заполнить(Выборка);
			Шапка.Параметры.СуммаПрописью 						= СуммаПрописью;
			Шапка.Параметры.ПредставлениеОрганизации 		= ПредставлениеОрганизации;
			Шапка.Параметры.ПредставлениеПодразделения 	= ПредставлениеПодразделения;
			Шапка.Параметры.ФИОГлавногоБухгалтера 			= Константы.ГлавныйБухгалтер.Получить();
			Шапка.Параметры.Подразделение 						= ПредставлениеПодразделения;
			ТабДок.Вывести(Шапка);
			
			ВставлятьРазделительСтраниц = Истина;
			
			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, НомерСтрокиНачало, ОбъектыПечати, Выборка.Основание);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ТабДок;
	
КонецФункции

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПриходныйКассовыйОрдер") Тогда
		
		ПодготовитьПечатнуюФорму("ПриходныйКассовыйОрдер", "Приходный кассовый ордер", "Документ.ПриходДенежныхСредств.ПриходныйКассовыйОрдер",
		МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
		
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "РасходныйКассовыйордер") Тогда
		
		ПодготовитьПечатнуюФорму("РасходныйКассовыйОрдер", "Расходный кассовый ордер", "Документ.РасходДенежныхСредств.РасходныйКассовыйОрдер",
		МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
		
	КонецЕсли;
	
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
КонецПроцедуры

Процедура ПодготовитьПечатнуюФорму(Знач ИмяМакета, ПредставлениеМакета, ПолныйПутьКМакету = "", МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода)
	
	Если ИмяМакета = "ПриходныйКассовыйОрдер" Тогда
		
		ТабДок = ПечатьКассовыйОрдер(МассивОбъектов, ОбъектыПечати, ИмяМакета); 
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, ИмяМакета, 
		ПредставлениеМакета, ТабДок, , ПолныйПутьКМакету);
		
	ИначеЕсли ИмяМакета = "РасходныйКассовыйОрдер" Тогда
		
		ТабДок = ПечатьКассовыйОрдер(МассивОбъектов, ОбъектыПечати, ИмяМакета);
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, ИмяМакета, 
		ПредставлениеМакета, ТабДок, , ПолныйПутьКМакету);
		
	КонецЕсли;
	
КонецПроцедуры
