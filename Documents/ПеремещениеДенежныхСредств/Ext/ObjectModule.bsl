
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	СчетДт = ?(ТипЗнч(СчетПолучателя) = Тип("СправочникСсылка.Кассы"), ПланыСчетов.Управленческий.Касса, ПланыСчетов.Управленческий.РасчетныйСчет);
	СчетКт = ?(ТипЗнч(Счет) = Тип("СправочникСсылка.Кассы"), ПланыСчетов.Управленческий.Касса, ПланыСчетов.Управленческий.РасчетныйСчет);
	
	Если Подразделение <> ПодразделениеПолучатель Тогда
		СформироватьДвижениеДенегВРазныхПодразделенях(СчетДт, СчетКт);
	Иначе
		СформироватьДвижениеДенегВОдномПодразделении(СчетДт, СчетКт);
	КонецЕсли;
	
	Движения.Управленческий.Записывать = Истина;
	
КонецПроцедуры

Функция СформироватьДвижениеДенегВОдномПодразделении(СчетДт, СчетКт)
	
	Проводки = СоздатьПроводку(Подразделение);
	
	Проводки.СчетКт = СчетКт;
	Проводки.СубконтоКт[СчетКт.ВидыСубконто[1].ВидСубконто] = Счет;
	Проводки.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.СтатьиДДС] = СтатьяДДС;
	
	Проводки.СчетДт = СчетДт;
	Проводки.СубконтоДт[СчетДт.ВидыСубконто[1].ВидСубконто] = СчетПолучателя;
	Проводки.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.СтатьиДДС] = СтатьяДДСПолучателя;
	
КонецФункции

Функция СформироватьДвижениеДенегВРазныхПодразделенях(СчетДт, СчетКт);
	
	Проводки = СоздатьПроводку(Подразделение);
	
	Проводки.СчетКт = СчетКт;
	Проводки.СубконтоКт[СчетКт.ВидыСубконто[1].ВидСубконто] = Счет;
	Проводки.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.СтатьиДДС] = СтатьяДДС;
	
	Проводки.СчетДт = ПланыСчетов.Управленческий.ВзаиморасчетыСПодразделениями;
	Проводки.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Подразделения] = ПодразделениеПолучатель;
	
	Проводки = СоздатьПроводку(ПодразделениеПолучатель);
	
	Проводки.СчетДт = СчетДт;
	Проводки.СубконтоДт[СчетДт.ВидыСубконто[1].ВидСубконто] = СчетПолучателя;
	Проводки.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.СтатьиДДС] = СтатьяДДСПолучателя;
	
	Проводки.СчетКт = ПланыСчетов.Управленческий.ВзаиморасчетыСПодразделениями;
	Проводки.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Подразделения] = Подразделение;
	
КонецФункции

Функция СоздатьПроводку(фнПодразделение)
	
	Проводки = Движения.Управленческий.Добавить();
	Проводки.Подразделение = фнПодразделение;
	Проводки.Период = Дата;
	Проводки.Сумма = СуммаДокумента;
	
	Возврат Проводки;
	
КонецФункции

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;
	
	ДатыЗапретаИзменения.ПроверитьДатуЗапретаИзмененияПередЗаписьюДокумента(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Перем Ошибки;
	
	Если Подразделение = ПодразделениеПолучатель Тогда
		
		Если СтатьяДДС <> Справочники.СтатьиДвиженияДенежныхСредств.ПеремещениеДенежныхСредств Тогда
			ТекстСообщения = "При перемещении денежных средств в одном подразделении статья оплаты должна быть 'Перемещение денежных средств'";
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "Объект.СтатьяДДС", ТекстСообщения);
		КонецЕсли;
		
		Если СтатьяДДСПолучателя <> Справочники.СтатьиДвиженияДенежныхСредств.ПеремещениеДенежныхСредств Тогда
			ТекстСообщения = "При перемещении денежных средств в одном подразделении статья поступления должна быть 'Перемещение денежных средств'";
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "Объект.СтатьяДДСПолучателя", ТекстСообщения);
		КонецЕсли;
		
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки, Отказ);
	
КонецПроцедуры
