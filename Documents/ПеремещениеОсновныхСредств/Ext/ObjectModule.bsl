
Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.ОсновныеСредства.Очистить();
	Движения.ОсновныеСредства.Записать();
	
	Ошибки = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПеремещениеОСОсновныеСредства.НомерСтроки,
	|	ПеремещениеОСОсновныеСредства.ОС,
	|	ПеремещениеОСОсновныеСредства.МОЛОтправитель,
	|	ПеремещениеОСОсновныеСредства.МОЛПолучатель
	|ПОМЕСТИТЬ тчДок
	|ИЗ
	|	Документ.ПеремещениеОсновныхСредств.ОсновныеСредства КАК ПеремещениеОСОсновныеСредства
	|ГДЕ
	|	ПеремещениеОСОсновныеСредства.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	тчДок.НомерСтроки,
	|	тчДок.ОС,
	|	тчДок.МОЛОтправитель,
	|	тчДок.МОЛПолучатель,
	|	ЕСТЬNULL(СостояниеОССрезПоследних.МОЛ, ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)) КАК МОЛОстаток,
	|	СостояниеОССрезПоследних.НачислятьАмортизацию,
	|	СостояниеОССрезПоследних.СрокАмортизации,
	|	СостояниеОССрезПоследних.Стоимость,
	|	СостояниеОССрезПоследних.Регистратор,
	|	СостояниеОССрезПоследних.ПринятоКУчету
	|ИЗ
	|	тчДок КАК тчДок
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеСредства.СрезПоследних(
	|				&МоментВремени,
	|				ОсновноеСредство В
	|						(ВЫБРАТЬ
	|							т.ОС
	|						ИЗ
	|							тчДок КАК т)
	|					И ПринятоКУчету) КАК СостояниеОССрезПоследних
	|		ПО тчДок.ОС = СостояниеОССрезПоследних.ОсновноеСредство";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.МОЛОтправитель <> Выборка.МОЛОстаток Тогда
				
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("У МОЛа  %1 нет в подотчете %2",Выборка.МОЛОтправитель, Выборка.ОС);
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "Объект.ОсновныеСредства[%1].ОС", ТекстОшибки,,Выборка.НомерСтроки-1);
		
		КонецЕсли;
		
		Проводки = Движения.ОсновныеСредства.Добавить();
		Проводки.Период = Дата;
		Проводки.ОсновноеСредство = Выборка.ОС;
		Проводки.МОЛ = Выборка.МОЛПолучатель;
		Проводки.НачислятьАмортизацию = Выборка.НачислятьАмортизацию;
		Проводки.СрокАмортизации = Выборка.СрокАмортизации;
		Проводки.Стоимость = Выборка.Стоимость;
		Проводки.ПринятоКУчету = Выборка.ПринятоКУчету;

	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки, Отказ);

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	ДатыЗапретаИзменения.ПроверитьДатуЗапретаИзмененияПередЗаписьюДокумента(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
КонецПроцедуры
