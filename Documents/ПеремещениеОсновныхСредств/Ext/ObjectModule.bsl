
Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.СостояниеОсновныхСредств.Очистить();
	Движения.СостояниеОсновныхСредств.Записать();
	
	Ошибки = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПеремещениеОСОсновныеСредства.НомерСтроки,
	|	ПеремещениеОСОсновныеСредства.ОсновноеСредство КАК ОсновноеСредство
	|ПОМЕСТИТЬ тчДок
	|ИЗ
	|	Документ.ПеремещениеОсновныхСредств.ОсновныеСредства КАК ПеремещениеОСОсновныеСредства
	|ГДЕ
	|	ПеремещениеОСОсновныеСредства.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	тчДок.НомерСтроки,
	|	тчДок.ОсновноеСредство,
	|	ЕСТЬNULL(СостояниеОССрезПоследних.МОЛ, ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)) КАК МОЛОстаток,
	|	СостояниеОССрезПоследних.НачислятьАмортизацию,
	|	СостояниеОССрезПоследних.СрокАмортизации,
	|	СостояниеОССрезПоследних.ПервоначальнаяСтоимость,
	|	СостояниеОССрезПоследних.ПринятоКУчету
	|ИЗ
	|	тчДок КАК тчДок
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостояниеОсновныхСредств.СрезПоследних(
	|				&МоментВремени,
	|				ОсновноеСредство В
	|						(ВЫБРАТЬ
	|							т.ОсновноеСредство
	|						ИЗ
	|							тчДок КАК т)
	|					И ПринятоКУчету) КАК СостояниеОССрезПоследних
	|		ПО тчДок.ОсновноеСредство = СостояниеОССрезПоследних.ОсновноеСредство";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если МОЛОтправитель <> Выборка.МОЛОстаток Тогда
			
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("У МОЛа %1 нет в подотчете %2",МОЛОтправитель, Выборка.ОсновноеСредство);
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "Объект.ОсновныеСредства[%1].ОС", ТекстОшибки,,Выборка.НомерСтроки-1);
			
		КонецЕсли;
		
		Проводка = Движения.СостояниеОсновныхСредств.Добавить();
		Проводка.Период = Дата;
		Проводка.МОЛ = МОЛПолучатель;
		Проводка.ОсновноеСредство = Выборка.ОсновноеСредство;
		Проводка.НачислятьАмортизацию = Выборка.НачислятьАмортизацию;
		Проводка.СрокАмортизации = Выборка.СрокАмортизации;
		Проводка.ПринятоКУчету = Выборка.ПринятоКУчету;
		
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки, Отказ);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДатыЗапретаИзменения.ПроверитьДатуЗапретаИзмененияПередЗаписьюДокумента(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	
	МассивОС = ОсновныеСредства.ВыгрузитьКолонку("ОсновноеСредство");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОС", МассивОС);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УправленческийОстатки.Субконто1 КАК ОсновноеСредство,
	|	СУММА(УправленческийОстатки.СуммаОстатокДт) КАК Стоимость
	|ИЗ
	|	РегистрБухгалтерии.Управленческий.Остатки(
	|			&МоментВремени,
	|			Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПервоначальнаяСтоимостьОС),
	|			,
	|			Подразделение = &Подразделение
	|				И Субконто1 В (&МассивОС)) КАК УправленческийОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	УправленческийОстатки.Субконто1";
	
	тзСтоимость = Запрос.Выполнить().Выгрузить();
	
	Для каждого СтрокаОС Из ОсновныеСредства Цикл
		
		СтрокаСтоимость = тзСтоимость.Найти(СтрокаОс.ОсновноеСредство);
		
		Если СтрокаСтоимость <> Неопределено Тогда
			СтрокаОС.Стоимость = СтрокаСтоимость.Стоимость;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры
