
Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	ЛексСервер.ПолучитьПредставлениеДокумента(Данные, Представление, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "МакетОС2") Тогда
		
		ПодготовитьПечатнуюФорму("МакетОС2", "Внутреннее перемещение (ОС-2)", "Документ.ПеремещениеОсновныхСредств.МакетОС2",
		МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
		
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "МакетПеремещениеОС") Тогда
		
		ПодготовитьПечатнуюФорму("МакетПеремещениеОС", "Внутреннее перемещение ОС", "Документ.ПеремещениеОсновныхСредств.МакетПеремещениеОС",
		МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПечатьОС2(МассивОбъектов, ОбъектыПечати) Экспорт
	
	ТабДок = Новый ТабличныйДокумент;
	
	ФормСтрока = "Л = ru_RU; ДП = Истина";
	ПарПредмета="рубль, рубля, рублей, м, копейка, копейки, копеек, ж, 0";
	
	Макет = Документы.ПеремещениеОсновныхСредств.ПолучитьМакет("МакетОС2");
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("МассивОбъектов", МассивОбъектов);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПеремещениеОсновныхСредств.Ссылка,
	|	ПеремещениеОсновныхСредств.ВерсияДанных,
	|	ПеремещениеОсновныхСредств.ПометкаУдаления,
	|	ПеремещениеОсновныхСредств.Номер,
	|	ПеремещениеОсновныхСредств.Дата КАК ДатаДок,
	|	ПеремещениеОсновныхСредств.Проведен,
	|	ПеремещениеОсновныхСредств.Автор,
	|	ПеремещениеОсновныхСредств.Подразделение,
	|	ПеремещениеОсновныхСредств.Комментарий,
	|	ПеремещениеОсновныхСредств.МОЛОтправитель КАК СдатчикРасшифровкаПодписи,
	|	ПеремещениеОсновныхСредств.МОЛПолучатель КАК ПолучательРасшифровкаПодписи,
	|	ПеремещениеОсновныхСредств.МОЛОтправитель.Должность КАК СдатчикДолжность,
	|	ПеремещениеОсновныхСредств.МОЛПолучатель.Должность КАК ПолучательДолжность,
	|	ПеремещениеОсновныхСредств.Подразделение КАК ПодрСдатчик,
	|	ПеремещениеОсновныхСредств.Подразделение КАК ПодрПолучатель,
	|	ПеремещениеОсновныхСредств.СуммаДокумента,
	|	ПеремещениеОсновныхСредств.ОсновныеСредства.(
	|		Ссылка,
	|		НомерСтроки КАК НС,
	|		МОЛОтправитель,
	|		МОЛПолучатель,
	|		ОсновноеСредство КАК НаименованиеОС,
	|		Стоимость КАК СуммаПеремещения,
	|		ОсновноеСредство.ИнвентарныйНомер КАК ИнвНомер
	|	)
	|ИЗ
	|	Документ.ПеремещениеОсновныхСредств КАК ПеремещениеОсновныхСредств
	|ГДЕ
	|	ПеремещениеОсновныхСредств.Ссылка В(&МассивОбъектов)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ВставлятьРазделительСтраниц = Ложь;
	
	Пока Выборка.Следующий() Цикл
		
		НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1;
		
		Если ВставлятьРазделительСтраниц Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ОбластьШапка.Параметры.НомерДок = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(Выборка.Номер);
		
		ОбластьШапка.Параметры.Заполнить(Выборка);
		ТабДок.Вывести(ОбластьШапка);
		
		ВыборкаСтроки = Выборка.ОсновныеСредства.Выбрать();
		Пока ВыборкаСтроки.Следующий() Цикл
			ОбластьСтрока.Параметры.Заполнить(ВыборкаСтроки);
			ТабДок.Вывести(ОбластьСтрока);
		КонецЦикла;
		
		ОбластьПодвал.Параметры.Заполнить(Выборка);
		ТабДок.Вывести(ОбластьПодвал);
		
		ВставлятьРазделительСтраниц = Истина;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, НомерСтрокиНачало, ОбъектыПечати, Выборка.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции

Процедура ПодготовитьПечатнуюФорму(Знач ИмяМакета, ПредставлениеМакета, ПолныйПутьКМакету = "", МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода)
	
	ВывестиДокумент = Ложь;
	
	Если ИмяМакета = "МакетОС2" Тогда
		
		ТабДок = ПечатьОС2(МассивОбъектов, ОбъектыПечати);
		ВывестиДокумент = Истина;
		
	ИначеЕсли ИмяМакета = "МакетПеремещениеОС" Тогда
		
		ТабДок = ПечатьОС(МассивОбъектов, ОбъектыПечати);
		ВывестиДокумент = Истина;
		
	КонецЕсли;
	
	Если ВывестиДокумент Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, ИмяМакета, ПредставлениеМакета, ТабДок, , ПолныйПутьКМакету);
	КонецЕсли;
	
КонецПроцедуры

Функция ПечатьОС(МассивОбъектов, ОбъектыПечати) Экспорт
	
	ТабДок = Новый ТабличныйДокумент;
	
	ФормСтрока = "Л = ru_RU; ДП = Истина";
	ПарПредмета="рубль, рубля, рублей, м, копейка, копейки, копеек, ж, 0";
	
	Макет = Документы.ПеремещениеОсновныхСредств.ПолучитьМакет("МакетПеремещениеОС");
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("МассивОбъектов", МассивОбъектов);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПеремещениеОсновныхСредствОсновныеСредства.Ссылка КАК Ссылка,
	|	ПеремещениеОсновныхСредствОсновныеСредства.Ссылка.Дата,
	|	ПеремещениеОсновныхСредствОсновныеСредства.Ссылка.Номер,
	|	ПеремещениеОсновныхСредствОсновныеСредства.Ссылка.МОЛОтправитель,
	|	ПеремещениеОсновныхСредствОсновныеСредства.Ссылка.МОЛПолучатель КАК МОЛПолучатель,
	|	ПеремещениеОсновныхСредствОсновныеСредства.Ссылка.Подразделение,
	|	ПеремещениеОсновныхСредствОсновныеСредства.Ссылка.СуммаДокумента,
	|	ПеремещениеОсновныхСредствОсновныеСредства.НомерСтроки,
	|	ПеремещениеОсновныхСредствОсновныеСредства.ОсновноеСредство,
	|	ПеремещениеОсновныхСредствОсновныеСредства.Стоимость
	|ИЗ
	|	Документ.ПеремещениеОсновныхСредств.ОсновныеСредства КАК ПеремещениеОсновныхСредствОсновныеСредства
	|ГДЕ
	|	ПеремещениеОсновныхСредствОсновныеСредства.Ссылка В(&МассивОбъектов)
	|	И ПеремещениеОсновныхСредствОсновныеСредства.Ссылка.Проведен
	|ИТОГИ ПО
	|	Ссылка";
	
	ВыборкаДокументы = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	Шапка = Макет.ПолучитьОбласть("Шапка");
	Подвал = Макет.ПолучитьОбласть("Подвал");
	ОбластьОСШапка = Макет.ПолучитьОбласть("ОСШапка");
	ОбластьОССтрока = Макет.ПолучитьОбласть("ОССтрока");
	
	ВставлятьРазделительСтраниц = Ложь;
	
	Пока ВыборкаДокументы.Следующий() Цикл
		
		ВыборкаОС = ВыборкаДокументы.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		ОбластьЗаголовок.Параметры.НомерДокумента = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(ВыборкаДокументы.Номер);
		ОбластьЗаголовок.Параметры.ДатаДокумента = Формат(ВыборкаДокументы.Дата, "ДЛФ=DD");
		ТабДок.Вывести(ОбластьЗаголовок);
		
		Шапка.Параметры.Заполнить(ВыборкаДокументы);
		Шапка.Параметры.МОЛОтправитель = ВыборкаДокументы.МОЛОтправитель;
		Шапка.Параметры.МОЛПолучатель = ВыборкаДокументы.МОЛПолучатель;
		ТабДок.Вывести(Шапка, ВыборкаДокументы.Уровень());
		
		СуммаОС = 0;
		ТабДок.Вывести(ОбластьОСШапка);
		
		Пока ВыборкаОС.Следующий() Цикл
			
			НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1;
			
			Если ВставлятьРазделительСтраниц Тогда
				ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			
			ОбластьОССтрока.Параметры.Заполнить(ВыборкаОС);
			ТабДок.Вывести(ОбластьОССтрока);
			
			СуммаОС = СуммаОС + ВыборкаОС.Стоимость;
			
		КонецЦикла;
		
		Подвал.Параметры.Заполнить(ВыборкаДокументы);
		Подвал.Параметры.СуммаДокумента = СуммаОС;
		Подвал.Параметры.МОЛОтправитель = ВыборкаДокументы.МОЛОтправитель;
		Подвал.Параметры.МОЛПолучатель = ВыборкаДокументы.МОЛПолучатель;
		Подвал.Параметры.СуммаДокументаПрописью = ЧислоПрописью(СуммаОС, ФормСтрока, ПарПредмета);
		ТабДок.Вывести(Подвал);
		
		ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		
		ВставлятьРазделительСтраниц = Истина;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументы.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции
