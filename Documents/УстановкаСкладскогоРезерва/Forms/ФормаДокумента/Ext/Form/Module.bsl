
&НаСервере
Функция ЗаполнитьСписокНоменклатуры(ГруппаНоменклатурыСсылка)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Родитель", ГруппаНоменклатурыСсылка);
	Запрос.УстановитьПараметр("Подразделение", Объект.Подразделение);
	Запрос.УстановитьПараметр("Склад", Объект.Склад);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Номенклатура.Ссылка КАК Номенклатура,
	|	УправленческийОстатки.КоличествоОстаток КАК Количество
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
	|				,
	|				Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.РезервныйЗапасМатериалов),
	|				,
	|				Подразделение = &Подразделение
	|					И Субконто1 = &Склад) КАК УправленческийОстатки
	|		ПО Номенклатура.Ссылка = УправленческийОстатки.Субконто2
	|ГДЕ
	|	НЕ Номенклатура.ЭтоГруппа
	|	И ВЫБОР
	|			КОГДА &Родитель = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|				ТОГДА УправленческийОстатки.КоличествоОстаток ЕСТЬ NULL 
	|			ИНАЧЕ Номенклатура.Родитель В ИЕРАРХИИ (&Родитель)
	|		КОНЕЦ
	|	И Номенклатура.Базовый
	|	И НЕ Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура.Родитель,
	|	Номенклатура.Наименование";
	
	Результат = Запрос.Выполнить();
	Объект.СписокНоменклатуры.Загрузить(Результат.Выгрузить());
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьГруппой(Команда)
	
	Если Объект.Списокноменклатуры.Количество() > 0 И КодВозвратаДиалога.Нет = Вопрос("Табличная часть будет очищена. Продолжить?", РежимДиалогаВопрос.ДаНет, 0) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Группа = ОткрытьФормуМодально("Справочник.Номенклатура.ФормаВыбораГруппы");
	
	Если ЗначениеЗаполнено(Группа) Тогда
		
		Объект.СписокНоменклатуры.Очистить();
		ЗаполнитьСписокНоменклатуры(Группа);
		Объект.Комментарий = "По группе '" + Группа + "'";
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
КонецПроцедуры
