
&НаСервере
Функция ЗаполнитьСписокНоменклатуры(ГруппаНоменклатурыСсылка)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Родитель", ГруппаНоменклатурыСсылка);
	Запрос.УстановитьПараметр("Подразделение", Объект.Подразделение);
	Запрос.УстановитьПараметр("Склад", Объект.Склад);
	Запрос.УстановитьПараметр("Период", Объект.Дата);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СпрНоменклатура.Ссылка КАК Номенклатура,
	|	РезервныйЗапасМатериалов.Количество КАК Количество
	|ИЗ
	|	Справочник.Номенклатура КАК СпрНоменклатура
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РезервныйЗапасМатериалов.СрезПоследних(
	|			&Период,
	|			Подразделение = &Подразделение
	|			И Склад = &Склад) КАК РезервныйЗапасМатериалов
	|	ПО РезервныйЗапасМатериалов.Номенклатура = СпрНоменклатура.Ссылка
	|ГДЕ
	|	НЕ СпрНоменклатура.ЭтоГруппа
	|	И ВЫБОР
	|			КОГДА &Родитель = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|				ТОГДА РезервныйЗапасМатериалов.Количество ЕСТЬ NULL 
	|			ИНАЧЕ СпрНоменклатура.Родитель В ИЕРАРХИИ (&Родитель)
	|		КОНЕЦ
	|	И СпрНоменклатура.Базовый
	|	И НЕ СпрНоменклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура.Родитель,
	|	Номенклатура.Наименование";
	
	Результат = Запрос.Выполнить();
	Объект.СписокНоменклатуры.Загрузить(Результат.Выгрузить());
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьГруппой(Команда)
	
	Если Объект.Списокноменклатуры.Количество() > 0 И КодВозвратаДиалога.Нет = Вопрос("Табличная часть будет очищена. Продолжить?", РежимДиалогаВопрос.ДаНет, 0) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Группа = ОткрытьФормуМодально("Справочник.Номенклатура.ФормаВыбораГруппы");
	
	Если ЗначениеЗаполнено(Группа) Тогда
		
		Объект.СписокНоменклатуры.Очистить();
		ЗаполнитьСписокНоменклатуры(Группа);
		Объект.Комментарий = "По группе '" + Группа + "'";
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
КонецПроцедуры
