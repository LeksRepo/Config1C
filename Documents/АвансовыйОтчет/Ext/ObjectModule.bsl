
Процедура ОбработкаПроведения(Отказ, Режим)
	
	ОприходованиеБухгалтером = ВидОперации = Перечисления.ХозяйственнаяОперация.ОприходованиеБухгалтером;
	
	Если ОприходованиеБухгалтером Тогда
		СчетКт = ПланыСчетов.Управленческий.Подотчет;
	Иначе
		СчетКт = ПланыСчетов.Управленческий.ПромежуточныеВзаиморасчетыСПодотчетниками;
	КонецЕсли;
	
	Для Каждого ТекСтрокаСписокПлатежей Из СписокПлатежей Цикл
		
		Движение = Движения.Управленческий.Добавить();
		Движение.Подразделение = Подразделение;
		Движение.Период = Дата;
		Движение.Сумма = ТекСтрокаСписокПлатежей.Сумма;
		Движение.Содержание = ТекСтрокаСписокПлатежей.Комментарий;
		
		Движение.СчетДт = ТекСтрокаСписокПлатежей.СчетУчета;
		Для Каждого Элемент Из Движение.СчетДт.ВидыСубконто Цикл
			Движение.СубконтоДт[Элемент.ВидСубконто] = ТекСтрокаСписокПлатежей["Субконто" +Элемент.НомерСтроки];
		КонецЦикла;
		
		Если Движение.СчетДт.Количественный Тогда
			Движение.КоличествоДт = ТекСтрокаСписокПлатежей.Количество;
		КонецЕсли;
		
		Движение.СчетКт = СчетКт;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.ФизическиеЛица] = ФизЛицо;
		Если ОприходованиеБухгалтером Тогда
			Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.СтатьиДДС] = ТекСтрокаСписокПлатежей.СтатьяДДС;
		КонецЕсли;
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СУММА(АвансовыйОтчетСписокНоменклатуры.Сумма) КАК Сумма,
		|	МАКСИМУМ(АвансовыйОтчетСписокНоменклатуры.Комментарий) КАК Комментарий,
		|	АвансовыйОтчетСписокНоменклатуры.Номенклатура,
		|	СУММА(АвансовыйОтчетСписокНоменклатуры.Количество) КАК Количество,
		|	АвансовыйОтчетСписокНоменклатуры.Контрагент
		|ИЗ
		|	Документ.АвансовыйОтчет.СписокНоменклатуры КАК АвансовыйОтчетСписокНоменклатуры
		|ГДЕ
		|	АвансовыйОтчетСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	АвансовыйОтчетСписокНоменклатуры.Номенклатура,
		|	АвансовыйОтчетСписокНоменклатуры.Контрагент";

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Движение = Движения.Управленческий.Добавить();
		Движение.Подразделение = Подразделение;
		Движение.Период = Дата;
		Движение.Сумма = ВыборкаДетальныеЗаписи.Сумма;
		Движение.Содержание = ВыборкаДетальныеЗаписи.Комментарий;
		
		Движение.СчетДт = ПланыСчетов.Управленческий.МатериалыНаСкладе;
		Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Склады] = Склад;
		Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = ВыборкаДетальныеЗаписи.Номенклатура;
		Движение.КоличествоДт = ВыборкаДетальныеЗаписи.Количество;
		
		Движение.СчетКт = СчетКт;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.ФизическиеЛица] = ФизЛицо;
		Если ОприходованиеБухгалтером Тогда
			// мнения разделились
			//Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.СтатьиДДС] = ТекСтрокаСписокНоменклатуры.Номенклатура.СтатьяДвиженияДенежныхСредств;
			Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.СтатьиДДС] = Справочники.СтатьиДвиженияДенежныхСредств.ПлатежиЗаОперативныйЗакуп;
		КонецЕсли;
		
		Движение = Движения.ЦеныПоставщиков.Добавить();
		Базовая  = ВыборкаДетальныеЗаписи.Номенклатура.Базовый ;
		
		Количество = ?(Базовая, ВыборкаДетальныеЗаписи.Количество, ВыборкаДетальныеЗаписи.Количество * ВыборкаДетальныеЗаписи.Номенклатура.КоэффициентБазовых);

		Движение.Период = Дата;
		Движение.Подразделение = Подразделение;
		Движение.Сумма = ВыборкаДетальныеЗаписи.Сумма / Количество;
		Движение.Номенклатура = ?(Базовая, ВыборкаДетальныеЗаписи.Номенклатура, ВыборкаДетальныеЗаписи.Номенклатура.БазоваяНоменклатура);
		Движение.Поставщик = ВыборкаДетальныеЗаписи.Контрагент;
		Движение.Регистратор = Ссылка;
		
	КонецЦикла;


	
	//Для Каждого ТекСтрокаСписокНоменклатуры Из СписокНоменклатуры Цикл
	//	Движение = Движения.Управленческий.Добавить();
	//	Движение.Подразделение = Подразделение;
	//	Движение.Период = Дата;
	//	Движение.Сумма = ТекСтрокаСписокНоменклатуры.Сумма;
	//	Движение.Содержание = ТекСтрокаСписокНоменклатуры.Комментарий;
	//	
	//	Движение.СчетДт = ПланыСчетов.Управленческий.МатериалыНаСкладе;
	//	Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Склады] = Склад;
	//	Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = ТекСтрокаСписокНоменклатуры.Номенклатура;
	//	Движение.КоличествоДт = ТекСтрокаСписокНоменклатуры.Количество;
	//	
	//	Движение.СчетКт = СчетКт;
	//	Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.ФизическиеЛица] = ФизЛицо;
	//	Если ОприходованиеБухгалтером Тогда
	//		// мнения разделились
	//		//Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.СтатьиДДС] = ТекСтрокаСписокНоменклатуры.Номенклатура.СтатьяДвиженияДенежныхСредств;
	//		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.СтатьиДДС] = Справочники.СтатьиДвиженияДенежныхСредств.ПлатежиЗаОперативныйЗакуп;
	//	КонецЕсли;
	//	
	//	Движение = Движения.ЦеныПоставщиков.Добавить();
	//	Базовая  = ТекСтрокаСписокНоменклатуры.Номенклатура.Базовый;
	//	Движение.Период = Дата;
	//	Движение.Подразделение = Подразделение;
	//	Движение.Сумма = ?(Базовая, ТекСтрокаСписокНоменклатуры.Сумма, ТекСтрокаСписокНоменклатуры.Сумма * ТекСтрокаСписокНоменклатуры.Номенклатура.КоэффициентБазовых);
	//	Движение.Номенклатура = ?(Базовая, ТекСтрокаСписокНоменклатуры.Номенклатура, ТекСтрокаСписокНоменклатуры.Номенклатура.БазоваяНоменклатура);
	//	Движение.Поставщик = ТекСтрокаСписокНоменклатуры.Контрагент;
	//	Движение.Регистратор = Ссылка;
	//	
	//КонецЦикла;
	
	Движения.Управленческий.Записывать = Истина;
	Движения.ЦеныПоставщиков.Записывать = Истина;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДатыЗапретаИзменения.ПроверитьДатуЗапретаИзмененияПередЗаписьюДокумента(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	СуммаДокумента = СписокПлатежей.Итог("Сумма") + СписокНоменклатуры.Итог("Сумма");
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ОперативныйЗакуп") Тогда
		
		ДокументОснование = ДанныеЗаполнения;
		Подразделение = ДанныеЗаполнения.Подразделение;
		Склад = ДанныеЗаполнения.Подразделение.ОсновнойСклад;
		Комментарий = "Введено на основании " + ДанныеЗаполнения;
		
		Запрос = Новый Запрос;
		Запрос.Параметры.Вставить("Документ", ДанныеЗаполнения);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	СписокНом.Номенклатура КАК Номенклатура,
		|	СписокНом.Поставщик КАК Контрагент
		|ИЗ
		|	Документ.ОперативныйЗакуп.СписокНоменклатуры КАК СписокНом
		|ГДЕ
		|	СписокНом.Ссылка = &Документ
		|	И СписокНом.КоличествоАваносвый > 0
		|
		|СГРУППИРОВАТЬ ПО
		|	СписокНом.Номенклатура,
		|	СписокНом.Поставщик";
		
		СписокНоменклатуры.Загрузить(Запрос.Выполнить().Выгрузить());
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если НЕ ЗначениеЗаполнено(Склад) И СписокНоменклатуры.Количество() > 0 Тогда
		
		Отказ= Истина;
		ТекстСообщения = "Заполните значение склада";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Ссылка, "Склад");
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	
	ДатыЗапретаИзменения. ПроверитьДатуЗапретаИзмененияПередУдалением(ЭтотОбъект, Отказ);
	
КонецПроцедуры