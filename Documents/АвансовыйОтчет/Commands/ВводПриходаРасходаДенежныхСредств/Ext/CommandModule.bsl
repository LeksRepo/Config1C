
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Док = ПараметрКоманды;
	
	Остаток = ПолучитьОстатокПоПодотчету(Док);

	Если Остаток > 0 Тогда
		Форма = "Документ.ПриходДенежныхСредств.ФормаОбъекта";	
	ИначеЕсли Остаток < 0 Тогда
		Форма = "Документ.РасходДенежныхСредств.ФормаОбъекта";
		Остаток = -Остаток;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("Основание, Остаток", Док, Остаток);
	
	Если Остаток <> 0 Тогда
		ОткрытьФорму(Форма, ПараметрыФормы, ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно, ПараметрыВыполненияКоманды.НавигационнаяСсылка);
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Остатков по подотчетному лицу не найдено");
	КонецЕсли;	
		
КонецПроцедуры

&НаСервере
Функция ПолучитьОстатокПоПодотчету(Документ)
	
	Остаток = 0;
	Счет = ПланыСчетов.Управленческий.Подотчет;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Период", ТекущаяДата());
	Запрос.УстановитьПараметр("Подразделение", Документ.Подразделение);
	Запрос.УстановитьПараметр("ФизЛицо", Документ.ФизЛицо);
	Запрос.УстановитьПараметр("Счет", Счет);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УправленческийОстатки.СуммаОстаток КАК СуммаОстаток
	|ИЗ
	|	РегистрБухгалтерии.Управленческий.Остатки(
	|			&Период,
	|			Счет = &Счет,
	|			,
	|			Подразделение = &Подразделение
	|				И Субконто2 = &ФизЛицо) КАК УправленческийОстатки";
	
	Результат = Запрос.Выполнить();
	
	Если НЕ Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Остаток = Выборка.СуммаОстаток;
		
	КонецЕсли;
	
	Возврат Остаток;
	
КонецФункции
