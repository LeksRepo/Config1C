
&НаСервере
Функция ЗаполнитьВопросы()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПравоваяБаза", Объект.Тема);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВопросыПользователям.ТекстВопроса,
	|	ВопросыПользователям.СписокОтветов.(
	|		Ссылка,
	|		НомерСтроки,
	|		Ответ,
	|		Правильный
	|	),
	|	ВопросыПользователям.Ссылка
	|ИЗ
	|	Справочник.ВопросыПользователям КАК ВопросыПользователям
	|ГДЕ
	|	НЕ ВопросыПользователям.ПометкаУдаления
	|	И ВЫБОР
	|			КОГДА &ПравоваяБаза = ЗНАЧЕНИЕ(Справочник.ПравоваяБаза.ПустаяСсылка)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ВопросыПользователям.Владелец = &ПравоваяБаза
	|		КОНЕЦ";
	
	ВсеВопросыПоТеме = Запрос.Выполнить().Выгрузить();
	ВсегоВопросов = ВсеВопросыПоТеме.Количество();
	
	//Если ЗначениеЗаполнено(Объект.Тема) Тогда
	//	Объект.КоличествоВопросов = ВсегоВопросов;
	//КонецЕсли;
	
	Если ВсегоВопросов = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("По теме " + Объект.Тема + " нет вопросов");
	Иначе
		
		МассивНомеровВопросов = Новый Массив;
		Для Сч = 0 По ВсегоВопросов - 1 Цикл
			МассивНомеровВопросов.Добавить(Сч);
		КонецЦикла;
		
		// Чтобы вопросы в разном порядке были.
		МассивСЧ = Новый Массив;
		ГСЧ = Новый ГенераторСлучайныхЧисел();
		
		Пока МассивСЧ.Количество() < Объект.КоличествоВопросов Цикл
			
			СлучайноеЧисло = ГСЧ.СлучайноеЧисло(0, МассивНомеровВопросов.ВГраница());
			МассивСЧ.Добавить(МассивНомеровВопросов.Получить(СлучайноеЧисло));
			МассивНомеровВопросов.Удалить(СлучайноеЧисло);
			
		КонецЦикла;
		
		Для каждого СлучайноеЧисло Из МассивСЧ Цикл
			
			НовыйВопрос = СлучайныеВопросы.Добавить();
			НовыйВопрос.Ссылка = ВсеВопросыПоТеме[СлучайноеЧисло].Ссылка;
			НовыйВопрос.СписокОтветов.Загрузить(ВсеВопросыПоТеме[СлучайноеЧисло].СписокОтветов); // Ответы надо тоже в разном порядке загружать.
			НовыйВопрос.ТекстВопроса = ВсеВопросыПоТеме[СлучайноеЧисло].ТекстВопроса;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ПолучитьВопрос(НомерВопроса)
	
	ОбновитьФорму();
	ТекстВопроса = СлучайныеВопросы[НомерВопроса].ТекстВопроса;
	ОтветПользователя.Очистить();
	Для каждого Ответ Из СлучайныеВопросы[НомерВопроса].СписокОтветов Цикл
		СтрокаТЧ = ОтветПользователя.Добавить();
		СтрокаТЧ.Ответ = Ответ.Ответ;
		СтрокаТЧ.ОтметкаПользователя = Ответ.ОтветПользователя;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция ЗаписатьОтвет(НомерВопроса)
	
	Для каждого Ответ Из СлучайныеВопросы[НомерВопроса].СписокОтветов Цикл
		
		Ответ.ОтветПользователя = ОтветПользователя[СлучайныеВопросы[НомерВопроса].СписокОтветов.Индекс(Ответ)].ОтметкаПользователя;
		
	КонецЦикла;
	
КонецФункции

&НаКлиенте
Процедура СледующийВопрос(Команда)
	
	ЗаписатьОтвет(ТекущийВопрос);
	ТекущийВопрос = ТекущийВопрос + 1;
	ПолучитьВопрос(ТекущийВопрос);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредыдущийВопрос(Команда)
	
	ЗаписатьОтвет(ТекущийВопрос);
	ТекущийВопрос = ТекущийВопрос - 1;
	ПолучитьВопрос(ТекущийВопрос);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьФорму()
	
	НадписьТекущийВопрос = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Вопрос %1 из %2", ТекущийВопрос+1, Объект.КоличествоВопросов);
	Модифицированность = Истина;
	
	Если ТекущийВопрос <= 0 Тогда
		
		Элементы.ПредыдущийВопрос.Доступность = Ложь;
		Элементы.СледующийВопрос.Доступность = Истина;
		
	ИначеЕсли ТекущийВопрос = Объект.КоличествоВопросов - 1 Тогда
		
		Элементы.ПредыдущийВопрос.Доступность = Истина;
		Элементы.СледующийВопрос.Доступность = Ложь;
		
	Иначе
		
		Элементы.ПредыдущийВопрос.Доступность = Истина;
		Элементы.СледующийВопрос.Доступность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьТестирование()
	
	ЗаполнитьВопросы();
	ТекущийВопрос = 0;
	Объект.ВремяНачалаТестирования = ТекущаяДата();
	ПолучитьВопрос(ТекущийВопрос);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Тема", Объект.Тема);
		ПараметрыФормы.Вставить("КоличествоВопросов", Объект.КоличествоВопросов);
		
		Ответ = ОткрытьФормуМодально("Документ.ТестированиеПользователей.Форма.ФормаНачалоТеста", ПараметрыФормы, ЭтаФорма);
		
		Если ЗначениеЗаполнено(Ответ) И ТипЗнч(Ответ) = Тип("Булево") Тогда
			Отказ = НЕ Ответ;
		Иначе
			Отказ = Истина;
		КонецЕсли;
		
		Если НЕ Отказ Тогда
			НачатьТестирование();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Объект.ВремяОкончанияТестирования = ТекущаяДата();
	Объект.Длительность = (Объект.ВремяОкончанияТестирования - Объект.ВремяНачалаТестирования) / 60 + 1;
	ВвестиЗначение(Объект.Комментарий, "Несколько слов напоследок", Тип("Строка"));
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		Если Параметры.Свойство("Тема") Тогда
			
			Если ЗначениеЗаполнено(Параметры.Тема) Тогда
				Объект.Тема = Параметры.Тема;
				Объект.КоличествоВопросов = ПолучитьКоличествоВопросов(Объект.Тема);
			Иначе
				Объект.КоличествоВопросов = 20;
			КонецЕсли;
			
		Иначе
			
			Отказ = Истина;
			Сообщить("Выберите тему на рабочем столе, и нажмите кнопку 'Пройти тестирование по теме'.");
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ВсегоПравильных = 0;
	Для каждого Вопрос Из СлучайныеВопросы Цикл
		
		ПравильныйОтвет = Истина;
		ОбъектВопрос = ТекущийОбъект.ВопросыТеста.Добавить();
		ОбъектВопрос.Вопрос = Вопрос.Ссылка;
		
		Для каждого Ответ Из Вопрос.СписокОтветов Цикл
			
			Если НЕ (Ответ.Правильный = Ответ.ОтветПользователя) Тогда
				
				ПравильныйОтвет = Ложь;
				Прервать;
				
			КонецЕсли;
			
		КонецЦикла;
		
		ОбъектВопрос.ПравильныйОтвет = ПравильныйОтвет;
		ТекущийОбъект.КоличествоПравильныхОтветов = ТекущийОбъект.КоличествоПравильныхОтветов + ПравильныйОтвет;
		
	КонецЦикла;
	
	ТекущийОбъект.ПроцентПравильныхОтветов = ТекущийОбъект.КоличествоПравильныхОтветов / ТекущийОбъект.КоличествоВопросов * 100;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтветыПользователяВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ОтветыПользователя.ТекущиеДанные;
	ТекущиеДанные.ОтметкаПользователя = НЕ ТекущиеДанные.ОтметкаПользователя;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", Объект.Ссылка);
	ОткрытьФорму("Документ.ТестированиеПользователей.Форма.ФормаСозданногоДокумента", ПараметрыФормы);
	Закрыть();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьКоличествоВопросов(СсылкаПравоваяБаза)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Владелец", СсылкаПравоваяБаза);	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВопросыПользователям.Ссылка) КАК Количество
	|ИЗ
	|	Справочник.ВопросыПользователям КАК ВопросыПользователям
	|ГДЕ
	|	НЕ ВопросыПользователям.ПометкаУдаления
	|	И ВопросыПользователям.Владелец = &Владелец";
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Выборка.Следующий();
	
	Возврат Выборка.Количество;
	
КонецФункции
