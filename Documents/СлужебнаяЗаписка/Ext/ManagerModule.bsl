
Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	ЛексСервер.ПолучитьПредставлениеДокумента(Данные, Представление, СтандартнаяОбработка);
	
КонецПроцедуры

// Записыват в Регистр сведений текущий статус СлужебнойЗаписки
//
// Параметры
//  Записка - ДокументСсылка.СлужебнаяЗаписка - Ссылка на СлужебнуюЗаписку для которой меняем статус
Функция  ОбработатьСтатусыСлужебныхЗаписок(Записка) Экспорт
	
	ДатаВремя = ТекущаяДата();
	СтатусыСлужебнойЗаписки = Перечисления.СтатусыСлужебнойЗаписки;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Записка);
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СлужебнаяЗаписка.Ссылка,
		|	СлужебнаяЗаписка.Дата,
		|	СлужебнаяЗаписка.Ознакомлен,
		|	СлужебнаяЗаписка.Утверждаю,
		|	СлужебнаяЗаписка.ДатаКонтроля,
		|	СлужебнаяЗаписка.НеСогласен,
		|	СлужебнаяЗаписка.Проведен
		|ИЗ
		|	Документ.СлужебнаяЗаписка КАК СлужебнаяЗаписка
		|ГДЕ
		|	СлужебнаяЗаписка.Ссылка = &Ссылка";

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		НаборЗаписей = РегистрыСведений.СтатусыСлужебныхЗаписок.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.СлужебнаяЗаписка.Установить(Записка);
		НаборЗаписей.Прочитать();
	
		Если НаборЗаписей.Количество() = 0 Тогда
			
			Запись = НаборЗаписей.Добавить();
			
		Иначе
			
			Запись = НаборЗаписей[0];
			
		КонецЕсли;
		
		Если ВыборкаДетальныеЗаписи.ДатаКонтроля < НачалоДня(ДатаВремя) Тогда
			
			НовыйСтатус = СтатусыСлужебнойЗаписки.Просрочена;
			
		ИначеЕсли НЕ ВыборкаДетальныеЗаписи.Ознакомлен и НЕ ВыборкаДетальныеЗаписи.Утверждаю и НЕ ВыборкаДетальныеЗаписи.НеСогласен Тогда
			
			НовыйСтатус = СтатусыСлужебнойЗаписки.Новая;
			
		ИначеЕсли ВыборкаДетальныеЗаписи.Проведен Тогда
			
			НовыйСтатус = СтатусыСлужебнойЗаписки.Архивная;
			
		Иначе
			
			НовыйСтатус = СтатусыСлужебнойЗаписки.Прочитана;
			
		КонецЕсли;
		
		Запись.Статус = НовыйСтатус;
		Запись.СлужебнаяЗаписка = Записка;
		Запись.Период = ДатаВремя;
		
		НаборЗаписей.Записать();
		
	КонецЦикла;
	
КонецФункции // ОбработатьСтатусыСлужебныхЗаписок()