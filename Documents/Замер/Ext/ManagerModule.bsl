	
Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка) Экспорт
	
	ЛексСервер.ПолучитьПредставлениеДокумента(Данные, Представление, СтандартнаяОбработка);
	
КонецПроцедуры

Функция РольДоступняДляРедактирования() Экспорт
	
	Пользователь = ПользователиКлиентСервер.ТекущийПользователь();
	
	ПользовательСупервайзер = УправлениеДоступомПереопределяемый.ЕстьДоступКПрофилюГруппДоступа(Пользователь, Справочники.ПрофилиГруппДоступа.Супервайзер);
	ПользовательАдминистратор = УправлениеДоступомПереопределяемый.ЕстьДоступКПрофилюГруппДоступа(Пользователь, Справочники.ПрофилиГруппДоступа.Администратор);
	
	ДоступныСтатусы = ПользовательСупервайзер ИЛИ ПользовательАдминистратор;
	
	Возврат ДоступныСтатусы;
	
КонецФункции

Функция ПечатьАнкеты(МассивОбъектов, ОбъектыПечати) Экспорт
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.ИмяПараметровПечати = "ПараметрыПечати_ПечатьАнкеты";
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Истина;
	ТабДок.ТолькоПросмотр = Истина;
	ТабДок.ОтображатьЗаголовки = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("МассивОбъектов", МассивОбъектов);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Замер.АдресЗамера КАК Адрес,
	|	Замер.ИмяЗаказчика КАК Заказчик,
	|	Замер.Офис,
	|	Замер.Дата,
	|	Замер.Агент КАК Дизайнер,
	|	Замер.Замерщик,
	|	Замер.Ссылка
	|ИЗ
	|	Документ.Замер КАК Замер
	|ГДЕ
	|	Замер.Ссылка В(&МассивОбъектов)";
	
	ВыборкаДокументы = Запрос.Выполнить().Выбрать();
	
	Макет = Документы.Замер.ПолучитьМакет("ПечатьАнкеты");
	ОбластьТело = Макет.ПолучитьОбласть("Тело");
	
	ВставлятьРазделительСтраниц = Ложь;
	
	Пока ВыборкаДокументы.Следующий() Цикл
		
		Если ВставлятьРазделительСтраниц Тогда
			
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
			
		КонецЕсли;
		
		НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1;
				
		ОбластьТело.Параметры.Заполнить(ВыборкаДокументы);
		ОбластьТело.Параметры.Дата = Формат(ВыборкаДокументы.Дата, "ДФ=dd.MM.yyyy");
		ОбластьТело.Параметры.Время = Формат(ВыборкаДокументы.Дата, "ДФ=ЧЧ:мм");
		ТабДок.Вывести(ОбластьТело);

		
		ВставлятьРазделительСтраниц = Истина;
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументы.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПечатьАнкеты") Тогда
		ПодготовитьПечатнуюФорму("ПечатьАнкеты", "Анкета замера", "Документ.Замер.ПечатьАнкеты",
		МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	КонецЕсли;
	
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
КонецПроцедуры

Процедура ПодготовитьПечатнуюФорму(Знач ИмяМакета, ПредставлениеМакета, ПолныйПутьКМакету = "", МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода)
	
	НужноПечататьМакет = УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, ИмяМакета);
	
	Если НужноПечататьМакет Тогда
		
		Если ИмяМакета = "ПечатьАнкеты" Тогда
			
			ТабДок = ПечатьАнкеты(МассивОбъектов, ОбъектыПечати);
			
		КонецЕсли;
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, ИмяМакета,
		ПредставлениеМакета, ТабДок,, ПолныйПутьКМакету);
		
	КонецЕсли;
	
КонецПроцедуры
