////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если СуммаОплаты <> 0 Тогда
		
		Движения.Управленческий.Записывать 	= Истина;
		
		Проводка 							= Движения.Управленческий.Добавить();
		Проводка.Подразделение 	= ЭтотОбъект.Подразделение;
		Проводка.Период 				= ЭтотОбъект.ДатаПриемаОплаты;
		Проводка.Сумма		 			= ЭтотОбъект.СуммаОплаты;
		Проводка.Содержание			= ЭтотОбъект.Комментарий;
		Проводка.СчетДт 				= ПланыСчетов.Управленческий.ОперационнаяКасса;
		Проводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.СтатьиДДС] 			= Справочники.СтатьиДвиженияДенежныхСредств.ПоступленияПрочие;
		Проводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.ФизическиеЛица] 	= ЭтотОбъект.Автор.ФизическоеЛицо;
		Проводка.СчетКт 				= ПланыСчетов.Управленческий.Доходы;
		Проводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.СтатьиДР] 				= Справочники.СтатьиДоходовРасходов.ДоходыПрочие;
		
		Проводка 							= Движения.Управленческий.Добавить();
		Проводка.Подразделение 	= ЭтотОбъект.Подразделение;
		Проводка.Период 				= ЭтотОбъект.ДатаПриемаОплаты; // будем подумать
		Проводка.СчетДт 				= ПланыСчетов.Управленческий.РасходыРозничнойСети;
		Проводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.СтатьиДР] 				= Справочники.СтатьиДоходовРасходов.РасходыЗарплатаЗамерыИАктивныеПродажи;
		Проводка.СчетКт 				= ПланыСчетов.Управленческий.ВзаиморасчетыССотрудниками;
		Проводка.Сумма		 			= ЭтотОбъект.СуммаОплаты;
		Проводка.Содержание			= ЭтотОбъект.Комментарий;
		
		Проводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.ФизическиеЛица] 	= ЭтотОбъект.Замерщик;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Сотрудник", ЭтотОбъект.Замерщик);
	Запрос.УстановитьПараметр("Дата", ЭтотОбъект.Дата);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ГрафикЗамеров.Период,
	|	ГрафикЗамеров.Сотрудник,
	|	ГрафикЗамеров.Занят
	|ИЗ
	|	РегистрСведений.ГрафикЗамеров КАК ГрафикЗамеров
	|ГДЕ
	|	ГрафикЗамеров.Сотрудник = &Сотрудник
	|	И ГрафикЗамеров.Период = &Дата";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		Текст = "Замерщик " + Замерщик + " занят " +Формат(Дата, "ДЛФ=Д") + " в " + Формат(Дата, "ДЛФ=T") + ". Выберете другое время";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст, ЭтотОбъект, "Дата");
		Отказ = Истина;
		
	Иначе
		
		Проводки 							= Движения.ГрафикЗамеров.Добавить();
		Проводки.Сотрудник 			= Замерщик;
		Проводки.Подразделение 	= Подразделение;
		Проводки.Занят 					= Истина;
		Проводки.Период 				= Дата;
		
		Движения.ГрафикЗамеров.Записывать = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Отказ = НЕ ПроверитьЗаполнение();
	Дата = НачалоЧаса(Дата);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
	ДатаПриемаОплаты 	= ТекущаяДата();
	Дата 							= НачалоЧаса(Дата);
	
	// если вводится из обработки ЗаписьНаЗамер
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("ОбработкаЗаписьНаЗамер") Тогда
		
		Замерщик 			= ДанныеЗаполнения.Замерщик;
		Дата 					= ДанныеЗаполнения.ДатаЗамера;
		Подразделение 	= ДанныеЗаполнения.Подразделение;
		
	КонецЕсли;
	
	//срабатывает подписка
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если СуммаОплаты <> 0 И НЕ ЗначениеЗаполнено(ДатаПриемаОплаты) Тогда
		ТекстСообщения = "Заполните дату приема оплаты";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ДатаПриемаОплаты");
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	Если НачалоЧаса(Дата) < ТекущаяДата() Тогда
		
		Отказ 					= Истина;
		ТекстСообщения 	= "Время замера уже прошло, нельзя отказаться от замера.";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "Дата");
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ