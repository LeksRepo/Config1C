
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПечатьНарядЗадания") Тогда
		ПодготовитьПечатнуюФорму("ПечатьНарядЗадания", "Наряд задание", "Документ.НарядЗадание.ПечатьНарядЗадания",
		МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПечатьМатериалПоПарковкам") Тогда
		ПодготовитьПечатнуюФорму("ПечатьМатериалПоПарковкам", "Материал по парковкам", "Документ.НарядЗадание.ПечатьМатериалПоПарковкам",
		МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПечатьОТК") Тогда
		ПодготовитьПечатнуюФорму("ПечатьОТК", "Отдел технического контроля", "Документ.НарядЗадание.ПечатьОТК",
		МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПечатьХлыстовойМатериал") Тогда
		ПодготовитьПечатнуюФорму("ПечатьХлыстовойМатериал", "Хлыстовой материал", "Документ.НарядЗадание.ПечатьХлыстовойМатериал",
		МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПечатьХлыстМатСклад") Тогда
		ПодготовитьПечатнуюФорму("ПечатьХлыстМатСклад", "Хлыстовой материал на склад", "Документ.НарядЗадание.ПечатьХлыстМатСклад",
		МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПечатьСтекло") Тогда
		ПодготовитьПечатнуюФорму("ПечатьСтекло", "Стекло", "Документ.НарядЗадание.ПечатьСтекло",
		МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПечатьРаскладка") Тогда
		ПодготовитьПечатнуюФорму("ПечатьРаскладка", "Раскладка", "Документ.НарядЗадание.ПечатьРаскладка",
		МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПечатьЛистовойМатериал") Тогда
		ПодготовитьПечатнуюФорму("ПечатьЛистовойМатериал", "Листовой материал", "Документ.НарядЗадание.ПечатьЛистовойМатериал",
		МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	КонецЕсли;
	
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
КонецПроцедуры

Процедура ПодготовитьПечатнуюФорму(Знач ИмяМакета, ПредставлениеМакета, ПолныйПутьКМакету = "", МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода)
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, ИмяМакета) Тогда
		
		Если ИмяМакета = "ПечатьНарядЗадания" Тогда
			ТабДок = ПечатьНарядЗадания(МассивОбъектов, ОбъектыПечати);
		ИначеЕсли ИмяМакета = "ПечатьМатериалПоПарковкам" Тогда
			ТабДок = ПечатьМатериалПоПарковкам(МассивОбъектов, ОбъектыПечати);
		ИначеЕсли ИмяМакета = "ПечатьОТК" Тогда
			ТабДок = ПечатьОТК(МассивОбъектов, ОбъектыПечати);
		ИначеЕсли ИмяМакета = "ПечатьХлыстовойМатериал" Тогда
			ТабДок = ПечатьХлыстовойМатериал(МассивОбъектов, ОбъектыПечати);
		ИначеЕсли ИмяМакета = "ПечатьХлыстМатСклад" Тогда
			ТабДок = ПечатьХлыстМатСклад(МассивОбъектов, ОбъектыПечати);	
		ИначеЕсли ИмяМакета = "ПечатьСтекло" Тогда
			ТабДок = ПечатьСтекло(МассивОбъектов, ОбъектыПечати);
		ИначеЕсли ИмяМакета = "ПечатьРаскладка" Тогда
			ТабДок = ПечатьРаскладка(МассивОбъектов, ОбъектыПечати);
		ИначеЕсли ИмяМакета = "ПечатьЛистовойМатериал" Тогда
			ТабДок = ПечатьЛистовойМатериал(МассивОбъектов, ОбъектыПечати);	
		КонецЕсли;
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
		ИмяМакета,
		ПредставлениеМакета,
		ТабДок,
		,
		ПолныйПутьКМакету);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПечатьЛистовойМатериал(МассивОбъектов, ОбъектыПечати) Экспорт
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.ИмяПараметровПечати = "ПараметрыПечати_ПечатьЛистовойМатериал";
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Ложь;
	ТабДок.ТолькоПросмотр = Истина;
	ТабДок.ОтображатьЗаголовки = Ложь;
	
	Макет = Документы.НарядЗадание.ПолучитьМакет("ПечатьЛистовойМатериал");
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапкаТаблицыЛисты = Макет.ПолучитьОбласть("ШапкаТаблицыЛисты");
	ОбластьСтрокаТаблицыЛисты = Макет.ПолучитьОбласть("СтрокаТаблицыЛисты");
	ОбластьШапкаТаблицыОбрезки = Макет.ПолучитьОбласть("ШапкаТаблицыОбрезки");
	ОбластьСтрокаТаблицыОбрезки = Макет.ПолучитьОбласть("СтрокаТаблицыОбрезки");
	
	ВставлятьРазделительСтраниц = Ложь;
	
	Для Каждого Наряд Из МассивОбъектов Цикл
		
		Если ВставлятьРазделительСтраниц Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1;

		ОбластьЗаголовок.Параметры.Наряд = Наряд;
		ТабДок.Вывести(ОбластьЗаголовок);
	
		Запрос = Новый Запрос;
		Запрос.Параметры.Вставить("Наряд", Наряд);

		Запрос.Текст =
		"ВЫБРАТЬ
		|	Листы.Номенклатура,
		|	Листы.НомерСтроки КАК НомерСтроки,
		|	Листы.Количество
		|ИЗ
		|	Документ.НарядЗадание.СписокЛистовНоменклатуры КАК Листы
		|ГДЕ
		|	Листы.Ссылка = &Наряд
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
		
		Результат = Запрос.Выполнить();
		
		Если НЕ Результат.Пустой() Тогда
			
			ТабДок.Вывести(ОбластьШапкаТаблицыЛисты);
			
			Выборка = Результат.Выбрать();
		
			Пока Выборка.Следующий() Цикл
				
				ОбластьСтрокаТаблицыЛисты.Параметры.Заполнить(Выборка);
				ТабДок.Вывести(ОбластьСтрокаТаблицыЛисты);

			КонецЦикла;
			
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Параметры.Вставить("Наряд", Наряд);

		Запрос.Текст =
		"ВЫБРАТЬ
		|	Остатки.Номенклатура,
		|	Остатки.НомерСтроки КАК НомерСтроки,
		|	Остатки.ВысотаОстатка,
		|	Остатки.ШиринаОстатка
		|ИЗ
		|	Документ.НарядЗадание.ОстаткиЛистовогоМатериала КАК Остатки
		|ГДЕ
		|	Остатки.Ссылка = &Наряд
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
		
		Результат = Запрос.Выполнить();
		
		Если НЕ Результат.Пустой() Тогда
			
			ТабДок.Вывести(ОбластьШапкаТаблицыОбрезки);
			
			Выборка = Результат.Выбрать();
		
			Пока Выборка.Следующий() Цикл
				
				ОбластьСтрокаТаблицыОбрезки.Параметры.Заполнить(Выборка);
				ТабДок.Вывести(ОбластьСтрокаТаблицыОбрезки);

			КонецЦикла;
			
		КонецЕсли;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, НомерСтрокиНачало, ОбъектыПечати, Наряд);	
		ВставлятьРазделительСтраниц = Истина;
		
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции

Функция ПечатьНарядЗадания(МассивОбъектов, ОбъектыПечати) Экспорт
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.ИмяПараметровПечати = "ПараметрыПечати_ПечатьНарядЗадания";
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Ложь;
	ТабДок.ТолькоПросмотр = Истина;
	ТабДок.ОтображатьЗаголовки = Ложь;
	
	ВидыСубконто = Новый Массив;
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("МассивОбъектов", МассивОбъектов);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НарядЗаданиеСписокСпецификаций.Ссылка КАК Ссылка,
	|	НарядЗаданиеСписокСпецификаций.НомерСтроки,
	|	НарядЗаданиеСписокСпецификаций.ДатаИзготовления,
	|	НарядЗаданиеСписокСпецификаций.Спецификация,
	|	НарядЗаданиеСписокСпецификаций.Спецификация.Изделие КАК Изделие,
	|	НарядЗаданиеСписокСпецификаций.Спецификация.Номер КАК НомерСпецификации,
	|	НарядЗаданиеСписокСпецификаций.СуммаНаряда,
	|	НарядЗаданиеСписокСпецификаций.Ссылка.Номер КАК НомерНаряда,
	|	НарядЗаданиеСписокСпецификаций.Ссылка.Дата КАК ДатаНаряда,
	|	НарядЗаданиеСписокСпецификаций.Ссылка.Подразделение,
	|	НарядЗаданиеСписокСпецификаций.Ссылка.СуммаДокумента КАК СуммаДокумента,
	|	НарядЗаданиеСписокСпецификаций.Спецификация.ДатаОтгрузки КАК ДатаОтгрузки,
	|	НарядЗаданиеСписокСпецификаций.Ссылка.ДатаИзготовления КАК ДатаИзготовленияНаряда,
	|	спрЗаметки.ТекстСодержания КАК Заметка,
	|	ВЫБОР
	|		КОГДА Комплектация.Ссылка ЕСТЬ NULL 
	|				ИЛИ НЕ Комплектация.Проведен
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЕстьПроведеннаяКомплектация
	|ИЗ
	|	Документ.НарядЗадание.СписокСпецификаций КАК НарядЗаданиеСписокСпецификаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Заметки КАК спрЗаметки
	|		ПО НарядЗаданиеСписокСпецификаций.Спецификация = спрЗаметки.Предмет
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Комплектация КАК Комплектация
	|		ПО НарядЗаданиеСписокСпецификаций.Спецификация = Комплектация.Спецификация
	|ГДЕ
	|	НарядЗаданиеСписокСпецификаций.Ссылка В(&МассивОбъектов)
	|ИТОГИ ПО
	|	Ссылка";
	
	Макет = Документы.НарядЗадание.ПолучитьМакет("ПечатьНарядЗадания");
	Заголовок = Макет.ПолучитьОбласть("Заголовок");
	Шапка = Макет.ПолучитьОбласть("Шапка");
	СпецификацииШапка = Макет.ПолучитьОбласть("СпецификацииШапка");
	СпецификацииСтрока = Макет.ПолучитьОбласть("СпецификацииСтрока");
	СтрокаЗаметка = Макет.ПолучитьОбласть("СтрокаЗаметка");
	
	ВыборкаДокументы = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВставлятьРазделительСтраниц = Ложь;
	
	МассивСпецификаций=Новый Массив();
	
	Пока ВыборкаДокументы.Следующий() Цикл
		
		Если ВставлятьРазделительСтраниц Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1;
		
		Заголовок.Параметры.НомерНаряда = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(ВыборкаДокументы.НомерНаряда);
		Заголовок.Параметры.ДатаИзготовления = Формат(ВыборкаДокументы.ДатаИзготовленияНаряда, "ДЛФ=DD");
		ТабДок.Вывести(Заголовок);
		
		Шапка.Параметры.Заполнить(ВыборкаДокументы);
		ТабДок.Вывести(Шапка);
		
		ТабДок.Вывести(СпецификацииШапка);
		
		ВыборкаСпецификации = ВыборкаДокументы.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаСпецификации.Следующий() Цикл
			
			МассивСпецификаций.Добавить(ВыборкаСпецификации.Спецификация);
			
			СпецификацииСтрока.Параметры.Заполнить(ВыборкаСпецификации);
			СпецификацииСтрока.Параметры.Изделие = ВыборкаСпецификации.Изделие;
			СпецификацииСтрока.Параметры.НомерСпецификации = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(ВыборкаСпецификации.НомерСпецификации);
			СпецификацииСтрока.Параметры.КоличествоДверей = Отчеты.ПроизводственныйОтчет.ПосчитатьКоличествоДверей(ВыборкаСпецификации.Спецификация);
			
			Комплектации = Отчеты.ПроизводственныйОтчет.ЕстьКомплектации(ВыборкаСпецификации.Спецификация);
			
			СпецификацииСтрока.Параметры.КомплектацияЦех = Комплектации.Цех;
			СпецификацииСтрока.Параметры.КомплектацияСклад = Комплектации.Склад;
			
			ТабДок.Вывести(СпецификацииСтрока);
			
			Если ЗначениеЗаполнено(ВыборкаСпецификации.Заметка) Тогда
				СтрокаЗаметка.Параметры.Заметка = ВыборкаСпецификации.Заметка;
				СтрокаЗаметка.Параметры.НомерСпецификации = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(ВыборкаСпецификации.НомерСпецификации);
				ТабДок.Вывести(СтрокаЗаметка);
			КонецЕсли;
			
		КонецЦикла;
		
		Отчеты.ПроизводственныйОтчет.ВывестиПроизводственныйОтчет(ТабДок, ВыборкаДокументы.Подразделение, МассивОбъектов, Новый Структура);
		
		ВставлятьРазделительСтраниц = Истина;
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументы.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции

Функция ПечатьОТК(Документ, ОбъектыПечати) Экспорт
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.ИмяПараметровПечати = "ПараметрыПечати_ОтделТехническогоКонтроля";
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Ложь;
	ТабДок.ТолькоПросмотр = Истина;
	ТабДок.ОтображатьЗаголовки = Ложь;
	
	Отчеты.ОтделТехническогоКонтроля.СформироватьОтчет(ТабДок, Документ.Подразделение, ТекущаяДата());
	
	Возврат ТабДок;
	
КонецФункции

Функция ПечатьРаскладка(МассивОбъектов, ОбъектыПечати) Экспорт
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.ИмяПараметровПечати = "ПараметрыПечати_ОтделТехническогоКонтроля";
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабДок.Защита = Ложь;
	ТабДок.ТолькоПросмотр = Истина;
	ТабДок.ОтображатьЗаголовки = Ложь;
	ТаблицаСпецификаций = МассивОбъектов.СписокСпецификаций.Выгрузить();
	МассивСпецификаций = ТаблицаСпецификаций.ВыгрузитьКолонку("Спецификация");
	Макет = Документы.НарядЗадание.ПолучитьМакет("Раскладка");
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьДетальСтрока = Макет.ПолучитьОбласть("ДетальСтрока");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивСпецификаций", МассивСпецификаций);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РаскройДеталей.Объект КАК Спецификация,
	|	РаскройДеталей.ТаблицаДеталей
	|ИЗ
	|	РегистрСведений.РаскройДеталей КАК РаскройДеталей
	|ГДЕ
	|	РаскройДеталей.Объект В (&МассивСпецификаций)";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	ВыборкаЗонаФРС = Новый ТаблицаЗначений;
	ВыборкаЗонаФРС.Колонки.Добавить("Спецификация", Новый ОписаниеТипов("ДокументСсылка.Спецификация"));
	ВыборкаЗонаФРС.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ВыборкаЗонаФРС.Колонки.Добавить("Парковка", Новый ОписаниеТипов("Число"));
	
	ВыборкаЗонаФасадов = ВыборкаЗонаФРС.СкопироватьКолонки();
	
	ВыборкаЗонаФРС.Колонки.Добавить("ФРСКоличество", Новый ОписаниеТипов("Число"));
	ВыборкаЗонаФРС.Колонки.Добавить("ФРСВысота", Новый ОписаниеТипов("Число"));
	ВыборкаЗонаФРС.Колонки.Добавить("ФРСШирина", Новый ОписаниеТипов("Число"));
	
	ВыборкаЗонаФасадов.Колонки.Добавить("ФасадыКоличество", Новый ОписаниеТипов("Число"));
	ВыборкаЗонаФасадов.Колонки.Добавить("ФасадыВысота", Новый ОписаниеТипов("Число"));
	ВыборкаЗонаФасадов.Колонки.Добавить("ФасадыШирина", Новый ОписаниеТипов("Число"));
	
	Пока Выборка.Следующий() Цикл
		
		НомерЛистаЗонаФРС = 0;
		НомерЛистаЗонаФасадов = 0;
		тзДетали = Выборка.ТаблицаДеталей.Получить();
		ТаблицаДляТестовЗонаФРС = тзДетали.СкопироватьКолонки();
		ТаблицаДляТестовЗонаФасадов = тзДетали.СкопироватьКолонки();
		тзДетали.Сортировать("Номенклатура");
		Для каждого Строка Из тзДетали Цикл
			
			НоменклатурнаяГруппа = Строка.Номенклатура.НоменклатурнаяГруппа;
			
			Если (НоменклатурнаяГруппа = Справочники.НоменклатурныеГруппы.ДВП 
				ИЛИ НоменклатурнаяГруппа = Справочники.НоменклатурныеГруппы.ЛДСП16
				ИЛИ НоменклатурнаяГруппа = Справочники.НоменклатурныеГруппы.ЛДСП22
				ИЛИ НоменклатурнаяГруппа = Справочники.НоменклатурныеГруппы.ЛДСП26
				ИЛИ НоменклатурнаяГруппа = Справочники.НоменклатурныеГруппы.МДФ18
				ИЛИ НоменклатурнаяГруппа = Справочники.НоменклатурныеГруппы.Пристенок
				ИЛИ НоменклатурнаяГруппа = Справочники.НоменклатурныеГруппы.Столешница)
				И Строка.ПоследнийЛист Тогда
				НоваяСтрока = ТаблицаДляТестовЗонаФРС.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
				
			ИначеЕсли (НоменклатурнаяГруппа = Справочники.НоменклатурныеГруппы.ЗеркалоДекоративное
				ИЛИ НоменклатурнаяГруппа = Справочники.НоменклатурныеГруппы.Зеркало
				ИЛИ НоменклатурнаяГруппа = Справочники.НоменклатурныеГруппы.Стекло
				ИЛИ НоменклатурнаяГруппа = Справочники.НоменклатурныеГруппы.АГТПанель
				ИЛИ НоменклатурнаяГруппа = Справочники.НоменклатурныеГруппы.ЛДСП10
				ИЛИ НоменклатурнаяГруппа = Справочники.НоменклатурныеГруппы.МДФ8
				ИЛИ НоменклатурнаяГруппа = Справочники.НоменклатурныеГруппы.МДФ10
				ИЛИ НоменклатурнаяГруппа = Справочники.НоменклатурныеГруппы.ЩитМебельный) 
				И Строка.ПоследнийЛист Тогда
				НоваяСтрока = ТаблицаДляТестовЗонаФасадов.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
				
			КонецЕсли;
			
		КонецЦикла;
		Если ТаблицаДляТестовЗонаФРС.Количество() > 0 Тогда
			ТаблицаДляТестовЗонаФРС.Свернуть("ВысотаДетали, Номенклатура, ШиринаДетали, НомерЛиста, ВсегоЛистов", "Количество");
		КонецЕсли;
		Если ТаблицаДляТестовЗонаФасадов.Количество() > 0 Тогда
			ТаблицаДляТестовЗонаФасадов.Свернуть("ВысотаДетали, Номенклатура, ШиринаДетали, НомерЛиста, ВсегоЛистов", "Количество");
		КонецЕсли;
		ТекущаяНоменклатура = Справочники.Номенклатура.ПустаяСсылка();
		ТаблицаДляТестовЗонаФРС.Сортировать("Номенклатура");
		Для каждого Строка Из ТаблицаДляТестовЗонаФРС Цикл
			
			Если ТекущаяНоменклатура <> Строка.Номенклатура и Строка.ВсегоЛистов > 1 Тогда
				НоваяСтрока = ВыборкаЗонаФРС.Добавить();
				НоваяСтрока.Спецификация = Выборка.Спецификация;
				НоваяСтрока.Номенклатура = Строка.Номенклатура;
				НоваяСтрока.Парковка = Строка.Номенклатура.Парковка;
				НоваяСтрока.ФРСКоличество = Строка.ВсегоЛистов - 1;
				НоваяСтрока.ФРСВысота = Строка.Номенклатура.ДлинаДетали;
				НоваяСтрока.ФРСШирина = Строка.Номенклатура.ШиринаДетали;
				НоменклатурнаяГруппа = Строка.Номенклатура.НоменклатурнаяГруппа;
				ТекущаяНоменклатура = Строка.Номенклатура;
			КонецЕсли;
			
			НоваяСтрока = ВыборкаЗонаФРС.Добавить();
			НоваяСтрока.Спецификация = Выборка.Спецификация;
			НоваяСтрока.Номенклатура = Строка.Номенклатура;
			НоваяСтрока.Парковка = Строка.Номенклатура.Парковка;
			НоваяСтрока.ФРСКоличество = Строка.Количество;
			НоваяСтрока.ФРСВысота = Строка.ВысотаДетали;
			НоваяСтрока.ФРСШирина = Строка.ШиринаДетали;
			
		КонецЦикла;
		
		ТекущаяНоменклатура = Справочники.Номенклатура.ПустаяСсылка();
		ТаблицаДляТестовЗонаФасадов.Сортировать("Номенклатура");
		Для каждого Строка Из ТаблицаДляТестовЗонаФасадов Цикл
			
			Если ТекущаяНоменклатура <> Строка.Номенклатура и Строка.ВсегоЛистов > 1 Тогда
				НоваяСтрока = ВыборкаЗонаФасадов.Добавить();
				НоваяСтрока.Спецификация = Выборка.Спецификация;
				НоваяСтрока.Номенклатура = Строка.Номенклатура;
				НоваяСтрока.Парковка = Строка.Номенклатура.Парковка;
				НоваяСтрока.ФасадыКоличество = Строка.ВсегоЛистов - 1;
				НоваяСтрока.ФасадыВысота = Строка.Номенклатура.ДлинаДетали;
				НоваяСтрока.ФасадыШирина = Строка.Номенклатура.ШиринаДетали;
				НоменклатурнаяГруппа = Строка.Номенклатура.НоменклатурнаяГруппа;
				ТекущаяНоменклатура = Строка.Номенклатура;
			КонецЕсли;
			
			НоваяСтрока = ВыборкаЗонаФасадов.Добавить();
			НоваяСтрока.Спецификация = Выборка.Спецификация;
			НоваяСтрока.Номенклатура = Строка.Номенклатура;
			НоваяСтрока.Парковка = Строка.Номенклатура.Парковка;
			НоваяСтрока.ФасадыКоличество = Строка.Количество;
			НоваяСтрока.ФасадыВысота = Строка.ВысотаДетали;
			НоваяСтрока.ФасадыШирина = Строка.ШиринаДетали;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивСпецификаций", МассивСпецификаций);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СпецификацияСписокНоменклатуры.Ссылка КАК Спецификация,
	|	СпецификацияСписокНоменклатуры.Номенклатура КАК Номенклатура,
	|	СпецификацияСписокНоменклатуры.Количество КАК ХлыстовойДлина,
	|	СпецификацияСписокНоменклатуры.Номенклатура.Парковка КАК Парковка,
	|	СпецификацияСписокНоменклатуры.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения
	|ИЗ
	|	Документ.Спецификация.СписокНоменклатуры КАК СпецификацияСписокНоменклатуры
	|ГДЕ
	|	СпецификацияСписокНоменклатуры.Ссылка В(&МассивСпецификаций)
	|	И СпецификацияСписокНоменклатуры.Номенклатура.НоменклатурнаяГруппа.ВидМатериала = ЗНАЧЕНИЕ(Перечисление.ВидыМатериалов.Хлыстовой)
	|	И СпецификацияСписокНоменклатуры.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Материал)
	|	И СпецификацияСписокНоменклатуры.Номенклатура.МестоОбработки = ЗНАЧЕНИЕ(Перечисление.МестоОбработки.Цех)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СпецификацияСписокНоменклатуры.Ссылка КАК Спецификация,
	|	СпецификацияСписокНоменклатуры.Номенклатура КАК Номенклатура,
	|	СпецификацияСписокНоменклатуры.Количество КАК РулонныйКоличество,
	|	СпецификацияСписокНоменклатуры.Номенклатура.Парковка КАК Парковка,
	|	СпецификацияСписокНоменклатуры.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения
	|ИЗ
	|	Документ.Спецификация.СписокНоменклатуры КАК СпецификацияСписокНоменклатуры
	|ГДЕ
	|	СпецификацияСписокНоменклатуры.Ссылка В(&МассивСпецификаций)
	|	И СпецификацияСписокНоменклатуры.Номенклатура.НоменклатурнаяГруппа.ВидМатериала = ЗНАЧЕНИЕ(Перечисление.ВидыМатериалов.Рулонный)
	|	И СпецификацияСписокНоменклатуры.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Материал)
	|	И СпецификацияСписокНоменклатуры.Номенклатура.МестоОбработки = ЗНАЧЕНИЕ(Перечисление.МестоОбработки.Цех)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СпецификацияСписокНоменклатуры.Ссылка КАК Спецификация,
	|	СпецификацияСписокНоменклатуры.Номенклатура КАК Номенклатура,
	|	СпецификацияСписокНоменклатуры.Количество КАК ШтучныйКоличество,
	|	СпецификацияСписокНоменклатуры.Номенклатура.Парковка КАК Парковка,
	|	СпецификацияСписокНоменклатуры.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения
	|ИЗ
	|	Документ.Спецификация.СписокНоменклатуры КАК СпецификацияСписокНоменклатуры
	|ГДЕ
	|	СпецификацияСписокНоменклатуры.Ссылка В(&МассивСпецификаций)
	|	И СпецификацияСписокНоменклатуры.Номенклатура.НоменклатурнаяГруппа.ВидМатериала = ЗНАЧЕНИЕ(Перечисление.ВидыМатериалов.Штучный)
	|	И СпецификацияСписокНоменклатуры.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Материал)
	|	И СпецификацияСписокНоменклатуры.Номенклатура.МестоОбработки = ЗНАЧЕНИЕ(Перечисление.МестоОбработки.Цех)";
	
	МассивРезультатовЗапроса = Запрос.ВыполнитьПакет();
	//ВыборкаЛистовогоМатериала = МассивРезультатовЗапроса[0].Выгрузить();
	//ВыборкаЗеркала = МассивРезультатовЗапроса[1].Выгрузить();
	ВыборкаХлыстовогоМатериала = МассивРезультатовЗапроса[0].Выгрузить();
	Если ВыборкаХлыстовогоМатериала.Количество() > 0 Тогда
		ВыборкаХлыстовогоМатериала.Свернуть("Спецификация, Номенклатура, ЕдиницаИзмерения", "ХлыстовойДлина");
	КонецЕсли;
	ВыборкаРулонногоМатериала = МассивРезультатовЗапроса[1].Выгрузить();
	Если ВыборкаРулонногоМатериала.Количество() > 0 Тогда
		ВыборкаРулонногоМатериала.Свернуть("Спецификация, Номенклатура, ЕдиницаИзмерения", "РулонныйКоличество");
	КонецЕсли;
	ВыборкаШтучногоМатериала = МассивРезультатовЗапроса[2].Выгрузить();
	Если ВыборкаШтучногоМатериала.Количество() > 0 Тогда
		ВыборкаШтучногоМатериала.Свернуть("Спецификация, Номенклатура, ЕдиницаИзмерения", "ШтучныйКоличество");
	КонецЕсли;
	
	НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1;
	
	ОбластьЗаголовок.Параметры.НомерНаряда = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(МассивОбъектов.Номер);
	ОбластьЗаголовок.Параметры.ДатаНаряда = Формат(МассивОбъектов.Дата, "ДЛФ=DD");
	ОбластьЗаголовок.Параметры.НомераСпецификаций = СформироватьНомераСпецификаций(МассивОбъектов.Ссылка);
	
	ТабДок.Вывести(ОбластьЗаголовок);
	ТабДок.Вывести(ОбластьШапка);
	ВставлятьРазделительСтраниц = Ложь;
	ВыборкаЗонаФРС.Сортировать("Номенклатура убыв, ФРСШирина убыв, ФРСВысота убыв");
	ВыборкаЗонаФасадов.Сортировать("Номенклатура");
	ВыборкаХлыстовогоМатериала.Сортировать("Номенклатура");
	ВыборкаРулонногоМатериала.Сортировать("Номенклатура");
	ВыборкаШтучногоМатериала.Сортировать("Номенклатура");
	
	Для каждого Спецификация Из МассивСпецификаций Цикл
		
		ОбластьДетальСтрока.Параметры.СпецификацияНомер = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(Спецификация.Номер);
		ТабДок.Вывести(ОбластьДетальСтрока);
		ОбластьДетальСтрока.Параметры.СпецификацияНомер = "";
		
		Для Каждого Строка Из ВыборкаЗонаФРС Цикл
			
			Если Строка.Спецификация = Спецификация Тогда
				
				ОбластьДетальСтрока.Параметры.Заполнить(Строка);
				ТабДок.Вывести(ОбластьДетальСтрока);
				
			КонецЕсли;
			
		КонецЦикла;
		
		ОбластьДетальСтрока = Макет.ПолучитьОбласть("ДетальСтрока");
		
		Для Каждого Строка Из ВыборкаЗонаФасадов Цикл
			
			Если Строка.Спецификация = Спецификация Тогда
				
				ОбластьДетальСтрока.Параметры.Заполнить(Строка);
				ТабДок.Вывести(ОбластьДетальСтрока);
				
			КонецЕсли;
			
		КонецЦикла;
		
		ОбластьДетальСтрока = Макет.ПолучитьОбласть("ДетальСтрока");
		
		Для Каждого Строка Из ВыборкаХлыстовогоМатериала Цикл
			
			Если Строка.Спецификация = Спецификация Тогда
				
				ОбластьДетальСтрока.Параметры.Заполнить(Строка);
				ТабДок.Вывести(ОбластьДетальСтрока);
				
			КонецЕсли;
			
		КонецЦикла;
		
		ОбластьДетальСтрока = Макет.ПолучитьОбласть("ДетальСтрока");
		
		Для Каждого Строка Из ВыборкаРулонногоМатериала Цикл
			
			Если Строка.Спецификация = Спецификация Тогда
				
				ОбластьДетальСтрока.Параметры.Заполнить(Строка);
				ТабДок.Вывести(ОбластьДетальСтрока);
				
			КонецЕсли;
			
		КонецЦикла;
		
		ОбластьДетальСтрока = Макет.ПолучитьОбласть("ДетальСтрока");
		
		Для Каждого Строка Из ВыборкаШтучногоМатериала Цикл
			
			Если Строка.Спецификация = Спецификация Тогда
				
				ОбластьДетальСтрока.Параметры.Заполнить(Строка);
				ТабДок.Вывести(ОбластьДетальСтрока);
				
			КонецЕсли;
			
		КонецЦикла;
		
		ОбластьДетальСтрока = Макет.ПолучитьОбласть("ДетальСтрока");
		
	КонецЦикла;
	
	УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, НомерСтрокиНачало, ОбъектыПечати, МассивОбъектов.Ссылка);
	
	Возврат ТабДок;
	
КонецФункции

Функция ПечатьМатериалПоПарковкам(МассивОбъектов, ОбъектыПечати) Экспорт
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.ИмяПараметровПечати = "ПараметрыПечати_ПечатьМатериалПоПарковкам";
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Ложь;
	ТабДок.ТолькоПросмотр = Истина;
	ТабДок.ОтображатьЗаголовки = Ложь;
	
	Макет = Документы.НарядЗадание.ПолучитьМакет("ПечатьМатериалПоПарковкам");
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьПарковка = Макет.ПолучитьОбласть("Парковка");
	ОбластьНоменклатураШапка = Макет.ПолучитьОбласть("НоменклатураШапка");
	ОбластьНоменклатураСтрока = Макет.ПолучитьОбласть("НоменклатураСтрока");

	ОбластьКомплектацииШапка = Макет.ПолучитьОбласть("КомплектацииШапка");
	ОбластьКомплектацииСтрока = Макет.ПолучитьОбласть("КомплектацииСтрока");
	
	ОбластьГравировкиШапка = Макет.ПолучитьОбласть("ГравировкиШапка");
	ОбластьГравировкиСтрока = Макет.ПолучитьОбласть("ГравировкиСтрока");
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("МассивНарядов", МассивОбъектов);
	Запрос.Параметры.Вставить("Подразделение", МассивОбъектов[0].Подразделение);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НарядЗаданиеСписокНоменклатуры.Ссылка КАК НарядСсылка,
	|	НарядЗаданиеСписокНоменклатуры.Ссылка.Номер КАК НомерНаряда,
	|	НарядЗаданиеСписокНоменклатуры.Ссылка.Дата КАК ДатаНаряда,
	|	НарядЗаданиеСписокНоменклатуры.Номенклатура КАК Номенклатура,
	|	НарядЗаданиеСписокНоменклатуры.Номенклатура.Парковка КАК Парковка,
	|	НарядЗаданиеСписокНоменклатуры.Номенклатура.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	НарядЗаданиеСписокНоменклатуры.Номенклатура.КраткоеНаименование КАК КраткоеНаименование,
	|	НарядЗаданиеСписокНоменклатуры.КоличествоТребуется КАК КоличествоТребуется,
	|	НарядЗаданиеСписокНоменклатуры.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	НарядЗаданиеСписокНоменклатурыПоСпецификациям.Спецификация.Номер КАК НомерСпецификации,
	|	НарядЗаданиеСписокНоменклатурыПоСпецификациям.Спецификация КАК Спецификация,
	|	НарядЗаданиеСписокНоменклатурыПоСпецификациям.Номенклатура.ЕдиницаИзмерения КАК НоменклатураСпецификацииЕдиницаИзмерения,
	|	НарядЗаданиеСписокНоменклатурыПоСпецификациям.Номенклатура.НоменклатурнаяГруппа.ВидМатериала КАК ВидМатериала,
	|	НарядЗаданиеСписокНоменклатурыПоСпецификациям.Количество КАК КоличествоВСпецификации,
	|	номПодраздел.АдресХранения КАК АдресХранения,
	|	ВЫБОР
	|		КОГДА НЕ МатПодЗаказ.Комментарий ЕСТЬ NULL
	|			ТОГДА МатПодЗаказ.Комментарий
	|		ИНАЧЕ ЕСТЬNULL(МатЗаказчика.Комментарий, """")
	|	КОНЕЦ КАК КомментарийПодЗаказ,
	|	ВЫБОР
	|		КОГДА МатПодЗаказ.Номенклатура ЕСТЬ NULL
	|				И МатЗаказчика.Номенклатура ЕСТЬ NULL
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА НЕ МатПодЗаказ.Номенклатура ЕСТЬ NULL
	|					ТОГДА 1
	|				ИНАЧЕ 2
	|			КОНЕЦ
	|	КОНЕЦ КАК ВидПредоставления
	|ИЗ
	|	Документ.НарядЗадание.СписокНоменклатуры КАК НарядЗаданиеСписокНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.НарядЗадание.СписокНоменклатурыПоСпецификациям КАК НарядЗаданиеСписокНоменклатурыПоСпецификациям
	|		ПО НарядЗаданиеСписокНоменклатуры.Ссылка = НарядЗаданиеСписокНоменклатурыПоСпецификациям.Ссылка
	|			И (НарядЗаданиеСписокНоменклатуры.Номенклатура = НарядЗаданиеСписокНоменклатурыПоСпецификациям.Номенклатура
	|				ИЛИ НарядЗаданиеСписокНоменклатуры.Номенклатура.БазоваяНоменклатура = НарядЗаданиеСписокНоменклатурыПоСпецификациям.Номенклатура)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Спецификация.СписокМатериаловПодЗаказ КАК МатПодЗаказ
	|		ПО (МатПодЗаказ.Ссылка = НарядЗаданиеСписокНоменклатурыПоСпецификациям.Спецификация)
	|			И (МатПодЗаказ.Номенклатура = НарядЗаданиеСписокНоменклатурыПоСпецификациям.Номенклатура
	|				ИЛИ МатПодЗаказ.Номенклатура = НарядЗаданиеСписокНоменклатурыПоСпецификациям.Номенклатура.БазоваяНоменклатура)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Спецификация.СписокМатериаловЗаказчика КАК МатЗаказчика
	|		ПО (МатЗаказчика.Ссылка = НарядЗаданиеСписокНоменклатурыПоСпецификациям.Спецификация)
	|			И (МатЗаказчика.Номенклатура = НарядЗаданиеСписокНоменклатурыПоСпецификациям.Номенклатура
	|				ИЛИ МатЗаказчика.Номенклатура = НарядЗаданиеСписокНоменклатурыПоСпецификациям.Номенклатура.БазоваяНоменклатура)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатураПодразделений.СрезПоследних(, Подразделение = &Подразделение) КАК номПодраздел
	|		ПО (НарядЗаданиеСписокНоменклатуры.Номенклатура = номПодраздел.Номенклатура
	|				ИЛИ НарядЗаданиеСписокНоменклатуры.Номенклатура.БазоваяНоменклатура = номПодраздел.Номенклатура)
	|ГДЕ
	|	НарядЗаданиеСписокНоменклатуры.Ссылка В(&МассивНарядов)
	|	И НарядЗаданиеСписокНоменклатуры.Номенклатура.НоменклатурнаяГруппа.ВидМатериала = ЗНАЧЕНИЕ(Перечисление.ВидыМатериалов.Рулонный)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НарядЗаданиеСписокНоменклатуры.Номенклатура.НоменклатурнаяГруппа.ВидМатериала,
	|	НарядЗаданиеСписокНоменклатуры.Номенклатура.НоменклатурнаяГруппа
	|ИТОГИ
	|	МАКСИМУМ(КоличествоТребуется),
	|	МАКСИМУМ(ЕдиницаИзмерения),
	|	МАКСИМУМ(АдресХранения)
	|ПО
	|	НарядСсылка,
	|	ВидМатериала,
	|	Парковка,
	|	Номенклатура";
	
	ВставлятьРазделительСтраниц = Ложь;
	ВыборкаДокументы = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаДокументы.Следующий() Цикл
		
		Если ВставлятьРазделительСтраниц Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1;
		
		ОбластьЗаголовок.Параметры.НомерНаряда = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(ВыборкаДокументы.НомерНаряда);
		ОбластьЗаголовок.Параметры.ДатаНаряда = Формат(ВыборкаДокументы.ДатаНаряда, "ДЛФ=DD");
		ОбластьЗаголовок.Параметры.НомераСпецификаций = СформироватьНомераСпецификаций(ВыборкаДокументы.НарядСсылка);
		
		ТабДок.Вывести(ОбластьЗаголовок);
		
		ВыборкаВидМатериала = ВыборкаДокументы.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаВидМатериала.Следующий() Цикл
			
			ОбластьНоменклатураШапка.Параметры.ВидМатериала = ВыборкаВидМатериала.ВидМатериала;
			ТабДок.Вывести(ОбластьНоменклатураШапка);
			
			ВыборкаПарковка = ВыборкаВидМатериала.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаПарковка.Следующий() Цикл
				
				ОбластьПарковка.Параметры.Парковка = ВыборкаПарковка.Парковка;
				ТабДок.Вывести(ОбластьПарковка);
				
				ВыборкаНоменклатура = ВыборкаПарковка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаНоменклатура.Следующий() Цикл
					
					ОбластьНоменклатураСтрока.Параметры.Заполнить(ВыборкаНоменклатура);
					
					Если ВыборкаНоменклатура.НоменклатурнаяГруппа.ПринадлежитЭлементу(Справочники.НоменклатурныеГруппы.Кант)
						ИЛИ ВыборкаНоменклатура.НоменклатурнаяГруппа.ПринадлежитЭлементу(Справочники.НоменклатурныеГруппы.Кромка) Тогда
						
						ОбластьНоменклатураСтрока.Параметры.КраткоеНаименованиеТорцевойЛенты = "кратк: " + ВыборкаНоменклатура.КраткоеНаименование;
					Иначе
						ОбластьНоменклатураСтрока.Параметры.КраткоеНаименованиеТорцевойЛенты = "";
					КонецЕсли;
					
					ТабДок.Вывести(ОбластьНоменклатураСтрока);
					
					ВыборкаСпецификация = ВыборкаНоменклатура.Выбрать();
					Пока ВыборкаСпецификация.Следующий() Цикл
						
						ОбластьСпецификацияСтрока = Макет.ПолучитьОбласть("СпецификацияСтрока");
						ОбластьСпецификацияСтрока.Параметры.Заполнить(ВыборкаСпецификация);
						ОбластьСпецификацияСтрока.Параметры.НомерСпецификации = ?(ЗначениеЗаполнено(ВыборкаСпецификация.КомментарийПодЗаказ),"( " + ВыборкаСпецификация.КомментарийПодЗаказ + " ) ", "") + ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(ВыборкаСпецификация.НомерСпецификации);
						
						Если ВыборкаСпецификация.ВидПредоставления = 0 Тогда			
							ОбластьСпецификацияСтрока.Рисунки.Удалить(ОбластьСпецификацияСтрока.Рисунки.КартинкаПодЗаказ);
							ОбластьСпецификацияСтрока.Рисунки.Удалить(ОбластьСпецификацияСтрока.Рисунки.КартинкаПредоставляемый);	
						ИначеЕсли ВыборкаСпецификация.ВидПредоставления = 1 Тогда
							ОбластьСпецификацияСтрока.Рисунки.Удалить(ОбластьСпецификацияСтрока.Рисунки.КартинкаПредоставляемый);
						Иначе
							ОбластьСпецификацияСтрока.Рисунки.Удалить(ОбластьСпецификацияСтрока.Рисунки.КартинкаПодЗаказ);
						КонецЕсли;
						
						ТабДок.Вывести(ОбластьСпецификацияСтрока);
						
					КонецЦикла;
	
				КонецЦикла;
				
			КонецЦикла;
						
		КонецЦикла; // ВыборкаВидМатериала.Следующий()
		
		// Гравировки.
		
		тзГравировки = ПолучитьГравировки(ВыборкаДокументы.НарядСсылка);
		
		Если тзГравировки.Количество() > 0 Тогда
			
			ТабДок.Вывести(ОбластьГравировкиШапка);
			
			Для Каждого Строка Из тзГравировки Цикл
				
				ОбластьГравировкиСтрока.Параметры.НомерСпецификации = Строка.НомерСпецификация;
				ТабДок.Вывести(ОбластьГравировкиСтрока);
				
			КонецЦикла;
			
		КонецЕсли;
		
		ВставлятьРазделительСтраниц = Истина;
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументы.НарядСсылка);
		
	КонецЦикла; // ВыборкаДокументы.Следующий()
	
	Возврат ТабДок;
	
КонецФункции

Функция ПечатьХлыстовойМатериал(МассивОбъектов, ОбъектыПечати) Экспорт
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.ИмяПараметровПечати = "ПараметрыПечати_ПечатьХлыстовойМатериал";
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Истина;
	ТабДок.ТолькоПросмотр = Истина;
	ТабДок.ОтображатьЗаголовки = Ложь;
	
	Макет = Документы.НарядЗадание.ПолучитьМакет("ПечатьХлыстовойМатериал");
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьХлыстСтрока = Макет.ПолучитьОбласть("ХлыстСтрока");
	ОбластьШапкаДляОбщегоКоличества = Макет.ПолучитьОбласть("ШапкаДляОбщегоКоличества");
	ОбластьСтрокаОбщегоКоличества = Макет.ПолучитьОбласть("СтрокаОбщегоКоличества");
	ОбластьПустаяСтрока = Макет.ПолучитьОбласть("ПустаяСтрока");
	ОбластьСпецификацияСтрока = Макет.ПолучитьОбласть("СпецификацияСтрока");
	ОбластьПарковка = Макет.ПолучитьОбласть("Парковка");
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("МассивНарядов", МассивОбъектов);
	Запрос.Параметры.Вставить("Подразделение", МассивОбъектов[0].Подразделение);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НарядЗаданиеРаскройХлыстов.Ссылка КАК Ссылка,
	|	НарядЗаданиеРаскройХлыстов.Ссылка.Номер КАК НомерНаряда,
	|	НарядЗаданиеРаскройХлыстов.Ссылка.Дата КАК ДатаНаряда,
	|	НарядЗаданиеРаскройХлыстов.Количество КАК РазмерОтрезка,
	|	НарядЗаданиеРаскройХлыстов.Номенклатура КАК Номенклатура,
	|	НарядЗаданиеРаскройХлыстов.НомерХлыста КАК НомерХлыста,
	|	НарядЗаданиеРаскройХлыстов.Остаток,
	|	НарядЗаданиеРаскройХлыстов.РазмерХлыста КАК РазмерХлыста,
	|	НарядЗаданиеРаскройХлыстов.Спецификация,
	|	НарядЗаданиеРаскройХлыстов.Спецификация.Номер КАК НомерСпецификации,
	|	НарядЗаданиеРаскройХлыстов.Двери КАК Двери,
	|	НарядЗаданиеРаскройХлыстов.Номенклатура.Парковка КАК НоменклатураПарковка,
	|	номПодраздел.АдресХранения КАК АдресХранения,
	|	ВЫБОР
	|		КОГДА НЕ МатПодЗаказ.Комментарий ЕСТЬ NULL 
	|			ТОГДА МатПодЗаказ.Комментарий
	|		ИНАЧЕ ЕСТЬNULL(МатЗаказчика.Комментарий, """")
	|	КОНЕЦ КАК КомментарийПодЗаказ,
	|	ВЫБОР
	|		КОГДА МатПодЗаказ.Номенклатура ЕСТЬ NULL 
	|				И МатЗаказчика.Номенклатура ЕСТЬ NULL 
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА НЕ МатПодЗаказ.Номенклатура ЕСТЬ NULL 
	|					ТОГДА 1
	|				ИНАЧЕ 2
	|			КОНЕЦ
	|	КОНЕЦ КАК ВидПредоставления
	|ИЗ
	|	Документ.НарядЗадание.РаскройХлыстов КАК НарядЗаданиеРаскройХлыстов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатураПодразделений.СрезПоследних(, Подразделение = &Подразделение) КАК номПодраздел
	|		ПО (НарядЗаданиеРаскройХлыстов.Номенклатура = номПодраздел.Номенклатура
	|				ИЛИ НарядЗаданиеРаскройХлыстов.Номенклатура.БазоваяНоменклатура = номПодраздел.Номенклатура)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Спецификация.СписокМатериаловПодЗаказ КАК МатПодЗаказ
	|		ПО (МатПодЗаказ.Ссылка = НарядЗаданиеРаскройХлыстов.Спецификация)
	|			И (МатПодЗаказ.Номенклатура = НарядЗаданиеРаскройХлыстов.Номенклатура
	|				ИЛИ МатПодЗаказ.Номенклатура = НарядЗаданиеРаскройХлыстов.Номенклатура.БазоваяНоменклатура)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Спецификация.СписокМатериаловЗаказчика КАК МатЗаказчика
	|		ПО (МатЗаказчика.Ссылка = НарядЗаданиеРаскройХлыстов.Спецификация)
	|			И (МатЗаказчика.Номенклатура = НарядЗаданиеРаскройХлыстов.Номенклатура
	|				ИЛИ МатЗаказчика.Номенклатура = НарядЗаданиеРаскройХлыстов.Номенклатура.БазоваяНоменклатура)
	|ГДЕ
	|	НарядЗаданиеРаскройХлыстов.Ссылка В(&МассивНарядов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НарядЗаданиеРаскройХлыстов.Номенклатура.Цвет,
	|	НомерСпецификации,
	|	НомерХлыста
	|ИТОГИ
	|	СУММА(РазмерОтрезка),
	|	МАКСИМУМ(РазмерХлыста),
	|	МАКСИМУМ(АдресХранения)
	|ПО
	|	Ссылка,
	|	НоменклатураПарковка,
	|	Номенклатура,
	|	НомерХлыста";
	
	ВставлятьРазделительСтраниц = Ложь;
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДокументы = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаДокументы.Следующий() Цикл
		
		Если ВставлятьРазделительСтраниц Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1;
		
		ОбластьЗаголовок.Параметры.НомерНаряда = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(ВыборкаДокументы.НомерНаряда);
		ОбластьЗаголовок.Параметры.ДатаНаряда = Формат(ВыборкаДокументы.ДатаНаряда, "ДЛФ=DD");
		ОбластьЗаголовок.Параметры.НомераСпецификаций = СформироватьНомераСпецификаций(ВыборкаДокументы.Ссылка);
		
		ТабДок.Вывести(ОбластьЗаголовок);
		ТабДок.Вывести(ОбластьПустаяСтрока);
		ТабДок.Вывести(ОбластьШапкаДляОбщегоКоличества);
		
		ВыборкаПарковка = ВыборкаДокументы.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаПарковка.Следующий() Цикл
			
			ВыборкаДляОбщегоКоличества = ВыборкаПарковка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаДляОбщегоКоличества.Следующий() Цикл
				
				Если ЗначениеЗаполнено(ВыборкаДляОбщегоКоличества.Номенклатура) Тогда
					ОбластьСтрокаОбщегоКоличества.Параметры.Заполнить(ВыборкаДляОбщегоКоличества);
					ОбластьСтрокаОбщегоКоличества.Параметры.ЕдИзм = ВыборкаДляОбщегоКоличества.Номенклатура.ЕдиницаИзмерения;
					ТабДок.Вывести(ОбластьСтрокаОбщегоКоличества);
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
		ТабДок.Вывести(ОбластьПустаяСтрока);
		
		ТабДок.Вывести(ОбластьШапка);
		
		ВыборкаПарковка = ВыборкаДокументы.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаПарковка.Следующий() Цикл
			
			ОбластьПарковка.Параметры.Парковка = ВыборкаПарковка.НоменклатураПарковка;
			ТабДок.Вывести(ОбластьПарковка);
			
			ВыборкаНоменклатура = ВыборкаПарковка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаНоменклатура.Следующий() Цикл
				
				ВыборкаХлысты = ВыборкаНоменклатура.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаХлысты.Следующий() Цикл
					
					ОбластьХлыстСтрока.Параметры.Заполнить(ВыборкаХлысты);
					ТабДок.Вывести(ОбластьХлыстСтрока);
					
					РазмерЗаготовки = ВыборкаХлысты.РазмерХлыста;
					
					ВыборкаСпецификации = ВыборкаХлысты.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					Пока ВыборкаСпецификации.Следующий() Цикл
						
						Если НЕ ВыборкаСпецификации.Остаток Тогда
							
							ОбластьСпецификацияСтрока = Макет.ПолучитьОбласть("СпецификацияСтрока");
							ОбластьСпецификацияСтрока.Параметры.Заполнить(ВыборкаСпецификации);
							СтрокаСпецификация = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(ВыборкаСпецификации.НомерСпецификации);
							ОбластьСпецификацияСтрока.Параметры.Спецификация = ?(ЗначениеЗаполнено(ВыборкаСпецификации.КомментарийПодЗаказ),"( " + ВыборкаСпецификации.КомментарийПодЗаказ + " ) ", "") + СтрокаСпецификация;
							
							Если ВыборкаСпецификации.ВидПредоставления = 0 Тогда			
								ОбластьСпецификацияСтрока.Рисунки.Удалить(ОбластьСпецификацияСтрока.Рисунки.КартинкаПодЗаказ);
								ОбластьСпецификацияСтрока.Рисунки.Удалить(ОбластьСпецификацияСтрока.Рисунки.КартинкаПредоставляемый);	
							ИначеЕсли ВыборкаСпецификации.ВидПредоставления = 1 Тогда
								ОбластьСпецификацияСтрока.Рисунки.Удалить(ОбластьСпецификацияСтрока.Рисунки.КартинкаПредоставляемый);
							Иначе
								ОбластьСпецификацияСтрока.Рисунки.Удалить(ОбластьСпецификацияСтрока.Рисунки.КартинкаПодЗаказ);
							КонецЕсли;

							ТабДок.Вывести(ОбластьСпецификацияСтрока);
							
						Иначе
							РазмерЗаготовки = ВыборкаСпецификации.РазмерХлыста - ВыборкаСпецификации.РазмерОтрезка;
						КонецЕсли;
						
					КонецЦикла;
					
					ОбластьСпецификацияСтрока = Макет.ПолучитьОбласть("СпецификацияСтрока");
					ОбластьСпецификацияСтрока.Параметры.Спецификация = "Размер заготовки";
					ОбластьСпецификацияСтрока.Параметры.РазмерОтрезка = РазмерЗаготовки;
					
					ОбластьСпецификацияСтрока.Рисунки.Удалить(ОбластьСпецификацияСтрока.Рисунки.КартинкаПодЗаказ);
					ОбластьСпецификацияСтрока.Рисунки.Удалить(ОбластьСпецификацияСтрока.Рисунки.КартинкаПредоставляемый);
					
					ОбластьСпецификацияСтрока.Области.СпецификацияСтрока.Шрифт = Новый Шрифт(,, Истина);
					ТабДок.Вывести(ОбластьСпецификацияСтрока);
					ОбластьСпецификацияСтрока.Области.СпецификацияСтрока.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Право;
					ОбластьСпецификацияСтрока.Области.СпецификацияСтрока.Шрифт = Новый Шрифт(,, Ложь);
					
				КонецЦикла;
				
			КонецЦикла;
			
			ВставлятьРазделительСтраниц = Истина;
			
		КонецЦикла;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументы.Ссылка);
		
	КонецЦикла; // ВыборкаДокументы.Следующий()
	
	Возврат ТабДок;
	
КонецФункции

Функция ПечатьСтекло(МассивОбъектов, ОбъектыПечати) Экспорт
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.ИмяПараметровПечати = "ПараметрыПечати_ПечатьСтекло";
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Ложь;
	ТабДок.ТолькоПросмотр = Истина;
	ТабДок.ОтображатьЗаголовки = Ложь;
	
	Макет = Документы.НарядЗадание.ПолучитьМакет("ПечатьСтекло");
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьДетальСтрока = Макет.ПолучитьОбласть("ДетальСтрока");
	
	МассивНоменклатурныхГрупп = Новый Массив;
	МассивНоменклатурныхГрупп.Добавить(Справочники.НоменклатурныеГруппы.Стекло);
	МассивНоменклатурныхГрупп.Добавить(Справочники.НоменклатурныеГруппы.СтеклоДляСкругляемогоФасада);
	МассивНоменклатурныхГрупп.Добавить(Справочники.НоменклатурныеГруппы.Зеркало);
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("МассивНарядов", МассивОбъектов);
	Запрос.Параметры.Вставить("МассивНоменклатурныхГрупп", МассивНоменклатурныхГрупп);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НарядЗаданиеСписокЛистовыхДеталей.Ссылка КАК Ссылка,
	|	НарядЗаданиеСписокЛистовыхДеталей.Высота,
	|	НарядЗаданиеСписокЛистовыхДеталей.Номенклатура КАК Номенклатура,
	|	НарядЗаданиеСписокЛистовыхДеталей.Спецификация,
	|	НарядЗаданиеСписокЛистовыхДеталей.Спецификация.Номер КАК НомерСпецификации,
	|	НарядЗаданиеСписокЛистовыхДеталей.Ширина,
	|	НарядЗаданиеСписокЛистовыхДеталей.Количество,
	|	НарядЗаданиеСписокЛистовыхДеталей.Ссылка.Номер КАК НомерНаряда,
	|	НарядЗаданиеСписокЛистовыхДеталей.Ссылка.Дата КАК ДатаНаряда
	|ИЗ
	|	Документ.НарядЗадание.СписокЛистовыхДеталей КАК НарядЗаданиеСписокЛистовыхДеталей
	|ГДЕ
	|	НарядЗаданиеСписокЛистовыхДеталей.Ссылка В(&МассивНарядов)
	|	И НарядЗаданиеСписокЛистовыхДеталей.Номенклатура.НоменклатурнаяГруппа В(&МассивНоменклатурныхГрупп)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура
	|ИТОГИ
	|	МАКСИМУМ(НомерНаряда),
	|	МАКСИМУМ(ДатаНаряда)
	|ПО
	|	Ссылка";
	
	ВставлятьРазделительСтраниц = Ложь;
	
	ВыборкаДокументы = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаДокументы.Следующий() Цикл
		
		Если ВставлятьРазделительСтраниц Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1;
		
		ОбластьЗаголовок.Параметры.НомерНаряда = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(ВыборкаДокументы.НомерНаряда);
		ОбластьЗаголовок.Параметры.ДатаНаряда = Формат(ВыборкаДокументы.ДатаНаряда, "ДЛФ=DD");
		ОбластьЗаголовок.Параметры.НомераСпецификаций = СформироватьНомераСпецификаций(ВыборкаДокументы.Ссылка);
		
		ТабДок.Вывести(ОбластьЗаголовок);
		ТабДок.Вывести(ОбластьШапка);
		
		ВыборкаНоменклатура = ВыборкаДокументы.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаНоменклатура.Следующий() Цикл
			
			ОбластьДетальСтрока.Параметры.Заполнить(ВыборкаНоменклатура);
			ОбластьДетальСтрока.Параметры.Спецификация = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(ВыборкаНоменклатура.НомерСпецификации);
			ТабДок.Вывести(ОбластьДетальСтрока);
			
		КонецЦикла;
		
		ВставлятьРазделительСтраниц = Истина;
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументы.Ссылка);
		
	КонецЦикла; // ВыборкаДокументы.Следующий()
	
	Возврат ТабДок;
	
КонецФункции

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	ЛексСервер.ПолучитьПредставлениеДокумента(Данные, Представление, СтандартнаяОбработка);
	
КонецПроцедуры

Функция СформироватьНомераСпецификаций(Ссылка)
	
	Результат = "";
	
	Для каждого Строка Из Ссылка.СписокСпецификаций Цикл
		
		Номер = ОбщегоНазначения.ПолучитьЗначениеРеквизита(Строка.Спецификация, "Номер");
		Результат = Результат + ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(Номер) + ", ";
		
	КонецЦикла;
	
	Если Результат <> "" Тогда
		Результат = Лев(Результат, СтрДлина(Результат) - 2) + "." ;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьХлыстовойМатериалНаДвери(Наряд,Дверь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("Наряд", Наряд);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НарядЗаданиеРаскройХлыстов.Ссылка КАК Ссылка,
	|	НарядЗаданиеРаскройХлыстов.Ссылка.Номер КАК НомерНаряда,
	|	НарядЗаданиеРаскройХлыстов.Ссылка.Дата КАК ДатаНаряда,
	|	НарядЗаданиеРаскройХлыстов.Количество КАК РазмерОтрезка,
	|	НарядЗаданиеРаскройХлыстов.Номенклатура КАК Номенклатура,
	|	НарядЗаданиеРаскройХлыстов.НомерХлыста КАК НомерХлыста,
	|	НарядЗаданиеРаскройХлыстов.Остаток,
	|	НарядЗаданиеРаскройХлыстов.РазмерХлыста КАК РазмерХлыста,
	|	НарядЗаданиеРаскройХлыстов.Спецификация,
	|	НарядЗаданиеРаскройХлыстов.Спецификация.Номер КАК НомерСпецификации,
	|	НарядЗаданиеРаскройХлыстов.Двери КАК Двери
	|ИЗ
	|	Документ.НарядЗадание.РаскройХлыстов КАК НарядЗаданиеРаскройХлыстов
	|ГДЕ
	|	НарядЗаданиеРаскройХлыстов.Ссылка = &Наряд
	|
	|УПОРЯДОЧИТЬ ПО
	|	НарядЗаданиеРаскройХлыстов.Номенклатура.Цвет,
	|	НомерСпецификации,
	|	НомерХлыста
	|ИТОГИ
	|	МАКСИМУМ(РазмерХлыста)
	|ПО
	|	Ссылка,
	|	Номенклатура,
	|	НомерХлыста";
	
	МассивДанные = Новый Массив();
	
	ВыборкаДокументы = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаДокументы.Следующий() Цикл
		
		ВыборкаНоменклатура = ВыборкаДокументы.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаНоменклатура.Следующий() Цикл
			
			ВыборкаХлысты = ВыборкаНоменклатура.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаХлысты.Следующий() Цикл
				
				ЕстьНужнаяДверь = Ложь;
				МассивХлыст = Новый Массив();
				
				РазмерЗаготовки = ВыборкаХлысты.РазмерХлыста;
				
				ВыборкаСпецификации = ВыборкаХлысты.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				
				Пока ВыборкаСпецификации.Следующий() Цикл
					Если НЕ ВыборкаСпецификации.Остаток Тогда
						Если ВыборкаСпецификации.Двери = Дверь Тогда
							
							ЕстьНужнаяДверь = Истина;
							МассивХлыст.Добавить("1" + "♥" + ВыборкаСпецификации.РазмерОтрезка);
							
						КонецЕсли;
					Иначе
						РазмерЗаготовки = ВыборкаСпецификации.РазмерХлыста - ВыборкаСпецификации.РазмерОтрезка;
					КонецЕсли;
				КонецЦикла;
				
				Если ЕстьНужнаяДверь Тогда
					
					НовыйХлыст = Новый Массив();
					НовыйХлыст.Добавить("0" + "♥" + ВыборкаХлысты.Номенклатура + "♥" + РазмерЗаготовки);
					Для Каждого Ст ИЗ МассивХлыст Цикл
						НовыйХлыст.Добавить(Ст);	
					КонецЦикла;
					
					МассивДанные.Добавить(НовыйХлыст);
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
	МассивРезультат = Новый Массив();
	
	Если МассивДанные.Количество() > 0 Тогда
		
		Для Каждого МассивХлыст ИЗ МассивДанные Цикл
			Для Каждого Отрезок ИЗ МассивХлыст Цикл
				МассивРезультат.Добавить(Отрезок);
			КонецЦикла;
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат СтроковыеФункцииКлиентСервер.ПолучитьСтрокуИзМассиваПодстрок(МассивРезультат, "♦");
	
КонецФункции

Функция ПолучитьГравировки(НарядСсылка)
	
	Результат = Новый ТаблицаЗначений;
	
	ОбщаяНоменклатура = ЛексСерверПовтИсп.ПолучитьОбщуюНоменклатуруПолностью(НарядСсылка.Подразделение);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивСсылок", НарядСсылка.СписокСпецификаций.ВыгрузитьКолонку("Спецификация"));
	Запрос.УстановитьПараметр("Номенклатура", ОбщаяНоменклатура.Гравировка);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СпецификацияСписокНоменклатуры.Ссылка.Номер КАК НомерСпецификация
	|ИЗ
	|	Документ.Спецификация.СписокНоменклатуры КАК СпецификацияСписокНоменклатуры
	|ГДЕ
	|	СпецификацияСписокНоменклатуры.Номенклатура = &Номенклатура
	|	И СпецификацияСписокНоменклатуры.Ссылка В(&МассивСсылок)";
	
	РезультатЗапроса = Запрос.Выполнить();
	Результат = РезультатЗапроса.Выгрузить();
	
	Возврат Результат;
	
КонецФункции

Функция ПечатьХлыстМатСклад(МассивОбъектов, ОбъектыПечати) Экспорт
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.ИмяПараметровПечати = "ПараметрыПечати_ПечатьХлыстМатСклад";
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Истина;
	ТабДок.ТолькоПросмотр = Истина;
	ТабДок.ОтображатьЗаголовки = Ложь;
	
	Макет = Документы.НарядЗадание.ПолучитьМакет("ПечатьХлыстовойМатериаСклад");
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьПарковка = Макет.ПолучитьОбласть("Парковка");
	
	ОбластьСтрокаСпецификация = Макет.ПолучитьОбласть("СтрокаСпецификация");
	ОбластьСтрокаНоменклатура = Макет.ПолучитьОбласть("СтрокаНоменклатура");	
	
	ВставлятьРазделительСтраниц = Ложь;
	
	Для Каждого Наряд ИЗ МассивОбъектов Цикл
		
		Если ВставлятьРазделительСтраниц Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Наряд", Наряд);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Склад.Номенклатура КАК Номенклатура,
		|	Склад.Номенклатура.ЕдиницаИзмерения КАК ЕдИзм,
		|	Склад.Номенклатура.Парковка КАК Парковка,
		|	Склад.ЕдиницаИзмерения,
		|	Склад.КоличествоСклад КАК Количество,
		|	Склад.Ссылка КАК Спецификация
		|ИЗ
		|	Документ.Спецификация.СкладГотовойПродукции КАК Склад
		|ГДЕ
		|	Склад.Ссылка В
		|			(ВЫБРАТЬ
		|				СписокСпец.Спецификация
		|			ИЗ
		|				Документ.НарядЗадание.СписокСпецификаций КАК СписокСпец
		|			ГДЕ
		|				СписокСпец.Ссылка = &Наряд)
		|	И Склад.КоличествоСклад > 0
		|	И Склад.Номенклатура.НоменклатурнаяГруппа.ВидМатериала = ЗНАЧЕНИЕ(Перечисление.ВидыМатериалов.Хлыстовой)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Парковка,
		|	Склад.Ссылка.Номер,
		|	Склад.Номенклатура.Наименование,
		|	Склад.КоличествоСклад УБЫВ
		|ИТОГИ ПО
		|	Парковка,
		|	Спецификация";
		
		Результат = Запрос.Выполнить();
			
		Если НЕ Результат.Пустой() Тогда

			НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1;
			
			ОбластьЗаголовок.Параметры.НомерНаряда = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(Наряд.Номер);
			ОбластьЗаголовок.Параметры.ДатаНаряда = Формат(Наряд.ДатаИзготовления,"ДФ=dd.MM.yyyy");
			ОбластьЗаголовок.Параметры.НомераСпецификаций = СформироватьНомераСпецификаций(Наряд);
			
			ТабДок.Вывести(ОбластьЗаголовок);
			
			ВыборкаПарковка = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Парковка");
			
			Пока ВыборкаПарковка.Следующий() Цикл
				
				 ОбластьПарковка.Параметры.Парковка = ВыборкаПарковка.Парковка;
				 
				 ТабДок.Вывести(ОбластьПарковка);			 
				 ТабДок.Вывести(ОбластьШапка);
				 
				 ВыборкаСпец = ВыборкаПарковка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Спецификация");
			
				 Пока ВыборкаСпец.Следующий() Цикл
					 
					    ОбластьСтрокаСпецификация.Параметры.Спецификация = ВыборкаСпец.Спецификация;
						ТабДок.Вывести(ОбластьСтрокаСпецификация); 
						 
						ВыборкаНом = ВыборкаСпец.Выбрать();
						
						Пока ВыборкаНом.Следующий() Цикл
					 
							    ОбластьСтрокаНоменклатура.Параметры.Заполнить(ВыборкаНом);
								ТабДок.Вывести(ОбластьСтрокаНоменклатура); 
								
						КонецЦикла;
					 
				 КонецЦикла;
				
			КонецЦикла;
			
			ВставлятьРазделительСтраниц = Истина;
			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, НомерСтрокиНачало, ОбъектыПечати, Наряд);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ТабДок;

КонецФункции