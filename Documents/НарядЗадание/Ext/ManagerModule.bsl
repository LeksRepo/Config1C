
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПечатьНарядЗадания") Тогда
		ПодготовитьПечатнуюФорму("ПечатьНарядЗадания", "Наряд задание", "Документ.НарядЗадание.ПечатьНарядЗадания",
		МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПечатьМатериалПоЦеховымЗонам") Тогда
		ПодготовитьПечатнуюФорму("ПечатьМатериалПоЦеховымЗонам", "Материал по цеховым зонам", "Документ.НарядЗадание.ПечатьМатериалПоЦеховымЗонам",
		МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПечатьМатериалПоВидам") Тогда
		ПодготовитьПечатнуюФорму("ПечатьМатериалПоВидам", "Материал по видам", "Документ.НарядЗадание.ПечатьМатериалПоВидам",
		МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПечатьОТК") Тогда
		ПодготовитьПечатнуюФорму("ПечатьОТК", "Отдел технического контроля", "Документ.НарядЗадание.ПечатьОТК",
		МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПечатьХлыстовойМатериал") Тогда
		ПодготовитьПечатнуюФорму("ПечатьХлыстовойМатериал", "Хлыстовой материал", "Документ.НарядЗадание.ПечатьХлыстовойМатериал",
		МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПечатьЛистовойМатериал") Тогда
		ПодготовитьПечатнуюФорму("ПечатьЛистовойМатериал", "Листовой материал", "Документ.НарядЗадание.ПечатьЛистовойМатериал",
		МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПечатьРаскладка") Тогда
		ПодготовитьПечатнуюФорму("ПечатьРаскладка", "Раскладка", "Документ.НарядЗадание.ПечатьРаскладка",
		МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	КонецЕсли;
	
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
КонецПроцедуры

Процедура ПодготовитьПечатнуюФорму(Знач ИмяМакета, ПредставлениеМакета, ПолныйПутьКМакету = "", МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода)
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, ИмяМакета) Тогда
		
		Если ИмяМакета = "ПечатьНарядЗадания" Тогда
			ТабДок = ПечатьНарядЗадания(МассивОбъектов, ОбъектыПечати);
		ИначеЕсли ИмяМакета = "ПечатьМатериалПоЦеховымЗонам" Тогда
			ТабДок = ПечатьМатериалПоЦеховымЗонам(МассивОбъектов, ОбъектыПечати);
		ИначеЕсли ИмяМакета = "ПечатьМатериалПоВидам" Тогда
			ТабДок = ПечатьМатериалПоВидам(МассивОбъектов, ОбъектыПечати);
		ИначеЕсли ИмяМакета = "ПечатьОТК" Тогда
			ТабДок = ПечатьОТК(МассивОбъектов, ОбъектыПечати);
		ИначеЕсли ИмяМакета = "ПечатьХлыстовойМатериал" Тогда
			ТабДок = ПечатьХлыстовойМатериал(МассивОбъектов, ОбъектыПечати);
		ИначеЕсли ИмяМакета = "ПечатьЛистовойМатериал" Тогда
			ТабДок = ПечатьЛистовойМатериал(МассивОбъектов, ОбъектыПечати);
		ИначеЕсли ИмяМакета = "ПечатьРаскладка" Тогда
			ТабДок = ПечатьРаскладка(МассивОбъектов, ОбъектыПечати);
		КонецЕсли;
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, ИмяМакета, ПредставлениеМакета, ТабДок,, ПолныйПутьКМакету);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПечатьНарядЗадания(МассивОбъектов, ОбъектыПечати) Экспорт
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.ИмяПараметровПечати = "ПараметрыПечати_ПечатьНарядЗадания";
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Ложь;
	ТабДок.ТолькоПросмотр = Истина;
	ТабДок.ОтображатьЗаголовки = Ложь;
	
	ВидыСубконто = Новый Массив;
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("МассивОбъектов", МассивОбъектов);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НарядЗаданиеСписокСпецификаций.Ссылка КАК Ссылка,
	|	НарядЗаданиеСписокСпецификаций.НомерСтроки,
	|	НарядЗаданиеСписокСпецификаций.ДатаИзготовления,
	|	НарядЗаданиеСписокСпецификаций.Спецификация,
	|	НарядЗаданиеСписокСпецификаций.Спецификация.Изделие КАК Изделие,
	|	НарядЗаданиеСписокСпецификаций.Спецификация.Номер КАК НомерСпецификации,
	|	НарядЗаданиеСписокСпецификаций.СуммаНаряда,
	|	НарядЗаданиеСписокСпецификаций.Ссылка.Номер КАК НомерНаряда,
	|	НарядЗаданиеСписокСпецификаций.Ссылка.Дата КАК ДатаНаряда,
	|	НарядЗаданиеСписокСпецификаций.Ссылка.Подразделение,
	|	НарядЗаданиеСписокСпецификаций.Ссылка.СуммаДокумента КАК СуммаДокумента,
	|	НарядЗаданиеСписокСпецификаций.Спецификация.ДатаОтгрузки КАК ДатаОтгрузки,
	|	НарядЗаданиеСписокСпецификаций.Ссылка.ДатаИзготовления КАК ДатаИзготовленияНаряда,
	|	спрЗаметки.ТекстСодержания КАК Заметка,
	|	ВЫБОР
	|		КОГДА Комплектация.Ссылка ЕСТЬ NULL 
	|				ИЛИ НЕ Комплектация.Проведен
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЕстьПроведеннаяКомплектация
	|ИЗ
	|	Документ.НарядЗадание.СписокСпецификаций КАК НарядЗаданиеСписокСпецификаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Заметки КАК спрЗаметки
	|		ПО НарядЗаданиеСписокСпецификаций.Спецификация = спрЗаметки.Предмет
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Комплектация КАК Комплектация
	|		ПО НарядЗаданиеСписокСпецификаций.Спецификация = Комплектация.Спецификация
	|ГДЕ
	|	НарядЗаданиеСписокСпецификаций.Ссылка В(&МассивОбъектов)
	|ИТОГИ ПО
	|	Ссылка";
	
	Макет = Документы.НарядЗадание.ПолучитьМакет("ПечатьНарядЗадания");
	Заголовок = Макет.ПолучитьОбласть("Заголовок");
	Шапка = Макет.ПолучитьОбласть("Шапка");
	СпецификацииШапка = Макет.ПолучитьОбласть("СпецификацииШапка");
	СпецификацииСтрока = Макет.ПолучитьОбласть("СпецификацииСтрока");
	СтрокаЗаметка = Макет.ПолучитьОбласть("СтрокаЗаметка");
	
	ВыборкаДокументы = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВставлятьРазделительСтраниц = Ложь;
	
	МассивСпецификаций=Новый Массив();
	
	Пока ВыборкаДокументы.Следующий() Цикл
		
		Если ВставлятьРазделительСтраниц Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1;
		
		Заголовок.Параметры.НомерНаряда = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(ВыборкаДокументы.НомерНаряда);
		Заголовок.Параметры.ДатаИзготовления = Формат(ВыборкаДокументы.ДатаИзготовленияНаряда, "ДЛФ=DD");
		ТабДок.Вывести(Заголовок);
		
		Шапка.Параметры.Заполнить(ВыборкаДокументы);
		ТабДок.Вывести(Шапка);
		
		ТабДок.Вывести(СпецификацииШапка);
		
		ВыборкаСпецификации = ВыборкаДокументы.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаСпецификации.Следующий() Цикл
			
			МассивСпецификаций.Добавить(ВыборкаСпецификации.Спецификация);
			
			СпецификацииСтрока.Параметры.Заполнить(ВыборкаСпецификации);
			СпецификацииСтрока.Параметры.Изделие = ВыборкаСпецификации.Изделие;
			СпецификацииСтрока.Параметры.НомерСпецификации = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(ВыборкаСпецификации.НомерНаряда);
			СпецификацииСтрока.Параметры.КоличествоДверей = Отчеты.ПроизводственныйОтчет.ПосчитатьКоличествоДверей(ВыборкаСпецификации.Спецификация);
			
			Комплектации = Отчеты.ПроизводственныйОтчет.ЕстьКомплектации(ВыборкаСпецификации.Спецификация);
			
			СпецификацииСтрока.Параметры.КомплектацияЦех = Комплектации.Цех;
			СпецификацииСтрока.Параметры.КомплектацияСклад = Комплектации.Склад;
			
			ТабДок.Вывести(СпецификацииСтрока);
			
			Если ЗначениеЗаполнено(ВыборкаСпецификации.Заметка) Тогда
				СтрокаЗаметка.Параметры.Заметка = ВыборкаСпецификации.Заметка;
				СтрокаЗаметка.Параметры.НомерСпецификации = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(ВыборкаСпецификации.НомерСпецификации);
				ТабДок.Вывести(СтрокаЗаметка);
			КонецЕсли;
			
		КонецЦикла;
		
		Отчеты.ПроизводственныйОтчет.ВывестиПроизводственныйОтчет(ТабДок, ВыборкаДокументы.Подразделение, МассивОбъектов, Новый Структура);
		
		ВставлятьРазделительСтраниц = Истина;
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументы.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции

Функция ПечатьМатериалПоЦеховымЗонам(МассивОбъектов, ОбъектыПечати) Экспорт
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.ИмяПараметровПечати = "ПараметрыПечати_ПечатьМатериалПоЦеховымЗонамть";
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Ложь;
	ТабДок.ТолькоПросмотр = Истина;
	ТабДок.ОтображатьЗаголовки = Ложь;
	
	Макет = Документы.НарядЗадание.ПолучитьМакет("ПечатьМатериалПоЦеховымЗонам");
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьСписокНоменклатурыШапка = Макет.ПолучитьОбласть("СписокНоменклатурыШапка");
	ОбластьСписокНоменклатурыСтрока = Макет.ПолучитьОбласть("СписокНоменклатурыСтрока");
	ОбластьМатериалЗаказчика = Макет.ПолучитьОбласть("МатериалЗаказчика");
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("МассивНарядов", МассивОбъектов);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НарядЗаданиеСписокНоменклатуры.Номенклатура КАК Номенклатура,
	|	НарядЗаданиеСписокНоменклатуры.Ссылка КАК Ссылка,
	|	НарядЗаданиеСписокНоменклатуры.КоличествоТребуется КАК КоличествоТребуется,
	|	НарядЗаданиеСписокНоменклатуры.Номенклатура.ЦеховаяЗона КАК ЦеховаяЗона,
	|	НарядЗаданиеСписокНоменклатуры.НомерСтроки КАК НомерСтроки,
	|	НарядЗаданиеСписокНоменклатуры.Ссылка.Номер КАК НомерДокумента,
	|	НарядЗаданиеСписокНоменклатуры.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	НарядЗаданиеСписокНоменклатуры.Ссылка.Дата КАК ДатаДокумента,
	|	НарядЗаданиеСписокНоменклатурыПоСпецификациям.Спецификация.Номер КАК НомерСпецификации
	|ИЗ
	|	Документ.НарядЗадание.СписокНоменклатуры КАК НарядЗаданиеСписокНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.НарядЗадание.СписокНоменклатурыПоСпецификациям КАК НарядЗаданиеСписокНоменклатурыПоСпецификациям
	|		ПО НарядЗаданиеСписокНоменклатуры.Ссылка = НарядЗаданиеСписокНоменклатурыПоСпецификациям.Ссылка
	|			И (НарядЗаданиеСписокНоменклатуры.Номенклатура = НарядЗаданиеСписокНоменклатурыПоСпецификациям.Номенклатура
	|				ИЛИ НарядЗаданиеСписокНоменклатуры.Номенклатура.БазоваяНоменклатура = НарядЗаданиеСписокНоменклатурыПоСпецификациям.Номенклатура)
	|ГДЕ
	|	НарядЗаданиеСписокНоменклатуры.Ссылка В(&МассивНарядов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НарядЗаданиеСписокНоменклатуры.Номенклатура.Наименование
	|ИТОГИ
	|	МАКСИМУМ(КоличествоТребуется)
	|ПО
	|	Ссылка,
	|	ЦеховаяЗона,
	|	Номенклатура";
	
	ВставлятьРазделительСтраниц = Ложь;
	ВыборкаДокументы = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаДокументы.Следующий() Цикл
		
		Если ВставлятьРазделительСтраниц Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1;
		
		ОбластьЗаголовок.Параметры.НомерДокумента = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(ВыборкаДокументы.НомерДокумента);
		ОбластьЗаголовок.Параметры.ДатаДокумента = Формат(ВыборкаДокументы.ДатаДокумента, "ДЛФ=DD");
		ОбластьЗаголовок.Параметры.НомераСпецификаций = СформироватьНомераСпецификаций(ВыборкаДокументы.Ссылка);
		
		ТабДок.Вывести(ОбластьЗаголовок);
		
		НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1;
		
		ВыборкаЦеховаяЗона = ВыборкаДокументы.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаЦеховаяЗона.Следующий() Цикл
			
			ОбластьСписокНоменклатурыШапка.Параметры.ЦеховаяЗона = ВыборкаЦеховаяЗона.ЦеховаяЗона;
			
			ТабДок.Вывести(ОбластьСписокНоменклатурыШапка);
			
			ВыборкаНоменклатура = ВыборкаЦеховаяЗона.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаНоменклатура.Следующий() Цикл
				
				ВыборкаСпец = ВыборкаНоменклатура.Выбрать();
				НомераСпецификаций = "";
				
				Пока ВыборкаСпец.Следующий() Цикл
					
					Номер = ПрефиксацияОбъектовКлиентСервер.УдалитьЛидирующиеНулиИзНомераОбъекта(ВыборкаСпец.НомерСпецификации);
					НомераСпецификаций = НомераСпецификаций + Номер + ", ";
					
				КонецЦикла;
				
				ОбластьСписокНоменклатурыСтрока.Параметры.Заполнить(ВыборкаНоменклатура);
				НомераСпецификаций = Лев(НомераСпецификаций,СтрДлина(НомераСпецификаций)-2);
				
				ОбластьСписокНоменклатурыСтрока.Параметры.Спецификации = НомераСпецификаций;
				ТабДок.Вывести(ОбластьСписокНоменклатурыСтрока);
				
			КонецЦикла;
			
		КонецЦикла;
		
		ВставлятьРазделительСтраниц = Истина;
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументы.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции

Функция ПечатьОТК(Документ, ОбъектыПечати) Экспорт
		
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.ИмяПараметровПечати = "ПараметрыПечати_ОтделТехническогоКонтроля";
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Ложь;
	ТабДок.ТолькоПросмотр = Истина;
	ТабДок.ОтображатьЗаголовки = Ложь;
	
	Отчеты.ОтделТехническогоКонтроля.СформироватьОтчет(ТабДок, Документ.Подразделение, ТекущаяДата());
	
	Возврат ТабДок;
	
КонецФункции

Функция ПечатьРаскладка(МассивОбъектов, ОбъектыПечати) Экспорт
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.ИмяПараметровПечати = "ПараметрыПечати_ОтделТехническогоКонтроля";
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабДок.Защита = Ложь;
	ТабДок.ТолькоПросмотр = Истина;
	ТабДок.ОтображатьЗаголовки = Ложь;
	ТаблицаСпецификаций = МассивОбъектов.СписокСпецификаций.Выгрузить();
	МассивСпецификаций = ТаблицаСпецификаций.ВыгрузитьКолонку("Спецификация");
	Макет = Документы.НарядЗадание.ПолучитьМакет("Раскладка");
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьДетальСтрока = Макет.ПолучитьОбласть("ДетальСтрока");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивСпецификаций", МассивСпецификаций);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РаскройДеталей.Объект КАК Спецификация,
	|	РаскройДеталей.ТаблицаДеталей
	|ИЗ
	|	РегистрСведений.РаскройДеталей КАК РаскройДеталей
	|ГДЕ
	|	РаскройДеталей.Объект В (&МассивСпецификаций)";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	ВыборкаЗонаФРС = Новый ТаблицаЗначений;
	ВыборкаЗонаФРС.Колонки.Добавить("Спецификация", Новый ОписаниеТипов("ДокументСсылка.Спецификация"));
	ВыборкаЗонаФРС.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ВыборкаЗонаФРС.Колонки.Добавить("Парковка", Новый ОписаниеТипов("Число"));
	
	ВыборкаЗонаФасадов = ВыборкаЗонаФРС.СкопироватьКолонки();
	
	ВыборкаЗонаФРС.Колонки.Добавить("ФРСКоличество", Новый ОписаниеТипов("Число"));
	ВыборкаЗонаФРС.Колонки.Добавить("ФРСВысота", Новый ОписаниеТипов("Число"));
	ВыборкаЗонаФРС.Колонки.Добавить("ФРСШирина", Новый ОписаниеТипов("Число"));
	
	ВыборкаЗонаФасадов.Колонки.Добавить("ФасадыКоличество", Новый ОписаниеТипов("Число"));
	ВыборкаЗонаФасадов.Колонки.Добавить("ФасадыВысота", Новый ОписаниеТипов("Число"));
	ВыборкаЗонаФасадов.Колонки.Добавить("ФасадыШирина", Новый ОписаниеТипов("Число"));

	Пока Выборка.Следующий() Цикл
		
		НомерЛистаЗонаФРС = 0;
		НомерЛистаЗонаФасадов = 0;
		тзДетали = Выборка.ТаблицаДеталей.Получить();
		ТаблицаДляТестовЗонаФРС = тзДетали.СкопироватьКолонки();
		ТаблицаДляТестовЗонаФасадов = тзДетали.СкопироватьКолонки();
		
		Для каждого Строка Из тзДетали Цикл
			
			НоменклатурнаяГруппа = Строка.Номенклатура.НоменклатурнаяГруппа;
			
			Если (НоменклатурнаяГруппа = Справочники.НоменклатурныеГруппы.ДВП 
				ИЛИ НоменклатурнаяГруппа = Справочники.НоменклатурныеГруппы.ЛДСП16
				ИЛИ НоменклатурнаяГруппа = Справочники.НоменклатурныеГруппы.ЛДСП22
				ИЛИ НоменклатурнаяГруппа = Справочники.НоменклатурныеГруппы.ЛДСП26
				ИЛИ НоменклатурнаяГруппа = Справочники.НоменклатурныеГруппы.МДФ18
				ИЛИ НоменклатурнаяГруппа = Справочники.НоменклатурныеГруппы.Пристенок
				ИЛИ НоменклатурнаяГруппа = Справочники.НоменклатурныеГруппы.Столешница) и Строка.ПоследнийЛист
				Тогда
					НоваяСтрока = ТаблицаДляТестовЗонаФРС.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
				
			ИначеЕсли (НоменклатурнаяГруппа = Справочники.НоменклатурныеГруппы.ЗеркалоДекоративное
				ИЛИ НоменклатурнаяГруппа = Справочники.НоменклатурныеГруппы.Зеркало
				ИЛИ НоменклатурнаяГруппа = Справочники.НоменклатурныеГруппы.Стекло
				ИЛИ НоменклатурнаяГруппа = Справочники.НоменклатурныеГруппы.АГТПанель
				ИЛИ НоменклатурнаяГруппа = Справочники.НоменклатурныеГруппы.ЛДСП10
				ИЛИ НоменклатурнаяГруппа = Справочники.НоменклатурныеГруппы.МДФ8
				ИЛИ НоменклатурнаяГруппа = Справочники.НоменклатурныеГруппы.МДФ10
				ИЛИ НоменклатурнаяГруппа = Справочники.НоменклатурныеГруппы.ЩитМебельный) И Строка.ПоследнийЛист
				Тогда
					НоваяСтрока = ТаблицаДляТестовЗонаФасадов.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
				
			КонецЕсли;
			
		КонецЦикла;
		Если ТаблицаДляТестовЗонаФРС.Количество() > 0 Тогда
			ТаблицаДляТестовЗонаФРС.Свернуть("ВысотаДетали, Номенклатура, ШиринаДетали, НомерЛиста, ВсегоЛистов", "Количество");
		КонецЕсли;
		Если ТаблицаДляТестовЗонаФасадов.Количество() > 0 Тогда
			ТаблицаДляТестовЗонаФасадов.Свернуть("ВысотаДетали, Номенклатура, ШиринаДетали, НомерЛиста, ВсегоЛистов", "Количество");
		КонецЕсли;
		НоменклатурнаяГруппа = Справочники.НоменклатурныеГруппы.ПустаяСсылка();
		
		Для каждого Строка Из ТаблицаДляТестовЗонаФРС Цикл
			
			Если НоменклатурнаяГруппа <> Строка.Номенклатура.НоменклатурнаяГруппа И Строка.ВсегоЛистов > 1 Тогда
				НоваяСтрока = ВыборкаЗонаФРС.Добавить();
				НоваяСтрока.Спецификация = Выборка.Спецификация;
				НоваяСтрока.Номенклатура = Строка.Номенклатура;
				НоваяСтрока.Парковка = Строка.Номенклатура.Парковка;
				НоваяСтрока.ФРСКоличество = Строка.ВсегоЛистов - 1;
				НоваяСтрока.ФРСВысота = Строка.Номенклатура.ДлинаДетали;
				НоваяСтрока.ФРСШирина = Строка.Номенклатура.ШиринаДетали;
				НоменклатурнаяГруппа = Строка.Номенклатура.НоменклатурнаяГруппа;
			КонецЕсли;
			
			НоваяСтрока = ВыборкаЗонаФРС.Добавить();
			НоваяСтрока.Спецификация = Выборка.Спецификация;
			НоваяСтрока.Номенклатура = Строка.Номенклатура;
			НоваяСтрока.Парковка = Строка.Номенклатура.Парковка;
			НоваяСтрока.ФРСКоличество = Строка.Количество;
			НоваяСтрока.ФРСВысота = Строка.ВысотаДетали;
			НоваяСтрока.ФРСШирина = Строка.ШиринаДетали;
			
		КонецЦикла;
		
		НоменклатурнаяГруппа = Справочники.НоменклатурныеГруппы.ПустаяСсылка();
		
		Для каждого Строка Из ТаблицаДляТестовЗонаФасадов Цикл
			
			Если НоменклатурнаяГруппа <> Строка.Номенклатура.НоменклатурнаяГруппа И Строка.ВсегоЛистов > 1 Тогда
				НоваяСтрока = ВыборкаЗонаФасадов.Добавить();
				НоваяСтрока.Спецификация = Выборка.Спецификация;
				НоваяСтрока.Номенклатура = Строка.Номенклатура;
				НоваяСтрока.Парковка = Строка.Номенклатура.Парковка;
				НоваяСтрока.ФасадыКоличество = Строка.ВсегоЛистов - 1;
				НоваяСтрока.ФасадыВысота = Строка.Номенклатура.ДлинаДетали;
				НоваяСтрока.ФасадыШирина = Строка.Номенклатура.ШиринаДетали;
				НоменклатурнаяГруппа = Строка.Номенклатура.НоменклатурнаяГруппа;
			КонецЕсли;
			
			НоваяСтрока = ВыборкаЗонаФасадов.Добавить();
			НоваяСтрока.Спецификация = Выборка.Спецификация;
			НоваяСтрока.Номенклатура = Строка.Номенклатура;
			НоваяСтрока.Парковка = Строка.Номенклатура.Парковка;
			НоваяСтрока.ФасадыКоличество = Строка.Количество;
			НоваяСтрока.ФасадыВысота = Строка.ВысотаДетали;
			НоваяСтрока.ФасадыШирина = Строка.ШиринаДетали;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивСпецификаций", МассивСпецификаций);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СпецификацияСписокНоменклатуры.Ссылка КАК Спецификация,
		|	СпецификацияСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СпецификацияСписокНоменклатуры.Количество КАК ХлыстовойДлина,
		|	СпецификацияСписокНоменклатуры.ЕдиницаИзмерения,
		|	СпецификацияСписокНоменклатуры.Номенклатура.Парковка КАК Парковка
		|ИЗ
		|	Документ.Спецификация.СписокНоменклатуры КАК СпецификацияСписокНоменклатуры
		|ГДЕ
		|	СпецификацияСписокНоменклатуры.Ссылка В(&МассивСпецификаций)
		|	И СпецификацияСписокНоменклатуры.Номенклатура.НоменклатурнаяГруппа.ВидМатериала = ЗНАЧЕНИЕ(Перечисление.ВидыМатериалов.Хлыстовой)
		|	И СпецификацияСписокНоменклатуры.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Материал)
		|	И СпецификацияСписокНоменклатуры.Номенклатура.МестоОбработки = ЗНАЧЕНИЕ(Перечисление.МестоОбработки.Цех)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СпецификацияСписокНоменклатуры.Ссылка КАК Спецификация,
		|	СпецификацияСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СпецификацияСписокНоменклатуры.Количество КАК РулонныйКоличество,
		|	СпецификацияСписокНоменклатуры.ЕдиницаИзмерения,
		|	СпецификацияСписокНоменклатуры.Номенклатура.Парковка КАК Парковка
		|ИЗ
		|	Документ.Спецификация.СписокНоменклатуры КАК СпецификацияСписокНоменклатуры
		|ГДЕ
		|	СпецификацияСписокНоменклатуры.Ссылка В(&МассивСпецификаций)
		|	И СпецификацияСписокНоменклатуры.Номенклатура.НоменклатурнаяГруппа.ВидМатериала = ЗНАЧЕНИЕ(Перечисление.ВидыМатериалов.Рулонный)
		|	И СпецификацияСписокНоменклатуры.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Материал)
		|	И СпецификацияСписокНоменклатуры.Номенклатура.МестоОбработки = ЗНАЧЕНИЕ(Перечисление.МестоОбработки.Цех)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СпецификацияСписокНоменклатуры.Ссылка КАК Спецификация,
		|	СпецификацияСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СпецификацияСписокНоменклатуры.Количество КАК ШтучныйКоличество,
		|	СпецификацияСписокНоменклатуры.ЕдиницаИзмерения,
		|	СпецификацияСписокНоменклатуры.Номенклатура.Парковка КАК Парковка
		|ИЗ
		|	Документ.Спецификация.СписокНоменклатуры КАК СпецификацияСписокНоменклатуры
		|ГДЕ
		|	СпецификацияСписокНоменклатуры.Ссылка В(&МассивСпецификаций)
		|	И СпецификацияСписокНоменклатуры.Номенклатура.НоменклатурнаяГруппа.ВидМатериала = ЗНАЧЕНИЕ(Перечисление.ВидыМатериалов.Штучный)
		|	И СпецификацияСписокНоменклатуры.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Материал)
		|	И СпецификацияСписокНоменклатуры.Номенклатура.МестоОбработки = ЗНАЧЕНИЕ(Перечисление.МестоОбработки.Цех)";
		
	МассивРезультатовЗапроса = Запрос.ВыполнитьПакет();
	//ВыборкаЛистовогоМатериала = МассивРезультатовЗапроса[0].Выгрузить();
	//ВыборкаЗеркала = МассивРезультатовЗапроса[1].Выгрузить();
	ВыборкаХлыстовогоМатериала = МассивРезультатовЗапроса[0].Выгрузить();
	Если ВыборкаХлыстовогоМатериала.Количество() > 0 Тогда
		ВыборкаХлыстовогоМатериала.Свернуть("Спецификация, Номенклатура, ЕдиницаИзмерения", "ХлыстовойДлина");
	КонецЕсли;
	ВыборкаРулонногоМатериала = МассивРезультатовЗапроса[1].Выгрузить();
	Если ВыборкаРулонногоМатериала.Количество() > 0 Тогда
		ВыборкаРулонногоМатериала.Свернуть("Спецификация, Номенклатура, ЕдиницаИзмерения", "РулонныйКоличество");
	КонецЕсли;
	ВыборкаШтучногоМатериала = МассивРезультатовЗапроса[2].Выгрузить();
	Если ВыборкаШтучногоМатериала.Количество() > 0 Тогда
		ВыборкаШтучногоМатериала.Свернуть("Спецификация, Номенклатура, ЕдиницаИзмерения", "ШтучныйКоличество");
	КонецЕсли;
	НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1;
	
	ОбластьЗаголовок.Параметры.НомерНаряда = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(МассивОбъектов.Номер);
	ОбластьЗаголовок.Параметры.ДатаНаряда = Формат(МассивОбъектов.Дата, "ДЛФ=DD");
	ОбластьЗаголовок.Параметры.НомераСпецификаций = СформироватьНомераСпецификаций(МассивОбъектов.Ссылка);
	
	ТабДок.Вывести(ОбластьЗаголовок);
	ТабДок.Вывести(ОбластьШапка);
	ВставлятьРазделительСтраниц = Ложь;
	ВыборкаЗонаФРС.Сортировать("Номенклатура убыв, ФРСШирина убыв, ФРСВысота убыв");
	ВыборкаЗонаФасадов.Сортировать("Номенклатура");
	ВыборкаХлыстовогоМатериала.Сортировать("Номенклатура");
	ВыборкаРулонногоМатериала.Сортировать("Номенклатура");
	ВыборкаШтучногоМатериала.Сортировать("Номенклатура");
	
	Для каждого Спецификация Из МассивСпецификаций Цикл
		
		ОбластьДетальСтрока.Параметры.СпецификацияНомер = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(Спецификация.Номер);
		ТабДок.Вывести(ОбластьДетальСтрока);
		ОбластьДетальСтрока.Параметры.СпецификацияНомер = "";
		
		Для Каждого Строка Из ВыборкаЗонаФРС Цикл
			
			Если Строка.Спецификация = Спецификация Тогда
			
				ОбластьДетальСтрока.Параметры.Заполнить(Строка);
				ТабДок.Вывести(ОбластьДетальСтрока);
			
			КонецЕсли;
			
		КонецЦикла;
		
		ОбластьДетальСтрока = Макет.ПолучитьОбласть("ДетальСтрока");
		
		Для Каждого Строка Из ВыборкаЗонаФасадов Цикл
			
			Если Строка.Спецификация = Спецификация Тогда
			
				ОбластьДетальСтрока.Параметры.Заполнить(Строка);
				ТабДок.Вывести(ОбластьДетальСтрока);
			
			КонецЕсли;
			
		КонецЦикла;
		
		ОбластьДетальСтрока = Макет.ПолучитьОбласть("ДетальСтрока");
		
		Для Каждого Строка Из ВыборкаХлыстовогоМатериала Цикл
		
			Если Строка.Спецификация = Спецификация Тогда
			
				ОбластьДетальСтрока.Параметры.Заполнить(Строка);
				ТабДок.Вывести(ОбластьДетальСтрока);
			
			КонецЕсли;
		
		КонецЦикла;
		
		ОбластьДетальСтрока = Макет.ПолучитьОбласть("ДетальСтрока");
		
		Для Каждого Строка Из ВыборкаРулонногоМатериала Цикл
		
			Если Строка.Спецификация = Спецификация Тогда
			
				ОбластьДетальСтрока.Параметры.Заполнить(Строка);
				ТабДок.Вывести(ОбластьДетальСтрока);
			
			КонецЕсли;
		
		КонецЦикла;
		
		ОбластьДетальСтрока = Макет.ПолучитьОбласть("ДетальСтрока");
		
		Для Каждого Строка Из ВыборкаШтучногоМатериала Цикл
		
			Если Строка.Спецификация = Спецификация Тогда
			
				ОбластьДетальСтрока.Параметры.Заполнить(Строка);
				ТабДок.Вывести(ОбластьДетальСтрока);
			
			КонецЕсли;
		
		КонецЦикла;
		
		ОбластьДетальСтрока = Макет.ПолучитьОбласть("ДетальСтрока");
	
	КонецЦикла;
	
	УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, НомерСтрокиНачало, ОбъектыПечати, МассивОбъектов.Ссылка);
	
	Возврат ТабДок;
	
КонецФункции

Функция ПечатьМатериалПоВидам(МассивОбъектов, ОбъектыПечати) Экспорт
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.ИмяПараметровПечати = "ПараметрыПечати_ПечатьМатериалПоВидам";
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Ложь;
	ТабДок.ТолькоПросмотр = Истина;
	ТабДок.ОтображатьЗаголовки = Ложь;
	
	Макет = Документы.НарядЗадание.ПолучитьМакет("ПечатьМатериалПоВидам");
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьНоменклатураШапка = Макет.ПолучитьОбласть("НоменклатураШапка");
	ОбластьНоменклатураСтрока = Макет.ПолучитьОбласть("НоменклатураСтрока");
	ОбластьСпецификацияСтрока = Макет.ПолучитьОбласть("СпецификацияСтрока");
	
	ОбластьМатериалЗаказчикаШапка = Макет.ПолучитьОбласть("МатериалЗаказчикаШапка");
	ОбластьМатериалЗаказчикаСтрока = Макет.ПолучитьОбласть("МатериалЗаказчикаСтрока");
	
	ОбластьКомплектацииШапка = Макет.ПолучитьОбласть("КомплектацииШапка");
	ОбластьКомплектацииСтрока = Макет.ПолучитьОбласть("КомплектацииСтрока");
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("МассивНарядов", МассивОбъектов);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НарядЗаданиеСписокНоменклатуры.Ссылка КАК НарядСсылка,
	|	НарядЗаданиеСписокНоменклатуры.Ссылка.Номер КАК НомерНаряда,
	|	НарядЗаданиеСписокНоменклатуры.Ссылка.Дата КАК ДатаНаряда,
	|	НарядЗаданиеСписокНоменклатуры.Номенклатура КАК Номенклатура,
	|	НарядЗаданиеСписокНоменклатуры.Номенклатура.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	НарядЗаданиеСписокНоменклатуры.Номенклатура.КраткоеНаименование КАК КраткоеНаименование,
	|	НарядЗаданиеСписокНоменклатуры.КоличествоТребуется КАК КоличествоТребуется,
	|	НарядЗаданиеСписокНоменклатуры.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	НарядЗаданиеСписокНоменклатурыПоСпецификациям.Спецификация.Номер КАК НомерСпецификации,
	|	НарядЗаданиеСписокНоменклатурыПоСпецификациям.Спецификация КАК Спецификация,
	|	НарядЗаданиеСписокНоменклатурыПоСпецификациям.Номенклатура.ЕдиницаИзмерения КАК НоменклатураСпецификацииЕдиницаИзмерения,
	|	НарядЗаданиеСписокНоменклатурыПоСпецификациям.Номенклатура.НоменклатурнаяГруппа.ВидМатериала КАК ВидМатериала,
	|	НарядЗаданиеСписокНоменклатурыПоСпецификациям.Количество КАК КоличествоВСпецификации
	|ИЗ
	|	Документ.НарядЗадание.СписокНоменклатуры КАК НарядЗаданиеСписокНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.НарядЗадание.СписокНоменклатурыПоСпецификациям КАК НарядЗаданиеСписокНоменклатурыПоСпецификациям
	|		ПО НарядЗаданиеСписокНоменклатуры.Ссылка = НарядЗаданиеСписокНоменклатурыПоСпецификациям.Ссылка
	|			И (НарядЗаданиеСписокНоменклатуры.Номенклатура = НарядЗаданиеСписокНоменклатурыПоСпецификациям.Номенклатура
	|				ИЛИ НарядЗаданиеСписокНоменклатуры.Номенклатура.БазоваяНоменклатура = НарядЗаданиеСписокНоменклатурыПоСпецификациям.Номенклатура)
	|ГДЕ
	|	НарядЗаданиеСписокНоменклатуры.Ссылка В(&МассивНарядов)
	|	И НЕ НарядЗаданиеСписокНоменклатурыПоСпецификациям.ПредоставитЗаказчик
	|
	|УПОРЯДОЧИТЬ ПО
	|	НарядЗаданиеСписокНоменклатуры.Номенклатура.НоменклатурнаяГруппа.ВидМатериала,
	|	НарядЗаданиеСписокНоменклатуры.Номенклатура.НоменклатурнаяГруппа
	|ИТОГИ
	|	МАКСИМУМ(КоличествоТребуется),
	|	МАКСИМУМ(ЕдиницаИзмерения)
	|ПО
	|	НарядСсылка,
	|	ВидМатериала,
	|	Номенклатура";
	
	ВставлятьРазделительСтраниц = Ложь;
	ВыборкаДокументы = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаДокументы.Следующий() Цикл
		
		Если ВставлятьРазделительСтраниц Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1;
		
		ОбластьЗаголовок.Параметры.НомерНаряда = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(ВыборкаДокументы.НомерНаряда);
		ОбластьЗаголовок.Параметры.ДатаНаряда = Формат(ВыборкаДокументы.ДатаНаряда, "ДЛФ=DD");
		ОбластьЗаголовок.Параметры.НомераСпецификаций = СформироватьНомераСпецификаций(ВыборкаДокументы.НарядСсылка);
		
		ТабДок.Вывести(ОбластьЗаголовок);
		
		ВыборкаВидМатериала = ВыборкаДокументы.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаВидМатериала.Следующий() Цикл
			
			ОбластьНоменклатураШапка.Параметры.ВидМатериала = ВыборкаВидМатериала.ВидМатериала;
			ТабДок.Вывести(ОбластьНоменклатураШапка);
			
			ВыборкаНоменклатура = ВыборкаВидМатериала.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаНоменклатура.Следующий() Цикл
				
				ОбластьНоменклатураСтрока.Параметры.Заполнить(ВыборкаНоменклатура);
				
				Если ВыборкаНоменклатура.НоменклатурнаяГруппа.ПринадлежитЭлементу(Справочники.НоменклатурныеГруппы.Кант)
					ИЛИ ВыборкаНоменклатура.НоменклатурнаяГруппа.ПринадлежитЭлементу(Справочники.НоменклатурныеГруппы.Кромка) Тогда
					
					ОбластьНоменклатураСтрока.Параметры.КраткоеНаименованиеТорцевойЛенты = "кратк: " + ВыборкаНоменклатура.КраткоеНаименование;
				Иначе
					ОбластьНоменклатураСтрока.Параметры.КраткоеНаименованиеТорцевойЛенты = "";
				КонецЕсли;
				
				ТабДок.Вывести(ОбластьНоменклатураСтрока);
				
				ВыборкаСпецификация = ВыборкаНоменклатура.Выбрать();
				Пока ВыборкаСпецификация.Следующий() Цикл
					
					ОбластьСпецификацияСтрока.Параметры.Заполнить(ВыборкаСпецификация);
					ОбластьСпецификацияСтрока.Параметры.НомерСпецификации = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(ВыборкаСпецификация.НомерСпецификации);
					ТабДок.Вывести(ОбластьСпецификацияСтрока);
					
				КонецЦикла;
				
			КонецЦикла;
			
			// { Васильев Александр Леонидович [09.09.2015]
			// Сделка с совестью, запрос в цикле.
			// Видов материала всего 4, поэтому терпимо.
			// Иначе запрос и обработка слишком усложняется.
			// } Васильев Александр Леонидович [09.09.2015]
			
			ЗапросМатериалЗаказчика = Новый Запрос;
			ЗапросМатериалЗаказчика.Параметры.Вставить("Наряд", ВыборкаДокументы.НарядСсылка);
			ЗапросМатериалЗаказчика.Параметры.Вставить("ВидМатериала", ВыборкаВидМатериала.ВидМатериала);
			ЗапросМатериалЗаказчика.Текст =
			"ВЫБРАТЬ
			|	НарядЗаданиеСписокНоменклатурыПоСпецификациям.Спецификация.Номер КАК НомерСпецификации,
			|	НарядЗаданиеСписокНоменклатурыПоСпецификациям.Спецификация КАК Спецификация,
			|	НарядЗаданиеСписокНоменклатурыПоСпецификациям.Номенклатура КАК Номенклатура,
			|	НарядЗаданиеСписокНоменклатурыПоСпецификациям.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
			|	НарядЗаданиеСписокНоменклатурыПоСпецификациям.Количество КАК Количество
			|ИЗ
			|	Документ.НарядЗадание.СписокНоменклатурыПоСпецификациям КАК НарядЗаданиеСписокНоменклатурыПоСпецификациям
			|ГДЕ
			|	НарядЗаданиеСписокНоменклатурыПоСпецификациям.ПредоставитЗаказчик
			|	И НарядЗаданиеСписокНоменклатурыПоСпецификациям.Ссылка = &Наряд
			|	И НарядЗаданиеСписокНоменклатурыПоСпецификациям.Номенклатура.НоменклатурнаяГруппа.ВидМатериала = &ВидМатериала";
			
			РезультатЗапроса = ЗапросМатериалЗаказчика.Выполнить();
			
			Если НЕ РезультатЗапроса.Пустой() Тогда
				
				ТабДок.Вывести(ОбластьМатериалЗаказчикаШапка);
				ВыборкаМатериалЗаказчика = РезультатЗапроса.Выбрать();
				
				Пока ВыборкаМатериалЗаказчика.Следующий() Цикл
					
					ОбластьМатериалЗаказчикаСтрока.Параметры.Заполнить(ВыборкаМатериалЗаказчика);
					ОбластьМатериалЗаказчикаСтрока.Параметры.НомерСпецификации = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(ВыборкаМатериалЗаказчика.НомерСпецификации);
					ТабДок.Вывести(ОбластьМатериалЗаказчикаСтрока);
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЦикла; // ВыборкаВидМатериала.Следующий()
		
		// { Васильев Александр Леонидович [09.10.2015]
		// Здесь вывести комплектации.
		// } Васильев Александр Леонидович [09.10.2015]
		
		ТабДок.Вывести(ОбластьКомплектацииШапка);
		
		Для каждого Строка Из ВыборкаДокументы.НарядСсылка.СписокСпецификаций Цикл
			
			Спецификация = Строка.Спецификация;
			СвойстваСпецификации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Спецификация, "Номер, КомплектацияЦех, КомплектацияСклад");
			ОбластьКомплектацииСтрока.Параметры.Заполнить(СвойстваСпецификации);
			ОбластьКомплектацииСтрока.Параметры.Спецификация = Спецификация;
			ОбластьКомплектацииСтрока.Параметры.НомерСпецификации = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(СвойстваСпецификации.Номер);
			ТабДок.Вывести(ОбластьКомплектацииСтрока);
			
		КонецЦикла;
		
		ВставлятьРазделительСтраниц = Истина;
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументы.НарядСсылка);
		
	КонецЦикла; // ВыборкаДокументы.Следующий()
	
	Возврат ТабДок;
	
КонецФункции

Функция ПечатьХлыстовойМатериал(МассивОбъектов, ОбъектыПечати) Экспорт
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.ИмяПараметровПечати = "ПараметрыПечати_ПечатьХлыстовойМатериал";
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Ложь;
	ТабДок.ТолькоПросмотр = Истина;
	ТабДок.ОтображатьЗаголовки = Ложь;
	
	Макет = Документы.НарядЗадание.ПолучитьМакет("ПечатьХлыстовойМатериал");
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьХлыстСтрока = Макет.ПолучитьОбласть("ХлыстСтрока");
	ОбластьСпецификацияСтрока = Макет.ПолучитьОбласть("СпецификацияСтрока");
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("МассивНарядов", МассивОбъектов);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НарядЗаданиеРаскройХлыстов.Ссылка КАК Ссылка,
	|	НарядЗаданиеРаскройХлыстов.Ссылка.Номер КАК НомерНаряда,
	|	НарядЗаданиеРаскройХлыстов.Ссылка.Дата КАК ДатаНаряда,
	|	НарядЗаданиеРаскройХлыстов.Количество КАК РазмерОтрезка,
	|	НарядЗаданиеРаскройХлыстов.Номенклатура КАК Номенклатура,
	|	НарядЗаданиеРаскройХлыстов.НомерХлыста КАК НомерХлыста,
	|	НарядЗаданиеРаскройХлыстов.Остаток,
	|	НарядЗаданиеРаскройХлыстов.РазмерХлыста КАК РазмерХлыста,
	|	НарядЗаданиеРаскройХлыстов.Спецификация,
	|	НарядЗаданиеРаскройХлыстов.Спецификация.Номер КАК НомерСпецификации,
	|	НарядЗаданиеРаскройХлыстов.Двери КАК Двери
	|ИЗ
	|	Документ.НарядЗадание.РаскройХлыстов КАК НарядЗаданиеРаскройХлыстов
	|ГДЕ
	|	НарядЗаданиеРаскройХлыстов.Ссылка В(&МассивНарядов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НарядЗаданиеРаскройХлыстов.Номенклатура.Цвет,
	|	НомерСпецификации,
	|	НомерХлыста
	|ИТОГИ
	|	МАКСИМУМ(РазмерХлыста)
	|ПО
	|	Ссылка,
	|	Номенклатура,
	|	НомерХлыста";
	
	ВставлятьРазделительСтраниц = Ложь;
	
	ВыборкаДокументы = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаДокументы.Следующий() Цикл
		
		Если ВставлятьРазделительСтраниц Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1;
		
		ОбластьЗаголовок.Параметры.НомерНаряда = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(ВыборкаДокументы.НомерНаряда);
		ОбластьЗаголовок.Параметры.ДатаНаряда = Формат(ВыборкаДокументы.ДатаНаряда, "ДЛФ=DD");
		ОбластьЗаголовок.Параметры.НомераСпецификаций = СформироватьНомераСпецификаций(ВыборкаДокументы.Ссылка);
		
		ТабДок.Вывести(ОбластьЗаголовок);
		ТабДок.Вывести(ОбластьШапка);
		
		ВыборкаНоменклатура = ВыборкаДокументы.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаНоменклатура.Следующий() Цикл
			
			ВыборкаХлысты = ВыборкаНоменклатура.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаХлысты.Следующий() Цикл
				
				ОбластьХлыстСтрока.Параметры.Заполнить(ВыборкаХлысты);
				ТабДок.Вывести(ОбластьХлыстСтрока);
				
				РазмерЗаготовки = ВыборкаХлысты.РазмерХлыста;
				
				ВыборкаСпецификации = ВыборкаХлысты.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаСпецификации.Следующий() Цикл
					
					Если НЕ ВыборкаСпецификации.Остаток Тогда
						ОбластьСпецификацияСтрока.Параметры.Заполнить(ВыборкаСпецификации);
						СтрокаСпецификация = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(ВыборкаСпецификации.НомерСпецификации);
						ОбластьСпецификацияСтрока.Параметры.Спецификация = СтрокаСпецификация;
						ТабДок.Вывести(ОбластьСпецификацияСтрока);
					Иначе
						РазмерЗаготовки = ВыборкаСпецификации.РазмерХлыста - ВыборкаСпецификации.РазмерОтрезка;
					КонецЕсли;
					
				КонецЦикла;
				
				ОбластьСпецификацияСтрока.Параметры.Спецификация = "Размер заготовки";
				ОбластьСпецификацияСтрока.Параметры.РазмерОтрезка = РазмерЗаготовки;
				ОбластьСпецификацияСтрока.Области.СпецификацияСтрока.Шрифт = Новый Шрифт(,, Истина);
				ОбластьСпецификацияСтрока.Области.СпецификацияСтрока.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
				ТабДок.Вывести(ОбластьСпецификацияСтрока);
				ОбластьСпецификацияСтрока.Области.СпецификацияСтрока.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Право;
				ОбластьСпецификацияСтрока.Области.СпецификацияСтрока.Шрифт = Новый Шрифт(,, Ложь);
				
			КонецЦикла;
			
		КонецЦикла;
		
		ВставлятьРазделительСтраниц = Истина;
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументы.Ссылка);
		
	КонецЦикла; // ВыборкаДокументы.Следующий()
	
	Возврат ТабДок;
	
КонецФункции

Функция ПечатьЛистовойМатериал(МассивОбъектов, ОбъектыПечати) Экспорт
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.ИмяПараметровПечати = "ПараметрыПечати_ПечатьЛистовойМатериал";
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Ложь;
	ТабДок.ТолькоПросмотр = Истина;
	ТабДок.ОтображатьЗаголовки = Ложь;
	
	Макет = Документы.НарядЗадание.ПолучитьМакет("ПечатьЛистовойМатериал");
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьДетальСтрока = Макет.ПолучитьОбласть("ДетальСтрока");
	
	МассивНоменклатурныхГрупп = Новый Массив;
	МассивНоменклатурныхГрупп.Добавить(Справочники.НоменклатурныеГруппы.Стекло);
	МассивНоменклатурныхГрупп.Добавить(Справочники.НоменклатурныеГруппы.СтеклоДляСкругляемогоФасада);
	МассивНоменклатурныхГрупп.Добавить(Справочники.НоменклатурныеГруппы.Зеркало);
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("МассивНарядов", МассивОбъектов);
	Запрос.Параметры.Вставить("МассивНоменклатурныхГрупп", МассивНоменклатурныхГрупп);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НарядЗаданиеСписокЛистовыхДеталей.Ссылка КАК Ссылка,
	|	НарядЗаданиеСписокЛистовыхДеталей.Высота,
	|	НарядЗаданиеСписокЛистовыхДеталей.Номенклатура КАК Номенклатура,
	|	НарядЗаданиеСписокЛистовыхДеталей.Спецификация,
	|	НарядЗаданиеСписокЛистовыхДеталей.Спецификация.Номер КАК НомерСпецификации,
	|	НарядЗаданиеСписокЛистовыхДеталей.Ширина,
	|	НарядЗаданиеСписокЛистовыхДеталей.Количество,
	|	НарядЗаданиеСписокЛистовыхДеталей.Ссылка.Номер КАК НомерНаряда,
	|	НарядЗаданиеСписокЛистовыхДеталей.Ссылка.Дата КАК ДатаНаряда
	|ИЗ
	|	Документ.НарядЗадание.СписокЛистовыхДеталей КАК НарядЗаданиеСписокЛистовыхДеталей
	|ГДЕ
	|	НарядЗаданиеСписокЛистовыхДеталей.Ссылка В(&МассивНарядов)
	|	И НарядЗаданиеСписокЛистовыхДеталей.Номенклатура.НоменклатурнаяГруппа В(&МассивНоменклатурныхГрупп)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура
	|ИТОГИ
	|	МАКСИМУМ(НомерНаряда),
	|	МАКСИМУМ(ДатаНаряда)
	|ПО
	|	Ссылка";
	
	ВставлятьРазделительСтраниц = Ложь;
	
	ВыборкаДокументы = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаДокументы.Следующий() Цикл
		
		Если ВставлятьРазделительСтраниц Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1;
		
		ОбластьЗаголовок.Параметры.НомерНаряда = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(ВыборкаДокументы.НомерНаряда);
		ОбластьЗаголовок.Параметры.ДатаНаряда = Формат(ВыборкаДокументы.ДатаНаряда, "ДЛФ=DD");
		ОбластьЗаголовок.Параметры.НомераСпецификаций = СформироватьНомераСпецификаций(ВыборкаДокументы.Ссылка);
		
		ТабДок.Вывести(ОбластьЗаголовок);
		ТабДок.Вывести(ОбластьШапка);
		
		ВыборкаНоменклатура = ВыборкаДокументы.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаНоменклатура.Следующий() Цикл
			
			ОбластьДетальСтрока.Параметры.Заполнить(ВыборкаНоменклатура);
			ОбластьДетальСтрока.Параметры.Спецификация = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(ВыборкаНоменклатура.НомерСпецификации);
			ТабДок.Вывести(ОбластьДетальСтрока);
			
		КонецЦикла;
		
		ВставлятьРазделительСтраниц = Истина;
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументы.Ссылка);
		
	КонецЦикла; // ВыборкаДокументы.Следующий()
	
	Возврат ТабДок;
	
КонецФункции

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	ЛексСервер.ПолучитьПредставлениеДокумента(Данные, Представление, СтандартнаяОбработка);
	
КонецПроцедуры

Функция СформироватьНомераСпецификаций(Ссылка)
	
	Результат = "";
	
	Для каждого Строка Из Ссылка.СписокСпецификаций Цикл
		
		Номер = ОбщегоНазначения.ПолучитьЗначениеРеквизита(Строка.Спецификация, "Номер");
		Результат = Результат + ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(Номер) + ", ";
		
	КонецЦикла;
	
	Если Результат <> "" Тогда
		Результат = Лев(Результат, СтрДлина(Результат) - 2) + "." ;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьХлыстовойМатериалНаДвери(Наряд,Дверь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("Наряд", Наряд);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НарядЗаданиеРаскройХлыстов.Ссылка КАК Ссылка,
	|	НарядЗаданиеРаскройХлыстов.Ссылка.Номер КАК НомерНаряда,
	|	НарядЗаданиеРаскройХлыстов.Ссылка.Дата КАК ДатаНаряда,
	|	НарядЗаданиеРаскройХлыстов.Количество КАК РазмерОтрезка,
	|	НарядЗаданиеРаскройХлыстов.Номенклатура КАК Номенклатура,
	|	НарядЗаданиеРаскройХлыстов.НомерХлыста КАК НомерХлыста,
	|	НарядЗаданиеРаскройХлыстов.Остаток,
	|	НарядЗаданиеРаскройХлыстов.РазмерХлыста КАК РазмерХлыста,
	|	НарядЗаданиеРаскройХлыстов.Спецификация,
	|	НарядЗаданиеРаскройХлыстов.Спецификация.Номер КАК НомерСпецификации,
	|	НарядЗаданиеРаскройХлыстов.Двери КАК Двери
	|ИЗ
	|	Документ.НарядЗадание.РаскройХлыстов КАК НарядЗаданиеРаскройХлыстов
	|ГДЕ
	|	НарядЗаданиеРаскройХлыстов.Ссылка = &Наряд
	|
	|УПОРЯДОЧИТЬ ПО
	|	НарядЗаданиеРаскройХлыстов.Номенклатура.Цвет,
	|	НомерСпецификации,
	|	НомерХлыста
	|ИТОГИ
	|	МАКСИМУМ(РазмерХлыста)
	|ПО
	|	Ссылка,
	|	Номенклатура,
	|	НомерХлыста";
	
	МассивДанные = Новый Массив();
	
	ВыборкаДокументы = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаДокументы.Следующий() Цикл
		
		ВыборкаНоменклатура = ВыборкаДокументы.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаНоменклатура.Следующий() Цикл
			
			ВыборкаХлысты = ВыборкаНоменклатура.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаХлысты.Следующий() Цикл
				
				ЕстьНужнаяДверь = Ложь;
				МассивХлыст = Новый Массив();
				
				РазмерЗаготовки = ВыборкаХлысты.РазмерХлыста;
				
				ВыборкаСпецификации = ВыборкаХлысты.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				
				Пока ВыборкаСпецификации.Следующий() Цикл
					Если НЕ ВыборкаСпецификации.Остаток Тогда
						Если ВыборкаСпецификации.Двери = Дверь Тогда
							
							ЕстьНужнаяДверь = Истина;
							МассивХлыст.Добавить("1" + "♥" + ВыборкаСпецификации.РазмерОтрезка);
							
						КонецЕсли;
					Иначе
						РазмерЗаготовки = ВыборкаСпецификации.РазмерХлыста - ВыборкаСпецификации.РазмерОтрезка;
					КонецЕсли;
				КонецЦикла;
				
				Если ЕстьНужнаяДверь Тогда
					
					НовыйХлыст = Новый Массив();
					НовыйХлыст.Добавить("0" + "♥" + ВыборкаХлысты.Номенклатура + "♥" + РазмерЗаготовки);
					Для Каждого Ст ИЗ МассивХлыст Цикл
						НовыйХлыст.Добавить(Ст);	
					КонецЦикла;

					МассивДанные.Добавить(НовыйХлыст);	
					
				КонецЕсли;
								
			КонецЦикла;
			
		КонецЦикла;
				
	КонецЦикла;
	
	МассивРезультат = Новый Массив();
	
	Если МассивДанные.Количество() > 0 Тогда
		
		Для Каждого МассивХлыст ИЗ МассивДанные Цикл	
			Для Каждого Отрезок ИЗ МассивХлыст Цикл
				МассивРезультат.Добавить(Отрезок);	
			КонецЦикла;			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат СтроковыеФункцииКлиентСервер.ПолучитьСтрокуИзМассиваПодстрок(МассивРезультат, "♦");;
	
КонецФункции