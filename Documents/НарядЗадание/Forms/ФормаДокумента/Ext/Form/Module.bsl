
#Область Общего_назначения

&НаКлиентеНаСервереБезКонтекста
Функция ДомножитьДоЦелого(фЧисло)
	
	Пока фЧисло % 1 <> 0 Цикл
		
		фЧисло = фЧисло * 10;
		
	КонецЦикла;
	
	Возврат фЧисло;
	
КонецФункции

// Фильтрует хлыстовой материал и группирует номенклатуру.
//
// Параметры
//  тзНоменклатура  - ТаблицаЗначений - таблица с отобранной номенклатурой
//                 из спецификаций
// Возвращаемое значение:
//   ТаблицаЗначений   - таблица для загрузки в СписокНоменклатуры
&НаСервере
Функция ПолучитьСписокНоменклатуры(тзНоменклатура)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("тзНоменклатура", тзНоменклатура);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(тзНоменклатура.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	|	тзНоменклатура.Количество,
	|	тзНоменклатура.КоличествоНачальное КАК КоличествоНачальное,
	|	тзНоменклатура.ПредоставитЗаказчик
	|ПОМЕСТИТЬ втНоменклатура
	|ИЗ
	|	&тзНоменклатура КАК тзНоменклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втНоменклатура.Номенклатура,
	|	СУММА(втНоменклатура.Количество) КАК КоличествоТребуется,
	|	СУММА(втНоменклатура.КоличествоНачальное) КАК КоличествоНачальное
	|ИЗ
	|	втНоменклатура КАК втНоменклатура
	|ГДЕ
	|	втНоменклатура.Номенклатура.НоменклатурнаяГруппа.ВидМатериала <> ЗНАЧЕНИЕ(Перечисление.ВидыМатериалов.Хлыстовой)
	|
	|СГРУППИРОВАТЬ ПО
	|	втНоменклатура.Номенклатура";
	
	тзРезультат = Запрос.Выполнить().Выгрузить();
	
	Возврат тзРезультат;
	
КонецФункции

&НаСервере
Функция ПолучитьТЗНоменклатуры(МассивСпецификаций)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивСпецификация", МассивСпецификаций);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СпецификацияСписокНоменклатуры.Ссылка КАК Спецификация,
	|	СпецификацияСписокНоменклатуры.Номенклатура КАК Номенклатура,
	|	СУММА(ВЫБОР
	|			КОГДА СпецификацияСписокНоменклатуры.Номенклатура.НоменклатурнаяГруппа.ВидМатериала = ЗНАЧЕНИЕ(Перечисление.ВидыМатериалов.Хлыстовой)
	|				ТОГДА СпецификацияСписокНоменклатуры.КоличествоТребуется
	|			ИНАЧЕ СпецификацияСписокНоменклатуры.Количество
	|		КОНЕЦ) КАК Количество,
	|	СпецификацияСписокНоменклатуры.ПредоставитЗаказчик,
	|	СпецификацияСписокНоменклатуры.Двери
	|ПОМЕСТИТЬ втДанные
	|ИЗ
	|	Документ.Спецификация.СписокНоменклатуры КАК СпецификацияСписокНоменклатуры
	|ГДЕ
	|	СпецификацияСписокНоменклатуры.Ссылка В(&МассивСпецификация)
	|	И ВЫБОР
	|			КОГДА СпецификацияСписокНоменклатуры.ПоКаталогуМестоОбработки <> """"
	|				ТОГДА СпецификацияСписокНоменклатуры.ПоКаталогуМестоОбработки = ""Цех""
	|			ИНАЧЕ НЕ СпецификацияСписокНоменклатуры.ЧерезСклад
	|		КОНЕЦ
	|	И СпецификацияСписокНоменклатуры.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Материал)
	|
	|СГРУППИРОВАТЬ ПО
	|	СпецификацияСписокНоменклатуры.Номенклатура,
	|	СпецификацияСписокНоменклатуры.Ссылка,
	|	ВЫБОР
	|		КОГДА СпецификацияСписокНоменклатуры.Номенклатура.НоменклатурнаяГруппа.ВидМатериала = ЗНАЧЕНИЕ(Перечисление.ВидыМатериалов.Хлыстовой)
	|			ТОГДА СпецификацияСписокНоменклатуры.НомерСтроки
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	СпецификацияСписокНоменклатуры.ПредоставитЗаказчик,
	|	СпецификацияСписокНоменклатуры.Двери
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втДанные.Номенклатура
	|ИЗ
	|	втДанные КАК втДанные
	|ГДЕ
	|	втДанные.Номенклатура.НоменклатурнаяГруппа.ВидМатериала = ЗНАЧЕНИЕ(Перечисление.ВидыМатериалов.Хлыстовой)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДанные.Спецификация,
	|	втДанные.Номенклатура,
	|	втДанные.Количество,
	|	втДанные.Количество КАК КоличествоНачальное,
	|	втДанные.ПредоставитЗаказчик,
	|	втДанные.Двери
	|ИЗ
	|	втДанные КАК втДанные
	|ГДЕ
	|	втДанные.Количество > 0";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Ответ = Новый Структура;
	Ответ.Вставить("тзХлыстовойМатериал", РезультатЗапроса[1].Выгрузить());
	Ответ.Вставить("тзНоменклатура", РезультатЗапроса[2].Выгрузить());
	
	Возврат Ответ;
	
КонецФункции

&НаСервере
Процедура ЗагрузитьСпецификации(АдресТаблицы)
	
	Если ЭтоАдресВременногоХранилища(АдресТаблицы) Тогда
		ТЗ = ПолучитьИзВременногоХранилища(АдресТаблицы);
		Объект.СписокСпецификаций.Загрузить(ТЗ);
		Объект.СписокСпецификаций.Сортировать("ЦветЛДСПОсновной, СуммаНаряда Убыв");
		//////Сортировка
		ТаблицаДляКопирования = Объект.СписокСпецификаций.Выгрузить();
		ТаблицаДляНастройки = ТаблицаДляКопирования.Скопировать();
		ТаблицаДляНастройки.Свернуть("ЦветЛДСПОсновной", "СуммаНаряда");
		ТаблицаДляНастройки.Сортировать("СуммаНаряда Убыв");
		Объект.СписокСпецификаций.Очистить();
		Для каждого Цвет Из ТаблицаДляНастройки Цикл
			Для каждого Строка Из ТаблицаДляКопирования Цикл
				Если Цвет.ЦветЛДСПОсновной = Строка.ЦветЛДСПОсновной Тогда
					НоваяСтрока = Объект.СписокСпецификаций.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		///////
		ЗаполнитьНоменклатуройСервер();
		ПроверитьНаличиеКомплектаций(Объект.СписокСпецификаций.Выгрузить().ВыгрузитьКолонку("Спецификация"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область События_формы

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ПроверитьНаличиеКомплектаций(Объект.СписокСпецификаций.Выгрузить().ВыгрузитьКолонку("Спецификация"));
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	ПроверитьНаличиеКомплектаций(Объект.СписокСпецификаций.Выгрузить().ВыгрузитьКолонку("Спецификация"));
	
КонецПроцедуры

#КонецОбласти

#Область Команды

&НаКлиенте
Процедура ВыбратьСпецификации(Команда)
	
	Режим = РежимДиалогаВопрос.ДаНет;
	Текст = "Табличные части будут изменены." + Символы.ПС + "Продолжить?" ;
	
	Если Объект.СписокСпецификаций.Количество() > 0 
		И Вопрос(Текст, Режим, 0) = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Возврат;
	КонецЕсли;
	
	ПаараметрыФормы = Новый Структура;
	ПаараметрыФормы.Вставить("Подразделение", Объект.Подразделение);
	ПаараметрыФормы.Вставить("ТЗ", Объект.СписокСпецификаций);
	ПаараметрыФормы.Вставить("Статус", ПредопределенноеЗначение("Перечисление.СтатусыСпецификации.Размещен"));
	ПаараметрыФормы.Вставить("УпорядочитьПоДате", Истина);
	
	АдресТаблицы = ОткрытьФормуМодально("ОбщаяФорма.ФормаВыбораСпецификации", ПаараметрыФормы, ЭтаФорма);
	Если АдресТаблицы <> Неопределено Тогда
		ПоказатьОповещениеПользователя("Номенклатуры перезаполнена",, "Изменился список спецификаций, табличная часть номенклатуры сформирована заново");
		ЗагрузитьСпецификации(АдресТаблицы);
		ЗаполнитьНоменклатуройСервер();
	Иначе	
		Модифицированность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНоменклатуру(Команда)
	
	ЗаполнитьНоменклатуройСервер();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНоменклатуройСервер() Экспорт
	
	ТаблицаСпецификаций = Объект.СписокСпецификаций.Выгрузить();
	МассивСпецификаций = ТаблицаСпецификаций.ВыгрузитьКолонку("Спецификация");
	
	Таблицы = ПолучитьТЗНоменклатуры(МассивСпецификаций);
	Объект.СписокНоменклатурыПоСпецификациям.Загрузить(Таблицы.тзНоменклатура);
	Объект.СписокХлыстовыхМатериалов.Загрузить(Таблицы.тзХлыстовойМатериал);
	
	тзНоменклатураОстатки = ПолучитьСписокНоменклатуры(Таблицы.тзНоменклатура);
	Объект.СписокНоменклатуры.Загрузить(тзНоменклатураОстатки);
	
	ПерекроитьВесьХлыстовойМатериал(Объект);	
	ЗаполнитьЛистовойМатериал(Объект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЛистовойМатериал(Объект)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокНоменклатуры", Объект.СписокНоменклатуры.Выгрузить());
	Запрос.УстановитьПараметр("СписокЛистовойМат", Объект.СписокЛистовНоменклатуры.Выгрузить());
	Запрос.УстановитьПараметр("ВидМатериалаЛистовой", Перечисления.ВидыМатериалов.Листовой);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЛСписок.Номенклатура КАК Номенклатура,
	|	ЛСписок.Количество КАК Количество,
	|	ЛСписок.НомерСтроки КАК НомерСтроки
	|ПОМЕСТИТЬ втЛистовойМат
	|ИЗ
	|	&СписокЛистовойМат КАК ЛСписок
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НСписок.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ втНоменклатура
	|ИЗ
	|	&СписокНоменклатуры КАК НСписок
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НСписок.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ втНом
	|ИЗ
	|	втНоменклатура КАК НСписок
	|ГДЕ
	|	ВЫРАЗИТЬ(НСписок.Номенклатура КАК Справочник.Номенклатура).НоменклатурнаяГруппа.ВидМатериала = &ВидМатериалаЛистовой
	|
	|СГРУППИРОВАТЬ ПО
	|	НСписок.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	сНом.Номенклатура КАК Номенклатура,
	|	ЕСТЬNULL(сЛист.Количество, 0) КАК Количество,
	|	ЕСТЬNULL(сЛист.НомерСтроки, 0) КАК НомерСтроки
	|ИЗ
	|	втНом КАК сНом
	|		ЛЕВОЕ СОЕДИНЕНИЕ втЛистовойМат КАК сЛист
	|		ПО (сЛист.Номенклатура = сНом.Номенклатура)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЕСТЬNULL(сЛист.НомерСтроки, 0)";
	
	Объект.СписокЛистовНоменклатуры.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьРаскройНаХлысты(Команда)
	
	ТекущиеДанные = Элементы.СписокХлыстовыхМатериалов.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		ПерекроитьХлысты(ТекущиеДанные.Номенклатура, Ложь, Объект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Таблица_СписокНоменклатуры

&НаКлиенте
Процедура СписокНоменклатурыПриАктивизацииСтроки(Элемент)
	
	Если Элемент.ТекущиеДанные <> Неопределено Тогда
		
		Номенклатура = Элемент.ТекущиеДанные.Номенклатура;
		СвойстваНоменклатуры = ЛексСервер.ЗначенияРеквизитовОбъекта(Номенклатура, "Базовый, БазоваяНоменклатура");
		Если НЕ СвойстваНоменклатуры.Базовый Тогда
			Номенклатура = СвойстваНоменклатуры.БазоваяНоменклатура;
		КонецЕсли;
		
		ОтборСтруктура = Новый Структура;
		
		Если ЗначениеЗаполнено(Номенклатура) Тогда
			ОтборСтруктура.Вставить("Номенклатура", Номенклатура);
		Иначе
			ОтборСтруктура.Вставить("Номенклатура", ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка"));
		КонецЕсли;
		
		ОтборСтрок = Новый ФиксированнаяСтруктура(ОтборСтруктура);
		Элементы.СписокНоменклатурыПоСпецификациям.ОтборСтрок = ОтборСтрок;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область Таблица_СписокСпецификаций

&НаКлиенте
Процедура СписокСпецификацийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОткрытьЗначение(Элементы.СписокСпецификаций.ТекущиеДанные.Спецификация);
	
КонецПроцедуры

#КонецОбласти

#Область Таблица_СписокНоменклатурыПоСпецификациям

&НаКлиенте
Процедура СписокНоменклатурыПоСпецификациямПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыПоСпецификациямПередУдалением(Элемент, Отказ)
	
	Октаз = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыПоСпецификациямВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.СписокНоменклатурыПоСпецификациям.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		ОткрытьЗначение(ТекущиеДанные.Спецификация);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Хлыстовой_раскрой

&НаСервере
Функция ОпределитьКоличествоХлыстовогоМатериала(Номенклатура, тзОстатки = Неопределено)
	
	// Заполненная кладовщиком таблица с обрезками хлыстов
	Если тзОстатки = Неопределено Тогда
		тзОстатки = Новый ТаблицаЗначений;
		тзОстатки.Колонки.Добавить("Количество");
	КонецЕсли;
	
	тзОстатки.Сортировать("Количество Возр");
	
	// Для хранения используемых целых хлыстов и обрезков
	тзИспользуемыеХлысты = Новый ТаблицаЗначений;
	тзИспользуемыеХлысты.Колонки.Добавить("Количество");
	
	// Определение размеров хлыста, кратности
	ОсновнаяПоСкладу = ЛексСервер.ПолучитьОсновнуюПоСкладу(Объект.Подразделение, Номенклатура);
	
	Если ЗначениеЗаполнено(ОсновнаяПоСкладу)
		И НЕ ОсновнаяПоСкладу.Базовый Тогда
		ДлинаХлыста = ОсновнаяПоСкладу.КоэффициентБазовых;
		РазмерЦелогоХлыста = ДлинаХлыста * 1000; // И да простит меня Аллах за такое упрощение.
	Иначе
		ДлинаХлыста = Номенклатура.ДлинаДетали / 1000 ;
		РазмерЦелогоХлыста = Номенклатура.ДлинаДетали; // И да простит меня Аллах за такое упрощение.
	КонецЕсли;
	
	// Сбор отрезков хлыстового материала
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Номенклатура", Номенклатура);
	СтрокиДеталей = Объект.СписокНоменклатурыПоСпецификациям.НайтиСтроки(ПараметрыОтбора);
	
	МассивДеталей = Новый Массив;
	
	тзДетали = Новый ТаблицаЗначений;
	тзДетали.Колонки.Добавить("Спецификация");
	тзДетали.Колонки.Добавить("РазмерОтрезка");
	тзДетали.Колонки.Добавить("Двери");
	
	Для каждого Деталь Из СтрокиДеталей Цикл
		
		// { Васильев Александр Леонидович [30.10.2015]
		// Сделать.
		// После установки контроля на размеры в спецификации
		// удалить разбиение детали больше хлыста.
		// } Васильев Александр Леонидович [30.10.2015]
		
		Если Деталь.Количество > ДлинаХлыста Тогда
			
			КоличествоХлыстов = Цел(Деталь.Количество / ДлинаХлыста);
			Для ъ = 1 По КоличествоХлыстов Цикл
				
				НоваяСтрока = тзДетали.Добавить();
				НоваяСтрока.Спецификация = Деталь.Спецификация;
				НоваяСтрока.РазмерОтрезка = РазмерЦелогоХлыста;
				НоваяСтрока.Двери = Деталь.Двери;
				
			КонецЦикла;
			
			НоваяСтрока = тзДетали.Добавить();
			НоваяСтрока.Спецификация = Деталь.Спецификация;
			НоваяСтрока.РазмерОтрезка = (Деталь.Количество % ДлинаХлыста) * 1000;
			НоваяСтрока.Двери = Деталь.Двери;
			
		Иначе
			
			НоваяСтрока = тзДетали.Добавить();
			НоваяСтрока.Спецификация = Деталь.Спецификация;
			НоваяСтрока.РазмерОтрезка = Деталь.Количество * 1000;
			НоваяСтрока.Двери = Деталь.Двери;
			
		КонецЕсли;
		
	КонецЦикла;
	
	// Распределение отрезков на хлыстах
	НомерХлыста = 1;
	НомерОстатка = 0;
	
	МассивОстатков = Объект.ОстаткиХлыстов.НайтиСтроки(ПараметрыОтбора);
	КоличествоОстатков = МассивОстатков.Количество();
	
	МассивДеталей = тзДетали.ВыгрузитьКолонку("РазмерОтрезка");
	
	Пока МассивДеталей.Количество() > 0 Цикл
		
		// Сначала формируем раскрой на все обрезки
		
		Если КоличествоОстатков > 0
			И КоличествоОстатков > НомерОстатка Тогда
			РазмерХлыста = МассивОстатков[НомерОстатка].Количество;
			НомерОстатка = НомерОстатка + 1;
			ОтрезкиДляРазмещения = ПолучитьЭлементыМенее(МассивДеталей, РазмерХлыста);
			
			// Ни один отрезок не помещается в такой остаток
			Если ОтрезкиДляРазмещения.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			
		Иначе
			
			РазмерХлыста = РазмерЦелогоХлыста;
			ОтрезкиДляРазмещения = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(МассивДеталей);
			
		КонецЕсли;
		
		// Основной алгоритм распределения отрезков на хлысте
		Структура = Раскрой.МаксимальнаяСуммаПодмножеств(ОтрезкиДляРазмещения, РазмерХлыста, 10);
		
		Для каждого РазмещеннаяДеталь Из Структура.Массив Цикл
			
			НоваяСтрока = Объект.РаскройХлыстов.Добавить();
			НоваяСтрока.Количество = РазмещеннаяДеталь;
			НоваяСтрока.Номенклатура = Номенклатура;
			НоваяСтрока.НомерХлыста = НомерХлыста;
			НоваяСтрока.РазмерХлыста = РазмерХлыста;
			
			ДанныеОтрезка = ПолучитьДанныеПоОтрезку(тзДетали, РазмещеннаяДеталь);
			
			НоваяСтрока.Спецификация = ДанныеОтрезка.Спецификация;
			НоваяСтрока.Двери = ДанныеОтрезка.Двери;
			
			Индекс = МассивДеталей.Найти(РазмещеннаяДеталь);
			МассивДеталей.Удалить(Индекс);
			
		КонецЦикла;
		
		Если Структура.МинимальныйОстаток > 0 Тогда
			//Номенклатура.Кратность * 1000 Тогда
			
			НоваяСтрока = Объект.РаскройХлыстов.Добавить();
			НоваяСтрока.Остаток = Истина;
			НоваяСтрока.Количество = Структура.МинимальныйОстаток;
			НоваяСтрока.Номенклатура = Номенклатура;
			НоваяСтрока.НомерХлыста = НомерХлыста;
			НоваяСтрока.РазмерХлыста = РазмерХлыста;
			
		КонецЕсли;
		
		НомерХлыста = НомерХлыста +1;
		
	КонецЦикла;
	
КонецФункции

&НаСервере
Функция ПолучитьДанныеПоОтрезку(МассивСтрок, Размер)
	
	Для Итер = 0 По МассивСтрок.Количество() Цикл
		
		Если МассивСтрок[Итер].РазмерОтрезка = Размер Тогда
			
			Спецификация = МассивСтрок[Итер].Спецификация;
			Двери = МассивСтрок[Итер].Двери;
			
			МассивСтрок.Удалить(Итер);
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если НЕ ЗначениеЗаполнено(Спецификация) Тогда
		
		ВызватьИсключение "Не определена спецификация к отрезку " + Размер;
		
	КонецЕсли;
	
	Данные = Новый Структура();
	Данные.Вставить("Спецификация",Спецификация);
	Данные.Вставить("Двери",Двери);
	
	Возврат Данные;
	
КонецФункции

&НаСервере
Функция ПолучитьЭлементыМенее(фнМассив, фнРазмер)
	
	Результат = Новый Массив;
	
	Для каждого Элем Из фнМассив Цикл
		Если Элем <= фнРазмер Тогда
			Результат.Добавить(Элем);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ПолучитьСтрокуСпискаНоменклатуры(Номенклатура)
	
	Строки = НайтиСтрокуСпискаНоменклатуры(Номенклатура);
	
	Если Строки.Количество() = 1 Тогда
		
		Строка = Строки[0];
		
	Иначе
		
		НоменклатураОтбора = ЛексСервер.ПолучитьОсновнуюПоСкладу(Объект.Подразделение, Номенклатура);
		Строки = НайтиСтрокуСпискаНоменклатуры(НоменклатураОтбора);
		
		Если Строки.Количество() = 1 Тогда
			
			Строка = Строки[0];
			
		Иначе
			
			ВызватьИсключение "Ошибка 295: Номенклатура " + Номенклатура + " не найдена в таблице";
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Строка;
	
КонецФункции

&НаСервере
Функция НайтиСтрокуСпискаНоменклатуры(Номенклатура)
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Номенклатура", Номенклатура);
	Возврат Объект.СписокНоменклатуры.НайтиСтроки(ПараметрыОтбора);
	
КонецФункции

&НаКлиенте
Процедура СписокХлыстовыхМатериаловПриАктивизацииСтроки(Элемент)
	
	Если Элемент.ТекущиеДанные <> Неопределено Тогда
		
		ОтборСтруктура = Новый Структура;
		
		Номенклатура = Элемент.ТекущиеДанные.Номенклатура;
		ОтборСтруктура.Вставить("Номенклатура", Номенклатура);
		ОтборСтрок = Новый ФиксированнаяСтруктура(ОтборСтруктура);
		
		Элементы.РаскройХлыстов.ОтборСтрок = ОтборСтрок;
		Элементы.ОстаткиХлыстов.ОтборСтрок = ОтборСтрок;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПерекроитьВесьХлыстовойМатериал(Объект)
	
	Объект.РаскройХлыстов.Очистить();
	
	Для каждого Строка Из Объект.СписокХлыстовыхМатериалов Цикл
		
		ПерекроитьХлысты(Строка.Номенклатура, Истина, Объект);
		
	КонецЦикла;
	
КонецФункции

&НаСервере
Функция ПерекроитьХлысты(фнНоменклатура, РаскройВсехМатериалов, Объект)
	
	Если НЕ РаскройВсехМатериалов Тогда
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("Номенклатура", фнНоменклатура);
		
		СтрокиКУдалению = Объект.РаскройХлыстов.НайтиСтроки(ПараметрыОтбора);
		
		Для каждого Строка Из СтрокиКУдалению Цикл
			ъ = Объект.РаскройХлыстов.Индекс(Строка);
			Объект.РаскройХлыстов.Удалить(ъ);
		КонецЦикла;
		
	КонецЕсли;
	
	тзОстатки = Объект.ОстаткиХлыстов.Выгрузить();
	ОпределитьКоличествоХлыстовогоМатериала(фнНоменклатура, тзОстатки);
	
КонецФункции

#КонецОбласти

#Область Комплектации

&НаСервереБезКонтекста
Функция ПолучитьКомплектации(МассивСпецификаций)
	
	Результат = Новый Соответствие;

	Для каждого Спецификация Из МассивСпецификаций Цикл
		
		Если НЕ Спецификация.ТребуетсяКомплектация Тогда
			Продолжить;
		КонецЕсли;
		
		МассивКомплектации = ЛексСервер.НайтиПодчиненныеДокументы(Спецификация, "Документ.Комплектация", "Спецификация");
		
		Если НЕ ЗначениеЗаполнено(МассивКомплектации) Тогда
			МассивКомплектации.Добавить(Документы.Комплектация.ПустаяСсылка());
		Конецесли;
		
		Результат.Вставить(Спецификация, МассивКомплектации);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОткрытьКомплектацию(Команда)
	
	МассивСпецификаций = Новый Массив;
	МассивВыделенныхСтрок = Элементы.СписокСпецификаций.ВыделенныеСтроки;
	
	Для каждого ЭлементСтроки Из Объект.СписокСпецификаций Цикл
		Идентификатор = ЭлементСтроки.ПолучитьИдентификатор();
		Если МассивВыделенныхСтрок.Найти(Идентификатор) <> Неопределено Тогда
			МассивСпецификаций.Добавить(ЭлементСтроки.Спецификация);
		КонецЕсли;
	КонецЦикла;
	
	ПакетДокументов = ПолучитьКомплектации(МассивСпецификаций);
	
	Для каждого СтрокаСпецификация  Из ПакетДокументов Цикл
		
		КомплектацииМассив = ПакетДокументов.Получить(СтрокаСпецификация.Ключ);
		
		Для  каждого Комплектация Из КомплектацииМассив Цикл
			
			Уникальность =  Ложь;
			ПараметрыФормы = Новый Структура ("Ключ",Комплектация);
			
			Если НЕ ЗначениеЗаполнено(Комплектация) Тогда
				ПараметрыФормы.Вставить("Основание", СтрокаСпецификация.Ключ);
				Уникальность =  Истина;
			КонецЕсли;
			
			ОткрытьФорму("Документ.Комплектация.ФормаОбъекта", ПараметрыФормы, ЭтаФорма, Уникальность);
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПроверитьНаличиеКомплектаций(МассивСпецификаций)
	
	ПризнакМодифицированности = Модифицированность;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивСпецификаций", МассивСпецификаций);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Спецификация.Ссылка,
		|	Спецификация.ТребуетсяКомплектация
		|ПОМЕСТИТЬ ВТ_МассивДокументов
		|ИЗ
		|	Документ.Спецификация КАК Спецификация
		|ГДЕ
		|	Спецификация.Ссылка В(&МассивСпецификаций)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗапросКомплектации.Ссылка) КАК КомплектацияКоличество,
		|	ВТ_МассивДокументов.Ссылка КАК Спецификация,
		|	ВТ_МассивДокументов.ТребуетсяКомплектация КАК ТребуетсяКомплектация
		|ИЗ
		|	ВТ_МассивДокументов КАК ВТ_МассивДокументов
		|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			Комплектация.Ссылка КАК Ссылка,
		|			Комплектация.Спецификация КАК Спецификация
		|		ИЗ
		|			Документ.Комплектация КАК Комплектация
		|		ГДЕ
		|			Комплектация.Проведен
		|			И Комплектация.Спецификация В(&МассивСпецификаций)) КАК ЗапросКомплектации
		|		ПО ВТ_МассивДокументов.Ссылка = ЗапросКомплектации.Спецификация
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_МассивДокументов.Ссылка,
		|	ВТ_МассивДокументов.ТребуетсяКомплектация";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаСпецификации = РезультатЗапроса.Выбрать();
	
	Для каждого Строка Из Объект.СписокСпецификаций Цикл
		
		ВыборкаСпецификации.Сбросить();
		
		Если ВыборкаСпецификации.НайтиСледующий(Новый Структура("Спецификация", Строка.Спецификация)) Тогда
		
			Если ВыборкаСпецификации.ТребуетсяКомплектация Тогда
				УсловиеТребуетсяКомплектация = ВыборкаСпецификации.КомплектацияКоличество > 0
			Иначе
				УсловиеТребуетсяКомплектация = Истина;
			КонецЕсли; 
			Строка.ЕстьПроведеннаяКомплектация    =  УсловиеТребуетсяКомплектация;
			
		КонецЕсли;
				
	КонецЦикла;
	
	Модифицированность = ПризнакМодифицированности;
	
КонецФункции

&НаКлиенте
Процедура ОстаткиХлыстовПриИзменении(Элемент)
	
	тдОстаток = Элементы.ОстаткиХлыстов.ТекущиеДанные;
	тдХлыстовойМатериал = Элементы.СписокХлыстовыхМатериалов.ТекущиеДанные;
	тдОстаток.Номенклатура = тдХлыстовойМатериал.Номенклатура;
	
КонецПроцедуры

&НаКлиенте
Процедура РаскройХлыстовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.РаскройХлыстов.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьЗначение(ТекущиеДанные.Спецификация);
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
		
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ОчиститьСтрокуРаскроя(Объект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

Функция ОчиститьСтрокуРаскроя(Наряд)
	
	НаборЗаписей = РегистрыСведений.РаскройДеталей.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Объект.Установить(Наряд);
	
	НаборЗаписей.Записать();
	
КонецФункции

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ПоявиласьКомплектация" Тогда
		ПроверитьНаличиеКомплектаций(Параметр);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокСпецификацийПослеУдаления(Элемент)
	ЗаполнитьНоменклатуройСервер();
КонецПроцедуры

&НаКлиенте
Процедура ОстаткиЛистовогоМатериалаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") 
		И ВыбранноеЗначение.Свойство("АдресХранилища")
		И ЭтоАдресВременногоХранилища(ВыбранноеЗначение.АдресХранилища) Тогда
		
		ОстаткиЛистовогоМатериалаОбработкаВыбораНаСервере(ВыбранноеЗначение.АдресХранилища);
		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОстаткиЛистовогоМатериалаОбработкаВыбораНаСервере(АдресХранилища)
	 
	Значение = ПолучитьИзВременногоХранилища(АдресХранилища);
	Объект.ОстаткиЛистовогоМатериала.Очистить();
	
	Если ТипЗнч(Значение) = Тип("ТаблицаЗначений") Тогда
		Для каждого Строка Из Значение Цикл
			НоваяСтрока = Объект.ОстаткиЛистовогоМатериала.Добавить();
			НоваяСтрока.Номенклатура = Строка.Номенклатура;
			НоваяСтрока.ВысотаОстатка = Строка.Высота;
			НоваяСтрока.ШиринаОстатка = Строка.Ширина;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЛистовНоменклатурыПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СписокЛистовНоменклатурыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	ЗаполнитьНоменклатуройСервер();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.Дата = ТекущаяДата();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодобратьИсключаемыеОстатки(Команда)

	ПараметрыФомы = Новый Структура;
	ПараметрыФомы.Вставить("Подразделение", Объект.Подразделение);
	ПараметрыФомы.Вставить("Дата", Объект.Дата);
	СтруктураАдресов = ПолучитьАдресаТаблицИсключаемыеОстатки();
	ПараметрыФомы.Вставить("АдресТаблицыОбрезков", СтруктураАдресов.АдресТаблицыОбрезков);
	ОткрытьФорму("РегистрНакопления.ОбрезкиМатериалов.Форма.ФормаПодбора", ПараметрыФомы, Элементы.СписокИсключаемыхОстатков);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьАдресаТаблицИсключаемыеОстатки()
	
	СтруктураАдресов = Новый Структура;
	СтруктураАдресов.Вставить("АдресТаблицыОбрезков", ПоместитьВоВременноеХранилище(Объект.СписокИсключаемыхОстатков.Выгрузить()));
	
	Возврат СтруктураАдресов;
	
КонецФункции

&НаКлиенте
Процедура СписокИсключаемыхОстатковОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") И ВыбранноеЗначение.Свойство("АдресХранилища") 
		И ЭтоАдресВременногоХранилища(ВыбранноеЗначение.АдресХранилища) Тогда
		ЗагрузитьИсключаемыеОстатки(ВыбранноеЗначение.АдресХранилища);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗагрузитьИсключаемыеОстатки(фАдресТаблицы)
	
	Объект.СписокИсключаемыхОстатков.Загрузить(ПолучитьИзВременногоХранилища(фАдресТаблицы));
	
КонецФункции
