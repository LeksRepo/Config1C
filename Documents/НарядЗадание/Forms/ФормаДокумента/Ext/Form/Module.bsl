
#Область Общего_назначения

&НаКлиентеНаСервереБезКонтекста
Функция ДомножитьДоЦелого(фЧисло)
	
	Пока фЧисло % 1 <> 0 Цикл
		
		фЧисло = фЧисло * 10;
		
	КонецЦикла;
	
	Возврат фЧисло;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьОсновнуюПоСкладу(фПодразделение, фНоменклатура)
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Номенклатура", фНоменклатура);
	Запрос.УстановитьПараметр("Подразделение", фПодразделение);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НоменклатураПодразделений.ОсновнаяПоСкладу
	|ИЗ
	|	РегистрСведений.НоменклатураПодразделений КАК НоменклатураПодразделений
	|ГДЕ
	|	НоменклатураПодразделений.Подразделение = &Подразделение
	|	И НоменклатураПодразделений.Номенклатура = &Номенклатура";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Результат = Выборка.ОсновнаяПоСкладу;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьКомплектации(МассивСпецификаций)
	
	Результат = Новый Массив;
	
	Для каждого Спецификация Из МассивСпецификаций Цикл
		
		Комплектация = Документы.Комплектация.ПолучитьКомплектацию(Спецификация);
		
		Если ЗначениеЗаполнено(Комплектация) Тогда
			Результат.Добавить(Комплектация);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОткрытьКомплектацию(Команда)
	
	МассивСпецификаций = Новый Массив;
	
	Для каждого ЭлементСтроки Из Объект.СписокСпецификаций Цикл
		Идентификатор = ЭлементСтроки.ПолучитьИдентификатор();
		Если Элементы.СписокСпецификаций.ВыделенныеСтроки.Найти(Идентификатор) <> Неопределено Тогда
			МассивСпецификаций.Добавить(ЭлементСтроки.Спецификация);
		КонецЕсли;
	КонецЦикла;
	
	МассивКомплектаций = ПолучитьКомплектации(МассивСпецификаций);
	
	Для каждого Комплектация Из МассивКомплектаций Цикл
		
		ОткрытьФорму("Документ.Комплектация.ФормаОбъекта", Новый Структура("Ключ",Комплектация), Элементы.СписокСпецификаций);
		
	КонецЦикла;
	
	Если МассивКомплектаций.Количество() <> МассивСпецификаций.Количество() Тогда
		
		ПараметрыФормы = Новый Структура;
		
		ТД = Элементы.СписокСпецификаций.ТекущиеДанные;
		Если ТекущаяДата() <> Неопределено Тогда
			ПараметрыФормы.Вставить("Основание", ТД.Спецификация);
		КонецЕсли;
		
		ОткрытьФорму("Документ.Комплектация.ФормаОбъекта", ПараметрыФормы, Элементы.СписокСпецификаций);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПроверитьНаличиеКомплектаций(МассивСпецификаций)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивСпецификаций", МассивСпецификаций);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Спец.Ссылка КАК Спецификация,
	|	ВЫБОР
	|		КОГДА Компл.Ссылка ЕСТЬ NULL 
	|				И Спец.ТребуетсяКомплектация
	|				Или НЕ Компл.Проведен
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК НеобходимаКомплектация
	|ИЗ
	|	Документ.Спецификация КАК Спец
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Комплектация КАК Компл
	|		ПО (Компл.Спецификация = Спец.Ссылка)
	|ГДЕ
	|	Спец.Ссылка В(&МассивСпецификаций)";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Для каждого Строка Из Объект.СписокСпецификаций Цикл
			
			Если Строка.Спецификация = ВыборкаДетальныеЗаписи.Спецификация Тогда
				
				Строка.ЕстьПроведеннаяКомплектация = ВыборкаДетальныеЗаписи.НеобходимаКомплектация;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецФункции

&НаСервере
Функция ПолучитьКоличествоМатериалов(тзНоменклатура)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Подразделение", Объект.Подразделение);
	Запрос.УстановитьПараметр("Склад", Объект.Подразделение.ОсновнойСклад);
	Запрос.УстановитьПараметр("тзНоменклатура", тзНоменклатура);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	тзНоменклатура.Номенклатура,
	|	тзНоменклатура.Количество
	|ПОМЕСТИТЬ втНоменклатура
	|ИЗ
	|	&тзНоменклатура КАК тзНоменклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втНоменклатура.Номенклатура,
	|	СУММА(втНоменклатура.Количество) КАК Количество
	|ПОМЕСТИТЬ втСгрупНоменклатура
	|ИЗ
	|	втНоменклатура КАК втНоменклатура
	|
	|СГРУППИРОВАТЬ ПО
	|	втНоменклатура.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ВЫРАЗИТЬ(ОстаткиНаСкладе.Субконто2 КАК Справочник.Номенклатура).Базовый
	|			ТОГДА ОстаткиНаСкладе.Субконто2
	|		ИНАЧЕ ВЫРАЗИТЬ(ОстаткиНаСкладе.Субконто2 КАК Справочник.Номенклатура).БазоваяНоменклатура
	|	КОНЕЦ КАК Номенклатура,
	|	СУММА(ВЫБОР
	|			КОГДА ОстаткиНаСкладе.Субконто2.Базовый
	|				ТОГДА ОстаткиНаСкладе.КоличествоОстаток
	|			ИНАЧЕ ОстаткиНаСкладе.Субконто2.КоэффициентБазовых * ОстаткиНаСкладе.КоличествоОстаток
	|		КОНЕЦ) КАК ОстатокНаСкладе
	|ПОМЕСТИТЬ ОстаткиНаСкладе
	|ИЗ
	|	РегистрБухгалтерии.Управленческий.Остатки(
	|			,
	|			Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.МатериалыНаСкладе),
	|			,
	|			Подразделение = &Подразделение
	|				И Субконто1 = &Склад) КАК ОстаткиНаСкладе
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА ВЫРАЗИТЬ(ОстаткиНаСкладе.Субконто2 КАК Справочник.Номенклатура).Базовый
	|			ТОГДА ОстаткиНаСкладе.Субконто2
	|		ИНАЧЕ ВЫРАЗИТЬ(ОстаткиНаСкладе.Субконто2 КАК Справочник.Номенклатура).БазоваяНоменклатура
	|	КОНЕЦ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	втСгрупНоменклатура.Номенклатура,
	|	втСгрупНоменклатура.Количество КАК КоличествоТребуется,
	|	ОстаткиВЦеху.КоличествоОстаток КАК ОстатокВЦеху,
	|	ОстаткиНаСкладе.ОстатокНаСкладе
	|ИЗ
	|	втСгрупНоменклатура КАК втСгрупНоменклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
	|				,
	|				Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ОсновноеПроизводство),
	|				,
	|				Подразделение = &Подразделение
	|					И Субконто1 В
	|						(ВЫБРАТЬ
	|							втСгрупНоменклатура.Номенклатура
	|						ИЗ
	|							втСгрупНоменклатура)) КАК ОстаткиВЦеху
	|		ПО втСгрупНоменклатура.Номенклатура = ОстаткиВЦеху.Субконто1
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиНаСкладе КАК ОстаткиНаСкладе
	|		ПО втСгрупНоменклатура.Номенклатура = ОстаткиНаСкладе.Номенклатура";
	
	тзРезультат = Запрос.Выполнить().Выгрузить();
	
	Для каждого Строка Из тзРезультат Цикл
		
		Если Строка.Номенклатура.НоменклатурнаяГруппа.ВидМатериала = Перечисления.ВидыМатериалов.Хлыстовой Тогда
			
			ОпределитьКоличествоХлыстовогоМатериала(Строка);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат тзРезультат;
	
КонецФункции

&НаСервере
Функция ОпределитьКоличествоХлыстовогоМатериала(фСтрока)
	
	Номенклатура = фСтрока.Номенклатура;
	ОсновнаяПоСкладу = ПолучитьОсновнуюПоСкладу(Объект.Подразделение, Номенклатура);
	
	Если НЕ ЗначениеЗаполнено(ОсновнаяПоСкладу) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	КоличествоХлыстов = 0;
	ДлинаХлыста = ОсновнаяПоСкладу.КоэффициентБазовых; // Тут вопрос. Брать коэффициент или длину.	
	Кратность = Номенклатура.Кратность;
	
	Если ДлинаХлыста % Кратность = 0 Тогда
		ЕдиницаОтрезка = Кратность;
		ОтрезковВХлысте = ДлинаХлыста / Кратность;
	Иначе
		ЕдиницаОтрезка = 1;
		ОтрезковВХлысте = ДлинаХлыста;
	КонецЕсли;
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Номенклатура", Номенклатура);
	СтрокиДеталей = Объект.СписокНоменклатурыПоСпецификациям.НайтиСтроки(ПараметрыОтбора);
	
	МассивДеталей = Новый Массив;
	
	Для каждого Деталь Из СтрокиДеталей Цикл
		
		Если Деталь.Количество > ДлинаХлыста Тогда
			
			МассивДеталей.Добавить((Деталь.Количество % ДлинаХлыста) / ЕдиницаОтрезка);
			КоличествоХлыстов = Цел(Деталь.Количество / ДлинаХлыста);
			
		Иначе
			
			МассивДеталей.Добавить(Деталь.Количество / ЕдиницаОтрезка);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Пока МассивДеталей.Количество() > 0 Цикл
		
		Структура = РегистрыСведений.РаскройДеталей.МаксимальнаяСуммаПодмножеств(МассивДеталей, ОтрезковВХлысте, 0);
		
		Для каждого РазмещеннаяДеталь Из Структура.Массив Цикл
			
			Индекс = МассивДеталей.Найти(РазмещеннаяДеталь);
			МассивДеталей.Удалить(Индекс);
			
		КонецЦикла;
		
		КоличествоХлыстов = КоличествоХлыстов + 1;
		
	КонецЦикла;
	
	фСтрока.Номенклатура = ОсновнаяПоСкладу;
	фСтрока.КоличествоТребуется = КоличествоХлыстов;
	
КонецФункции

&НаСервере
Функция ПолучитьНоменклатуруСпецификаций(МассивСпецификаций)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивСпецификация", МассивСпецификаций);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СпецификацияСписокНоменклатуры.Ссылка КАК Спецификация,
	|	СпецификацияСписокНоменклатуры.Номенклатура КАК Номенклатура,
	|	СУММА(СпецификацияСписокНоменклатуры.Количество) КАК Количество,
	|	ВЫБОР
	|		КОГДА СпецификацияСписокНоменклатуры.Номенклатура.НоменклатурнаяГруппа.ВидМатериала = ЗНАЧЕНИЕ(Перечисление.ВидыМатериалов.Хлыстовой)
	|			ТОГДА СпецификацияСписокНоменклатуры.НомерСтроки
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НомерСтроки
	|ИЗ
	|	Документ.Спецификация.СписокНоменклатуры КАК СпецификацияСписокНоменклатуры
	|ГДЕ
	|	СпецификацияСписокНоменклатуры.Ссылка В(&МассивСпецификация)
	|	И НЕ СпецификацияСписокНоменклатуры.ЧерезСклад
	|	И СпецификацияСписокНоменклатуры.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Материал)
	|	И НЕ СпецификацияСписокНоменклатуры.ПредоставитЗаказчик
	|
	|СГРУППИРОВАТЬ ПО
	|	СпецификацияСписокНоменклатуры.Номенклатура,
	|	СпецификацияСписокНоменклатуры.Ссылка,
	|	ВЫБОР
	|		КОГДА СпецификацияСписокНоменклатуры.Номенклатура.НоменклатурнаяГруппа.ВидМатериала = ЗНАЧЕНИЕ(Перечисление.ВидыМатериалов.Хлыстовой)
	|			ТОГДА СпецификацияСписокНоменклатуры.НомерСтроки
	|		ИНАЧЕ 0
	|	КОНЕЦ";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

&НаСервере
Процедура ОбновитьСписокВыбораНоменклатуры()
	
	МассивНоменклатуры = Объект.СписокНоменклатуры.Выгрузить().ВыгрузитьКолонку("Номенклатура");
	
	СписокСпецификаций = Объект.СписокСпецификаций.Выгрузить().ВыгрузитьКолонку("Спецификация");
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	МАКСИМУМ(Номенклатура.Ссылка) КАК Ссылка
	|ПОМЕСТИТЬ ОсновнаяНоменклатура
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Базовый
	|
	|СГРУППИРОВАТЬ ПО
	|	Номенклатура.БазоваяНоменклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СпецификацияСписокНоменклатуры.Ссылка КАК Спецификация,
	|	ЕСТЬNULL(ОсновнаяНоменклатура.Ссылка, СпецификацияСписокНоменклатуры.Номенклатура) КАК Номенклатура,
	|	СУММА(СпецификацияСписокНоменклатуры.Количество * ЕСТЬNULL(ОсновнаяНоменклатура.Ссылка.КоэффициентБазовых, 1)) КАК Количество,
	|	СпецификацияСписокНоменклатуры.ПредоставитЗаказчик КАК ПредоставитЗаказчик
	|ИЗ
	|	Документ.Спецификация.СписокНоменклатуры КАК СпецификацияСписокНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОсновнаяНоменклатура КАК ОсновнаяНоменклатура
	|		ПО СпецификацияСписокНоменклатуры.Номенклатура = ОсновнаяНоменклатура.Ссылка.БазоваяНоменклатура
	|ГДЕ
	|	СпецификацияСписокНоменклатуры.Ссылка В(&СписокСпецификаций)
	|	И ЕСТЬNULL(ОсновнаяНоменклатура.Ссылка, СпецификацияСписокНоменклатуры.Номенклатура) В (&Номенклатура)
	|
	|СГРУППИРОВАТЬ ПО
	|	СпецификацияСписокНоменклатуры.Ссылка,
	|	ЕСТЬNULL(ОсновнаяНоменклатура.Ссылка, СпецификацияСписокНоменклатуры.Номенклатура),
	|	СпецификацияСписокНоменклатуры.ПредоставитЗаказчик";
	Запрос.УстановитьПараметр("СписокСпецификаций", СписокСпецификаций);
	Запрос.УстановитьПараметр("Номенклатура", МассивНоменклатуры);
	
	ТаблицаРасшифровки.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьСпецификации(АдресТаблицы)
	
	ТЗ = ПолучитьИзВременногоХранилища(АдресТаблицы);
	Объект.СписокСпецификаций.Загрузить(ТЗ);
	ЗаполнитьНоменклатуройСервер();
	ПроверитьНаличиеКомплектаций(Объект.СписокСпецификаций.Выгрузить().ВыгрузитьКолонку("Спецификация"));
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНоменклатуройСервер() Экспорт 
	
	ТаблицаСпецификаций = Объект.СписокСпецификаций.Выгрузить();
	МассивСпецификаций = ТаблицаСпецификаций.ВыгрузитьКолонку("Спецификация");
	
	тзНоменклатура = ПолучитьНоменклатуруСпецификаций(МассивСпецификаций);
	Объект.СписокНоменклатурыПоСпецификациям.Загрузить(тзНоменклатура);
	
	тзНоменклатураОстатки = ПолучитьКоличествоМатериалов(тзНоменклатура);
	Объект.СписокНоменклатуры.Загрузить(тзНоменклатураОстатки);
	
	ОбновитьСписокВыбораНоменклатуры();
	
КонецПроцедуры

#КонецОбласти

#Область События_формы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбновитьСписокВыбораНоменклатуры();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ОбновитьСписокВыбораНоменклатуры();
	ПроверитьНаличиеКомплектаций(Объект.СписокСпецификаций.Выгрузить().ВыгрузитьКолонку("Спецификация"));
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	ПроверитьНаличиеКомплектаций(Объект.СписокСпецификаций.Выгрузить().ВыгрузитьКолонку("Спецификация"));
	
КонецПроцедуры

#КонецОбласти

#Область Команды

&НаКлиенте
Процедура ВыбратьСпецификации(Команда)
	
	Режим = РежимДиалогаВопрос.ДаНет;
	Текст = "Табличные части будут изменены." + Символы.ПС + "Продолжить?" ;
	
	Если Объект.СписокСпецификаций.Количество() > 0 
		И Вопрос(Текст, Режим, 0) = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	АдресТаблицы = ОткрытьФормуМодально("ОбщаяФорма.ФормаВыбораСпецификации", Новый Структура("Подразделение, ТЗ, Статус, УпорядочитьПоДате", Объект.Подразделение, Объект.СписокСпецификаций, ПредопределенноеЗначение("Перечисление.СтатусыСпецификации.Размещен"), Истина), ЭтаФорма);
	Если АдресТаблицы <> Неопределено Тогда
		ЗагрузитьСпецификации(АдресТаблицы);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНоменклатуру(Команда)
	
	ЗаполнитьНоменклатуройСервер();
	
КонецПроцедуры

#КонецОбласти

#Область Таблица_СписокНоменклатуры

&НаКлиенте
Процедура СписокНоменклатурыПриАктивизацииСтроки(Элемент)
	
	Если Элемент.ТекущиеДанные <> Неопределено Тогда
		Номенклатура = Элемент.ТекущиеДанные.Номенклатура;
		ПредоставитЗаказчик = Элемент.ТекущиеДанные.ПредоставитЗаказчик;
		
		ОтборСтруктура = Новый Структура;
		ОтборСтруктура.Вставить("ПредоставитЗаказчик",ПредоставитЗаказчик);
		
		Если ЗначениеЗаполнено(Номенклатура) Тогда
			ОтборСтруктура.Вставить("Номенклатура",Номенклатура);
		Иначе
			ОтборСтруктура.Вставить("Номенклатура",ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка"));
		КонецЕсли;
		
		ОтборСтрок = Новый ФиксированнаяСтруктура(ОтборСтруктура);
		Элементы.ТаблицаРасшифровки.ОтборСтрок = ОтборСтрок;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область Таблица_СписокСпецификаций

&НаКлиенте
Процедура СписокСпецификацийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОткрытьЗначение(Элементы.СписокСпецификаций.ТекущиеДанные.Спецификация);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокСпецификацийОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.Спецификация") Тогда
		
		ПроверитьНаличиеКомплектаций(ВыбранноеЗначение);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокСпецификацийСпецификацияПриИзменении(Элемент)
	
	Элементы.СписокСпецификаций.ТекущиеДанные.ОсобыеУслуги = ЛексСервер.ЗначенияРеквизитовОбъекта(Элементы.СписокСпецификаций.ТекущиеДанные.Спецификация, "ОсобыеУслуги").ОсобыеУслуги;
	ПроверитьНаличиеКомплектаций(Элементы.СписокСпецификаций.ТекущиеДанные.Спецификация);
	
КонецПроцедуры

#КонецОбласти

#Область Таблица_Расшифровка

&НаКлиенте
Процедура ТаблицаРасшифровкиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ТаблицаРасшифровки.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		Спецификация = ТекущиеДанные.Спецификация;
		
		Если ЗначениеЗаполнено(Спецификация) Тогда
			ОткрытьЗначение(Спецификация);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
