
Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка) Экспорт
	
	ЛексСервер.ПолучитьПредставлениеДокумента(Данные, Представление, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПечатьОперативныйЗакуп") Тогда
		ПодготовитьПечатнуюФорму("ПечатьОперативныйЗакуп", "Оперативный закуп", "Документ.ОперативныйЗакуп.ПечатьОперативныйЗакуп",
		МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПодготовитьПечатнуюФорму(Знач ИмяМакета, ПредставлениеМакета, ПолныйПутьКМакету = "", МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода)
	
	Если ИмяМакета = "ПечатьОперативныйЗакуп" Тогда
		ТабДок = ПечатьОперативныйЗакуп(МассивОбъектов, ОбъектыПечати);
	КонецЕсли;
	
	УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, ИмяМакета,
	ПредставлениеМакета, ТабДок,, ПолныйПутьКМакету);
	
КонецПроцедуры

Функция ПечатьОперативныйЗакуп(МассивОбъектов, ОбъектыПечати) Экспорт
	
	ФормСтрока = "Л = ru_RU; ДП = Истина";
	ПарПредмета="рубль, рубля, рублей, м, копейка, копейки, копеек, ж, 0";
	
	Макет = Документы.ОперативныйЗакуп.ПолучитьМакет("ПечатьОперативныйЗакуп");
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	ОбластьКонтрагент = Макет.ПолучитьОбласть("Контрагент");
	Подвал = Макет.ПолучитьОбласть("Подвал");
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.ИмяПараметровПечати = "ПараметрыПечати_ОперативныйЗакуп";
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Ложь;
	ТабДок.ТолькоПросмотр = Истина;
	ТабДок.ОтображатьЗаголовки = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОперативныйЗакупСписокНоменклатуры.Ссылка КАК Ссылка,
	|	ОперативныйЗакупСписокНоменклатуры.Поставщик КАК Поставщик,
	|	ОперативныйЗакупСписокНоменклатуры.Поставщик.ЮридическийАдрес КАК ПоставщикЮрАдрес,
	|	ОперативныйЗакупСписокНоменклатуры.Поставщик.ПочтовыйАдрес КАК ПоставщикПочтовыйАдрес,
	|	ОперативныйЗакупСписокНоменклатуры.Номенклатура.Наименование КАК Номенклатура,
	|	ОперативныйЗакупСписокНоменклатуры.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ОперативныйЗакупСписокНоменклатуры.КоличествоАвансовый КАК Количество,
	|	ОперативныйЗакупСписокНоменклатуры.Цена КАК ПлановаяЦена,
	|	ОперативныйЗакупСписокНоменклатуры.Стоимость
	|ИЗ
	|	Документ.ОперативныйЗакуп.СписокНоменклатуры КАК ОперативныйЗакупСписокНоменклатуры
	|ГДЕ
	|	ОперативныйЗакупСписокНоменклатуры.Ссылка В(&МассивОбъектов)
	|	И ОперативныйЗакупСписокНоменклатуры.КоличествоАвансовый > 0
	|ИТОГИ ПО
	|	Ссылка,
	|	Поставщик";
	
	ВыборкаДокументы = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаДокументы.Следующий() Цикл
		
		ДанныеДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВыборкаДокументы.Ссылка, "Дата, СуммаДокумента, Номер, Подразделение, Автор, Комментарий");
		ОбластьШапка.Параметры.Заполнить(ДанныеДокумента);
		ОбластьШапка.Параметры.НомерДокумента = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(ДанныеДокумента.Номер);
		ОбластьШапка.Параметры.ДатаДокумента = Формат(ДанныеДокумента.Дата, "ДФ=dd.MM.yyyy");
		ТабДок.Вывести(ОбластьШапка);
		
		ТабДок.Вывести(ОбластьШапкаТаблицы);
		
		НомерСтроки = 1;
		
		ВыборкаКонтрагенты = ВыборкаДокументы.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам); 
		Пока ВыборкаКонтрагенты.Следующий() Цикл
			
			ОбластьКонтрагент.Параметры.Поставщик = ВыборкаКонтрагенты.Поставщик;
			Если ЗначениеЗаполнено(ВыборкаКонтрагенты.ПоставщикЮрАдрес) Тогда
				ОбластьКонтрагент.Параметры.АдресПоставщика = ВыборкаКонтрагенты.ПоставщикЮрАдрес;	
			Иначе
				ОбластьКонтрагент.Параметры.АдресПоставщика = ВыборкаКонтрагенты.ПоставщикПочтовыйАдрес;
			КонецЕсли;
				
			ТабДок.Вывести(ОбластьКонтрагент);
			
			Выборка = ВыборкаКонтрагенты.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока Выборка.Следующий() Цикл
				
				ОбластьСтрока.Параметры.НомерСтроки = НомерСтроки;
				ОбластьСтрока.Параметры.Заполнить(Выборка);
				ТабДок.Вывести(ОбластьСтрока);
				
				НомерСтроки = НомерСтроки + 1;
				
			КонецЦикла; //Выборка.Следующий()
			
		КонецЦикла; //ВыборкаКонтрагенты.Следующий()
		
		Подвал.Параметры.ИтогСтоимость = ДанныеДокумента.СуммаДокумента;
		Подвал.Параметры.ИтогСтоимостьПрописью = ЧислоПрописью(ДанныеДокумента.СуммаДокумента, ФормСтрока, ПарПредмета);
		ТабДок.Вывести(Подвал);
		
	КонецЦикла; //ВыборкаДокументы.Следующий()
	
	Возврат ТабДок;
	
КонецФункции