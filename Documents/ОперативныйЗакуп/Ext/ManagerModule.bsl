
Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка) Экспорт
	
	ЛексСервер.ПолучитьПредставлениеДокумента(Данные, Представление, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПечатьОперативныйЗакуп") Тогда
		ПодготовитьПечатнуюФорму("ПечатьОперативныйЗакуп", "Оперативный закуп", "Документ.ОперативныйЗакуп.ПечатьОперативныйЗакуп",
		МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПодготовитьПечатнуюФорму(Знач ИмяМакета, ПредставлениеМакета, ПолныйПутьКМакету = "", МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода)
	
	Если ИмяМакета = "ПечатьОперативныйЗакуп" Тогда
		ТабДок = ПечатьОперативныйЗакуп(МассивОбъектов, ОбъектыПечати);
	КонецЕсли;
	
	УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, ИмяМакета,
	ПредставлениеМакета, ТабДок,, ПолныйПутьКМакету);
	
КонецПроцедуры

Функция ПечатьОперативныйЗакуп(МассивОбъектов, ОбъектыПечати) Экспорт
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.ИмяПараметровПечати = "ПараметрыПечати_ОперативныйЗакуп";
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Ложь;
	ТабДок.ТолькоПросмотр = Истина;
	ТабДок.ОтображатьЗаголовки = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Список.Ссылка КАК Ссылка,
	|	Список.Поставщик КАК Поставщик,
	|	Список.Поставщик.ЮридическийАдрес КАК ПоставщикЮрАдрес,
	|	Список.Поставщик.ПочтовыйАдрес КАК ПоставщикПочтовыйАдрес,
	|	Список.Номенклатура КАК Номенклатура,
	|	Список.Номенклатура.Код КАК НоменклатураКод,
	|	Список.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	Список.КоличествоАвансовый КАК Количество,
	|	Список.Цена КАК ПлановаяЦена,
	|	Список.Стоимость
	|ИЗ
	|	Документ.ОперативныйЗакуп.СписокНоменклатуры КАК Список
	|ГДЕ
	|	Список.Ссылка В(&МассивОбъектов)
	|	И Список.КоличествоАвансовый > 0
	|ИТОГИ ПО
	|	Ссылка,
	|	Поставщик";
	
	ВыборкаДокументы = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ЗаполнитьПечатьОперативныйЗакуп(ТабДок, ВыборкаДокументы, Ложь);
	
	ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Список.Ссылка КАК Ссылка,
	|	Список.Поставщик КАК Поставщик,
	|	Список.Поставщик.ЮридическийАдрес КАК ПоставщикЮрАдрес,
	|	Список.Поставщик.ПочтовыйАдрес КАК ПоставщикПочтовыйАдрес,
	|	Список.Номенклатура КАК Номенклатура,
	|	Список.Номенклатура.Код КАК НоменклатураКод,
	|	Список.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	Список.КоличествоКупить КАК Количество,
	|	Список.Цена КАК ПлановаяЦена,
	|	Список.Спецификация КАК Спецификация
	|ИЗ
	|	Документ.ОперативныйЗакуп.НоменклатураПодЗаказ КАК Список
	|ГДЕ
	|	Список.Ссылка В(&МассивОбъектов)
	|	И Список.КоличествоКупить > 0
	|ИТОГИ ПО
	|	Ссылка,
	|	Поставщик";
	
	ВыборкаДокументы = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ЗаполнитьПечатьОперативныйЗакуп(ТабДок, ВыборкаДокументы, Истина);
	
	Возврат ТабДок;
	
КонецФункции

Процедура ЗаполнитьПечатьОперативныйЗакуп(ТабДок, ВыборкаДокументы, ПодЗаказ)
	
	ФормСтрока = "Л = ru_RU; ДП = Истина";
	ПарПредмета="рубль, рубля, рублей, м, копейка, копейки, копеек, ж, 0";
	
	Макет = Документы.ОперативныйЗакуп.ПолучитьМакет("ПечатьОперативныйЗакуп");
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	
	ОбластьКонтрагент = Макет.ПолучитьОбласть("Контрагент");
	ОбластьСоставШапка = Макет.ПолучитьОбласть("СоставШапка");
	ОбластьСоставСтрока = Макет.ПолучитьОбласть("СоставСтрока");
	Подвал = Макет.ПолучитьОбласть("Подвал");
	
	Если ПодЗаказ Тогда
		
		ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаПодЗаказ");
		ОбластьСтрока = Макет.ПолучитьОбласть("СтрокаПодЗаказ");
		
	Иначе
		
		ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
		ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
		
	КонецЕсли;
	
	Пока ВыборкаДокументы.Следующий() Цикл
		
		СуммаДокумента = 0;
		
		МассивНоменклатурыЕстьСостав = Новый Массив;
		
		ДанныеДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВыборкаДокументы.Ссылка, "Дата, СуммаДокумента, Номер, Подразделение, Автор, Комментарий");
		ОбластьШапка.Параметры.Заполнить(ДанныеДокумента);
		ОбластьШапка.Параметры.НомерДокумента = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(ДанныеДокумента.Номер);
		ОбластьШапка.Параметры.ДатаДокумента = Формат(ДанныеДокумента.Дата, "ДФ=dd.MM.yyyy");
		ТабДок.Вывести(ОбластьШапка);
		
		Если ПодЗаказ Тогда
			ТабДок.Вывести(Макет.ПолучитьОбласть("МатериалыПодЗаказ"));	
		КонецЕсли;
		
		ТабДок.Вывести(ОбластьШапкаТаблицы);
		
		НомерСтроки = 1;
		
		ВыборкаКонтрагенты = ВыборкаДокументы.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаКонтрагенты.Следующий() Цикл
			
			ОбластьКонтрагент.Параметры.Поставщик = ВыборкаКонтрагенты.Поставщик;
			Если ЗначениеЗаполнено(ВыборкаКонтрагенты.ПоставщикПочтовыйАдрес) Тогда
				ОбластьКонтрагент.Параметры.АдресПоставщика = ВыборкаКонтрагенты.ПоставщикПочтовыйАдрес;
			Иначе
				ОбластьКонтрагент.Параметры.АдресПоставщика = ВыборкаКонтрагенты.ПоставщикЮрАдрес;
			КонецЕсли;
				
			ТабДок.Вывести(ОбластьКонтрагент);
			
			ВыборкаНоменклатура = ВыборкаКонтрагенты.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаНоменклатура.Следующий() Цикл
				
				ОбластьСтрока.Параметры.НомерСтроки = НомерСтроки;
				ОбластьСтрока.Параметры.Заполнить(ВыборкаНоменклатура);
				ОбластьСтрока.Параметры.НоменклатураКод = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(ВыборкаНоменклатура.НоменклатураКод);
				
				Если ПодЗаказ Тогда
					ОбластьСтрока.Параметры.Спец = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(ВыборкаНоменклатура.Спецификация.Номер);	
				КонецЕсли;
				
				ТабДок.Вывести(ОбластьСтрока);
				
				СуммаДокумента = СуммаДокумента + ВыборкаНоменклатура.ПлановаяЦена * ВыборкаНоменклатура.Количество;
				НомерСтроки = НомерСтроки + 1;
				
				Если ВыборкаНоменклатура.Номенклатура.ЕстьОписаниеСостава Тогда
					МассивНоменклатурыЕстьСостав.Добавить(ВыборкаНоменклатура.Номенклатура);
				КонецЕсли;
				
			КонецЦикла; //Выборка.Следующий()
			
		КонецЦикла; //ВыборкаКонтрагенты.Следующий()
		
		Подвал.Параметры.ИтогСтоимость = СуммаДокумента;
		Подвал.Параметры.ИтогСтоимостьПрописью = ЧислоПрописью(СуммаДокумента, ФормСтрока, ПарПредмета);
		ТабДок.Вывести(Подвал);
		
		// Печать состава
		Если МассивНоменклатурыЕстьСостав.Количество() > 0 Тогда
			
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
			
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Массив", МассивНоменклатурыЕстьСостав);
			Запрос.Текст =
			"ВЫБРАТЬ
			|	НоменклатураОписаниеСостава.Ссылка КАК Номенклатура,
			|	НоменклатураОписаниеСостава.НомерСтроки,
			|	НоменклатураОписаниеСостава.Ингридиент,
			|	НоменклатураОписаниеСостава.Количество
			|ИЗ
			|	Справочник.Номенклатура.ОписаниеСостава КАК НоменклатураОписаниеСостава
			|ГДЕ
			|	НоменклатураОписаниеСостава.Ссылка В(&Массив)
			|ИТОГИ ПО
			|	Номенклатура";
			
			РезультатЗапроса = Запрос.Выполнить();
			
			ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаНоменклатура.Следующий() Цикл
				
				ОбластьСоставШапка.Параметры.Номенклатура = ВыборкаНоменклатура.Номенклатура;
				ТабДок.Вывести(ОбластьСоставШапка);
				
				ВыборкаИнгридиент = ВыборкаНоменклатура.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				
				Пока ВыборкаИнгридиент.Следующий() Цикл
					ОбластьСоставСтрока.Параметры.Заполнить(ВыборкаИнгридиент);
					ТабДок.Вывести(ОбластьСоставСтрока);
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры
