
Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка) Экспорт
	
	ЛексСервер.ПолучитьПредставлениеДокумента(Данные, Представление, СтандартнаяОбработка);
	
КонецПроцедуры

#Область ПЕЧАТЬ

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПечатьОперативныйЗакуп") Тогда
		ПодготовитьПечатнуюФорму("ПечатьОперативныйЗакуп", "Оперативный закуп", "Документ.ОперативныйЗакуп.ПечатьОперативныйЗакуп",
		МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ЗаявкаПоставщику") Тогда
		ПодготовитьПечатнуюФорму("ЗаявкаПоставщику", "Заявка поставщику", "Документ.ОперативныйЗакуп.ЗаявкаПоставщику",
		МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПодготовитьПечатнуюФорму(Знач ИмяМакета, ПредставлениеМакета, ПолныйПутьКМакету = "", МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода)
	
	Если ИмяМакета = "ПечатьОперативныйЗакуп" Тогда
		ТабДок = ПечатьОперативныйЗакуп(МассивОбъектов, ОбъектыПечати);
	ИначеЕсли ИмяМакета = "ЗаявкаПоставщику" Тогда
		ТабДок = ПечатьЗаявкаПоставщику(МассивОбъектов, ОбъектыПечати);
	КонецЕсли;
	
	Если ТабДок <> Неопределено Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, ИмяМакета,
		ПредставлениеМакета, ТабДок,, ПолныйПутьКМакету);
	КонецЕсли;
	
КонецПроцедуры

Функция ПечатьОперативныйЗакуп(Документ, ОбъектыПечати) Экспорт
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.ИмяПараметровПечати = "ПараметрыПечати_ОперативныйЗакуп";
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Ложь;
	ТабДок.ТолькоПросмотр = Истина;
	ТабДок.ОтображатьЗаголовки = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Документ", Документ);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Список.Ссылка КАК Ссылка,
	|	Список.Поставщик КАК Поставщик,
	|	Список.Номенклатура КАК Номенклатура,
	|	Список.Номенклатура.Наименование КАК Наименование,
	|	Список.Номенклатура.Код КАК НоменклатураКод,
	|	Список.КоличествоКупить КАК Количество,
	|	Список.Цена КАК ПлановаяЦена,
	|	Список.ПодЗаказ КАК ПодЗаказ,
	|	Список.Комментарий КАК Комментарий,
	|	Список.Цена * Список.КоличествоКупить КАК Сумма,
	|	Список.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения
	|ПОМЕСТИТЬ вТаб
	|ИЗ
	|	Документ.ОперативныйЗакуп.СписокНоменклатуры КАК Список
	|ГДЕ
	|	Список.Ссылка В(&Документ)
	|	И Список.КоличествоКупить > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Список.Ссылка КАК Ссылка,
	|	Список.Поставщик КАК Поставщик,
	|	Список.Номенклатура,
	|	Список.НоменклатураКод,
	|	Список.Количество,
	|	ВЫРАЗИТЬ(Список.ПлановаяЦена КАК ЧИСЛО(12, 2)) КАК ПлановаяЦена,
	|	Список.ПодЗаказ,
	|	Список.Комментарий,
	|	ВЫРАЗИТЬ(Список.Сумма КАК ЧИСЛО(12, 2)) КАК Сумма,
	|	Список.ЕдиницаИзмерения КАК ЕдиницаИзмерения
	|ИЗ
	|	вТаб КАК Список
	|
	|УПОРЯДОЧИТЬ ПО
	|	Список.Наименование
	|ИТОГИ ПО
	|	Ссылка,
	|	Поставщик";
	
	Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	СписокНоменклатуры = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Номенклатура");
	
	Инд = 0;
	
	Для Каждого Ном Из СписокНоменклатуры Цикл
		
		Если ЗначениеЗаполнено(Ном) И НЕ Ном.Базовый Тогда
			
			СписокНоменклатуры[Инд] = Ном.БазоваяНоменклатура;
			
		КонецЕсли;
		
		Инд = Инд + 1;
		
	КонецЦикла;
	
	Подразделение = Документ[0].Подразделение;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Документ", Документ);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.УстановитьПараметр("ОперативныйЗакуп", Документ[0]);
	Запрос.УстановитьПараметр("СписокНоменклатуры", СписокНоменклатуры);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОперативныйЗакупНарядЗадания.Ссылка КАК НарядЗаданиеСсылка
	|ПОМЕСТИТЬ втНарядЗадания
	|ИЗ
	|	Документ.ОперативныйЗакуп.НарядЗадания КАК ОперативныйЗакупНарядЗадания
	|ГДЕ
	|	ОперативныйЗакупНарядЗадания.Ссылка = &ОперативныйЗакуп
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НарядЗаданиеСписокСпецификаций.Спецификация КАК СпецификацияСсылка
	|ПОМЕСТИТЬ втСпецификации
	|ИЗ
	|	Документ.НарядЗадание.СписокСпецификаций КАК НарядЗаданиеСписокСпецификаций
	|ГДЕ
	|	НарядЗаданиеСписокСпецификаций.Ссылка В
	|			(ВЫБРАТЬ
	|				т.НарядЗаданиеСсылка
	|			ИЗ
	|				втНарядЗадания КАК т)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписокНом.Номенклатура КАК Номенклатура,
	|	СписокНом.Ссылка.Номер КАК Номер
	|ИЗ
	|	Документ.Спецификация.СписокНоменклатуры КАК СписокНом
	|ГДЕ
	|	СписокНом.Ссылка В
	|			(ВЫБРАТЬ
	|				т.СпецификацияСсылка
	|			ИЗ
	|				втСпецификации КАК т)
	|	И СписокНом.Номенклатура В(&СписокНоменклатуры)
	|
	|СГРУППИРОВАТЬ ПО
	|	СписокНом.Номенклатура,
	|	СписокНом.Ссылка.Номер
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номер";
	
	СписокСпецификаций = Запрос.Выполнить().Выгрузить();
	
	ФормСтрока = "Л = ru_RU; ДП = Истина";
	ПарПредмета="рубль, рубля, рублей, м, копейка, копейки, копеек, ж, 0";
	
	Макет = Документы.ОперативныйЗакуп.ПолучитьМакет("ПечатьОперативныйЗакуп");
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	
	ОбластьКонтрагент = Макет.ПолучитьОбласть("Контрагент");
	ОбластьСоставШапка = Макет.ПолучитьОбласть("СоставШапка");
	ОбластьСоставСтрока = Макет.ПолучитьОбласть("СоставСтрока");
	Подвал = Макет.ПолучитьОбласть("Подвал");
	
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	ОбластьСтрокаПодЗаказ = Макет.ПолучитьОбласть("СтрокаПодЗаказ");
	
	Логотип = ЛексСервер.ПолучитьЛоготипПоСсылке(Подразделение.Логотип);
	
	Пока Выборка.Следующий() Цикл
		
		СуммаДокумента = 0;
		
		МассивНоменклатурыЕстьСостав = Новый Массив;
		
		ДанныеДокумента = Выборка.Ссылка;
		
		Если ТипЗнч(Логотип) = Тип("ДвоичныеДанные") Тогда
			ОбластьШапка.Рисунки.D1.Картинка = Новый Картинка(Логотип);
			ОбластьШапка.Рисунки.D1.ВыводитьНаПечать = Истина;
		КонецЕсли;
		
		ОбластьШапка.Параметры.Подразделение = ДанныеДокумента.Подразделение;
		ОбластьШапка.Параметры.НомерДокумента = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(ДанныеДокумента.Номер);
		ОбластьШапка.Параметры.ДатаДокумента = Формат(ДанныеДокумента.Дата, "ДФ=dd.MM.yyyy");
		ТабДок.Вывести(ОбластьШапка);
		
		ТабДок.Вывести(ОбластьШапкаТаблицы);
		
		НомерСтроки = 1;
		
		ВыборкаКонтрагенты = Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаКонтрагенты.Следующий() Цикл
			
			ОбластьКонтрагент.Параметры.Поставщик = ВыборкаКонтрагенты.Поставщик;
			Если ЗначениеЗаполнено(ВыборкаКонтрагенты.Поставщик.ПочтовыйАдрес) Тогда
				ОбластьКонтрагент.Параметры.АдресПоставщика = ВыборкаКонтрагенты.Поставщик.ПочтовыйАдрес;
			Иначе
				ОбластьКонтрагент.Параметры.АдресПоставщика = ВыборкаКонтрагенты.Поставщик.ЮридическийАдрес;
			КонецЕсли;
			
			ТабДок.Вывести(ОбластьКонтрагент);
			
			ВыборкаНоменклатура = ВыборкаКонтрагенты.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаНоменклатура.Следующий() Цикл
				
				Если ВыборкаНоменклатура.ПодЗаказ Тогда
					ОбластьСтр = ОбластьСтрокаПодЗаказ;	
				Иначе
					ОбластьСтр = ОбластьСтрока;
				КонецЕсли;
				
				ОбластьСтр.Параметры.НомерСтроки = НомерСтроки;
				ОбластьСтр.Параметры.Заполнить(ВыборкаНоменклатура);
				ОбластьСтр.Параметры.НоменклатураКод = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(ВыборкаНоменклатура.НоменклатураКод);
				
				Если ВыборкаНоменклатура.ПодЗаказ Тогда
					
					Если ЗначениеЗаполнено(ВыборкаНоменклатура.Комментарий) Тогда
						ОбластьСтр.Параметры.Номенклатура = Строка(ВыборкаНоменклатура.Номенклатура) + Символы.ПС + "( "+ВыборкаНоменклатура.Комментарий+" )";
					КонецЕсли;
					
					ОбластьСтр.Параметры.Спец = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(ВыборкаНоменклатура.Спецификация.Номер);
				Иначе
					
					СтрокаСпец = "";
					Отбор = Новый Структура();
					Если ВыборкаНоменклатура.Номенклатура.Базовый Тогда
						Отбор.Вставить("Номенклатура", ВыборкаНоменклатура.Номенклатура);
					Иначе
						Отбор.Вставить("Номенклатура", ВыборкаНоменклатура.Номенклатура.БазоваяНоменклатура);
					КонецЕсли;
					
					Строки = СписокСпецификаций.НайтиСтроки(Отбор);
					
					Для Каждого Стр ИЗ Строки Цикл
						
						СтрокаСпец = СтрокаСпец + ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(Стр.Номер) + " / ";
						
					КонецЦикла;
					
					ОбластьСтр.Параметры.Спец = СтрокаСпец;
					
				КонецЕсли;
				
				ТабДок.Вывести(ОбластьСтр);
				
				СуммаДокумента = СуммаДокумента + ВыборкаНоменклатура.ПлановаяЦена * ВыборкаНоменклатура.Количество;
				НомерСтроки = НомерСтроки + 1;
				
				Если ВыборкаНоменклатура.Номенклатура.ЕстьОписаниеСостава Тогда
					МассивНоменклатурыЕстьСостав.Добавить(ВыборкаНоменклатура.Номенклатура);
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
		Подвал.Параметры.ИтогСтоимость = СуммаДокумента;
		Подвал.Параметры.ИтогСтоимостьПрописью = ЧислоПрописью(СуммаДокумента, ФормСтрока, ПарПредмета);
		ТабДок.Вывести(Подвал);
		
		// Печать состава
		Если МассивНоменклатурыЕстьСостав.Количество() > 0 Тогда
			
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
			
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Массив", МассивНоменклатурыЕстьСостав);
			Запрос.Текст =
			"ВЫБРАТЬ
			|	НоменклатураОписаниеСостава.Ссылка КАК Номенклатура,
			|	НоменклатураОписаниеСостава.НомерСтроки,
			|	НоменклатураОписаниеСостава.Ингридиент,
			|	НоменклатураОписаниеСостава.Количество
			|ИЗ
			|	Справочник.Номенклатура.ОписаниеСостава КАК НоменклатураОписаниеСостава
			|ГДЕ
			|	НоменклатураОписаниеСостава.Ссылка В(&Массив)
			|ИТОГИ ПО
			|	Номенклатура";
			
			РезультатЗапроса = Запрос.Выполнить();
			
			ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаНоменклатура.Следующий() Цикл
				
				ОбластьСоставШапка.Параметры.Номенклатура = ВыборкаНоменклатура.Номенклатура;
				ТабДок.Вывести(ОбластьСоставШапка);
				
				ВыборкаИнгридиент = ВыборкаНоменклатура.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				
				Пока ВыборкаИнгридиент.Следующий() Цикл
					ОбластьСоставСтрока.Параметры.Заполнить(ВыборкаИнгридиент);
					ТабДок.Вывести(ОбластьСоставСтрока);
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции

Функция ПечатьЗаявкаПоставщику(Документ, ОбъектыПечати) Экспорт
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.ИмяПараметровПечати = "ПараметрыПечати_ЗаявкаПоставщику";
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Ложь;
	ТабДок.ТолькоПросмотр = Истина;
	ТабДок.ОтображатьЗаголовки = Ложь;
	
	Макет = Документы.ОперативныйЗакуп.ПолучитьМакет("ЗаявкаПоставщику");
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	ОбластьСоставШапка = Макет.ПолучитьОбласть("СоставШапка");
	ОбластьСоставСтрока = Макет.ПолучитьОбласть("СоставСтрока");
	ОбластьСоставПоставщик = Макет.ПолучитьОбласть("СоставПоставщик");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылки", Документ);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Закуп.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	Закуп.КоличествоПоставщик КАК КоличествоПоставщик,
	|	Закуп.Номенклатура КАК Номенклатура,
	|	Закуп.Цена * Закуп.КоличествоПоставщик КАК Сумма,
	|	Закуп.Поставщик КАК Поставщик,
	|	Закуп.Комментарий КАК Комментарий
	|ПОМЕСТИТЬ вТаб
	|ИЗ
	|	Документ.ОперативныйЗакуп.СписокНоменклатуры КАК Закуп
	|ГДЕ
	|	Закуп.Ссылка В(&Ссылки)
	|	И Закуп.КоличествоПоставщик > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДанные.Поставщик КАК Поставщик,
	|	втДанные.Номенклатура,
	|	СУММА(втДанные.КоличествоПоставщик) КАК Количество,
	|	СУММА(втДанные.Сумма) КАК Сумма,
	|	втДанные.Поставщик.Телефон КАК Телефон,
	|	ВЫРАЗИТЬ(втДанные.Поставщик.ЮридическийАдрес КАК СТРОКА(100)) КАК Адрес,
	|	втДанные.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВЫРАЗИТЬ(втДанные.Комментарий КАК СТРОКА(100)) КАК Комментарий
	|ИЗ
	|	вТаб КАК втДанные
	|
	|СГРУППИРОВАТЬ ПО
	|	втДанные.Номенклатура,
	|	втДанные.Поставщик,
	|	ВЫРАЗИТЬ(втДанные.Поставщик.ЮридическийАдрес КАК СТРОКА(100)),
	|	втДанные.Поставщик.Телефон,
	|	втДанные.Номенклатура.ЕдиницаИзмерения,
	|	ВЫРАЗИТЬ(втДанные.Комментарий КАК СТРОКА(100))
	|ИТОГИ
	|	СУММА(Сумма)
	|ПО
	|	Поставщик";
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаПоставщик = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаПоставщик.Следующий() Цикл
		
		МассивНоменклатурыЕстьСостав = Новый Массив;
		
		ОбластьЗаголовок.Параметры.Заполнить(ВыборкаПоставщик);
		ОбластьЗаголовок.Параметры.ТекущаяДата = Формат(ТекущаяДата(), "ДЛФ=DD");
		ТабДок.Вывести(ОбластьЗаголовок);
		
		ОбластьПодвал.Параметры.Сумма = ВыборкаПоставщик.Сумма;
		
		ВыборкаНоменклатура = ВыборкаПоставщик.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаНоменклатура.Следующий() Цикл
			
			ОбластьСтрока.Параметры.Заполнить(ВыборкаНоменклатура);
			
			Если ЗначениеЗаполнено(ВыборкаНоменклатура.Комментарий) Тогда
				ОбластьСтрока.Параметры.Номенклатура = Строка(ВыборкаНоменклатура.Номенклатура) + Символы.ПС + "( "+ВыборкаНоменклатура.Комментарий+" )";
			КонецЕсли;
			
			Если ВыборкаНоменклатура.Номенклатура.ЕстьОписаниеСостава Тогда
				МассивНоменклатурыЕстьСостав.Добавить(ВыборкаНоменклатура.Номенклатура);
			КонецЕсли;
			
			ТабДок.Вывести(ОбластьСтрока);
			
		КонецЦикла;
		
		ТабДок.Вывести(ОбластьПодвал);
		
		ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		
		// Печать состава
		Если МассивНоменклатурыЕстьСостав.Количество() > 0 Тогда
			
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Массив", МассивНоменклатурыЕстьСостав);
			Запрос.Текст =
			"ВЫБРАТЬ
			|	НоменклатураОписаниеСостава.Ссылка КАК Номенклатура,
			|	НоменклатураОписаниеСостава.НомерСтроки,
			|	НоменклатураОписаниеСостава.Ингридиент,
			|	НоменклатураОписаниеСостава.Количество
			|ИЗ
			|	Справочник.Номенклатура.ОписаниеСостава КАК НоменклатураОписаниеСостава
			|ГДЕ
			|	НоменклатураОписаниеСостава.Ссылка В(&Массив)
			|ИТОГИ ПО
			|	Номенклатура";
			
			РезультатЗапроса = Запрос.Выполнить();
			
			Если НЕ РезультатЗапроса.Пустой() Тогда
				ОбластьСоставПоставщик.Параметры.Поставщик = ВыборкаПоставщик.Поставщик;
				ТабДок.Вывести(ОбластьСоставПоставщик);
			КонецЕсли;
			
			ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаНоменклатура.Следующий() Цикл
				
				ОбластьСоставШапка.Параметры.Номенклатура = ВыборкаНоменклатура.Номенклатура;
				ТабДок.Вывести(ОбластьСоставШапка);
				
				ВыборкаИнгридиент = ВыборкаНоменклатура.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				
				Пока ВыборкаИнгридиент.Следующий() Цикл
					ОбластьСоставСтрока.Параметры.Заполнить(ВыборкаИнгридиент);
					ТабДок.Вывести(ОбластьСоставСтрока);
				КонецЦикла;
				
			КонецЦикла;
			
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции

#КонецОбласти
