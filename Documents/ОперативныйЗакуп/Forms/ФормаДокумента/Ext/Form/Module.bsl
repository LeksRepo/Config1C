
&НаКлиенте
Процедура ТаблицаРасшифровкиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОткрытьЗначение(Элементы.ТаблицаРасшифровки.ТекущиеДанные.Спецификация);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаполнитьНаСервере()
	
	Если Объект.СписокНоменклатуры.Количество() > 0 ИЛИ Объект.СписокНоменклатурыПодЗаказ.Количество() > 0 Тогда
	
		Ответ = Вопрос("Данные введенные вручную будут утеряны. Продолжить?",РежимДиалогаВопрос.ДаНет);
		
		Если Ответ = КодВозвратаДиалога.Да Тогда
			 ЗаполнитьНаСервере();
		 КонецЕсли;
	Иначе
		 ЗаполнитьНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаСервере()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Объект.Подразделение.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	СвойстваПодразделения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.Подразделение, "ОсновнойСклад, Регион");
	
	#Область Запрос
	
	ПодразделениеСклад = Объект.Подразделение;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Подразделение", Объект.Подразделение);
	Запрос.УстановитьПараметр("ПодразделениеСклад", ПодразделениеСклад);
	Запрос.УстановитьПараметр("Склад", СвойстваПодразделения.ОсновнойСклад);
	//Запрос.УстановитьПараметр("Регион", СвойстваПодразделения.Регион);
	Запрос.УстановитьПараметр("Поставщик", Объект.Поставщик);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА УправленческийОстатки.Субконто2.Базовый
	|			ТОГДА ВЫРАЗИТЬ(УправленческийОстатки.Субконто2 КАК Справочник.Номенклатура)
	|		ИНАЧЕ ВЫРАЗИТЬ(УправленческийОстатки.Субконто2.БазоваяНоменклатура КАК Справочник.Номенклатура)
	|	КОНЕЦ КАК Номенклатура,
	|	СУММА(ВЫБОР
	|			КОГДА УправленческийОстатки.Субконто2.Базовый
	|				ТОГДА УправленческийОстатки.КоличествоОстатокДт
	|			ИНАЧЕ УправленческийОстатки.КоличествоОстатокДт * УправленческийОстатки.Субконто2.КоэффициентБазовых
	|		КОНЕЦ) КАК Количество
	|ПОМЕСТИТЬ ВТОстаткиНаСкладе
	|ИЗ
	|	РегистрБухгалтерии.Управленческий.Остатки(
	|			,
	|			Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.МатериалыНаСкладе),
	|			,
	|			Подразделение = &ПодразделениеСклад
	|				И Субконто1 = &Склад
	|				И НЕ Субконто2.МатериалЗаказчика) КАК УправленческийОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА УправленческийОстатки.Субконто2.Базовый
	|			ТОГДА ВЫРАЗИТЬ(УправленческийОстатки.Субконто2 КАК Справочник.Номенклатура)
	|		ИНАЧЕ ВЫРАЗИТЬ(УправленческийОстатки.Субконто2.БазоваяНоменклатура КАК Справочник.Номенклатура)
	|	КОНЕЦ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УправленческийОстатки.Субконто2 КАК Номенклатура,
	|	УправленческийОстатки.КоличествоОстатокДт КАК Количество
	|ПОМЕСТИТЬ ВТРезервныйЗапас
	|ИЗ
	|	РегистрБухгалтерии.Управленческий.Остатки(
	|			,
	|			Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.РезервныйЗапасМатериалов),
	|			,
	|			Подразделение = &ПодразделениеСклад
	|				И Субконто1 = &Склад) КАК УправленческийОстатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ВТОстаткиНаСкладе.Номенклатура ЕСТЬ NULL 
	|			ТОГДА ВТРезервныйЗапас.Номенклатура
	|		ИНАЧЕ ВТОстаткиНаСкладе.Номенклатура
	|	КОНЕЦ КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВТРезервныйЗапас.Количество, 0) - ЕСТЬNULL(ВТОстаткиНаСкладе.Количество, 0) > 0
	|			ТОГДА ЕСТЬNULL(ВТРезервныйЗапас.Количество, 0) - ЕСТЬNULL(ВТОстаткиНаСкладе.Количество, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Количество,
	|	ЕСТЬNULL(ВТОстаткиНаСкладе.Количество, 0) КАК ОстатокНаСкладе
	|ПОМЕСТИТЬ ВТДокупитьНаСклад
	|ИЗ
	|	ВТОстаткиНаСкладе КАК ВТОстаткиНаСкладе
	|		ПОЛНОЕ СОЕДИНЕНИЕ ВТРезервныйЗапас КАК ВТРезервныйЗапас
	|		ПО ВТОстаткиНаСкладе.Номенклатура = ВТРезервныйЗапас.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиВЦеху.Субконто1 КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(БазовыйЛимитЦехаСрезПоследних.Количество, 0) > ОстаткиВЦеху.КоличествоОстатокДт
	|			ТОГДА 0
	|		ИНАЧЕ ОстаткиВЦеху.КоличествоОстатокДт - ЕСТЬNULL(БазовыйЛимитЦехаСрезПоследних.Количество, 0)
	|	КОНЕЦ КАК ОстатокВЦехуСУчетомЛимита
	|ПОМЕСТИТЬ ВТЦеховыеОстатки
	|ИЗ
	|	РегистрБухгалтерии.Управленческий.Остатки(, Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ОсновноеПроизводство), , Подразделение = &Подразделение) КАК ОстаткиВЦеху
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.БазовыйЛимитЦеха.СрезПоследних(, Подразделение = &Подразделение) КАК БазовыйЛимитЦехаСрезПоследних
	|		ПО ОстаткиВЦеху.Субконто1 = БазовыйЛимитЦехаСрезПоследних.Номенклатура
	|ГДЕ
	|	ЕСТЬNULL(ОстаткиВЦеху.КоличествоОстатокДт, 0) - ЕСТЬNULL(БазовыйЛимитЦехаСрезПоследних.Количество, 0) > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СпецификацияСписокНоменклатуры.Номенклатура КАК Номенклатура,
	|	СУММА(СпецификацияСписокНоменклатуры.Количество) КАК КоличествоВсегоТребуется,
	|	СУММА(ВЫБОР
	|			КОГДА СтатусСпецификацииСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСпецификации.Рассчитывается)
	|				ТОГДА СпецификацияСписокНоменклатуры.Количество
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КоличествоРассчитывается,
	|	СписокМатериаловПодЗаказ.Поставщик КАК Поставщик,
	|	СписокМатериаловПодЗаказ.Ссылка КАК СсылкаСпец,
	|	СписокМатериаловПодЗаказ.Комментарий КАК Комментарий
	|ПОМЕСТИТЬ ВТТребуетсяПоСпецификациям
	|ИЗ
	|	Документ.Спецификация.СписокНоменклатуры КАК СпецификацияСписокНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусСпецификации.СрезПоследних КАК СтатусСпецификацииСрезПоследних
	|		ПО СпецификацияСписокНоменклатуры.Ссылка = СтатусСпецификацииСрезПоследних.Спецификация
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Спецификация.СписокМатериаловПодЗаказ КАК СписокМатериаловПодЗаказ
	|		ПО (СписокМатериаловПодЗаказ.Номенклатура = СпецификацияСписокНоменклатуры.Номенклатура)
	|			И (СписокМатериаловПодЗаказ.Ссылка = СпецификацияСписокНоменклатуры.Ссылка)
	|			И (СписокМатериаловПодЗаказ.Количество = СпецификацияСписокНоменклатуры.Количество)
	|ГДЕ
	|	СтатусСпецификацииСрезПоследних.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыСпецификации.Рассчитывается), ЗНАЧЕНИЕ(Перечисление.СтатусыСпецификации.Размещен), ЗНАЧЕНИЕ(Перечисление.СтатусыСпецификации.ПереданВЦех))
	|	И СпецификацияСписокНоменклатуры.Ссылка.Производство = &Подразделение
	|	И СпецификацияСписокНоменклатуры.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Материал)
	|	И НЕ СпецификацияСписокНоменклатуры.Номенклатура.МатериалЗаказчика
	|
	|СГРУППИРОВАТЬ ПО
	|	СпецификацияСписокНоменклатуры.Номенклатура,
	|	СписокМатериаловПодЗаказ.Поставщик,
	|	СписокМатериаловПодЗаказ.Ссылка,
	|	СписокМатериаловПодЗаказ.Комментарий
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТТребуетсяПоСпецификациям.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА ВТТребуетсяПоСпецификациям.СсылкаСпец ЕСТЬ NULL 
	|			ТОГДА СУММА(ВТТребуетсяПоСпецификациям.КоличествоВсегоТребуется - ЕСТЬNULL(ВТЦеховыеОстатки.ОстатокВЦехуСУчетомЛимита, 0))
	|		ИНАЧЕ СУММА(ВТТребуетсяПоСпецификациям.КоличествоВсегоТребуется)
	|	КОНЕЦ КАК Количество,
	|	ВТТребуетсяПоСпецификациям.СсылкаСпец КАК СсылкаСпец,
	|	ВТТребуетсяПоСпецификациям.Поставщик КАК Поставщик
	|ПОМЕСТИТЬ ВТДокупитьНаЦех
	|ИЗ
	|	ВТТребуетсяПоСпецификациям КАК ВТТребуетсяПоСпецификациям
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЦеховыеОстатки КАК ВТЦеховыеОстатки
	|		ПО ВТТребуетсяПоСпецификациям.Номенклатура = ВТЦеховыеОстатки.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОстаткиНаСкладе КАК ВТОстаткиНаСкладе
	|		ПО ВТТребуетсяПоСпецификациям.Номенклатура = ВТОстаткиНаСкладе.Номенклатура
	|ГДЕ
	|	ВЫБОР
	|			КОГДА ВТТребуетсяПоСпецификациям.СсылкаСпец ЕСТЬ NULL 
	|				ТОГДА ВТТребуетсяПоСпецификациям.КоличествоВсегоТребуется - ЕСТЬNULL(ВТЦеховыеОстатки.ОстатокВЦехуСУчетомЛимита, 0) > ЕСТЬNULL(ВТОстаткиНаСкладе.Количество, 0)
	|			ИНАЧЕ ВТТребуетсяПоСпецификациям.КоличествоВсегоТребуется > 0
	|		КОНЕЦ
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТТребуетсяПоСпецификациям.Номенклатура,
	|	ВТТребуетсяПоСпецификациям.СсылкаСпец,
	|	ВТТребуетсяПоСпецификациям.Поставщик
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ВТДокупитьНаСклад.Номенклатура ЕСТЬ NULL 
	|			ТОГДА ВТДокупитьНаЦех.Номенклатура
	|		ИНАЧЕ ВТДокупитьНаСклад.Номенклатура
	|	КОНЕЦ КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА ВТТребуетсяПоСпецификациям.СсылкаСпец ЕСТЬ NULL 
	|			ТОГДА СУММА(ЕСТЬNULL(ВТДокупитьНаСклад.Количество, 0) + ЕСТЬNULL(ВТДокупитьНаЦех.Количество, 0) - ЕСТЬNULL(ВТДокупитьНаСклад.ОстатокНаСкладе, 0))
	|		ИНАЧЕ СУММА(ЕСТЬNULL(ВТДокупитьНаЦех.Количество, 0))
	|	КОНЕЦ КАК Количество,
	|	ВТТребуетсяПоСпецификациям.СсылкаСпец КАК СсылкаСпец,
	|	ВТТребуетсяПоСпецификациям.Поставщик КАК Поставщик
	|ПОМЕСТИТЬ ВТМатериалыДляЗакупа
	|ИЗ
	|	ВТТребуетсяПоСпецификациям КАК ВТТребуетсяПоСпецификациям
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДокупитьНаСклад КАК ВТДокупитьНаСклад
	|			ПОЛНОЕ СОЕДИНЕНИЕ ВТДокупитьНаЦех КАК ВТДокупитьНаЦех
	|			ПО ВТДокупитьНаСклад.Номенклатура = ВТДокупитьНаЦех.Номенклатура
	|		ПО (ВТДокупитьНаСклад.Номенклатура = ВТТребуетсяПоСпецификациям.Номенклатура
	|				ИЛИ ВТДокупитьНаЦех.Номенклатура = ВТТребуетсяПоСпецификациям.Номенклатура)
	|			И (ВЫБОР
	|				КОГДА ВТДокупитьНаЦех.СсылкаСпец ЕСТЬ NULL 
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ВТДокупитьНаЦех.СсылкаСпец = ВТТребуетсяПоСпецификациям.СсылкаСпец
	|			КОНЕЦ)
	|ГДЕ
	|	ЕСТЬNULL(ВТДокупитьНаСклад.Количество, 0) + ЕСТЬNULL(ВТДокупитьНаЦех.Количество, 0) - ЕСТЬNULL(ВТДокупитьНаСклад.ОстатокНаСкладе, 0) > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА ВТДокупитьНаСклад.Номенклатура ЕСТЬ NULL 
	|			ТОГДА ВТДокупитьНаЦех.Номенклатура
	|		ИНАЧЕ ВТДокупитьНаСклад.Номенклатура
	|	КОНЕЦ,
	|	ВТТребуетсяПоСпецификациям.СсылкаСпец,
	|	ВТТребуетсяПоСпецификациям.Поставщик
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(спрНоменклатура.Ссылка, ВТМатериалыДляЗакупа.Номенклатура) КАК Номенклатура,
	|	ВЫРАЗИТЬ(ВТМатериалыДляЗакупа.Количество / ЕСТЬNULL(спрНоменклатура.КоэффициентБазовых, 1) + 0.49999 КАК ЧИСЛО(15, 0)) КАК Количество,
	|	ВЫБОР
	|		КОГДА ВТМатериалыДляЗакупа.Количество > ВТТребуетсяПоСпецификациям.КоличествоРассчитывается
	|			ТОГДА ВЫРАЗИТЬ((ВТМатериалыДляЗакупа.Количество - ВТТребуетсяПоСпецификациям.КоличествоРассчитывается) / ЕСТЬNULL(спрНоменклатура.КоэффициентБазовых, 1) + 0.49999 КАК ЧИСЛО(15, 0))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоСрочно,
	|	ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.ПлановаяЗакупочная, 0) * ЕСТЬNULL(спрНоменклатура.КоэффициентБазовых, 1) КАК Цена,
	|	ВЫБОР
	|		КОГДА ВТТребуетсяПоСпецификациям.Поставщик ЕСТЬ NULL 
	|			ТОГДА ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.Поставщик, """")
	|		ИНАЧЕ ВТТребуетсяПоСпецификациям.Поставщик
	|	КОНЕЦ КАК Поставщик,
	|	ВЫБОР
	|		КОГДА ВТТребуетсяПоСпецификациям.Поставщик ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ВПодЗаказ,
	|	ВТТребуетсяПоСпецификациям.СсылкаСпец,
	|	ВТТребуетсяПоСпецификациям.Комментарий
	|ПОМЕСТИТЬ ВТМатериалыСоСтоимостью
	|ИЗ
	|	ВТМатериалыДляЗакупа КАК ВТМатериалыДляЗакупа
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатурыПоПодразделениям.СрезПоследних(, Подразделение = &Подразделение) КАК ЦеныНоменклатурыСрезПоследних
	|		ПО ВТМатериалыДляЗакупа.Номенклатура = ЦеныНоменклатурыСрезПоследних.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК спрНоменклатура
	|		ПО ВТМатериалыДляЗакупа.Номенклатура = спрНоменклатура.БазоваяНоменклатура
	|			И (спрНоменклатура.ОсновнаяПоСкладу)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТТребуетсяПоСпецификациям КАК ВТТребуетсяПоСпецификациям
	|		ПО ВТМатериалыДляЗакупа.Номенклатура = ВТТребуетсяПоСпецификациям.Номенклатура
	|			И ВТМатериалыДляЗакупа.СсылкаСпец = ВТТребуетсяПоСпецификациям.СсылкаСпец
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТМатериалыСоСтоимостью.Номенклатура КАК Номенклатура,
	|	ВТМатериалыСоСтоимостью.ВПодЗаказ,
	|	ВТМатериалыСоСтоимостью.Поставщик,
	|	ВТМатериалыСоСтоимостью.Цена,
	|	ВТМатериалыСоСтоимостью.Количество,
	|	ВТМатериалыСоСтоимостью.Количество * ВТМатериалыСоСтоимостью.Цена КАК Стоимость,
	|	ВТМатериалыСоСтоимостью.КоличествоСрочно,
	|	ВТМатериалыСоСтоимостью.КоличествоСрочно * ВТМатериалыСоСтоимостью.Цена КАК СтоимостьСрочно,
	|	ВТМатериалыСоСтоимостью.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	НоменклатураПодразделений.Подразделение КАК Подразделение,
	|	НоменклатураПодразделений.ЗакупОптом КАК ЗакупОптом,
	|	ВТМатериалыСоСтоимостью.СсылкаСпец.Номер КАК Спецификация,
	|	ВТМатериалыСоСтоимостью.Комментарий КАК Комментарий
	|ПОМЕСТИТЬ ВТПолныйНаборДанных
	|ИЗ
	|	ВТМатериалыСоСтоимостью КАК ВТМатериалыСоСтоимостью
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатураПодразделений КАК НоменклатураПодразделений
	|		ПО (ВЫБОР
	|				КОГДА ВТМатериалыСоСтоимостью.Номенклатура.Базовый
	|					ТОГДА ВТМатериалыСоСтоимостью.Номенклатура = НоменклатураПодразделений.Номенклатура
	|				ИНАЧЕ ВТМатериалыСоСтоимостью.Номенклатура.БазоваяНоменклатура = НоменклатураПодразделений.Номенклатура
	|			КОНЕЦ)
	|ГДЕ
	|	НоменклатураПодразделений.Подразделение = &Подразделение
	|	И ВЫБОР
	|			КОГДА &Поставщик = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ &Поставщик = ВТМатериалыСоСтоимостью.Поставщик
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таб.Номенклатура,
	|	Таб.ВПодЗаказ,
	|	Таб.Поставщик,
	|	Таб.Цена,
	|	Таб.Количество,
	|	Таб.Стоимость,
	|	Таб.КоличествоСрочно,
	|	Таб.СтоимостьСрочно,
	|	Таб.ЕдиницаИзмерения,
	|	Таб.Подразделение,
	|	Таб.ЗакупОптом,
	|	Таб.Спецификация,
	|	Таб.Комментарий
	|ИЗ
	|	ВТПолныйНаборДанных КАК Таб
	|ГДЕ
	|	Таб.ВПодЗаказ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Таб.Поставщик.Наименование,
	|	Таб.Номенклатура.Наименование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таб.Номенклатура,
	|	Таб.ВПодЗаказ,
	|	Таб.Поставщик,
	|	Таб.Цена,
	|	Таб.Количество,
	|	Таб.Стоимость,
	|	Таб.КоличествоСрочно,
	|	Таб.СтоимостьСрочно,
	|	Таб.ЕдиницаИзмерения,
	|	Таб.Подразделение,
	|	Таб.ЗакупОптом,
	|	Таб.Спецификация,
	|	Таб.Комментарий
	|ИЗ
	|	ВТПолныйНаборДанных КАК Таб
	|ГДЕ
	|	НЕ Таб.ВПодЗаказ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Таб.Поставщик.Наименование,
	|	Таб.Номенклатура.Наименование";
	
	#КонецОбласти
	
	Результат = Запрос.ВыполнитьПакет();
	Объект.СписокНоменклатурыПодЗаказ.Загрузить(Результат[9].Выгрузить());
	Для каждого Строка Из Объект.СписокНоменклатурыПодЗаказ Цикл
		Строка.РучнойВвод = Строка.КоличествоСрочно;	
	КонецЦикла;
	
	Объект.СписокНоменклатуры.Загрузить(Результат[10].Выгрузить());
	Для каждого Строка Из Объект.СписокНоменклатуры Цикл
		Строка.РучнойВвод = Строка.КоличествоСрочно;	
	КонецЦикла;
	
	Объект.СуммаДокумента = Объект.СписокНоменклатуры.Итог("Стоимость")+Объект.СписокНоменклатурыПодЗаказ.Итог("Стоимость");
	Объект.СуммаДокументаСрочно = Объект.СписокНоменклатуры.Итог("СтоимостьСрочно")+Объект.СписокНоменклатурыПодЗаказ.Итог("СтоимостьСрочно");
	Объект.СуммаЗаявленная = Объект.СуммаДокументаСрочно;
	
	ПолучитьРасшифровку();
	
	// { Васильев Александр Леонидович [04.11.2014]
	
	// Изменилась давно уже формула...
	
	// Необходимо докупить на склад = Резервный остаток по складу - Остаток на складе (если сумма положительная -- купить, если отрицательная -- у нас запас)
	// Реальный остаток на складе = Остаток в цеху - Базовый лимит цеха (берем только положительные)
	// Необходимо докупить по цеху = Требуется по спецификациям - Реальный остаток на складе (берем только положительные)
	
	// итоговая формула:
	// Оперативный закуп = Необходимо докупить на склад + Необходимо докупить по цеху (берем только положительные)
	
	// } Васильев Александр Леонидович [04.11.2014]
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьРасшифровку()
	
	Если Объект.Подразделение.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Подразделение", Объект.Подразделение);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СпецификацияСписокНоменклатуры.Номенклатура КАК Номенклатура,
	|	СУММА(СпецификацияСписокНоменклатуры.Количество) КАК Количество,
	|	СтатусСпецификацииСрезПоследних.Статус,
	|	СпецификацияСписокНоменклатуры.Ссылка,
	|	СпецификацияСписокНоменклатуры.Ссылка.ДатаИзготовления,
	|	ВЫБОР
	|		КОГДА Комплектация.Ссылка ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Скомплектован
	|ПОМЕСТИТЬ Список
	|ИЗ
	|	Документ.Спецификация.СписокНоменклатуры КАК СпецификацияСписокНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусСпецификации.СрезПоследних КАК СтатусСпецификацииСрезПоследних
	|		ПО СпецификацияСписокНоменклатуры.Ссылка = СтатусСпецификацииСрезПоследних.Спецификация
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Комплектация КАК Комплектация
	|		ПО СпецификацияСписокНоменклатуры.Ссылка = Комплектация.Спецификация
	|ГДЕ
	|	СтатусСпецификацииСрезПоследних.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыСпецификации.Рассчитывается), ЗНАЧЕНИЕ(Перечисление.СтатусыСпецификации.Размещен), ЗНАЧЕНИЕ(Перечисление.СтатусыСпецификации.ПереданВЦех))
	|	И СпецификацияСписокНоменклатуры.Ссылка.Производство = &Подразделение
	|	И СпецификацияСписокНоменклатуры.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Материал)
	|
	|СГРУППИРОВАТЬ ПО
	|	СпецификацияСписокНоменклатуры.Номенклатура,
	|	СтатусСпецификацииСрезПоследних.Статус,
	|	СпецификацияСписокНоменклатуры.Ссылка,
	|	ВЫБОР
	|		КОГДА Комплектация.Ссылка ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ,
	|	СпецификацияСписокНоменклатуры.Ссылка.ДатаИзготовления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА спрНоменклатура.Ссылка <> НЕОПРЕДЕЛЕНО
	|			ТОГДА спрНоменклатура.Ссылка
	|		ИНАЧЕ Список.Номенклатура
	|	КОНЕЦ КАК Номенклатура,
	|	Список.Количество,
	|	Список.Ссылка КАК Спецификация,
	|	Список.Статус,
	|	Список.ДатаИзготовления,
	|	Список.Скомплектован
	|ИЗ
	|	Список КАК Список
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК спрНоменклатура
	|		ПО (спрНоменклатура.ОсновнаяПоСкладу)
	|			И Список.Номенклатура = спрНоменклатура.БазоваяНоменклатура";
	
	Результат = Запрос.Выполнить();
	ТаблицаРасшифровки.Загрузить(Результат.Выгрузить());
	
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеПриИзменении(Элемент)
	
	ПередЗаполнитьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	
	ПередЗаполнитьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыПриАктивизацииСтроки(Элемент)
	
	Данные = Элементы.СписокНоменклатуры.ТекущиеДанные;
	
	Если Данные <> Неопределено Тогда
		Номенклатура = Данные.Номенклатура;
		Если ЗначениеЗаполнено(Номенклатура) Тогда
			ОтборСтрок = Новый ФиксированнаяСтруктура("Номенклатура", Номенклатура);
		Иначе
			ОтборСтрок = Новый ФиксированнаяСтруктура("Номенклатура", ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка"));
		КонецЕсли;
		Элементы.ТаблицаРасшифровки.ОтборСтрок = ОтборСтрок;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыПодЗаказПриАктивизацииСтроки(Элемент)
	
	Данные = Элементы.СписокНоменклатурыПодЗаказ.ТекущиеДанные;
	
	Если Данные <> Неопределено Тогда
		Номенклатура = Данные.Номенклатура;
		Если ЗначениеЗаполнено(Номенклатура) Тогда
			ОтборСтрок = Новый ФиксированнаяСтруктура("Номенклатура", Номенклатура);
		Иначе
			ОтборСтрок = Новый ФиксированнаяСтруктура("Номенклатура", ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка"));
		КонецЕсли;
		Элементы.ТаблицаРасшифровки.ОтборСтрок = ОтборСтрок;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыПодЗаказПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыПодЗаказПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПоставщикПриИзменении(Элемент)
	ПередЗаполнитьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьЗаявленнуюСумму()
	
	Сумма = 0;
	
	Для Каждого Строка Из Объект.СписокНоменклатурыПодЗаказ Цикл
		
		Сумма = Сумма + Окр(Строка.РучнойВвод * Строка.Цена);
		
	КонецЦикла;
	
	Для Каждого Строка Из Объект.СписокНоменклатуры Цикл
		
		Сумма = Сумма + Окр(Строка.РучнойВвод * Строка.Цена);
		
	КонецЦикла;
			
	Объект.СуммаЗаявленная = Сумма;

КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыРучнойВводПриИзменении(Элемент)
	
	РассчитатьЗаявленнуюСумму();
		
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыПодЗаказРучнойВводПриИзменении(Элемент)
	
	РассчитатьЗаявленнуюСумму();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПолучитьРасшифровку();
КонецПроцедуры
