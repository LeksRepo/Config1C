
#Область ОБЩЕГО_НАЗНАЧЕНИЯ

&НаКлиенте
Функция ПересчитатьСуммыДокумента(ПодЗаказ)
	
	// { Васильев Александр Леонидович [24.05.2016]
	// Не больше 150 строк в документе.
	// Посмотрим, не будет ли тормозить на клиенте.
	// } Васильев Александр Леонидович [24.05.2016]
	
	Объект.СуммаПодотчет = 0;
	Объект.СуммаПоставщик = 0;
	
	Для каждого Строка Из Объект.СписокНоменклатуры Цикл
		
		Объект.СуммаПодотчет = Объект.СуммаПодотчет + Строка.КоличествоКупить * Строка.Цена;
		Объект.СуммаПоставщик = Объект.СуммаПоставщик + Строка.КоличествоПоставщик * Строка.Цена;
	
	КонецЦикла;
	
	Объект.СуммаДокумента = Объект.СуммаПодотчет + Объект.СуммаПоставщик;
	
КонецФункции

#КонецОбласти

#Область СОБЫТИЯ_ФОРМЫ

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьОтборРасшифровки();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ЗаполнитьНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаполнитьНаСервере()
	
	Если Объект.Подразделение.Пустая() Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Укажите подразделение",, "Подразделение");
		Возврат;
	КонецЕсли;
	
	Если Объект.СписокНоменклатуры.Количество() > 0 Тогда
		
		Ответ = Вопрос("Данные введенные вручную будут утеряны. Продолжить?",РежимДиалогаВопрос.ДаНет);
		
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаполнитьНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область КОМАНДЫ

&НаКлиенте
Процедура Заполнить(Команда)
	
	ПередЗаполнитьНаСервере();
	
КонецПроцедуры

&НаСервере
Функция ОпределитьКонтрагента()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Таб.Поставщик КАК Контрагент
	|ПОМЕСТИТЬ вТаб
	|ИЗ
	|	Документ.ОперативныйЗакуп.СписокНоменклатуры КАК Таб
	|ГДЕ
	|	Таб.КоличествоПоставщик > 0
	|	И Таб.Поставщик <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|	И Таб.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Контрагент КАК Контрагент
	|ИЗ
	|	вТаб КАК Т";
	
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Контрагент");
	
КонецФункции

&НаКлиенте
Процедура ПоступлениеМатериаловУслуг(Команда)
	
	Если Модифицированность Тогда
		Записать();
	КонецЕсли;
	
	Контрагенты = ОпределитьКонтрагента();
	КонтрагентПоступления = Неопределено;
	
	Если Контрагенты.Количество() = 1 Тогда
		
		КонтрагентПоступления = Контрагенты[0];
		
	Иначе
		
		СписокКонтрагентов = Новый СписокЗначений;
		СписокКонтрагентов.ЗагрузитьЗначения(Контрагенты);
		ВыбЭлемент = СписокКонтрагентов.ВыбратьЭлемент("Выберите контрагента");
		Если ВыбЭлемент <> Неопределено Тогда
			КонтрагентПоступления = ВыбЭлемент.Значение;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КонтрагентПоступления) Тогда
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Контрагент", КонтрагентПоступления);
		ДополнительныеПараметры.Вставить("Ссылка", Объект.Ссылка);
		
		ДанныеЗаполнения = Новый Структура;
		ДанныеЗаполнения.Вставить("Основание", ДополнительныеПараметры);
		
		ОткрытьФорму("Документ.ПоступлениеМатериаловУслуг.ФормаОбъекта", ДанныеЗаполнения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаСервере()
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОсновнойСклад = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Подразделение, "ОсновнойСклад");
	
	#Область Запрос_заполнения_номенклатуры
	
	Если ЗначениеЗаполнено(Объект.Дата) Тогда
		ДеньВыборки = Объект.Дата;
	Иначе
		ДеньВыборки = ТекущаяДата();
	КонецЕсли;
	
	Объект.СуммаДокумента = 0;
	Объект.СуммаПодотчет = 0;
	Объект.СуммаПоставщик = 0;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Подразделение", Объект.Подразделение);
	Запрос.УстановитьПараметр("ТекущаяДата", ДеньВыборки);
	Запрос.УстановитьПараметр("Склад", ОсновнойСклад);
	Запрос.УстановитьПараметр("ТекущийДокумент", Объект.Ссылка);
	Запрос.УстановитьПараметр("Поставщик", Объект.Поставщик);
	
	Наряды = Объект.НарядЗадания.Выгрузить(Неопределено, "Наряд"); 
	Запрос.УстановитьПараметр("НарядЗадания", ?(Наряды.Количество()>0, Наряды, NULL));
	
	Запрос.УстановитьПараметр("ТекущийДокумент", Объект.Ссылка);
	Запрос.УстановитьПараметр("Период", ТекущаяДата());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(НастройкиПодразделенийСрезПоследних.Значение КАК БУЛЕВО) КАК ИспользуетсяРаскройНаНаряд,
	|	НастройкиПодразделенийСрезПоследних.Подразделение
	|ПОМЕСТИТЬ РаскройНаНаряд
	|ИЗ
	|	РегистрСведений.НастройкиПодразделений.СрезПоследних(
	|			&Период,
	|			Подразделение = &Подразделение
	|				И ВидНастройки = ЗНАЧЕНИЕ(Перечисление.ВидыНастроекПодразделений.ИспользуетсяРаскройНаНаряд)) КАК НастройкиПодразделенийСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Спец.Ссылка КАК СпецификацияСсылка
	|ПОМЕСТИТЬ втСпецификации
	|ИЗ
	|	Документ.Спецификация КАК Спец
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.НарядЗадание.СписокСпецификаций КАК Наряд
	|		ПО Спец.Ссылка = Наряд.Спецификация
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусСпецификации.СрезПоследних КАК СтатусСпец
	|		ПО Спец.Ссылка = СтатусСпец.Спецификация
	|ГДЕ
	|	ВЫБОР
	|			КОГДА НЕ &НарядЗадания ЕСТЬ NULL 
	|				ТОГДА Наряд.Ссылка В (&НарядЗадания)
	|			ИНАЧЕ СтатусСпец.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыСпецификации.Размещен), ЗНАЧЕНИЕ(Перечисление.СтатусыСпецификации.ПереданВЦех))
	|					И Спец.Подразделение = &Подразделение
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Список.Номенклатура КАК Номенклатура,
	|	СУММА(Список.КоличествоКупить + Список.КоличествоПоставщик) КАК Количество
	|ПОМЕСТИТЬ номОперЗакупы
	|ИЗ
	|	Документ.ОперативныйЗакуп.СписокНоменклатуры КАК Список
	|ГДЕ
	|	Список.Ссылка.Дата МЕЖДУ НАЧАЛОПЕРИОДА(&ТекущаяДата, ДЕНЬ) И КОНЕЦПЕРИОДА(&ТекущаяДата, ДЕНЬ)
	|	И ВЫБОР
	|			КОГДА &ТекущийДокумент <> ЗНАЧЕНИЕ(Документ.ОперативныйЗакуп.ПустаяСсылка)
	|				ТОГДА Список.Ссылка <> &ТекущийДокумент
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И (Список.КоличествоКупить > 0
	|			ИЛИ Список.КоличествоПоставщик > 0)
	|	И Список.Ссылка.Подразделение = &Подразделение
	|
	|СГРУППИРОВАТЬ ПО
	|	Список.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА УправленческийОстатки.Субконто2 ЕСТЬ NULL 
	|			ТОГДА ОперЗакупы.Номенклатура
	|		ИНАЧЕ УправленческийОстатки.Субконто2
	|	КОНЕЦ КАК Номенклатура,
	|	ЕСТЬNULL(УправленческийОстатки.КоличествоРазвернутыйОстатокДт, 0) + ЕСТЬNULL(ОперЗакупы.Количество, 0) КАК Количество
	|ПОМЕСТИТЬ втОстаткиРегистр
	|ИЗ
	|	РегистрБухгалтерии.Управленческий.Остатки(
	|			,
	|			Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.МатериалыНаСкладе),
	|			,
	|			Подразделение = &Подразделение
	|				И Субконто1 = &Склад) КАК УправленческийОстатки
	|		ПОЛНОЕ СОЕДИНЕНИЕ номОперЗакупы КАК ОперЗакупы
	|		ПО ((ВЫРАЗИТЬ(УправленческийОстатки.Субконто2 КАК Справочник.Номенклатура)) = ОперЗакупы.Номенклатура)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА втОстаткиРегистр.Номенклатура.Базовый
	|			ТОГДА втОстаткиРегистр.Номенклатура
	|		ИНАЧЕ втОстаткиРегистр.Номенклатура.БазоваяНоменклатура
	|	КОНЕЦ КАК Номенклатура,
	|	СУММА(ВЫБОР
	|			КОГДА втОстаткиРегистр.Номенклатура.Базовый
	|				ТОГДА втОстаткиРегистр.Количество
	|			ИНАЧЕ втОстаткиРегистр.Количество * втОстаткиРегистр.Номенклатура.КоэффициентБазовых
	|		КОНЕЦ) КАК Количество
	|ПОМЕСТИТЬ втОстаткиНаСкладе
	|ИЗ
	|	втОстаткиРегистр КАК втОстаткиРегистр
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА втОстаткиРегистр.Номенклатура.Базовый
	|			ТОГДА втОстаткиРегистр.Номенклатура
	|		ИНАЧЕ втОстаткиРегистр.Номенклатура.БазоваяНоменклатура
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РезервныйЗапас.Номенклатура КАК Номенклатура,
	|	РезервныйЗапас.Количество КАК Количество
	|ПОМЕСТИТЬ втРезервныйЗапас
	|ИЗ
	|	РегистрСведений.РезервныйЗапасМатериалов.СрезПоследних(
	|			,
	|			Подразделение = &Подразделение
	|				И Склад = &Склад) КАК РезервныйЗапас
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТРезервныйЗапас.Номенклатура КАК Номенклатура,
	|	ВТРезервныйЗапас.Количество КАК Количество
	|ПОМЕСТИТЬ втДокупитьНаСклад
	|ИЗ
	|	втРезервныйЗапас КАК ВТРезервныйЗапас
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиНаСкладе КАК ВТОстаткиНаСкладе
	|		ПО (ВТОстаткиНаСкладе.Номенклатура = ВТРезервныйЗапас.Номенклатура)
	|ГДЕ
	|	ЕСТЬNULL(ВТОстаткиНаСкладе.Количество, 0) < ВТРезервныйЗапас.Количество
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НарядЗаданиеСписокЛистовНоменклатуры.Номенклатура КАК Номенклатура,
	|	СУММА(НарядЗаданиеСписокЛистовНоменклатуры.Количество * ЕСТЬNULL(НоменклатураПодразделенийСрезПоследних.ОсновнаяПоСкладу.КоэффициентБазовых, 1)) КАК Количество,
	|	НарядЗаданиеСписокЛистовНоменклатуры.Ссылка.Подразделение
	|ПОМЕСТИТЬ ТребуетсяЛистовогоПоНаряду
	|ИЗ
	|	РаскройНаНаряд КАК РаскройНаНаряд
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.НарядЗадание.СписокЛистовНоменклатуры КАК НарядЗаданиеСписокЛистовНоменклатуры
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатураПодразделений.СрезПоследних(&Период, Подразделение = &Подразделение) КАК НоменклатураПодразделенийСрезПоследних
	|			ПО НарядЗаданиеСписокЛистовНоменклатуры.Номенклатура = НоменклатураПодразделенийСрезПоследних.Номенклатура
	|		ПО РаскройНаНаряд.Подразделение = НарядЗаданиеСписокЛистовНоменклатуры.Ссылка.Подразделение
	|ГДЕ
	|	ВЫБОР
	|			КОГДА НЕ &НарядЗадания ЕСТЬ NULL 
	|					И РаскройНаНаряд.ИспользуетсяРаскройНаНаряд
	|				ТОГДА НарядЗаданиеСписокЛистовНоменклатуры.Ссылка В (&НарядЗадания)
	|		КОНЕЦ
	|
	|СГРУППИРОВАТЬ ПО
	|	НарядЗаданиеСписокЛистовНоменклатуры.Ссылка.Подразделение,
	|	НарядЗаданиеСписокЛистовНоменклатуры.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СпецификацияСписокНоменклатуры.Номенклатура КАК Номенклатура,
	|	СУММА(СпецификацияСписокНоменклатуры.Количество) КАК КоличествоВсегоТребуется
	|ПОМЕСТИТЬ втТребуетсяПоСпецификациям
	|ИЗ
	|	Документ.Спецификация.СписокНоменклатуры КАК СпецификацияСписокНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатураПодразделений.СрезПоследних(&Период, Подразделение = &Подразделение) КАК НоменклатураПодразделенийСрезПоследних
	|		ПО СпецификацияСписокНоменклатуры.Номенклатура = НоменклатураПодразделенийСрезПоследних.Номенклатура
	|			И СпецификацияСписокНоменклатуры.Ссылка.Подразделение = НоменклатураПодразделенийСрезПоследних.Подразделение
	|		ЛЕВОЕ СОЕДИНЕНИЕ РаскройНаНаряд КАК РаскройНаНаряд
	|		ПО (РаскройНаНаряд.Подразделение = СпецификацияСписокНоменклатуры.Ссылка.Подразделение)
	|ГДЕ
	|	СпецификацияСписокНоменклатуры.Ссылка В
	|			(ВЫБРАТЬ
	|				т.СпецификацияСсылка
	|			ИЗ
	|				втСпецификации КАК т)
	|	И СпецификацияСписокНоменклатуры.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Материал)
	|	И НЕ СпецификацияСписокНоменклатуры.ПредоставитЗаказчик
	|	И ВЫБОР
	|			КОГДА РаскройНаНаряд.ИспользуетсяРаскройНаНаряд
	|				ТОГДА СпецификацияСписокНоменклатуры.Номенклатура.НоменклатурнаяГруппа.ВидМатериала <> ЗНАЧЕНИЕ(Перечисление.ВидыМатериалов.Листовой)
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|
	|СГРУППИРОВАТЬ ПО
	|	СпецификацияСписокНоменклатуры.Номенклатура
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТТребуетсяПоСпецификациям.Номенклатура КАК Номенклатура,
	|	ВТТребуетсяПоСпецификациям.КоличествоВсегоТребуется - ЕСТЬNULL(ВТОстаткиНаСкладе.Количество, 0) КАК Количество
	|ПОМЕСТИТЬ втДокупитьНаЦех
	|ИЗ
	|	втТребуетсяПоСпецификациям КАК ВТТребуетсяПоСпецификациям
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиНаСкладе КАК ВТОстаткиНаСкладе
	|		ПО ВТТребуетсяПоСпецификациям.Номенклатура = ВТОстаткиНаСкладе.Номенклатура
	|ГДЕ
	|	ВТТребуетсяПоСпецификациям.КоличествоВсегоТребуется - ЕСТЬNULL(ВТОстаткиНаСкладе.Количество, 0) > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТребуетсяЛистовогоПоНаряду.Номенклатура,
	|	ТребуетсяЛистовогоПоНаряду.Количество - ЕСТЬNULL(втОстаткиНаСкладе.Количество, 0)
	|ИЗ
	|	ТребуетсяЛистовогоПоНаряду КАК ТребуетсяЛистовогоПоНаряду
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиНаСкладе КАК втОстаткиНаСкладе
	|		ПО ТребуетсяЛистовогоПоНаряду.Номенклатура = втОстаткиНаСкладе.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РаскройНаНаряд КАК РаскройНаНаряд
	|		ПО ТребуетсяЛистовогоПоНаряду.Подразделение = РаскройНаНаряд.Подразделение
	|ГДЕ
	|	ТребуетсяЛистовогоПоНаряду.Количество - ЕСТЬNULL(втОстаткиНаСкладе.Количество, 0) > 0
	|	И РаскройНаНаряд.ИспользуетсяРаскройНаНаряд
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ВТДокупитьНаСклад.Номенклатура, ВТДокупитьНаЦех.Номенклатура) КАК Номенклатура,
	|	ЕСТЬNULL(ВТДокупитьНаСклад.Количество, 0) + ЕСТЬNULL(ВТДокупитьНаЦех.Количество, 0) КАК Количество,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВТДокупитьНаСклад.Количество, 0) > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СработалРезерв
	|ПОМЕСТИТЬ втМатериалыДляЗакупа
	|ИЗ
	|	втДокупитьНаСклад КАК ВТДокупитьНаСклад
	|		ПОЛНОЕ СОЕДИНЕНИЕ втДокупитьНаЦех КАК ВТДокупитьНаЦех
	|		ПО ВТДокупитьНаСклад.Номенклатура = ВТДокупитьНаЦех.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЦеныНоменклатурыСрезПоследних.Поставщик,
	|	ВЫБОР
	|		КОГДА НомПодразделений.ОсновнаяПоСкладу ЕСТЬ NULL 
	|				ИЛИ НомПодразделений.ОсновнаяПоСкладу = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА втМатериалыДляЗакупа.Номенклатура
	|		ИНАЧЕ НомПодразделений.ОсновнаяПоСкладу
	|	КОНЕЦ КАК Номенклатура,
	|	ВЫРАЗИТЬ(втМатериалыДляЗакупа.Количество / ВЫБОР
	|			КОГДА ЕСТЬNULL(НомПодразделений.ОсновнаяПоСкладу.КоэффициентБазовых, 1) = 0
	|				ТОГДА 1
	|			ИНАЧЕ ЕСТЬNULL(НомПодразделений.ОсновнаяПоСкладу.КоэффициентБазовых, 1)
	|		КОНЕЦ + 0.49999 КАК ЧИСЛО(15, 0)) КАК Количество,
	|	втМатериалыДляЗакупа.СработалРезерв,
	|	ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.ПлановаяЗакупочная, 0) * ЕСТЬNULL(НомПодразделений.ОсновнаяПоСкладу.КоэффициентБазовых, 1) КАК Цена
	|ПОМЕСТИТЬ втМатериалыСоСтоимостью
	|ИЗ
	|	втМатериалыДляЗакупа КАК втМатериалыДляЗакупа
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатурыПоПодразделениям.СрезПоследних(, Подразделение = &Подразделение) КАК ЦеныНоменклатурыСрезПоследних
	|		ПО втМатериалыДляЗакупа.Номенклатура = ЦеныНоменклатурыСрезПоследних.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатураПодразделений.СрезПоследних(&Период, Подразделение = &Подразделение) КАК НомПодразделений
	|		ПО втМатериалыДляЗакупа.Номенклатура = НомПодразделений.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА НоменклатураПодразделений.ОсновнаяПоСкладу ЕСТЬ NULL 
	|				ИЛИ НоменклатураПодразделений.ОсновнаяПоСкладу = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА ВТМатериалыСоСтоимостью.Номенклатура
	|		ИНАЧЕ НоменклатураПодразделений.ОсновнаяПоСкладу
	|	КОНЕЦ КАК Номенклатура,
	|	ВТМатериалыСоСтоимостью.Цена,
	|	ВТМатериалыСоСтоимостью.Количество,
	|	ВТМатериалыСоСтоимостью.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВТМатериалыСоСтоимостью.СработалРезерв,
	|	ВТМатериалыСоСтоимостью.Поставщик КАК Поставщик,
	|	ЕСТЬNULL(НоменклатураПодразделений.ПодЗаказ, ИСТИНА) КАК ПодЗаказ
	|ИЗ
	|	втМатериалыСоСтоимостью КАК ВТМатериалыСоСтоимостью
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатураПодразделений.СрезПоследних(&Период, Подразделение = &Подразделение) КАК НоменклатураПодразделений
	|		ПО (ВЫБОР
	|				КОГДА ВТМатериалыСоСтоимостью.Номенклатура.Базовый
	|					ТОГДА ВТМатериалыСоСтоимостью.Номенклатура = НоменклатураПодразделений.Номенклатура
	|				ИНАЧЕ ВТМатериалыСоСтоимостью.Номенклатура.БазоваяНоменклатура = НоменклатураПодразделений.Номенклатура
	|			КОНЕЦ)
	|ГДЕ
	|	НЕ ЕСТЬNULL(НоменклатураПодразделений.ПодЗаказ, ИСТИНА)
	|	И ВЫБОР
	|			КОГДА &Поставщик = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ &Поставщик = ВТМатериалыСоСтоимостью.Поставщик
	|		КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура,
	|	Поставщик";
	
	#КонецОбласти
	
	ТЗНом = Запрос.Выполнить().Выгрузить();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Подразделение", Объект.Подразделение);
	Запрос.УстановитьПараметр("Поставщик", Объект.Поставщик);
	
	Наряды = Объект.НарядЗадания.Выгрузить(Неопределено, "Наряд"); 
	Запрос.УстановитьПараметр("НарядЗадания", ?(Наряды.Количество()>0, Наряды, NULL));
	Запрос.УстановитьПараметр("Период", ТекущаяДата());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Спец.Ссылка КАК СпецификацияСсылка
	|ПОМЕСТИТЬ втСпецификации
	|ИЗ
	|	Документ.Спецификация КАК Спец
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.НарядЗадание.СписокСпецификаций КАК Наряд
	|		ПО Спец.Ссылка = Наряд.Спецификация
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусСпецификации.СрезПоследних КАК СтатусСпец
	|		ПО Спец.Ссылка = СтатусСпец.Спецификация
	|ГДЕ
	|	ВЫБОР
	|			КОГДА НЕ &НарядЗадания ЕСТЬ NULL 
	|				ТОГДА Наряд.Ссылка В (&НарядЗадания)
	|			ИНАЧЕ СтатусСпец.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыСпецификации.Размещен), ЗНАЧЕНИЕ(Перечисление.СтатусыСпецификации.ПереданВЦех))
	|					И Спец.Подразделение = &Подразделение
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА НомПодразделений.ОсновнаяПоСкладу ЕСТЬ NULL 
	|				ИЛИ НомПодразделений.ОсновнаяПоСкладу = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА Спец.Номенклатура
	|		ИНАЧЕ НомПодразделений.ОсновнаяПоСкладу
	|	КОНЕЦ КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА ВЫБОР
	|				КОГДА НомПодразделений.ОсновнаяПоСкладу ЕСТЬ NULL 
	|						ИЛИ НомПодразделений.ОсновнаяПоСкладу = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|					ТОГДА Спец.Номенклатура.КоэффициентБазовых
	|				ИНАЧЕ НомПодразделений.ОсновнаяПоСкладу.КоэффициентБазовых
	|			КОНЕЦ = 0
	|			ТОГДА 1
	|		ИНАЧЕ ВЫБОР
	|				КОГДА НомПодразделений.ОсновнаяПоСкладу ЕСТЬ NULL 
	|						ИЛИ НомПодразделений.ОсновнаяПоСкладу = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|					ТОГДА Спец.Номенклатура.КоэффициентБазовых
	|				ИНАЧЕ НомПодразделений.ОсновнаяПоСкладу.КоэффициентБазовых
	|			КОНЕЦ
	|	КОНЕЦ КАК Коэффициент,
	|	Спец.Количество,
	|	Спец.Цена,
	|	Спец.Поставщик,
	|	Спец.Ссылка КАК Спецификация,
	|	Спец.Комментарий,
	|	Остатки.Количество КАК ОстаткиНаСкладе
	|ПОМЕСТИТЬ втМатериалы
	|ИЗ
	|	Документ.Спецификация.СписокМатериаловПодЗаказ КАК Спец
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатураПодразделений.СрезПоследних(&Период, Подразделение = &Подразделение) КАК НомПодразделений
	|		ПО Спец.Номенклатура = НомПодразделений.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ВЫБОР
	|				КОГДА УпрОстатки.Субконто2.Базовый
	|					ТОГДА УпрОстатки.Субконто2
	|				ИНАЧЕ УпрОстатки.Субконто2.БазоваяНоменклатура
	|			КОНЕЦ КАК Номенклатура,
	|			СУММА(ВЫБОР
	|					КОГДА УпрОстатки.Субконто2.Базовый
	|						ТОГДА УпрОстатки.КоличествоОстаток
	|					ИНАЧЕ УпрОстатки.КоличествоОстаток * УпрОстатки.Субконто2.КоэффициентБазовых
	|				КОНЕЦ) КАК Количество
	|		ИЗ
	|			РегистрБухгалтерии.Управленческий.Остатки(
	|					&Период,
	|					Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.МатериалыНаСкладе),
	|					,
	|					Субконто1.ОсновнойСклад = ИСТИНА
	|						И Подразделение = &Подразделение) КАК УпрОстатки
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ВЫБОР
	|				КОГДА УпрОстатки.Субконто2.Базовый
	|					ТОГДА УпрОстатки.Субконто2
	|				ИНАЧЕ УпрОстатки.Субконто2.БазоваяНоменклатура
	|			КОНЕЦ) КАК Остатки
	|		ПО Спец.Номенклатура = Остатки.Номенклатура
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &Поставщик <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				ТОГДА Спец.Поставщик = &Поставщик
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И Спец.Ссылка В
	|			(ВЫБРАТЬ
	|				т.СпецификацияСсылка
	|			ИЗ
	|				втСпецификации КАК т)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Материалы.Номенклатура КАК Номенклатура,
	|	Материалы.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВЫРАЗИТЬ(Материалы.Количество / Материалы.Коэффициент + 0.4999 КАК ЧИСЛО(15, 0)) КАК Количество,
	|	ВЫРАЗИТЬ(Материалы.Цена * Материалы.Коэффициент КАК ЧИСЛО(15, 0)) КАК Цена,
	|	Материалы.Поставщик КАК Поставщик,
	|	Материалы.Спецификация КАК Спецификация,
	|	Материалы.Комментарий КАК Комментарий,
	|	Материалы.ОстаткиНаСкладе / Материалы.Коэффициент КАК ОстаткиНаСкладе,
	|	ИСТИНА КАК ПодЗаказ
	|ИЗ
	|	втМатериалы КАК Материалы
	|
	|УПОРЯДОЧИТЬ ПО
	|	Материалы.Номенклатура.Наименование";
	
	ТЗНомПодЗаказ = Запрос.Выполнить().Выгрузить();
	
	Объект.СписокНоменклатуры.Очистить();
	
	Для Каждого Стр ИЗ ТЗНом Цикл
		
		НоваяСтрока = Объект.СписокНоменклатуры.Добавить();
		НоваяСтрока.Номенклатура = Стр.Номенклатура;
		НоваяСтрока.Цена = Стр.Цена;
		НоваяСтрока.Количество = Стр.Количество;
		//НоваяСтрока.ЕдиницаИзмерения = Стр.ЕдиницаИзмерения;
		НоваяСтрока.СработалРезерв = Стр.СработалРезерв;
		НоваяСтрока.Поставщик = Стр.Поставщик;
		НоваяСтрока.ПодЗаказ = Ложь;
		
	КонецЦикла;
	
	Для Каждого Стр ИЗ ТЗНомПодЗаказ Цикл
		
		НоваяСтрока = Объект.СписокНоменклатуры.Добавить();
		НоваяСтрока.Номенклатура = Стр.Номенклатура;
		НоваяСтрока.Цена = Стр.Цена;
		НоваяСтрока.Количество = Стр.Количество;
		//НоваяСтрока.ЕдиницаИзмерения = Стр.ЕдиницаИзмерения;
		НоваяСтрока.Поставщик = Стр.Поставщик;
		НоваяСтрока.Спецификация = Стр.Спецификация;
		НоваяСтрока.Комментарий = Стр.Комментарий;
		НоваяСтрока.ОстаткиНаСкладе = Стр.ОстаткиНаСкладе;
		НоваяСтрока.ПодЗаказ = Истина;
	
	КонецЦикла;
	
	ПолучитьРасшифровку();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьНаряды(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Подразделение", Объект.Подразделение);
	ПараметрыФормы.Вставить("ТЗ", Объект.НарядЗадания);
	
	АдресТаблицы = ОткрытьФормуМодально("ОбщаяФорма.ФормаВыбораНаряда", ПараметрыФормы, ЭтаФорма);
	
	Если АдресТаблицы <> Неопределено Тогда
		ЗагрузитьНаряды(АдресТаблицы);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьНаряды(АдресТаблицы)
	
	Если ЭтоАдресВременногоХранилища(АдресТаблицы) Тогда
		ТЗ = ПолучитьИзВременногоХранилища(АдресТаблицы);
		Объект.НарядЗадания.Загрузить(ТЗ);
		ЗаполнитьНаСервере();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СОБЫТИЯ_ЭЛЕМЕНТОВ_ФОРМЫ

&НаКлиенте
Процедура ПодразделениеПриИзменении(Элемент)
	
	ПередЗаполнитьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоставщикПриИзменении(Элемент)
	
	ПередЗаполнитьНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ТАБЛИЦА_СПИСОК_НОМЕНКЛАТУРЫ

&НаКлиенте
Процедура СписокНоменклатурыПриАктивизацииСтроки(Элемент)
	
	УстановитьОтборРасшифровки();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	//RonEXI: Разрешить добавление руками. Лайн Д.В 18 апреля 2016
	//Отказ = Истина;
	Заглушка = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыПодЗаказПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыПодЗаказПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыКоличествоКупитьПриИзменении(Элемент)
	
	ПересчитатьСуммыДокумента(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыКоличествоПоставщикПриИзменении(Элемент)
	
	ПересчитатьСуммыДокумента(Ложь);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСвойстваНоменклатуры(фнНоменклатура, фнПодразделение)
	
	Если НЕ фнНоменклатура.Базовый Тогда
		ОтборНоменклатура = фнНоменклатура.БазоваяНоменклатура;
	Иначе
		ОтборНоменклатура = фнНоменклатура;
	КонецЕсли;
	
	КоэффициентДляУмноженияЦены = ?(фнНоменклатура.КоэффициентБазовых <> 0, фнНоменклатура.КоэффициентБазовых, 1);
	Свойства = Новый Структура;
	Свойства.Вставить("ПлановаяЦена", 0);
	Свойства.Вставить("ЕдиницаИзмерения", ОтборНоменклатура.ЕдиницаИзмерения);
	Свойства.Вставить("Поставщик");
	Свойства.Вставить("КоличествоОстаток");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Номенклатура", ОтборНоменклатура);
	Запрос.УстановитьПараметр("Подразделение", фнПодразделение);
	Запрос.УстановитьПараметр("Склад" , ОбщегоНазначения.ЗначениеРеквизитаОбъекта(фнПодразделение, "ОсновнойСклад"));
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЦеныНоменклатурыПоПодразделениям.ПлановаяЗакупочная,
	|	ЦеныНоменклатурыПоПодразделениям.Поставщик,
	|	ЕСТЬNULL(УправленческийОстатки.КоличествоРазвернутыйОстатокДт, 0) КАК КоличествоОстаток
	|ИЗ
	|	РегистрСведений.ЦеныНоменклатурыПоПодразделениям КАК ЦеныНоменклатурыПоПодразделениям
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
	|				,
	|				Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.МатериалыНаСкладе),
	|				,
	|				Подразделение = &Подразделение
	|					И Субконто1 = &Склад) КАК УправленческийОстатки
	|		ПО ЦеныНоменклатурыПоПодразделениям.Номенклатура = УправленческийОстатки.Субконто2
	|ГДЕ
	|	ЦеныНоменклатурыПоПодразделениям.Подразделение = &Подразделение
	|	И ЦеныНоменклатурыПоПодразделениям.Номенклатура = &Номенклатура";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Свойства.ПлановаяЦена = Выборка.ПлановаяЗакупочная*КоэффициентДляУмноженияЦены;
		Свойства.Поставщик = Выборка.Поставщик;
		Свойства.КоличествоОстаток = Выборка.КоличествоОстаток;
		
	КонецЦикла;
	
	Возврат Свойства;
	
КонецФункции

#КонецОбласти

#Область ТАБЛИЦА_НОМЕНКЛАТУРА_ПОД_ЗАКАЗ

#КонецОбласти

#Область ТАБЛИЦА_НАРЯД_ЗАДАНИЯ

&НаКлиенте
Процедура НарядЗаданияНарядПриИзменении(Элемент)
	
	ПередЗаполнитьНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ТАБЛИЦА_РАСШИФРОВКА

&НаКлиенте
Процедура ТаблицаРасшифровкиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОткрытьЗначение(Элементы.ТаблицаРасшифровки.ТекущиеДанные.Спецификация);
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьРасшифровку()
	
	Если Объект.Подразделение.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Подразделение", Объект.Подразделение);
	
	Наряды = Объект.НарядЗадания.Выгрузить(Неопределено, "Наряд"); 
	Запрос.УстановитьПараметр("НарядЗадания", ?(Наряды.Количество()>0, Наряды, NULL));
	Запрос.УстановитьПараметр("Период", ТекущаяДата());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Спец.Ссылка КАК СпецификацияСсылка
	|ПОМЕСТИТЬ втСпецификации
	|ИЗ
	|	Документ.Спецификация КАК Спец
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.НарядЗадание.СписокСпецификаций КАК Наряд
	|		ПО Спец.Ссылка = Наряд.Спецификация
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусСпецификации.СрезПоследних КАК СтатусСпец
	|		ПО Спец.Ссылка = СтатусСпец.Спецификация
	|ГДЕ
	|	ВЫБОР
	|			КОГДА НЕ &НарядЗадания ЕСТЬ NULL 
	|				ТОГДА Наряд.Ссылка В (&НарядЗадания)
	|			ИНАЧЕ СтатусСпец.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыСпецификации.Размещен), ЗНАЧЕНИЕ(Перечисление.СтатусыСпецификации.ПереданВЦех))
	|					И Спец.Подразделение = &Подразделение
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СпецификацияСписокНоменклатуры.Номенклатура КАК Номенклатура,
	|	СУММА(СпецификацияСписокНоменклатуры.Количество) КАК Количество,
	|	СпецификацияСписокНоменклатуры.Ссылка,
	|	СпецификацияСписокНоменклатуры.Ссылка.ДатаИзготовления,
	|	ВЫБОР
	|		КОГДА Комплектация.Ссылка ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Скомплектован
	|ПОМЕСТИТЬ Список
	|ИЗ
	|	Документ.Спецификация.СписокНоменклатуры КАК СпецификацияСписокНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Комплектация КАК Комплектация
	|		ПО СпецификацияСписокНоменклатуры.Ссылка = Комплектация.Спецификация
	|ГДЕ
	|	СпецификацияСписокНоменклатуры.Ссылка В
	|			(ВЫБРАТЬ
	|				т.СпецификацияСсылка
	|			ИЗ
	|				втСпецификации КАК т)
	|	И СпецификацияСписокНоменклатуры.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Материал)
	|
	|СГРУППИРОВАТЬ ПО
	|	СпецификацияСписокНоменклатуры.Номенклатура,
	|	СпецификацияСписокНоменклатуры.Ссылка,
	|	ВЫБОР
	|		КОГДА Комплектация.Ссылка ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ,
	|	СпецификацияСписокНоменклатуры.Ссылка.ДатаИзготовления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА НомПодразделений.ОсновнаяПоСкладу ЕСТЬ NULL 
	|				ИЛИ НомПодразделений.ОсновнаяПоСкладу = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА Список.Номенклатура
	|		ИНАЧЕ НомПодразделений.ОсновнаяПоСкладу
	|	КОНЕЦ КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА Список.Номенклатура.Базовый
	|			ТОГДА Список.Номенклатура
	|		ИНАЧЕ Список.Номенклатура.БазоваяНоменклатура
	|	КОНЕЦ КАК БазоваяНом,
	|	Список.Количество,
	|	Список.Ссылка КАК Спецификация,
	|	Список.ДатаИзготовления,
	|	Список.Скомплектован
	|ПОМЕСТИТЬ Список2
	|ИЗ
	|	Список КАК Список
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатураПодразделений.СрезПоследних(&Период, Подразделение = &Подразделение) КАК НомПодразделений
	|		ПО Список.Номенклатура = НомПодразделений.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Список2.Номенклатура,
	|	Список2.БазоваяНом,
	|	Список2.Количество,
	|	Список2.Спецификация,
	|	Список2.ДатаИзготовления,
	|	Список2.Скомплектован,
	|	МатПодЗаказ.Комментарий КАК Комментарий
	|ИЗ
	|	Список2 КАК Список2
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Спецификация.СписокМатериаловПодЗаказ КАК МатПодЗаказ
	|		ПО Список2.Номенклатура = МатПодЗаказ.Номенклатура
	|			И Список2.Спецификация = МатПодЗаказ.Ссылка";

	
	Результат = Запрос.Выполнить();
	Объект.ТаблицаРасшифровки.Загрузить(Результат.Выгрузить());
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборРасшифровки()
		
	Данные = Элементы.СписокНоменклатуры.ТекущиеДанные;
	
	Если Данные <> Неопределено и ЗначениеЗаполнено(Данные.Номенклатура) Тогда
		
		ПодЗаказ = Данные.ПодЗаказ;
		
		НомДанные = ЛексСервер.ЗначенияРеквизитовОбъекта(Данные.Номенклатура, "Базовый, БазоваяНоменклатура");
		НомСсылка = Данные.Номенклатура;
		
		Если НЕ НомДанные.Базовый Тогда
			НомСсылка = НомДанные.БазоваяНоменклатура;
		Иначе
			
		КонецЕсли;
		
		Если ПодЗаказ Тогда
			ОтборСтрок = Новый ФиксированнаяСтруктура("Спецификация, БазоваяНом", Данные.Спецификация, НомСсылка);
		Иначе
			ОтборСтрок = Новый ФиксированнаяСтруктура("БазоваяНом", НомСсылка);
		КонецЕсли;
		
	Иначе
		
		ОтборСтрок = Новый ФиксированнаяСтруктура("Номенклатура", ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка"));
		
	КонецЕсли;
	
	Элементы.ТаблицаРасшифровки.ОтборСтрок = ОтборСтрок;
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппаТаблицыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	УстановитьОтборРасшифровки();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыНоменклатураПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.СписокНоменклатуры.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено
		И ЗначениеЗаполнено(Объект.Подразделение)
		И ЗначениеЗаполнено(ТекущиеДанные.Номенклатура) Тогда
		
		СвойстваНоменклатуры = ПолучитьСвойстваНоменклатуры(ТекущиеДанные.Номенклатура, Объект.Подразделение);
		ТекущиеДанные.Цена = СвойстваНоменклатуры.ПлановаяЦена;
		//ТекущиеДанные.ЕдиницаИзмерения = СвойстваНоменклатуры.ЕдиницаИзмерения;
		ТекущиеДанные.Поставщик = СвойстваНоменклатуры.Поставщик;
		ТекущиеДанные.ОстаткиНаСкладе = СвойстваНоменклатуры.КоличествоОстаток;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
