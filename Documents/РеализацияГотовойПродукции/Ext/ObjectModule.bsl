
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.Записать();
	СчетГотоваяПродукция = ПланыСчетов.Управленческий.ГотоваяПродукция;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РеализацияГотовойПродукцииСписокСпецификаций.Спецификация,
	|	ЕСТЬNULL(УправленческийОстатки.СуммаОстатокДт, 0) КАК СуммаОстатокДт,
	|	ЕСТЬNULL(УправленческийОстатки.КоличествоОстатокДт, 0) КАК КоличествоОстатокДт,
	|	РеализацияГотовойПродукцииСписокСпецификаций.Спецификация.УслугаМонтаж КАК УслугаМонтаж,
	|	СтатусСпецификацииСрезПоследних.Статус КАК СтатусСпецификации
	|ИЗ
	|	Документ.РеализацияГотовойПродукции.СписокСпецификаций КАК РеализацияГотовойПродукцииСписокСпецификаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(&Дата, Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ГотоваяПродукция), , Подразделение = &Подразделение) КАК УправленческийОстатки
	|		ПО РеализацияГотовойПродукцииСписокСпецификаций.Спецификация = УправленческийОстатки.Субконто1
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусСпецификации.СрезПоследних(, ) КАК СтатусСпецификацииСрезПоследних
	|		ПО РеализацияГотовойПродукцииСписокСпецификаций.Спецификация = СтатусСпецификацииСрезПоследних.Спецификация
	|ГДЕ
	|	РеализацияГотовойПродукцииСписокСпецификаций.Ссылка = &Ссылка";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		КоличествоОстаток = Выборка.КоличествоОстатокДт;
		Себестомость = Выборка.СуммаОстатокДт;
		
		Если КоличествоОстаток <> 1 Тогда
			Текст = "" + Выборка.Спецификация + " не числится на складе готовой продукции";
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст,,"Объект.СписокСпецификаций");
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		//Статус
		Если Выборка.СтатусСпецификации = Перечисления.СтатусыСпецификации.Изготовлен Тогда
			Документы.Спецификация.УстановитьСтатусСпецификации(Выборка.Спецификация, Перечисления.СтатусыСпецификации.Отгружен);
		КонецЕсли;
		
		СвойстваСпецификации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Выборка.Спецификация, "Контрагент, Подразделение, СуммаДокумента, ВнутренняяСтоимостьСпецификации");
		
		Если Выборка.УслугаМонтаж Тогда
			// заказ с монтажем, оприходуем на временный счет
			
			Проводка = СоздатьПроводку(Подразделение, Себестомость);
			Проводка.СчетДт = ПланыСчетов.Управленческий.ИзделияУКлиента;
			Проводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.СпецификацияДоговор] = Выборка.Спецификация;
			Проводка.КоличествоДт = 1;
			
			Проводка.СчетКт = ПланыСчетов.Управленческий.ГотоваяПродукция;
			Проводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.СпецификацияДоговор] = Выборка.Спецификация;
			Проводка.КоличествоКт = 1;
			
		Иначе // заказ без монтажа, оприходуем прибыль по отгрузке
			
			/////////////////////////////////////////
			// ПРОИЗВОДСТВО
			// Очень похожие проводки в Акте выполнения договора
			// Хорошо бы объединить...
			
			// Себестоимость
			Проводка = СоздатьПроводку(Подразделение, Себестомость);
			Проводка.СчетКт = СчетГотоваяПродукция;
			Проводка.КоличествоКт = 1;
			Проводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.СпецификацияДоговор] = Выборка.Спецификация;
			Проводка.СчетДт = ПланыСчетов.Управленческий.РасходыПроизводства;
			Проводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.СтатьиДР] = Справочники.СтатьиДоходовРасходов.РасходыНаПроизводственныйМатериал;
			
			// Доходы
			Проводка = СоздатьПроводку(Подразделение, СвойстваСпецификации.ВнутренняяСтоимостьСпецификации);
			Проводка.СчетКт = ПланыСчетов.Управленческий.Доходы;
			Проводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.СтатьиДР] = Справочники.СтатьиДоходовРасходов.ДоходыПроизводстваОтРозницы;
			Проводка.СчетДт = ПланыСчетов.Управленческий.ВзаиморасчетыСПодразделениями;
			Проводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Подразделения] = СвойстваСпецификации.Подразделение;
			
		КонецЕсли;
		
	КонецЦикла; // цикл по спецификациям
	
	ЛексСервер.РеализацияИзделийРозничныеПроводки(СписокСпецификаций.ВыгрузитьКолонку("Спецификация"), Дата, Движения);
	
	Движения.Управленческий.Записывать = Истина;
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	// Изменение статуса спецификации
	Для Каждого Элемент Из СписокСпецификаций Цикл
		
		Если Документы.Спецификация.ПолучитьСтатусСпецификации(Элемент.Спецификация) = Перечисления.СтатусыСпецификации.Отгружен Тогда
			
			Документы.Спецификация.УстановитьСтатусСпецификации(Элемент.Спецификация, Перечисления.СтатусыСпецификации.Изготовлен);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ВыпускПродукции") Тогда
		
		СписокСпецификаций.Загрузить(ЛексСервер.ПолучитьСпецификацииПоСтатусу(Подразделение, Перечисления.СтатусыСпецификации.Изготовлен));
		ЭтотОбъект.Подразделение	= ДанныеЗаполнения.Подразделение;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	МассивСпецификаций = СписокСпецификаций.ВыгрузитьКолонку("Спецификация");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("МассивСпецификаций", МассивСпецификаций);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РеализацияГотовойПродукцииСписокСпецификаций.Спецификация
	|ИЗ
	|	Документ.РеализацияГотовойПродукции.СписокСпецификаций КАК РеализацияГотовойПродукцииСписокСпецификаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусСпецификации.СрезПоследних КАК СтатусСпецификацииСрезПоследних
	|		ПО РеализацияГотовойПродукцииСписокСпецификаций.Ссылка = СтатусСпецификацииСрезПоследних.Спецификация
	|ГДЕ
	|	НЕ РеализацияГотовойПродукцииСписокСпецификаций.Спецификация В (&МассивСпецификаций)
	|	И РеализацияГотовойПродукцииСписокСпецификаций.Ссылка = &Ссылка
	|	И СтатусСпецификацииСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСпецификации.Отгружен)";
	
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		Документы.Спецификация.УстановитьСтатусСпецификации(Выборка.Спецификация, Перечисления.СтатусыСпецификации.Изготовлен);
	КонецЦикла;
	
КонецПроцедуры

Функция СоздатьПроводку(фПодразделение, фСумма) 
	
	Проводка = Движения.Управленческий.Добавить();
	Проводка.Период = Дата;
	Проводка.Подразделение = фПодразделение;
	Проводка.Сумма = фСумма;
	
	Возврат Проводка;
	
КонецФункции
