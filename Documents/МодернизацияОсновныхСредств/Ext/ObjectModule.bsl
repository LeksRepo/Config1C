
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	//Движения.СостояниеОсновныхСредств.Очистить();
	//Движения.СостояниеОсновныхСредств.Записать();
	//
	//Движения.Управленческий.Очистить();
	//Движения.Управленческий.Записать();
	
	Ошибки = Неопределено;
	
	МассивСчетов = Новый Массив;
	МассивСчетов.Добавить(ПланыСчетов.Управленческий.ПервоначальнаяСтоимостьОС);
	МассивСчетов.Добавить(ПланыСчетов.Управленческий.АмортизацияОС);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МодернизацияОсновныхСредств.ОС КАК ОсновноеСредство,
	|	МодернизацияОсновныхСредств.НомерСтроки,
	|	МодернизацияОсновныхСредств.НачислятьАмортизацию,
	|	МодернизацияОсновныхСредств.СрокАмортизации,
	|	МодернизацияОсновныхСредств.Статья,
	|	МодернизацияОсновныхСредств.Стоимость,
	|	МодернизацияОсновныхСредств.Ссылка.Контрагент
	|ПОМЕСТИТЬ тчДок
	|ИЗ
	|	Документ.МодернизацияОсновныхСредств.ОсновныеСредства КАК МодернизацияОсновныхСредств
	|ГДЕ
	|	МодернизацияОсновныхСредств.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	тчДок.ОсновноеСредство,
	|	ЕСТЬNULL(СостояниеОССрезПоследних.ПринятоКУчету, ЛОЖЬ) КАК ПринятоКУчету,
	|	СостояниеОССрезПоследних.СрокАмортизации,
	|	СостояниеОССрезПоследних.ПервоначальнаяСтоимость,
	|	тчДок.НомерСтроки,
	|	тчДок.НачислятьАмортизацию,
	|	тчДок.СрокАмортизации КАК НовыйСрокАмортизации,
	|	тчДок.Статья,
	|	тчДок.Стоимость,
	|	СостояниеОССрезПоследних.МОЛ,
	|	ЕСТЬNULL(УправленческийОстатки.Субконто1, ЗНАЧЕНИЕ(Справочник.ОсновныеСредства.ПустаяСсылка)) КАК ПроверкаПодразделения
	|ИЗ
	|	тчДок КАК тчДок
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостояниеОсновныхСредств.СрезПоследних(
	|				&МоментВремени,
	|				ОсновноеСредство В
	|					(ВЫБРАТЬ
	|						т.ОсновноеСредство
	|					ИЗ
	|						тчДок КАК т)) КАК СостояниеОССрезПоследних
	|		ПО тчДок.ОсновноеСредство = СостояниеОССрезПоследних.ОсновноеСредство
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(&МоментВремени, Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПервоначальнаяСтоимостьОС), , Подразделение = &Подразделение) КАК УправленческийОстатки
	|		ПО тчДок.ОсновноеСредство = УправленческийОстатки.Субконто1";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если ЗначениеЗаполнено(Выборка.ПроверкаПодразделения) Тогда
			
			Если НЕ Выборка.ПринятоКУчету ИЛИ Выборка.ПервоначальнаяСтоимость = 0 Тогда
				
				ТекстГруппыОшибок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Основное средство %1 (строка %%1) по данным управленческого учета не числится", Выборка.ОсновноеСредство);
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Основное средство %1  по данным управленческого учета не числится", Выборка.ОсновноеСредство);
				ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "Объект.ОсновныеСредства[" +(Выборка.НомерСтроки-1)  + "].ОС", ТекстОшибки, 1, Выборка.НомерСтроки - 1, ТекстГруппыОшибок);
				
			КонецЕсли;
			
			Если Ошибки <> Неопределено Тогда
				
				Продолжить;
				
			КонецЕсли;
			
			Проводки = Движения.СостояниеОсновныхСредств.Добавить();
			Проводки.ОсновноеСредство = Выборка.ОсновноеСредство;
			Проводки.НачислятьАмортизацию  = Выборка.НачислятьАмортизацию;
			Проводки.МОЛ = Выборка.МОЛ;
			Проводки.Период = Дата;
			Проводки.ПринятоКУчету = Истина;
			Проводки.СрокАмортизации = Выборка.СрокАмортизации + Выборка.СрокАмортизации;
			//////
			НоваяСтоимость = Выборка.ПервоначальнаяСтоимость +Выборка.Стоимость;
			Проводки = Движения.Управленческий.Добавить();
			Проводки.Период = Дата;
			Проводки.Подразделение = Подразделение;
			Проводки.Сумма = НоваяСтоимость;
			
			Проводки.СчетДт = ПланыСчетов.Управленческий.ПервоначальнаяСтоимостьОС;
			Проводки.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.ОсновныеСредства] = Выборка.ОсновноеСредство;
			
			Проводки.СчетКт = ПланыСчетов.Управленческий.ВзаиморасчетыСПоставщиками;
			Проводки.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Контрагенты] = Контрагент;
			
		Иначе
			
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Основное средство %1  по данным управленческого учета не числится в подразделении " + Подразделение, Выборка.ОсновноеСредство);
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "Объект.ОсновныеСредства[" +(Выборка.НомерСтроки-1)  + "].ОС", ТекстОшибки);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки, Отказ);
	
	Если НЕ Отказ Тогда
		
		Движения.Управленческий.Записывать = Истина;
		Движения.СостояниеОсновныхСредств.Записывать = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ТаблицаОС = ОсновныеСредства.Выгрузить();
	ТаблицаОС.Колонки.Добавить("ПодсчетОС", Новый ОписаниеТипов("Число"));
	ТаблицаОС.ЗаполнитьЗначения(1, "ПодсчетОС");
	ТаблицаОС.Свернуть("ОС", "ПодсчетОС");
	Ошибки = Неопределено;
	
	Для каждого Строка Из ТаблицаОС Цикл
	
		Если Строка.ПодсчетОС > 1 Тогда
		
			ТекстОшибки = "У основного средства " + Строка.ОС + " есть дубликаты";
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "Объект.ОсновныеСредства.ОС", ТекстОшибки);
		
		КонецЕсли;
	
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки, Отказ);
	
КонецПроцедуры
