
Процедура ПодготовитьПечатнуюФорму(Знач ИмяМакета, ПредставлениеМакета, ПолныйПутьКМакету = "", МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода)
	
	Если ИмяМакета = "МакетМодернизацияОС" Тогда
		
		ТабДок = ПечатьМодернизацияОС(МассивОбъектов, ОбъектыПечати);
		
	КонецЕсли;
	
	УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, ИмяМакета,
	ПредставлениеМакета, ТабДок,, ПолныйПутьКМакету);
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "МакетМодернизацияОС") Тогда
		
		ПодготовитьПечатнуюФорму("МакетМодернизацияОС", "Модернизация ОС", "Документ.МодернизацияОсновныхСредств.МакетМодернизацияОС",
		МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПечатьМодернизацияОС(МассивОбъектов, ОбъектыПечати) Экспорт
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Истина;
	ТабДок.ТолькоПросмотр = Истина;
	ТабДок.ОтображатьЗаголовки = Ложь;
	
	Макет = Документы.МодернизацияОсновныхСредств.ПолучитьМакет("МакетМодернизацияОС");
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("МассивОбъектов", МассивОбъектов);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МодернизацияОсновныхСредств.Автор КАК Автор,
	|	МодернизацияОсновныхСредств.Дата КАК Дата,
	|	МодернизацияОсновныхСредств.Номер КАК Номер,
	|	МодернизацияОсновныхСредств.Подразделение КАК Подразделение,
	|	МодернизацияОсновныхСредств.Ссылка КАК Ссылка,
	|	МодернизацияОсновныхСредств.ОсновныеСредства.(
	|		НомерСтроки,
	|		Комментарий,
	|		НачислятьАмортизацию,
	|		ОС,
	|		СрокАмортизации,
	|		Стоимость
	|	)
	|ИЗ
	|	Документ.МодернизацияОсновныхСредств КАК МодернизацияОсновныхСредств
	|ГДЕ
	|	МодернизацияОсновныхСредств.Ссылка В(&МассивОбъектов)";
	
	ВыборкаДокументы = Запрос.Выполнить().Выбрать();
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	Шапка = Макет.ПолучитьОбласть("Шапка");
	Подвал = Макет.ПолучитьОбласть("Подвал");
	ОбластьОСШапка = Макет.ПолучитьОбласть("ОСШапка");
	ОбластьОССтрока = Макет.ПолучитьОбласть("ОССтрока");
	
	ВставлятьРазделительСтраниц = Ложь;
	
	Пока ВыборкаДокументы.Следующий() Цикл
		НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1;
		Если ВставлятьРазделительСтраниц Тогда
				ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;

		ВыборкаОС = ВыборкаДокументы.ОсновныеСредства.Выбрать();
		ОбластьЗаголовок.Параметры.НомерДокумента = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(ВыборкаДокументы.Номер);
		ОбластьЗаголовок.Параметры.ДатаДокумента = Формат(ВыборкаДокументы.Дата, "ДЛФ=DD");
		ТабДок.Вывести(ОбластьЗаголовок);
		
		Шапка.Параметры.Заполнить(ВыборкаДокументы);
		ТабДок.Вывести(Шапка, ВыборкаДокументы.Уровень());
		ТабДок.Вывести(ОбластьОСШапка);
		
		Пока ВыборкаОС.Следующий() Цикл
			
			СуммаОС = 0;
			
			ОбластьОССтрока.Параметры.Заполнить(ВыборкаОС);
			ТабДок.Вывести(ОбластьОССтрока);
			
		КонецЦикла;
		Подвал.Параметры.Заполнить(ВыборкаДокументы);
		ТабДок.Вывести(Подвал);
		
		ВставлятьРазделительСтраниц = Истина;
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументы.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции
