
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДатыЗапретаИзменения.ПроверитьДатуЗапретаИзмененияПередЗаписьюДокумента(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	ПроведениеЛистовогоМатериала(Отказ);
	ПроведениеХлыстовогоМатериала(Отказ);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Перем Ошибки;
	
	Для каждого Строка Из СписокНоменклатуры Цикл
		
		Если НЕ Строка.Номенклатура.НоменклатурнаяГруппа.ВидМатериала = Перечисления.ВидыМатериалов.Листовой Тогда
			
			ТекстСообщения = "" + Строка.Номенклатура + " не листовой материал";
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "Объект.СписокНоменклатуры[%1].Номенклатура", ТекстСообщения,,Строка.НомерСтроки - 1);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для каждого Строка Из СписокХлыстовойНоменклатуры Цикл
		
		Если НЕ Строка.Номенклатура.НоменклатурнаяГруппа.ВидМатериала = Перечисления.ВидыМатериалов.Хлыстовой Тогда
			
			ТекстСообщения = "" + Строка.Номенклатура + " не хлыстовой материал";
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "Объект.СписокХлыстовойНоменклатуры[%1].Номенклатура", ТекстСообщения,,Строка.НомерСтроки - 1);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки, Отказ);
	
КонецПроцедуры

Процедура ПроведениеЛистовогоМатериала(Отказ)
	
	Движения.ОбрезкиЛистовойМатериал.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасходОбрезковСписокНоменклатуры.Номенклатура,
	|	РасходОбрезковСписокНоменклатуры.Ширина,
	|	РасходОбрезковСписокНоменклатуры.Высота,
	|	РасходОбрезковСписокНоменклатуры.Резерв,
	|	&Подразделение КАК Подразделение,
	|	РасходОбрезковСписокНоменклатуры.НомерСтроки КАК НомерСтроки
	|ПОМЕСТИТЬ втНоменклатура
	|ИЗ
	|	Документ.РасходОбрезков.СписокНоменклатуры КАК РасходОбрезковСписокНоменклатуры
	|ГДЕ
	|	РасходОбрезковСписокНоменклатуры.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втНоменклатура.Номенклатура,
	|	втНоменклатура.Ширина,
	|	втНоменклатура.Высота,
	|	втНоменклатура.Резерв,
	|	втНоменклатура.НомерСтроки
	|ИЗ
	|	втНоменклатура КАК втНоменклатура";
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Запись = Движения.ОбрезкиЛистовойМатериал.ДобавитьРасход();
		Запись.Период = Дата;
		Запись.Подразделение = Подразделение;
		Запись.Номенклатура = Выборка.Номенклатура;
		Запись.Ширина = Выборка.Ширина;
		Запись.Высота = Выборка.Высота;
		
		Если ЗначениеЗаполнено(Выборка.Резерв) Тогда
			Запись.КоличествоРезерв = 1;
			Запись.Спецификация = Выборка.Резерв;
		Иначе	
			Запись.Количество = 1;
		КонецЕсли;	
		
	КонецЦикла;
	
	Движения.Записать();
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	втНоменклатура.Номенклатура,
	|	МИНИМУМ(втНоменклатура.НомерСтроки) КАК НомерСтроки,
	|	втНоменклатура.Ширина,
	|	втНоменклатура.Высота,
	|	втНоменклатура.Резерв,
	|	втНоменклатура.Подразделение
	|ПОМЕСТИТЬ ВТ
	|ИЗ
	|	втНоменклатура КАК втНоменклатура
	|
	|СГРУППИРОВАТЬ ПО
	|	втНоменклатура.Номенклатура,
	|	втНоменклатура.Подразделение,
	|	втНоменклатура.Высота,
	|	втНоменклатура.Ширина,
	|	втНоменклатура.Резерв
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втНоменклатура.Номенклатура,
	|	втНоменклатура.НомерСтроки,
	|	-ОбрезкиЛистовойМатериалОстатки.КоличествоОстаток КАК Нехватка,
	|	-ОбрезкиЛистовойМатериалОстатки.КоличествоРезервОстаток КАК НехваткаРезерв,
	|	втНоменклатура.Ширина,
	|	втНоменклатура.Высота,
	|	втНоменклатура.Резерв
	|ИЗ
	|	ВТ КАК втНоменклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОбрезкиЛистовойМатериал.Остатки(, ) КАК ОбрезкиЛистовойМатериалОстатки
	|		ПО втНоменклатура.Номенклатура = ОбрезкиЛистовойМатериалОстатки.Номенклатура
	|			И втНоменклатура.Ширина = ОбрезкиЛистовойМатериалОстатки.Ширина
	|			И втНоменклатура.Высота = ОбрезкиЛистовойМатериалОстатки.Высота
	|			И втНоменклатура.Подразделение = ОбрезкиЛистовойМатериалОстатки.Подразделение
	|			И втНоменклатура.Резерв = ОбрезкиЛистовойМатериалОстатки.Спецификация
	|ГДЕ
	|	(ОбрезкиЛистовойМатериалОстатки.КоличествоОстаток < 0
	|			ИЛИ ОбрезкиЛистовойМатериалОстатки.КоличествоРезервОстаток < 0)";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Ошибки = Неопределено;
		ШаблонСтроки = "%1 (%2х%3) недостаточно %4 шт.";
		ИмяПоляОшибки = "Объект.СписокНоменклатуры[%1].Количество";
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			СтрокаОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСтроки,
			Выборка.Номенклатура,
			Выборка.Ширина,
			Выборка.Высота,
			Выборка.Нехватка + Выборка.НехваткаРезерв);
			
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, ИмяПоляОШибки, СтрокаОшибки,, Выборка.НомерСтроки - 1);
			
		КонецЦикла;
		
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки, Отказ);
	
	Если Не Отказ Тогда
		Движения.ОбрезкиЛистовойМатериал.Записывать = Истина;
	Иначе
		Движения.ОбрезкиЛистовойМатериал.Записывать = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроведениеХлыстовогоМатериала(Отказ)
	
	Движения.ОбрезкиХлыстовойМатериал.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Список.Номенклатура,
	|	Список.Длина,
	|	&Подразделение КАК Подразделение,
	|	Список.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	Документ.РасходОбрезков.СписокХлыстовойНоменклатуры КАК Список
	|ГДЕ
	|	Список.Ссылка = &Ссылка";
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Запись = Движения.ОбрезкиХлыстовойМатериал.ДобавитьРасход();
		Запись.Период = Дата;
		Запись.Подразделение = Подразделение;
		Запись.Номенклатура = Выборка.Номенклатура;
		Запись.Длина = Выборка.Длина;
		Запись.Количество = 1;	
		
	КонецЦикла;
	
	Движения.Записать();
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Список.Номенклатура КАК Номенклатура,
	|	Список.Длина,
	|	Список.НомерСтроки,
	|	&Подразделение КАК Подразделение
	|ПОМЕСТИТЬ ВТ
	|ИЗ
	|	Документ.РасходОбрезков.СписокХлыстовойНоменклатуры КАК Список
	|ГДЕ
	|	Список.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втНоменклатура.Номенклатура,
	|	втНоменклатура.НомерСтроки,
	|	-Обрезки.КоличествоОстаток КАК Нехватка,
	|	втНоменклатура.Длина
	|ИЗ
	|	ВТ КАК втНоменклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОбрезкиХлыстовойМатериал.Остатки(, ) КАК Обрезки
	|		ПО втНоменклатура.Номенклатура = Обрезки.Номенклатура
	|			И втНоменклатура.Длина = Обрезки.Длина
	|			И втНоменклатура.Подразделение = Обрезки.Подразделение
	|ГДЕ
	|	Обрезки.КоличествоОстаток < 0";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Ошибки = Неопределено;
		ШаблонСтроки = "%1 (%2) недостаточно %3 шт.";
		ИмяПоляОшибки = "Объект.СписокХлыстовойНоменклатуры";
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			СтрокаОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСтроки,
			Выборка.Номенклатура,
			Выборка.Длина,
			Выборка.Нехватка);
			
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, ИмяПоляОШибки, СтрокаОшибки,, Выборка.НомерСтроки - 1);
			
		КонецЦикла;
		
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки, Отказ);
	
	Если Не Отказ Тогда
		Движения.ОбрезкиХлыстовойМатериал.Записывать = Истина;
	Иначе
		Движения.ОбрезкиХлыстовойМатериал.Записывать = Ложь;
	КонецЕсли;
	
КонецПроцедуры