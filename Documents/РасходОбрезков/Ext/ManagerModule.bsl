
Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	ЛексСервер.ПолучитьПредставлениеДокумента(Данные, Представление, СтандартнаяОбработка);

КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "РасходОбрезков") Тогда
		ПодготовитьПечатнуюФорму("РасходОбрезков", "Расход обрезков", "Документ.РасходОбрезков.ПечатьРасходОбрезков",
		МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	КонецЕсли;
	
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
КонецПроцедуры

Процедура ПодготовитьПечатнуюФорму(Знач ИмяМакета, ПредставлениеМакета, ПолныйПутьКМакету = "", МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода)
	
	НужноПечататьМакет = УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, ИмяМакета);
	
	Если НужноПечататьМакет Тогда
		
		Если ИмяМакета = "РасходОбрезков" Тогда
			
			ТабДок = ПечатьРасходОбрезков(МассивОбъектов, ОбъектыПечати);
			
		КонецЕсли;
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, ИмяМакета,
		ПредставлениеМакета, ТабДок,, ПолныйПутьКМакету);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПечатьРасходОбрезков(МассивДокументов, ОбъектыПечати) Экспорт
	
	Макет = Документы.РасходОбрезков.ПолучитьМакет("РасходОбрезков");
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	ОбластьШапкаХлыстовой = Макет.ПолучитьОбласть("ШапкаХлыстовой");
	ОбластьСтрокаХлыстовой = Макет.ПолучитьОбласть("СтрокаХлыстовой");
	ОбластьШапкаТаблицаВиновные = Макет.ПолучитьОбласть("ШапкаТаблицаВиновные");
	ОбластьСтрокаТаблицаВиновные = Макет.ПолучитьОбласть("СтрокаТаблицаВиновные");
	ОбластьПодписи = Макет.ПолучитьОбласть("Подписи");
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.ИмяПараметровПечати = "ПараметрыПечати_ПечатьРасходОбрезков";
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Истина;
	ТабДок.ТолькоПросмотр = Истина;
	ТабДок.ОтображатьЗаголовки = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("МассивДокументов", МассивДокументов);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Список.Ссылка КАК Док,
	|	Список.НомерСтроки КАК НомерСтроки,
	|	Список.Номенклатура,
	|	Список.Высота,
	|	Список.Ширина,
	|	0 КАК Размер,
	|	0 КАК Вид
	|ПОМЕСТИТЬ втНоменклатура
	|ИЗ
	|	Документ.РасходОбрезков.СписокНоменклатуры КАК Список
	|ГДЕ
	|	Список.Ссылка В(&МассивДокументов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Список.Ссылка,
	|	Список.НомерСтроки,
	|	Список.Номенклатура,
	|	0,
	|	0,
	|	Список.Размер,
	|	1
	|ИЗ
	|	Документ.РасходОбрезков.СписокХлыстовойНоменклатуры КАК Список
	|ГДЕ
	|	Список.Ссылка В(&МассивДокументов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Список.Док КАК Док,
	|	Список.НомерСтроки КАК НомерСтроки,
	|	Список.Номенклатура,
	|	Список.Высота,
	|	Список.Ширина,
	|	Список.Размер,
	|	Список.Вид КАК Вид
	|ИЗ
	|	втНоменклатура КАК Список
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки,
	|	Вид
	|ИТОГИ ПО
	|	Док,
	|	Вид";
	
	ВыборкаДок = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ВставлятьРазделительСтраниц = Ложь;
	
	Пока ВыборкаДок.Следующий() Цикл
		
		НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1;
		
		Если ВставлятьРазделительСтраниц Тогда		
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();			
		КонецЕсли;
		
		Док = ВыборкаДок.Док;
		
		ОбластьЗаголовок.Параметры.Ссылка = Док;
		ОбластьЗаголовок.Параметры.Подразделение = Док.Подразделение; 
		ОбластьЗаголовок.Параметры.Комментарий = Док.Комментарий;
		ОбластьЗаголовок.Параметры.Директор = ФизическиеЛицаКлиентСервер.ФамилияИнициалыФизЛица(Строка(Док.Подразделение.Руководитель));
		
		ТабДок.Вывести(ОбластьЗаголовок);
		
		ВыборкаВид = ВыборкаДок.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаВид.Следующий() Цикл 
		
			Если ВыборкаВид.Вид = 0 Тогда
				 ОС = ОбластьСтрока; 
				 ТабДок.Вывести(ОбластьШапка);
			Иначе
				 ОС = ОбластьСтрокаХлыстовой;
				 ТабДок.Вывести(ОбластьШапкаХлыстовой);
			КонецЕсли;
			
			ВыборкаСтрока = ВыборкаВид.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
			Пока ВыборкаСтрока.Следующий() Цикл
				
				ОС.Параметры.Заполнить(ВыборкаСтрока);
				ТабДок.Вывести(ОС);
				
			КонецЦикла;
			
		КонецЦикла;
		
		Если Док.СписокВиновных.Количество() > 0 Тогда
				
			ТабДок.Вывести(ОбластьШапкаТаблицаВиновные);
			
			Для Каждого Стр Из Док.СписокВиновных Цикл
				ОбластьСтрокаТаблицаВиновные.Параметры.Заполнить(Стр);
				ТабДок.Вывести(ОбластьСтрокаТаблицаВиновные);
			КонецЦикла;
			
		КонецЕсли;
		
		ТабДок.Вывести(ОбластьПодписи);
				
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, НомерСтрокиНачало, ОбъектыПечати, Док);
		
		ВставлятьРазделительСтраниц = Истина;

	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции