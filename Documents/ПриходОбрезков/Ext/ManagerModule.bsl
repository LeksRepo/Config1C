
Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	ЛексСервер.ПолучитьПредставлениеДокумента(Данные, Представление, СтандартнаяОбработка);

КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПриходОбрезков") Тогда
		ПодготовитьПечатнуюФорму("ПриходОбрезков", "Приход обрезков", "Документ.ПриходОбрезков.ПечатьПриходОбрезков",
		МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	КонецЕсли;
	
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
КонецПроцедуры

Процедура ПодготовитьПечатнуюФорму(Знач ИмяМакета, ПредставлениеМакета, ПолныйПутьКМакету = "", МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода)
	
	НужноПечататьМакет = УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, ИмяМакета);
	
	Если НужноПечататьМакет Тогда
		
		Если ИмяМакета = "ПриходОбрезков" Тогда
			
			ТабДок = ПечатьПриходОбрезков(МассивОбъектов, ОбъектыПечати);
			
		КонецЕсли;
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, ИмяМакета,
		ПредставлениеМакета, ТабДок,, ПолныйПутьКМакету);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПечатьПриходОбрезков(МассивДокументов, ОбъектыПечати) Экспорт
	
	Макет = Документы.ПриходОбрезков.ПолучитьМакет("ПриходОбрезков");
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал"); 
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.ИмяПараметровПечати = "ПараметрыПечати_ПечатьПриходОбрезков";
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Истина;
	ТабДок.ТолькоПросмотр = Истина;
	ТабДок.ОтображатьЗаголовки = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("МассивДокументов", МассивДокументов);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Список.Ссылка КАК Док,
	|	Список.НомерСтроки КАК НомерСтроки,
	|	Список.Номенклатура,
	|	Список.Высота,
	|	Список.Ширина,
	|	Список.Количество
	|ИЗ
	|	Документ.ПриходОбрезков.СписокНоменклатуры КАК Список
	|ГДЕ
	|	Список.Ссылка В(&МассивДокументов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|ИТОГИ ПО
	|	Док";
	
	ВыборкаДок = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ВставлятьРазделительСтраниц = Ложь;
	
	Пока ВыборкаДок.Следующий() Цикл
		
		НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1;
		
		Если ВставлятьРазделительСтраниц Тогда		
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();			
		КонецЕсли;
		
		Док = ВыборкаДок.Док;
		
		ОбластьЗаголовок.Параметры.Ссылка = Док;
		ОбластьЗаголовок.Параметры.Подразделение = Док.Подразделение; 
		ОбластьЗаголовок.Параметры.Комментарий = Док.Комментарий;
		
		ТабДок.Вывести(ОбластьЗаголовок);
		ТабДок.Вывести(ОбластьШапка);
		
		ВыборкаСтрока = ВыборкаДок.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаСтрока.Следующий() Цикл
			
			ОбластьСтрока.Параметры.Заполнить(ВыборкаСтрока);
			ТабДок.Вывести(ОбластьСтрока);
			
		КонецЦикла;
		
		ОбластьПодвал.Параметры.Автор = Док.Автор;
		ТабДок.Вывести(ОбластьПодвал);
				
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, НомерСтрокиНачало, ОбъектыПечати, Док);
		
		ВставлятьРазделительСтраниц = Истина;

	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции
