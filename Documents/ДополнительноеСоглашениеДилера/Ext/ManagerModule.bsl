
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
		
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Соглашение") Тогда
		
		ПодготовитьПечатнуюФорму("Соглашение", "Соглашение", "Документ.ДополнительноеСоглашениеДилера.Соглашение",
		МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
		
	КонецЕсли;
	
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
КонецПроцедуры

Процедура ПодготовитьПечатнуюФорму(Знач ИмяМакета, ПредставлениеМакета, ПолныйПутьКМакету = "", МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода)
	
	Если ИмяМакета = "Соглашение" Тогда
		
		ТабДок = ПечатьСоглашение(МассивОбъектов, ОбъектыПечати);
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, ИмяМакета, 
		ПредставлениеМакета, ТабДок, , ПолныйПутьКМакету);
				
	КонецЕсли;
	
КонецПроцедуры

Функция ПечатьСоглашение(МассивОбъектов, ОбъектыПечати) 
	
	ФормСтрока = "Л = ru_RU; ДП = Истина";
	ПарПредмета = "рубль, рубля, рублей, м, копейка, копейки, копеек, ж, 0";
	
	ТабДок = Новый ТабличныйДокумент;	
	ТабДок.ИмяПараметровПечати = "ПараметрыПечати_Соглашение";
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Ложь;
	ТабДок.ТолькоПросмотр = Ложь;
	ТабДок.ОтображатьЗаголовки = Ложь;
	
	Макет = Документы.ДополнительноеСоглашениеДилера.ПолучитьМакет("Соглашение");

	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьДопСоглашение = Макет.ПолучитьОбласть("ДопСоглашение");
	ОбластьШапкаДоговора = Макет.ПолучитьОбласть("ШапкаДоговора");
	ОбластьРеквизиты = Макет.ПолучитьОбласть("Реквизиты");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДополнительноеСоглашениеДилера.Комментарий,
	|	ДополнительноеСоглашениеДилера.СуммаДокумента КАК Сумма,
	|	ДополнительноеСоглашениеДилера.Договор.Номер КАК НомерДоговора,
	|	ДополнительноеСоглашениеДилера.Договор.Дата КАК ДатаДоговора,
	|	ДополнительноеСоглашениеДилера.Ссылка,
	|	ДополнительноеСоглашениеДилера.Автор.ОбъектАвторизации.ПолноеНаименование КАК ПолноеНаименованиеОрганизации,
	|	ДополнительноеСоглашениеДилера.Автор.ОбъектАвторизации.РуководительРодительныйПадеж КАК АвторРодительныйПадеж,
	|	ДополнительноеСоглашениеДилера.Автор.ОбъектАвторизации.ДействуетНаОсновании КАК ДейсвтуетНаОсновании,
	|	ДополнительноеСоглашениеДилера.Автор.ОбъектАвторизации.ОГРН КАК ОГРН
	|ИЗ
	|	Документ.ДополнительноеСоглашениеДилера КАК ДополнительноеСоглашениеДилера
	|ГДЕ
	|	ДополнительноеСоглашениеДилера.Ссылка В(&МассивОбъектов)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1; 
		
		ОбластьЗаголовок.Параметры.Заполнить(Выборка);
		ОбластьЗаголовок.Параметры.ДатаДоговора = Формат(Выборка.ДатаДоговора, "ДЛФ=DD");
		ТабДок.Вывести(ОбластьЗаголовок);
		
		ОбластьШапкаДоговора.Параметры.Заполнить(Выборка);
		ТабДок.Вывести(ОбластьШапкаДоговора);
		
		ОбластьДопСоглашение.Параметры.Заполнить(Выборка);
		ТабДок.Вывести(ОбластьДопСоглашение);
		
		ОбластьРеквизиты.Параметры.Заполнить(Выборка);
		ТабДок.Вывести(ОбластьРеквизиты);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, НомерСтрокиНачало, ОбъектыПечати, Выборка.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции


