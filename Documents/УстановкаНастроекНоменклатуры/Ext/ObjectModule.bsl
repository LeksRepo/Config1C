
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДатыЗапретаИзменения.ПроверитьДатуЗапретаИзмененияПередЗаписьюДокумента(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Строка Из СписокНоменклатуры Цикл
		
		Если Строка.Номенклатура.ВидНоменклатуры = Перечисления.ВидыНоменклатуры.Услуга Тогда
			
			Строка.ОкруглятьДоЛистов = Ложь;
			Строка.АдресХранения = "";
			Строка.ЗакупОптом = Ложь;
			Строка.МожетПредоставитьЗаказчик = Ложь;
			Строка.ОсновнаяПоСкладу = Неопределено;
			Строка.ПодЗаказ = Ложь;
			Строка.Поставщик = Неопределено;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.НастройкиНоменклатуры.Записывать = Истина;
	
	Для каждого Строка Из СписокНоменклатуры Цикл
		
		Если Строка.Номенклатура.Базовый Тогда
			
			Движение = Движения.НастройкиНоменклатуры.Добавить();
			Движение.Период = Дата;
			Движение.Подразделение = Подразделение;
			ЗаполнитьЗначенияСвойств(Движение, Строка);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПоступлениеМатериаловУслуг") Тогда
		
		Док = ДанныеЗаполнения;
		Подразделение = Док.Подразделение; 
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("тз", Док.Материалы.Выгрузить());
		Запрос.УстановитьПараметр("Подразделение", Док.Подразделение);
		Запрос.УстановитьПараметр("Контрагент", Док.Контрагент);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(тзСписокНоменклатуры.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
		|	тзСписокНоменклатуры.Цена КАК Цена
		|ПОМЕСТИТЬ втСписокНоменклатуры
		|ИЗ
		|	&тз КАК тзСписокНоменклатуры
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА Список.Номенклатура.Базовый
		|			ТОГДА Список.Номенклатура
		|		ИНАЧЕ Список.Номенклатура.БазоваяНоменклатура
		|	КОНЕЦ КАК Номенклатура,
		|	ВЫБОР
		|		КОГДА Список.Номенклатура.Базовый
		|			ТОГДА Список.Цена
		|		ИНАЧЕ Список.Цена / Список.Номенклатура.КоэффициентБазовых
		|	КОНЕЦ КАК ПлановаяЗакупочная,
		|	ЕСТЬNULL(Данные.АдресХранения, """") КАК АдресХранения,
		|	ЕСТЬNULL(Данные.ДнейНаИзготовление, 0) КАК ДнейНаИзготовление,
		|	ЕСТЬNULL(Данные.ЗакупОптом, ЛОЖЬ) КАК ЗакупОптом,
		|	ЕСТЬNULL(Данные.МожетПредоставитьЗаказчик, ЛОЖЬ) КАК МожетПредоставитьЗаказчик,
		|	ЕСТЬNULL(Данные.ОкруглятьДоЛистов, ЛОЖЬ) КАК ОкруглятьДоЛистов,
		|	ЕСТЬNULL(Данные.ОсновнаяПоСкладу, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК ОсновнаяПоСкладу,
		|	ЕСТЬNULL(Данные.ПодЗаказ, ЛОЖЬ) КАК ПодЗаказ,
		|	&Контрагент КАК Поставщик,
		|	ЕСТЬNULL(Данные.Розничная, 0) КАК Розничная,
		|	ЕСТЬNULL(Данные.Розничная, 0) КАК РозничнаяПрошлая
		|ИЗ
		|	втСписокНоменклатуры КАК Список
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиНоменклатуры.СрезПоследних(, Подразделение = &Подразделение) КАК Данные
		|		ПО (Список.Номенклатура = Данные.Номенклатура
		|				ИЛИ Список.Номенклатура.БазоваяНоменклатура = Данные.Номенклатура)"; 
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			НоваяСтрока = СписокНоменклатуры.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Перем Ошибки;
	
	Для каждого Строка Из СписокНоменклатуры Цикл
		
		Если НЕ Строка.Номенклатура.Базовый Тогда
			
			ТекстСообщения = "Номенклатура '%1' не является базовой";
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Строка.Номенклатура);
			ПолеОшибки = "Объект.СписокНоменклатуры[%1].Номенклатура";
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, ПолеОшибки , ТекстСообщения, , Строка.НомерСтроки - 1);
			
		КонецЕсли;
		
		Если Строка.Номенклатура.Неноменклатурный Тогда
			
			Если ЗначениеЗаполнено(Строка.ОсновнаяПоСкладу) Тогда
				
				ТекстСообщения = "Для неноменклатурной позиции '%1' запрещено указывать основную по складу";
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Строка.Номенклатура);
				ПолеОшибки = "Объект.СписокНоменклатуры[%1].ОсновнаяПоСкладу";
				ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, ПолеОшибки , ТекстСообщения, , Строка.НомерСтроки - 1);
				
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Строка.Поставщик) Тогда
				
				ТекстСообщения = "Для неноменклатурной позиции '%1' запрещено указывать поставщика";
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Строка.Номенклатура);
				ПолеОшибки = "Объект.СписокНоменклатуры[%1].Поставщик";
				ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, ПолеОшибки , ТекстСообщения, , Строка.НомерСтроки - 1);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки, Отказ);
	
КонецПроцедуры