
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДатыЗапретаИзменения.ПроверитьДатуЗапретаИзмененияПередЗаписьюДокумента(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	Дата = НачалоЧаса(Дата);
	
	ЧасДаты = Час(Дата);
	Если ЧасДаты < 9 ИЛИ ЧасДаты > 18 Тогда
		Текст = "Время должно находится в интервале от 09:00 до 18:00";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст, ЭтотОбъект, "Дата");
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
	Дата = НачалоЧаса(ТекущаяДата());
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Дата = НачалоЧаса(ТекущаяДата());
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Сотрудник", Ответственный);
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("СсылкаЗамер", Основание);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УправленческийОбороты.СуммаОборот КАК ВстречаПоЗамеру,
	|	NULL КАК ЗанятНаДату
	|ПОМЕСТИТЬ ВТ
	|ИЗ
	|	РегистрБухгалтерии.Управленческий.Обороты(
	|			,
	|			,
	|			Регистратор,
	|			Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПоказателиСотрудника),
	|			,
	|			(ВЫРАЗИТЬ(Субконто1 КАК Перечисление.ВидыПоказателейСотрудников)) = ЗНАЧЕНИЕ(Перечисление.ВидыПоказателейСотрудников.ВстречаСКлиентом)
	|				И (ВЫРАЗИТЬ(Субконто2 КАК Справочник.ФизическиеЛица)) = &Сотрудник,
	|			,
	|			) КАК УправленческийОбороты
	|ГДЕ
	|	ВЫРАЗИТЬ(УправленческийОбороты.Регистратор КАК Документ.ВстречаСКлиентом).Основание = &СсылкаЗамер
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	NULL,
	|	ГрафикВстречИЗамеров.Занят
	|ИЗ
	|	РегистрСведений.ГрафикВстречИЗамеров КАК ГрафикВстречИЗамеров
	|ГДЕ
	|	ГрафикВстречИЗамеров.Сотрудник = &Сотрудник
	|	И ГрафикВстречИЗамеров.Период = &Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(ВТ.ВстречаПоЗамеру) КАК ВстречаПоЗамеру,
	|	КОЛИЧЕСТВО(ВТ.ЗанятНаДату) КАК ЗанятНаДату
	|ИЗ
	|	ВТ КАК ВТ";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Выборка.Следующий();
	
	Если ЗначениеЗаполнено(Выборка.ЗанятНаДату) Тогда
		Текст = "Дизайнер " + Ответственный + " занят " + Формат(Дата, "ДЛФ=Д") + " в " + Формат(Дата, "ДЛФ=T") + ". Выберите другое время";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст, ЭтотОбъект, "Дата");
		Отказ = Истина;
		
	Иначе
		
		Если НЕ ЗначениеЗаполнено(Выборка.ВстречаПоЗамеру) Тогда
			Движения.Управленческий.Записывать = Истина;
			
			Проводка = Движения.Управленческий.Добавить();
			Проводка.Подразделение = Подразделение;
			Проводка.Период = Дата;
			Проводка.Сумма = 1;
			Проводка.СчетДт = ПланыСчетов.Управленческий.ПоказателиСотрудника;
			Проводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.ВидыПоказателейСотрудников] = Перечисления.ВидыПоказателейСотрудников.ВстречаСКлиентом;
			Проводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.ФизическиеЛица] = Ответственный;
			
		КонецЕсли;
		
		Проводки = Движения.ГрафикВстречИЗамеров.Добавить();
		Проводки.Сотрудник = Ответственный;
		Проводки.Подразделение = Подразделение;
		Проводки.Занят = Истина;
		Проводки.Период = Дата;
		
		Движения.ГрафикВстречИЗамеров.Записывать = Истина;
		
	КонецЕсли;
	
	Выборка.Сбросить();
	
КонецПроцедуры
