
Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	ЛексСервер.ПолучитьПредставлениеДокумента(Данные, Представление, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "МакетОС4") Тогда
		
		ПодготовитьПечатнуюФорму("МакетОС4", "Списание основных средств (ОС-4)", "Документ.СписаниеОсновныхСредств.МакетОС4",
		МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
		
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "МакетСписаниеОС") Тогда
		
		ПодготовитьПечатнуюФорму("МакетСписаниеОС", "Списание основных средств", "Документ.СписаниеОсновныхСредств.МакетСписаниеОС",
		МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);

	КонецЕсли;
	
КонецПроцедуры

Функция ПечатьОС4(МассивОбъектов, ОбъектыПечати) Экспорт
	
	ТабДок = Новый ТабличныйДокумент;
	
	ФормСтрока = "Л = ru_RU; ДП = Истина";
	ПарПредмета="рубль, рубля, рублей, м, копейка, копейки, копеек, ж, 0";
	
	Макет = Документы.СписаниеОсновныхСредств.ПолучитьМакет("МакетОС4");
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("МассивОбъектов", МассивОбъектов);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СписаниеОсновныхСредствОсновныеСредства.Ссылка,
	|	СписаниеОсновныхСредствОсновныеСредства.Ссылка.Дата КАК ДатаДок,
	|	СписаниеОсновныхСредствОсновныеСредства.Ссылка.Номер КАК Номер,
	|	СписаниеОсновныхСредствОсновныеСредства.НомерСтроки,
	|	СписаниеОсновныхСредствОсновныеСредства.Ссылка.Подразделение КАК Подразделение,
	|	СписаниеОсновныхСредствОсновныеСредства.ОсновноеСредство КАК НаимОС,
	|	СписаниеОсновныхСредствОсновныеСредства.ОсновноеСредство.ИнвентарныйНомер КАК ИнвНомер,
	|	СписаниеОсновныхСредствОсновныеСредства.Стоимость КАК НачСтоимость,
	|	СписаниеОсновныхСредствОсновныеСредства.Амортизация КАК НачАмортизация,
	|	СписаниеОсновныхСредствОсновныеСредства.Стоимость - СписаниеОсновныхСредствОсновныеСредства.Амортизация КАК ОстСтоимость
	|ИЗ
	|	Документ.СписаниеОсновныхСредств.ОсновныеСредства КАК СписаниеОсновныхСредствОсновныеСредства
	|ГДЕ
	|	СписаниеОсновныхСредствОсновныеСредства.Ссылка В(&МассивОбъектов)
	|	И СписаниеОсновныхСредствОсновныеСредства.Ссылка.Проведен";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ОбластьТело = Макет.ПолучитьОбласть("Тело");
	
	ВставлятьРазделительСтраниц = Ложь;
	
	Пока Выборка.Следующий() Цикл
		
		НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1;
		
		Если ВставлятьРазделительСтраниц Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ОбластьТело.Параметры.НомерДок = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(Выборка.Номер) + "-" + Выборка.НомерСтроки;
		
		ОбластьТело.Параметры.Заполнить(Выборка);
		ТабДок.Вывести(ОбластьТело);
		
		ВставлятьРазделительСтраниц = Истина;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, НомерСтрокиНачало, ОбъектыПечати, Выборка.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции

Процедура ПодготовитьПечатнуюФорму(Знач ИмяМакета, ПредставлениеМакета, ПолныйПутьКМакету = "", МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода)
	
	ВывестиДокумент = Ложь;
	
	Если ИмяМакета = "МакетОС4" Тогда
		
		ТабДок = ПечатьОС4(МассивОбъектов, ОбъектыПечати);
		ВывестиДокумент = Истина;
		
	ИначеЕсли ИмяМакета = "МакетСписаниеОС" Тогда
		
		ТабДок = ПечатьСписаниеОС(МассивОбъектов, ОбъектыПечати);
		ВывестиДокумент = Истина;
		
	КонецЕсли;
	
	Если ВывестиДокумент Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, ИмяМакета, ПредставлениеМакета, ТабДок, , ПолныйПутьКМакету);
	КонецЕсли;
	
КонецПроцедуры

Функция ПечатьСписаниеОС(МассивОбъектов, ОбъектыПечати) Экспорт
	
	ТабДок = Новый ТабличныйДокумент;
	
	ФормСтрока = "Л = ru_RU; ДП = Истина";
	ПарПредмета="рубль, рубля, рублей, м, копейка, копейки, копеек, ж, 0";
	
	Макет = Документы.СписаниеОсновныхСредств.ПолучитьМакет("МакетСписаниеОС");
	
	Для Каждого Документ Из МассивОбъектов Цикл
		
		Запрос = Новый Запрос;
		Запрос.Параметры.Вставить("Документ", Документ);
		Запрос.Параметры.Вставить("Дата", ДАТА(Документ.Дата - 1));
		Запрос.Текст =
		"ВЫБРАТЬ
		|	СписаниеОСОсновныеСредства.ОсновноеСредство,
		|	СписаниеОСОсновныеСредства.НомерСтроки,
		|	СписаниеОСОсновныеСредства.Амортизация,
		|	СписаниеОСОсновныеСредства.Стоимость,
		|	СписаниеОСОсновныеСредства.Ссылка
		|ПОМЕСТИТЬ тчДок
		|ИЗ
		|	Документ.СписаниеОсновныхСредств.ОсновныеСредства КАК СписаниеОСОсновныеСредства
		|ГДЕ
		|	СписаниеОСОсновныеСредства.Ссылка = &Документ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	тчДок.ОсновноеСредство КАК ОсновноеСредство,
		|	тчДок.НомерСтроки,
		|	тчДок.Амортизация КАК Амортизация,
		|	тчДок.Стоимость,
		|	СостояниеОсновныхСредствСрезПоследних.МОЛ КАК МОЛ,
		|	тчДок.Ссылка.Номер КАК Номер,
		|	тчДок.Ссылка.Дата КАК Дата,
		|	тчДок.Ссылка КАК Ссылка,
		|	тчДок.Ссылка.Подразделение КАК Подразделение
		|ИЗ
		|	тчДок КАК тчДок
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостояниеОсновныхСредств.СрезПоследних(&Дата, ) КАК СостояниеОсновныхСредствСрезПоследних
		|		ПО тчДок.ОсновноеСредство = СостояниеОсновныхСредствСрезПоследних.ОсновноеСредство
		|ИТОГИ ПО
		|	МОЛ";
		
		ВыборкаМОЛ = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
		Шапка = Макет.ПолучитьОбласть("Шапка");
		Подвал = Макет.ПолучитьОбласть("Подвал");
		ОбластьОСШапка = Макет.ПолучитьОбласть("ОСШапка");
		ОбластьОССтрока = Макет.ПолучитьОбласть("ОССтрока");
		
		ВставлятьРазделительСтраниц = Ложь;
		
		Пока ВыборкаМОЛ.Следующий() Цикл
			
			ВыборкаОС = ВыборкаМОЛ.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1;
			
			Если ВставлятьРазделительСтраниц Тогда
				ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			
			ОбластьЗаголовок.Параметры.НомерДокумента = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(ВыборкаМОЛ.Номер);
			ОбластьЗаголовок.Параметры.ДатаДокумента = Формат(ВыборкаМОЛ.Дата, "ДЛФ=DD");
			ТабДок.Вывести(ОбластьЗаголовок);
			
			Шапка.Параметры.Заполнить(ВыборкаМОЛ);
			Шапка.Параметры.МОЛ = ВыборкаМОЛ.МОЛ;
			Шапка.Параметры.Подразделение = Документ.Подразделение;
			ТабДок.Вывести(Шапка, ВыборкаМОЛ.Уровень());
			
			СуммаОС = 0;
			
			ВыборкаОС = ВыборкаМОЛ.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			ТабДок.Вывести(ОбластьОСШапка);
			
			
			Пока ВыборкаОС.Следующий() Цикл
				
				СуммаОС = СуммаОС + ВыборкаОС.Стоимость;
				ОбластьОССтрока.Параметры.Заполнить(ВыборкаОС);
				ТабДок.Вывести(ОбластьОССтрока);
				
			КонецЦикла;
			
			
			Подвал.Параметры.Заполнить(ВыборкаМОЛ);
			Подвал.Параметры.СуммаДокумента = СуммаОС;
			Подвал.Параметры.МОЛ = ВыборкаМОЛ.МОЛ;
			Подвал.Параметры.СуммаДокументаПрописью = ЧислоПрописью(СуммаОС, ФормСтрока, ПарПредмета);
			ТабДок.Вывести(Подвал);
			
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
			
			ВставлятьРазделительСтраниц = Истина;
			
			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, НомерСтрокиНачало, ОбъектыПечати, ВыборкаМОЛ.Ссылка);
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции
