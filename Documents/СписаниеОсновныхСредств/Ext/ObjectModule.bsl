
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	ДатыЗапретаИзменения.ПроверитьДатуЗапретаИзмененияПередЗаписьюДокумента(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
КонецПроцедуры



Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.ОсновныеСредства.Очистить();
	Движения.ОсновныеСредства.Записать();
	
	Ошибки = Неопределено;
	
	МассивСчетов = Новый Массив;
	МассивСчетов.Добавить(ПланыСчетов.Управленческий.ПервоначальнаяСтоимостьОС);
	МассивСчетов.Добавить(ПланыСчетов.Управленческий.АмортизацияОС);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.УстановитьПараметр("МассивСчетов", МассивСчетов);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СписаниеОСОсновныеСредства.ОС,
	|	СписаниеОСОсновныеСредства.НомерСтроки
	|ПОМЕСТИТЬ тчДок
	|ИЗ
	|	Документ.СписаниеОсновныхСредств.ОсновныеСредства КАК СписаниеОСОсновныеСредства
	|ГДЕ
	|	СписаниеОСОсновныеСредства.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	тчДок.ОС,
	|	МАКСИМУМ(тчДок.НомерСтроки) КАК НомерСтроки,
	|	МАКСИМУМ(ЕСТЬNULL(СостояниеОССрезПоследних.ПринятоКУчету, ЛОЖЬ)) КАК ПринятоКУчету,
	|	МАКСИМУМ(СостояниеОССрезПоследних.МОЛ) КАК МОЛ,
	|	МАКСИМУМ(СостояниеОССрезПоследних.НачислятьАмортизацию) КАК НачислятьАмортизацию,
	|	МАКСИМУМ(СостояниеОССрезПоследних.СрокАмортизации) КАК СрокАмортизации,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА УправленческийОстатки.Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.АмортизацияОС)
	|				ТОГДА УправленческийОстатки.СуммаОстаток
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК АмортизацияСумма,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА УправленческийОстатки.Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПервоначальнаяСтоимостьОС)
	|				ТОГДА УправленческийОстатки.СуммаОстаток
	|		КОНЕЦ) КАК СуммаПС
	|ИЗ
	|	тчДок КАК тчДок
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеСредства.СрезПоследних(
	|				&МоментВремени,
	|				ПринятоКУчету
	|					И ОсновноеСредство В
	|						(ВЫБРАТЬ
	|							т.ОС
	|						ИЗ
	|							тчДок КАК т)) КАК СостояниеОССрезПоследних
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(, Счет В (&МассивСчетов), , Подразделение = &Подразделение) КАК УправленческийОстатки
	|			ПО СостояниеОССрезПоследних.ОсновноеСредство = УправленческийОстатки.Субконто1
	|		ПО тчДок.ОС = СостояниеОССрезПоследних.ОсновноеСредство
	|
	|СГРУППИРОВАТЬ ПО
	|	тчДок.ОС";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если НЕ Выборка.ПринятоКУчету Тогда			
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Основное средство %1 по данным управленческого учета не числится", Выборка.ОС);
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "Объект.ОсновныеСредства[%1].ОС", ТекстОшибки,,Выборка.НомерСтроки - 1);
		КонецЕсли;
		
		Проводки = Движения.ОсновныеСредства.Добавить();
		Проводки.Период = Дата;
		Проводки.ПринятоКУчету = Ложь;
		Проводки.НачислятьАмортизацию = Выборка.НачислятьАмортизацию;
		Проводки.СрокАмортизации = Выборка.СрокАмортизации;
		Проводки.Стоимость = Выборка.СуммаПС;
		Проводки.МОЛ = Выборка.МОЛ;
		Проводки.ОсновноеСредство = Выборка.ОС;

		Проводки = Движения.Управленческий.Добавить();
		Проводки.Период = Дата;
		Проводки.Подразделение = Подразделение;
		Проводки.СчетДт = ПланыСчетов.Управленческий.АмортизацияОС;
		Проводки.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.ОсновныеСредства] = Выборка.ОС;
		Проводки.СчетКт = ПланыСчетов.Управленческий.Доходы;
		//littox Литус Антон - 06.11.2014
		//Какая Статья?
		//06.11.2014 - littox Литус Антон 
		//Проводки.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.СтатьиДР] = Справочники.СтатьиДоходовРасходов.;
		Проводки.Сумма = Выборка.АмортизацияСумма;
		
		Проводки = Движения.Управленческий.Добавить();
		Проводки.Период = Дата;
		Проводки.Подразделение = Подразделение;
		Проводки.СчетДт = ПланыСчетов.Управленческий.Расходы;
		//littox Литус Антон - 06.11.2014
		//Какая Статья?
		//06.11.2014 - littox Литус Антон 
		//
		//Проводки.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.СтатьиДР] = Справочники.СтатьиДоходовРасходов.;
		Проводки.СчетКт = ПланыСчетов.Управленческий.ПервоначальнаяСтоимостьОС;
		Проводки.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.ОсновныеСредства] = Выборка.ОС;
		Проводки.Сумма = Выборка.СуммаПС;
		
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки, Отказ);

	//Если НЕ Отказ Тогда
	//	
	//	Движения.СостояниеОС.Записывать = Истина;
	//	Движения.ОборотыПоСтатьям.Записывать = Истина;
	//	Движения.ОсновныеСредства.Записывать = Истина;
	//	
	//КонецЕсли;
	
КонецПроцедуры
