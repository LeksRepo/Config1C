
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДатыЗапретаИзменения.ПроверитьДатуЗапретаИзмененияПередЗаписьюДокумента(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.СостояниеОсновныхСредств.Очистить();
	Движения.СостояниеОсновныхСредств.Записать();
	
	Движения.Управленческий.Очистить();
	Движения.Управленческий.Записать();
	
	Ошибки = Неопределено;
	
	МассивСчетов = Новый Массив;
	МассивСчетов.Добавить(ПланыСчетов.Управленческий.ПервоначальнаяСтоимостьОС);
	МассивСчетов.Добавить(ПланыСчетов.Управленческий.АмортизацияОС);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.УстановитьПараметр("МассивСчетов", МассивСчетов);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СписаниеОСОсновныеСредства.ОсновноеСредство,
	|	СписаниеОСОсновныеСредства.НомерСтроки
	|ПОМЕСТИТЬ тчДок
	|ИЗ
	|	Документ.СписаниеОсновныхСредств.ОсновныеСредства КАК СписаниеОСОсновныеСредства
	|ГДЕ
	|	СписаниеОСОсновныеСредства.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	тчДок.ОсновноеСредство,
	|	МИНИМУМ(тчДок.НомерСтроки) КАК НомерСтроки,
	|	СУММА(ВЫБОР
	|			КОГДА УправленческийОстатки.Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.АмортизацияОС)
	|				ТОГДА ЕСТЬNULL(УправленческийОстатки.СуммаОстатокКт, 0)
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Амортизация,
	|	СУММА(ВЫБОР
	|			КОГДА УправленческийОстатки.Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПервоначальнаяСтоимостьОС)
	|				ТОГДА ЕСТЬNULL(УправленческийОстатки.СуммаОстатокДт, 0)
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ПервоначальнаяСтоимость,
	|	МАКСИМУМ(СостояниеОССрезПоследних.ПринятоКУчету) КАК ПринятоКУчету
	|ИЗ
	|	тчДок КАК тчДок
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостояниеОсновныхСредств.СрезПоследних(
	|				&МоментВремени,
	|				ОсновноеСредство В
	|					(ВЫБРАТЬ
	|						т.ОсновноеСредство
	|					ИЗ
	|						тчДок КАК т)) КАК СостояниеОССрезПоследних
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(&МоментВремени, Счет В (&МассивСчетов), , Подразделение = &Подразделение) КАК УправленческийОстатки
	|			ПО СостояниеОССрезПоследних.ОсновноеСредство = УправленческийОстатки.Субконто1
	|		ПО тчДок.ОсновноеСредство = СостояниеОССрезПоследних.ОсновноеСредство
	|
	|СГРУППИРОВАТЬ ПО
	|	тчДок.ОсновноеСредство";
	
	Выборка = Запрос.Выполнить().Выбрать();
	ПолеОшибки = "Объект.ОсновныеСредства[%1].ОсновноеСредство";
	
	Пока Выборка.Следующий() Цикл
		
		Если НЕ Выборка.ПринятоКУчету ИЛИ (Выборка.ПервоначальнаяСтоимость + Выборка.Амортизация) = 0 Тогда
			ТекстГруппыОшибок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Основное средство %1 (строка %%1) по данным управленческого учета не числится", Выборка.ОсновноеСредство);
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Основное средство %1  по данным управленческого учета не числится", Выборка.ОсновноеСредство);
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, ПолеОшибки, ТекстОшибки, 1, Выборка.НомерСтроки - 1, ТекстГруппыОшибок);
		КонецЕсли;
		
		Если Ошибки <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Проводки = Движения.СостояниеОсновныхСредств.Добавить();
		Проводки.ОсновноеСредство = Выборка.ОсновноеСредство;
		Проводки.Период = Дата;
		Проводки.ПринятоКУчету = Ложь;
		
		Если Выборка.Амортизация > 0 Тогда
			Проводки = Движения.Управленческий.Добавить();
			Проводки.Период = Дата;
			Проводки.Подразделение = Подразделение;
			Проводки.Сумма = Выборка.Амортизация;
			
			Проводки.СчетДт = ПланыСчетов.Управленческий.АмортизацияОС;
			Проводки.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.ОсновныеСредства] = Выборка.ОсновноеСредство;
			
			Проводки.СчетКт = ПланыСчетов.Управленческий.ПервоначальнаяСтоимостьОС;
			Проводки.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.ОсновныеСредства] = Выборка.ОсновноеСредство;
		КонецЕсли;
		
		ОстаточнаяСтоимость = Выборка.ПервоначальнаяСтоимость - Выборка.Амортизация;
		
		Если ОстаточнаяСтоимость > 0 Тогда
			Проводки = Движения.Управленческий.Добавить();
			Проводки.Период = Дата;
			Проводки.Подразделение = Подразделение;
			Проводки.Сумма = ОстаточнаяСтоимость;
			
			Проводки.СчетДт = ПланыСчетов.Управленческий.Расходы;
			Проводки.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.СтатьиДР] = Справочники.СтатьиДоходовРасходов.РасходыВыбытиеОсновныхСредств;
			
			Проводки.СчетКт = ПланыСчетов.Управленческий.ПервоначальнаяСтоимостьОС;
			Проводки.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.ОсновныеСредства] = Выборка.ОсновноеСредство;
		КонецЕсли;
		
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки, Отказ);
	
	Если НЕ Отказ Тогда
		
		Движения.Управленческий.Записывать = Истина;
		Движения.СостояниеОсновныхСредств.Записывать = Истина;
		
	КонецЕсли;
	
КонецПроцедуры
