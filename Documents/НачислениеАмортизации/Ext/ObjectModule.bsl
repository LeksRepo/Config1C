
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДатыЗапретаИзменения.ПроверитьДатуЗапретаИзмененияПередЗаписьюДокумента(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	
	Ошибки = Неопределено;
	
	Дата = КонецМесяца(Дата);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("КонецМесяца", КонецМесяца(Дата));
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	НачислениеАмортизации.Ссылка
	|ИЗ
	|	Документ.НачислениеАмортизации КАК НачислениеАмортизации
	|ГДЕ
	|	НачислениеАмортизации.Дата МЕЖДУ &НачалоМесяца И &КонецМесяца
	|	И НачислениеАмортизации.Подразделение = &Подразделение";
	
	Результат = Запрос.Выполнить();
	
	Если НЕ Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		
		фДата = Формат(Дата, "ДФ=ММММ");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("За %1 по подразделению %2 уже есть документ %3", фДата,Подразделение,Выборка.Ссылка);
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "Объект.Дата", ТекстОшибки);
		
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки, Отказ);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	МассивСчетов = Новый Массив;
	МассивСчетов.Добавить(ПланыСчетов.Управленческий.ПервоначальнаяСтоимостьОС);
	МассивСчетов.Добавить(ПланыСчетов.Управленческий.АмортизацияОС);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.УстановитьПараметр("МассивСчетов", МассивСчетов);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СостояниеОС.ОсновноеСредство КАК ОсновноеСредство,
	|	МАКСИМУМ(СостояниеОС.СрокАмортизации) КАК СрокАмортизации,
	|	СУММА(УправленческийОстатки.СуммаОстаток) КАК СуммаОстаток,
	|	СУММА(ВЫБОР
	|			КОГДА УправленческийОстатки.Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.АмортизацияОС)
	|				ТОГДА УправленческийОстатки.СуммаОстаток
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК АмортизацияСумма,
	|	СУММА(ВЫБОР
	|			КОГДА УправленческийОстатки.Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПервоначальнаяСтоимостьОС)
	|				ТОГДА УправленческийОстатки.СуммаОстаток
	|		КОНЕЦ) КАК СуммаПС
	|ИЗ
	|	РегистрСведений.СостояниеОсновныхСредств.СрезПоследних КАК СостояниеОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(, Счет В (&МассивСчетов), , Подразделение = &Подразделение) КАК УправленческийОстатки
	|		ПО СостояниеОС.ОсновноеСредство = УправленческийОстатки.Субконто1
	|ГДЕ
	|	СостояниеОС.НачислятьАмортизацию
	|
	|СГРУППИРОВАТЬ ПО
	|	СостояниеОС.ОсновноеСредство";
	
	Если НЕ Запрос.Выполнить().Пустой() Тогда
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.СуммаОстаток > 0 Тогда
				
				Проводка = Движения.Управленческий.Добавить();
				Проводка.Период = Дата;
				Проводка.Подразделение = Подразделение;
				Проводка.Сумма = Окр(МИН(Выборка.СуммаПС / Выборка.СрокАмортизации, Выборка.СуммаОстаток), 2);
				Проводка.СчетДт = ПланыСчетов.Управленческий.Расходы;
				Проводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.СтатьиДР] = Справочники.СтатьиДоходовРасходов.АмортизацияОС; 
				Проводка.СчетКт = ПланыСчетов.Управленческий.АмортизацияОС;
				Проводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.ОсновныеСредства] = Выборка.ОсновноеСредство;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Движения.Управленческий.Записывать = Истина;
		
	КонецЕсли
	
КонецПроцедуры
