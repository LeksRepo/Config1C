
Процедура ОбработкаПроведения(Отказ, Режим)
	
	НаборЗаписей = СоздатьПроводки(ПодразделениеПолучатель, Подразделение, СтатьяПолучатель, ВидДвиженияБухгалтерии.Дебет);
	ТаблицаДвижений = НаборЗаписей.Выгрузить();
	НаборЗаписей = СоздатьПроводки(Подразделение, ПодразделениеПолучатель, Статья, ВидДвиженияБухгалтерии.Кредит);
	
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаДвижений, НаборЗаписей.Выгрузить());
	Движения.Управленческий.Загрузить(ТаблицаДвижений);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если Подразделение = ПодразделениеПолучатель Тогда
		
		Отказ = Истина;
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Нельзя выставить рекламацию самому себе";
		Сообщение.Поле = "ПодразделениеПолучатель";
		Сообщение.УстановитьДанные(ЭтотОбъект);
		Сообщение.Сообщить();
		
	КонецЕсли;
	
КонецПроцедуры

Функция СоздатьПроводки(ИзмерениеПодразделение, СубконтоПодразделение, Статья, ДебетКредит)
	
	НаборЗаписей = РегистрыБухгалтерии.Управленческий.СоздатьНаборЗаписей();
	Проводка = НаборЗаписей.Добавить();
	
	Проводка.Период = Дата;
	Проводка.Сумма = Сумма;
	Проводка.Подразделение = ИзмерениеПодразделение;
	
	Если ВидДвиженияБухгалтерии.Дебет = ДебетКредит Тогда
		
		Проводка.СчетДт = ПланыСчетов.Управленческий.ВзаиморасчетыСПодразделениями;
		Проводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Подразделения] = СубконтоПодразделение;
		
		Проводка.СчетКт = ПланыСчетов.Управленческий.Доходы;
		Проводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.СтатьиДР] = Статья;
		
	ИначеЕсли ВидДвиженияБухгалтерии.Кредит = ДебетКредит Тогда
		
		Проводка.СчетКт = ПланыСчетов.Управленческий.ВзаиморасчетыСПодразделениями;
		Проводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Подразделения] = СубконтоПодразделение;
		
		Проводка.СчетДт = ЛексСервер.ПолучитьЗатратныйСчетПодразделения(ИзмерениеПодразделение);
		Проводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.СтатьиДР] = Статья;
		
	КонецЕсли;
	
	Возврат НаборЗаписей;
	
КонецФункции
