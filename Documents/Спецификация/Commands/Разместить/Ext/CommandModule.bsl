
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Ошибки = Неопределено;
	Отказ = Ложь;
	
	Спецификация = ПараметрКоманды;
	
	Если НЕ ЗначениеЗаполнено(Спецификация)
		ИЛИ ТипЗнч(Спецификация) <> Тип("ДокументСсылка.Спецификация") Тогда
		Возврат;		
	КонецЕсли;
	
	Если НЕ ПроверитьЗаполнение(Спецификация) Тогда
		Возврат;	
	КонецЕсли;
	
	РезультатПроверки = Проверить(Спецификация, Ошибки);
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки);
	
	Если РезультатПроверки.Разместить Тогда
	
		Если РезультатПроверки.ВопросПередатьНаПроверку Тогда
			
			ТекстВопроса = "Передать %1 на проверку?";
			ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстВопроса, Спецификация);
			Режим = РежимДиалогаВопрос.ДаНет;
			Ответ = Вопрос(ТекстВопроса, Режим, 0);
			Если Ответ = КодВозвратаДиалога.Да Тогда
				Разместить(Спецификация, Истина);
			КонецЕсли;
		Иначе 
			Разместить(Спецификация, Ложь);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПараметрыВыполненияКоманды.Источник.ИмяФормы = "Документ.Спецификация.Форма.ФормаДокумента" Тогда
		ОповеститьОбИзменении(Спецификация);
		Оповестить("ИзмененСтатусСпецификации", Спецификация);
		ПараметрыВыполненияКоманды.Источник.Прочитать();	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПроверитьЗаполнение(Ссылка)
	
	Возврат Ссылка.ПолучитьОбъект().ПроверитьЗаполнение();
	
КонецФункции

&НаСервере
Функция ПередатьНаПроверку(СпецификацияСсылка)
	
	Документы.Спецификация.УстановитьСтатусСпецификации(СпецификацияСсылка, Перечисления.СтатусыСпецификации.Проверяется);
	
КонецФункции

&НаСервере
Функция Проверить(СпецификацияСсылка, Ошибки)
	
	Результат = Новый Структура;
	Результат.Вставить("ВопросПередатьНаПроверку", Ложь);
	Результат.Вставить("Разместить", Истина);
	
	ТекущийСтатус = Документы.Спецификация.ПолучитьСтатусСпецификации(СпецификацияСсылка);
	
	Если СпецификацияСсылка.Дилерский Тогда
		
		ЭтоСеансДилера = ПользователиКлиентСервер.ЭтоСеансВнешнегоПользователя();

		Если ЭтоСеансДилера Тогда
			
			Пользователь = ПользователиКлиентСервер.ТекущийВнешнийПользователь();
			ПроверятьСпецификациюДилера = ЛексСервер.ПроверятьСпецификациюДилера(Пользователь.ОбъектАвторизации);
			
			Если СпецификацияСсылка.КоробаРедактированы Тогда
			
				Результат.Разместить = Ложь;
			
				ТекстСообщения = "Каталожные модули были изменены. Для размещения спецификации обратитесь к супервайзеру.";
				ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, СпецификацияСсылка, ТекстСообщения);
				
			ИначеЕсли ПроверятьСпецификациюДилера ИЛИ (СпецификацияСсылка.СписокМатериаловПодЗаказ.Количество() > 0) Тогда
				
				Результат.ВопросПередатьНаПроверку = ТекущийСтатус = Перечисления.СтатусыСпецификации.Сохранен;
				
			КонецЕсли;
			
			Если ТекущийСтатус = Перечисления.СтатусыСпецификации.Проверяется Тогда
				
				 Результат.ВопросПередатьНаПроверку = Ложь;
				 Результат.Разместить = Ложь;
				 
				 ТекстСообщения = "%1 уже проверяется";
				 ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, СпецификацияСсылка);
				 ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, СпецификацияСсылка, ТекстСообщения);
				 
			КонецЕсли;
			
		КонецЕсли;
	
	КонецЕсли;	
	
	Если ТекущийСтатус <> Перечисления.СтатусыСпецификации.Проверяется
		И ТекущийСтатус <> Перечисления.СтатусыСпецификации.Сохранен 
		И ТекущийСтатус <> Перечисления.СтатусыСпецификации.Рассчитывается Тогда
		
		Результат.Разместить = Ложь;
		
		ТекстСообщения = "Разместить спецификацию можно только со статусами 'Сохранен', 'Рассчитывается' и 'Проверяется' . У %1 статус '%2'";
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, СпецификацияСсылка, ТекущийСтатус);
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, СпецификацияСсылка, ТекстСообщения);
		
	КонецЕсли;
	
	Для каждого Строка Из СпецификацияСсылка.СписокМатериаловПодЗаказ Цикл
		
		Если Строка.Цена = 0 ИЛИ Строка.Поставщик.Пустая() Тогда
			Результат.Разместить = Ложь;
			ТекстСообщения = "%1 установите цену и поставщика для материала 'Под заказ'";
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, СпецификацияСсылка, ТекущийСтатус);
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, СпецификацияСсылка, ТекстСообщения);
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура Разместить(СпецификацияСсылка, Проверка)
	
	ДокОбъект = СпецификацияСсылка.ПолучитьОбъект();
	
	Попытка
		
		НачатьТранзакцию();
		
		Если Проверка Тогда
			Документы.Спецификация.УстановитьСтатусСпецификации(СпецификацияСсылка, Перечисления.СтатусыСпецификации.Проверяется);
		Иначе
			Документы.Спецификация.УстановитьСтатусСпецификации(СпецификацияСсылка, Перечисления.СтатусыСпецификации.Размещен);
		КонецЕсли;
			
		ДокОбъект.Дата = ТекущаяДата();
		
		Если НЕ ЗначениеЗаполнено(ДокОбъект.Технолог) 
			И НЕ ДокОбъект.Дилерский Тогда
			ДокОбъект.Технолог = ПараметрыСеанса.ТекущийПользователь.ФизическоеЛицо;
		КонецЕсли;
		
		ДокОбъект.Записать(РежимЗаписиДокумента.Проведение);
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		Сообщить("Не удалось разместить спецификацию " + СпецификацияСсылка);
	КонецПопытки;
	
КонецПроцедуры
