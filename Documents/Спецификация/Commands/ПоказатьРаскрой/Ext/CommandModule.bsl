
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ХранимыйФайлРаскрой = "НовыйРаскрой";
	ХранимыйФайлКривойПил = "КривойПил";
	
	МассивСсылок = Новый Массив;
	
	Для каждого СпецификацияСсылка Из ПараметрКоманды Цикл
		
		МассивСсылок.Добавить(СпецификацияСсылка);
		
	КонецЦикла;
	
	МассивСтрок = ОпределительРаскроя(МассивСсылок);
	
	Для каждого Определитель Из МассивСтрок Цикл
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("СпецификацияСсылка", Определитель.Ссылка);
		
		//Если есть детали ЛДСП открываем флэш "раскрой"
		Если ЗначениеЗаполнено(Определитель.СтрокаНовогоРаскрояЛДСП) Тогда
			
			ПараметрыФормы.Вставить("Спецификация", Определитель.Ссылка);
			ПараметрыФормы.Вставить("ХранимыйФайл", ХранимыйФайлРаскрой);
			ПараметрыФормы.Вставить("СтрокаНовогоРаскрояЛДСП", Определитель.СтрокаНовогоРаскрояЛДСП);
			ПараметрыФормы.Вставить("ВидОтображения", "2");
			ОткрытьФорму("Документ.Спецификация.Форма.ФормаФлэш", ПараметрыФормы,, Истина);
			
		КонецЕсли;
		
		// Если есть детали ЛДСП или стекла с кривым пилом открываем флэш для "кривого пила"
		Если ЗначениеЗаполнено(Определитель.СтрокаКривогоПила) Тогда
			
			ПараметрыФормы.Вставить("Спецификация", Определитель.Ссылка);
			ПараметрыФормы.Вставить("ХранимыйФайл", ХранимыйФайлКривойПил);
			ПараметрыФормы.Вставить("СтрокаКривогоПила", Определитель.СтрокаКривогоПила);
			ОткрытьФорму("Документ.Спецификация.Форма.ФормаФлэш", ПараметрыФормы,, Истина);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ОпределительРаскроя(МассивСсылок)
	
	МассивСтрок = Новый Массив;
	
	// { Васильев Александр Леонидович [02.06.2015]
	// Сделать.
	// Запрос в цикле. Теперь стало проще, можно сделать корректно.
	// } Васильев Александр Леонидович [02.06.2015]
	
	Для каждого Ссылка Из МассивСсылок Цикл
		
		Структура = Новый Структура;
		Структура.Вставить("Ссылка", Ссылка);
		Структура.Вставить("СтрокаНовогоРаскрояЛДСП", "");
		Структура.Вставить("СтрокаКривогоПила", "");
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Спецификация", Ссылка);
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВЫРАЗИТЬ(РаскройДеталей.Объект КАК Документ.Спецификация) КАК Ссылка,
		|	РаскройДеталей.СтрокаРаскрой КАК Раскрой,
		|	РаскройДеталей.СтрокаКривогоПила КАК СтрокаКривогоПила
		|ИЗ
		|	РегистрСведений.РаскройДеталей КАК РаскройДеталей
		|ГДЕ
		|	РаскройДеталей.Объект= &Спецификация";
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Если ЗначениеЗаполнено(Выборка.Раскрой) Тогда
				Структура.Вставить("СтрокаНовогоРаскрояЛДСП", Выборка.Раскрой);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Выборка.СтрокаКривогоПила) Тогда
				Структура.Вставить("СтрокаКривогоПила", Выборка.СтрокаКривогоПила);
			КонецЕсли;
			
		КонецЦикла;
		
		МассивСтрок.Добавить(Структура);
		
	КонецЦикла;
	
	Возврат МассивСтрок;
	
КонецФункции
