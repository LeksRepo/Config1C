
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ХранимыйФайлРаскрой 			= "Раскрой";
	ХранимыйФайлКривойПил 		= "КривойПил";
	ХранимыйФайлРаскройСтекла 	= "РаскройСтекла";
	
	МассивСсылок = Новый Массив;
	
	Для каждого СпецификацияСсылка Из ПараметрКоманды Цикл
		
		МассивСсылок.Добавить(СпецификацияСсылка);
		
	КонецЦикла;
	
	МассивСтрок = ОпределительРаскроя(МассивСсылок);
	
	Для каждого Определитель Из МассивСтрок Цикл
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("СпецификацияСсылка", Определитель.Ссылка);
		
		//Если есть детали ЛДСП открываем флэш "раскрой" 
		Если ЗначениеЗаполнено(Определитель.СтрокаРаскрояЛДСП) Тогда
			
			ПараметрыФормы.Вставить("ХранимыйФайл", ХранимыйФайлРаскрой);
			ПараметрыФормы.Вставить("СтрокаРаскрояЛДСП", Определитель.СтрокаРаскрояЛДСП);
			ОткрытьФорму("Документ.Спецификация.Форма.ФормаФлэш", ПараметрыФормы,, Истина);
			
		КонецЕсли;
		
		// Если есть детали ЛДСП или стекла с кривым пилом открываем флэш для "кривого пила"
		Если ЗначениеЗаполнено(Определитель.СтрокаКривогоПила) Тогда
			
			ПараметрыФормы.Вставить("ХранимыйФайл", ХранимыйФайлКривойПил);
			ПараметрыФормы.Вставить("СтрокаКривогоПила", Определитель.СтрокаКривогоПила);
			ОткрытьФорму("Документ.Спецификация.Форма.ФормаФлэш", ПараметрыФормы,, Истина);
			
		КонецЕсли;
		
		//Если есть стекла открываем флэш для стекол
		Если ЗначениеЗаполнено(Определитель.СтрокаРаскрояСтекла) Тогда
			
			ПараметрыФормы.Вставить("ХранимыйФайл", ХранимыйФайлРаскройСтекла);
			ПараметрыФормы.Вставить("СтрокаРаскрояСтекла", Определитель.СтрокаРаскрояСтекла);
			ОткрытьФорму("Документ.Спецификация.Форма.ФормаФлэш", ПараметрыФормы,, Истина);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ОпределительРаскроя(МассивСсылок)
	
	МассивСтрок = Новый Массив;
	Дилер = РольДоступна("ДилерскийДоступКСпецификации");
	
	Для каждого Ссылка Из МассивСсылок Цикл
		
		Если Дилер И НЕ Ссылка.Проведен Тогда
			Продолжить;
		КонецЕсли;
		
		Структура = Новый Структура;
		Структура.Вставить("Ссылка",Ссылка);
		Структура.Вставить("СтрокаРаскрояЛДСП", "");
		Структура.Вставить("СтрокаКривогоПила", "");
		Структура.Вставить("СтрокаРаскрояСтекла", "");
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Спецификация", Ссылка);
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Спецификация.СтрокаРаскрояЛДСП,
		|	Спецификация.СтрокаКривогоПила,
		|	Спецификация.СтрокаРаскрояСтекла
		|ИЗ
		|	Документ.Спецификация КАК Спецификация
		|ГДЕ
		|	Спецификация.Ссылка = &Спецификация";
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Если ЗначениеЗаполнено(Выборка.СтрокаРаскрояЛДСП) Тогда
				Структура.Вставить("СтрокаРаскрояЛДСП", Выборка.СтрокаРаскрояЛДСП);
			КонецЕсли;
			
			Если  ЗначениеЗаполнено(Выборка.СтрокаКривогоПила) Тогда
				Структура.Вставить("СтрокаКривогоПила", Выборка.СтрокаКривогоПила);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Выборка.СтрокаРаскрояСтекла) Тогда
				Структура.Вставить("СтрокаРаскрояСтекла", Выборка.СтрокаРаскрояСтекла);
			КонецЕсли;
			
		КонецЦикла;
		
		МассивСтрок.Добавить(Структура);
		
	КонецЦикла;
	
	Возврат МассивСтрок;
	
КонецФункции