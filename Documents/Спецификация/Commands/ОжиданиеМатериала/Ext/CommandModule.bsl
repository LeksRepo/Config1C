
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Ошибки = Неопределено;
	Спецификация = ПараметрКоманды; 
	СтатусИзменен = Ложь;
	
	Если НЕ ЗначениеЗаполнено(Спецификация)
		ИЛИ ТипЗнч(Спецификация) <> Тип("ДокументСсылка.Спецификация") Тогда
		Возврат;		
	КонецЕсли;
	
	Если НЕ ПроверитьЗаполнение(Спецификация) Тогда
		Возврат;	
	КонецЕсли;
	
	РезультатПроверки = Проверить(Спецификация, Ошибки);
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки);
	
	Если РезультатПроверки.ПередатьНаОжиданиеМатериала Тогда	
		СтатусИзменен = ПередатьНаОжиданиеМатериала(Спецификация);	
	КонецЕсли;
	
	Если СтатусИзменен И ПараметрыВыполненияКоманды.Источник.ИмяФормы = "Документ.Спецификация.Форма.ФормаДокумента" Тогда
		ОповеститьОбИзменении(Спецификация);
		Оповестить("ИзмененСтатусСпецификации", Спецификация);
		ПараметрыВыполненияКоманды.Источник.Прочитать();	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция Проверить(СпецификацияСсылка, Ошибки)
	
	Результат = Новый Структура;
	Результат.Вставить("ПередатьНаОжиданиеМатериала", Истина);
	
	Для каждого Строка Из СпецификацияСсылка.СписокМатериаловПодЗаказ Цикл
		
		Если Строка.Цена = 0 ИЛИ Строка.Поставщик.Пустая() Тогда
			Результат.ПередатьНаОжиданиеМатериала = Ложь;
			ТекстСообщения = "%1 установите цену и поставщика для материала 'Под заказ'";
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, СпецификацияСсылка);
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, СпецификацияСсылка, ТекстСообщения);
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	ТекущийСтатус = Документы.Спецификация.ПолучитьСтатусСпецификации(СпецификацияСсылка);
	
	Если НЕ (ТекущийСтатус = Перечисления.СтатусыСпецификации.Рассчитывается
		ИЛИ ТекущийСтатус = Перечисления.СтатусыСпецификации.Размещен 
		ИЛИ ТекущийСтатус = Перечисления.СтатусыСпецификации.Проверяется) Тогда	
		
		Результат.ПередатьНаОжиданиеМатериала = Ложь;
		ТекстСообщения = "На ожидание материала передаются только спецификации со статусами ""Рассчитывается"", ""Проверяется"" или ""Размещен""";
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, СпецификацияСсылка, ТекстСообщения);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ПроверитьЗаполнение(Ссылка)
	
	Возврат Ссылка.ПолучитьОбъект().ПроверитьЗаполнение();
	
КонецФункции

&НаСервере
Функция ПередатьНаОжиданиеМатериала(СпецификацияСсылка)
	
	СтатусИзменен = Ложь;
	
	ДокОбъект = СпецификацияСсылка.ПолучитьОбъект();
	
	Попытка
		
		НачатьТранзакцию();
		
		Документы.Спецификация.УстановитьСтатусСпецификации(СпецификацияСсылка, Перечисления.СтатусыСпецификации.ОжиданиеМатериала);
		ДокОбъект.Записать(РежимЗаписиДокумента.Проведение);
		СтатусИзменен = Истина;
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		Сообщить("Не удалось передать спецификацию на ожидание материала." + СпецификацияСсылка);
	КонецПопытки;
	
	Возврат СтатусИзменен;
	
КонецФункции
