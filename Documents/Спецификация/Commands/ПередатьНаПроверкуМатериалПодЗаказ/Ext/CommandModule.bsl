
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Спецификация = ПараметрКоманды;
	
	Если НЕ ЗначениеЗаполнено(Спецификация) 
		ИЛИ ТипЗнч(Спецификация) <> Тип("ДокументСсылка.Спецификация") Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если НЕ ПроверитьЗаполнение(Спецификация) Тогда
		Возврат;	
	КонецЕсли;
	
	ПередатьСпецификациюНаПроверкуМатериалаПодЗаказ(Спецификация);
	ОповеститьОбИзменении(Спецификация);
	
	Если ПараметрыВыполненияКоманды.Источник.ИмяФормы = "Документ.Спецификация.Форма.ФормаДокумента" Тогда
		ПараметрыВыполненияКоманды.Источник.Прочитать();	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПроверитьЗаполнение(Ссылка)
	
	Возврат Ссылка.ПолучитьОбъект().ПроверитьЗаполнение();
	
КонецФункции

&НаСервере
Функция ПередатьСпецификациюНаПроверкуМатериалаПодЗаказ(СпецификацияСсылка)
	
	Отказ = Ложь;
	ТекстСообщения = "";
	
	ТекущийСтатус = Документы.Спецификация.ПолучитьСтатусСпецификации(СпецификацияСсылка);
	ЕстьМатериалПодЗаказ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СпецификацияСсылка, "ЕстьМатериалПодЗаказ");
	
	Если НЕ ЕстьМатериалПодЗаказ Тогда
		
		ТекстСообщения = "В %1 нет материала под заказ, проверка материала не требуется";
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, СпецификацияСсылка);
		Отказ = Истина;
		
	Иначе
		
		Если ТекущийСтатус <> Перечисления.СтатусыСпецификации.Сохранен Тогда
			
			ТекстСообщения = "На проверку материала можно передать спецификацию только со статусом 'Сохранен'. У %1 статус '%2'";
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, СпецификацияСсылка, ТекущийСтатус);
			Отказ = Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		
		Документы.Спецификация.УстановитьСтатусСпецификации(СпецификацияСсылка, Перечисления.СтатусыСпецификации.ПроверяетсяМатериал);
		
	Иначе
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, СпецификацияСсылка);
		
	КонецЕсли;
	
КонецФункции
