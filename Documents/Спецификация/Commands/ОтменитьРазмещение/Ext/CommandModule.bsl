
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Спецификация = ПараметрКоманды;
	
	Если НЕ ЗначениеЗаполнено(Спецификация) 
		ИЛИ ТипЗнч(Спецификация) <> Тип("ДокументСсылка.Спецификация") Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если НЕ ОтменитьРазмещениеСпецификации(Спецификация) Тогда
		ОповеститьОбИзменении(Спецификация);
		Оповестить("ИзмененСтатусСпецификации", Спецификация);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ОтменитьРазмещениеСпецификации(СпецификацияСсылка)
	
	Отказ = Ложь;
	ТекстСообщения = "";
	
	ТекущийСтатус = Документы.Спецификация.ПолучитьСтатусСпецификации(СпецификацияСсылка);
	
	Если ТекущийСтатус <> Перечисления.СтатусыСпецификации.Размещен Тогда
		
		ТекстСообщения = "Отменить спецификацию можно только со статусом 'Размещен'. У %1 статус '%2'";
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, СпецификацияСсылка, ТекущийСтатус);
		Отказ = Истина;
		
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		
		ДокОбъект = СпецификацияСсылка.ПолучитьОбъект();
		ДокОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
		
		Если НЕ ДокОбъект.Проведен Тогда
			
			Документы.Спецификация.УстановитьСтатусСпецификации(СпецификацияСсылка, Перечисления.СтатусыСпецификации.Сохранен);
			
		КонецЕсли;
		
	Иначе
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, СпецификацияСсылка);
		
	КонецЕсли;
	
	Возврат Отказ;
	
КонецФункции
