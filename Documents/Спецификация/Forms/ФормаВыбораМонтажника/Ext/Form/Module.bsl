
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НачалоПериода = Параметры.НачалоПериода;
	КонецПериода = Параметры.КонецПериода;
	Город = Параметры.Офис.Город;
	//
	ТабДок.Вывести(Сформировать(НачалоПериода, КонецПериода, Город));
	ТабДок.ФиксацияСверху = 1;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция Сформировать(НачалоПериода, КонецПериода, Город)
	
	ТабДокТаблица = Новый ТабличныйДокумент;
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	Макет = Документы.Спецификация.ПолучитьМакет("ВыборМонтажника");
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьШапкаТаблица = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьСтрока = Макет.ПолучитьОбласть("СтрокаОтчета");
	ОбластьСтрокаСотрудник = Макет.ПолучитьОбласть("СтрокаМонтажник");
	ОбластьРазделительНедель = Макет.ПолучитьОбласть("РазделительНедель");
	
	ТабДок.Вывести(ОбластьШапка);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("ГородРабочий", Город);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ФизическиеЛица.Ссылка КАК Ссылка,
	|	РабочиеДниМонтажников.День КАК День,
	|	РабочиеДниМонтажников.РабочийДень КАК РабочийДень,
	|	РабочиеДниМонтажников.Спецификация,
	|	РабочиеДниМонтажников.Спецификация.АдресМонтажа,
	|	РабочиеДниМонтажников.Спецификация.ДатаМонтажа
	|ИЗ
	|	Справочник.ФизическиеЛица КАК ФизическиеЛица
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РабочиеДниМонтажников КАК РабочиеДниМонтажников
	|		ПО (ФизическиеЛица.Ссылка = РабочиеДниМонтажников.Монтажник)
	|ГДЕ
	|	ФизическиеЛица.Должность = ЗНАЧЕНИЕ(Справочник.Должности.Монтажник)
	|	И НЕ ФизическиеЛица.ПометкаУдаления
	|	И ВЫБОР
	|			КОГДА РабочиеДниМонтажников.Регистратор ЕСТЬ NULL 
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ РабочиеДниМонтажников.День МЕЖДУ &НачалоПериода И &КонецПериода
	|		КОНЕЦ
	|	И ФизическиеЛица.Активность
	|	И ФизическиеЛица.ГородРабочий = &ГородРабочий
	|
	|УПОРЯДОЧИТЬ ПО
	|	День
	|ИТОГИ
	|	КОЛИЧЕСТВО(РабочийДень)
	|ПО
	|	Ссылка";
	КолвоДней = (КонецПериода - НачалоПериода) / 86400;
	
	Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока Выборка.Следующий() Цикл   	
		ОбластьСтрокаСотрудник.Параметры.Монтажник = Выборка.Ссылка;
		ТабДокТаблица.Вывести(ОбластьСтрокаСотрудник);
		ТабДокТаблица.Вывести(ОбластьШапкаТаблица); 		
			
		ВыборкаПоСотруднику = Выборка.Выбрать();
		//Записей по сотруднику в регистре меньше либо равно количеству дней в периоде,
		//а выводить нужно все даты в периоде, даже пустые, поэтому ищем каждую дату в выборке
		Для Сч = 0 По КолвоДней  Цикл
			
			СледДень = НачалоПериода+Сч*86400;
			СтуктураПоиска = Новый Структура("День", СледДень);
			ЭтоВсоскресенье = ДеньНедели(СледДень) = 7;

			Если ЭтоВсоскресенье Тогда				
				ОбластьРазделительНедель.Параметры.Дата = СледДень;
				Если ВыборкаПоСотруднику.НайтиСледующий(СтуктураПоиска) Тогда
					ОбластьРазделительНедель.Параметры.Адрес = ВыборкаПоСотруднику.СпецификацияАдресМонтажа;						
				Иначе
					ОбластьРазделительНедель.Параметры.Адрес = Неопределено;
					ОбластьРазделительНедель.Параметры.Расшифровка = Новый Структура("ДатаМонтажа, Монтажник, Воскресенье", СледДень, Выборка.Ссылка, Истина);							
				КонецЕсли;
				ТабДокТаблица.Вывести(ОбластьРазделительНедель);
			Иначе				
				ОбластьСтрока.Параметры.Дата = СледДень;
				Если ВыборкаПоСотруднику.НайтиСледующий(СтуктураПоиска) Тогда
					ОбластьСтрока.Параметры.Адрес = ВыборкаПоСотруднику.СпецификацияАдресМонтажа;						
				Иначе
					ОбластьСтрока.Параметры.Адрес = Неопределено;
					ОбластьСтрока.Параметры.Расшифровка = Новый Структура("ДатаМонтажа, Монтажник, Воскресенье", СледДень, Выборка.Ссылка, Ложь);							
				КонецЕсли;
				ТабДокТаблица.Вывести(ОбластьСтрока);
			КонецЕсли;				
						
		КонецЦикла;
		
		ТабДок.Присоединить(ТабДокТаблица);
		ТабДокТаблица.Очистить();
		
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции // Сформировать()

&НаКлиенте
Процедура ТабДокОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;	
	Если ТипЗнч(Расшифровка) = Тип("Структура") Тогда
		Если Расшифровка.Воскресенье Тогда
			ТекстВопроса = "Действительно хотите поставить монтаж на воскресенье?";
			Если Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
				Закрыть(Расшифровка);				
			КонецЕсли
		Иначе
			Закрыть(Расшифровка);
		КонецЕсли		
	Иначе
		Сообщить("Монтажник занят в этот день");
	КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьТабличныйДокумент()
	
	ТабДок = Сформировать(НачалоПериода, КонецПериода, Город);
	ТабДок.ФиксацияСверху = 1;
	
КонецПроцедуры

&НаКлиенте
Процедура НачалоПериодаПриИзменении(Элемент)
	
	СформироватьТабличныйДокумент();
	
КонецПроцедуры

&НаКлиенте
Процедура КонецПериодаПриИзменении(Элемент)
	
	СформироватьТабличныйДокумент();
	
КонецПроцедуры

&НаКлиенте
Процедура ГородПриИзменении(Элемент)
	
	СформироватьТабличныйДокумент();
	
КонецПроцедуры


