
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НачалоПериода = Параметры.НачалоПериода;
	КонецПериода = Параметры.КонецПериода;
	Город = Параметры.Офис.Город;
	
	ТабДок.Вывести(Сформировать(НачалоПериода, КонецПериода, Город));
	ТабДок.ФиксацияСверху = 1;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция Сформировать(фНачалоПериода, фКонецПериода, фГород)
	
	фТабДок = Новый ТабличныйДокумент;
	Макет = Документы.Договор.ПолучитьМакет("ВыборДняМонтажа");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	ОбластьСтрокаКрасная = Макет.ПолучитьОбласть("СтрокаКрасная");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", фНачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", фКонецПериода);
	Запрос.УстановитьПараметр("Город", фГород);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СвободныеМонтажникиОбороты.Период КАК Дата,
	|	ISNULL(СвободныеМонтажникиОбороты.КоличествоМонтажниковОборот, 0) КАК КоличествоМонтажников,
	|	ISNULL(СвободныеМонтажникиОбороты.КоличествоМонтажейОборот, 0) КАК КоличествоМонтажей
	|ИЗ
	|	РегистрНакопления.СвободныеМонтажники.Обороты(&НачалоПериода, &КонецПериода, День, Город = &Город) КАК СвободныеМонтажникиОбороты
	|
	|УПОРЯДОЧИТЬ ПО
	|	СвободныеМонтажникиОбороты.Период
	|ИТОГИ
	|	СУММА(КоличествоМонтажников),
	|	СУММА(КоличествоМонтажей)
	|ПО
	|	Дата ПЕРИОДАМИ(ДЕНЬ, &НачалоПериода, &КонецПериода)";
	
	фТабДок.Вывести(ОбластьШапка);
	
	Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Дата", "Все");
	Пока Выборка.Следующий() Цикл
		Дата = Выборка.Дата;
		Если ДеньНедели(Дата) = 7 Тогда
			ВыводимаяСтрока = ОбластьСтрокаКрасная;
		Иначе
			ВыводимаяСтрока = ОбластьСтрока;
		КонецЕсли; 
		ВыводимаяСтрока.Параметры.Заполнить(Выборка);
		ВыводимаяСтрока.Параметры.ДеньНедели = Формат(Дата, "ДФ=дддд");
		фТабДок.Вывести(ВыводимаяСтрока);
	КонецЦикла;

	Возврат фТабДок;

КонецФункции // Сформировать()

&НаКлиенте
Процедура ТабДокОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Закрыть(Расшифровка);
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьТабличныйДокумент()
	
	ТабДок = Сформировать(НачалоПериода, КонецПериода, Город);
	ТабДок.ФиксацияСверху = 1;
	
КонецПроцедуры

&НаКлиенте
Процедура НачалоПериодаПриИзменении(Элемент)
	
	СформироватьТабличныйДокумент();
	
КонецПроцедуры

&НаКлиенте
Процедура КонецПериодаПриИзменении(Элемент)
	
	СформироватьТабличныйДокумент();
	
КонецПроцедуры

&НаКлиенте
Процедура ГородПриИзменении(Элемент)
	
	СформироватьТабличныйДокумент();
	
КонецПроцедуры


