&НаКлиенте
Перем РабочийКаталог;

/// ОБРАБОТЧИКИ СОБЫТИЙ /////////////////////////////////////////////////////////////////////

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Модифицированность = Ложь;
	Производство = Параметры.Производство;
	
	ВыборНоменклатуры.Параметры.УстановитьЗначениеПараметра("ТипНоменклатуры", Справочники.Номенклатура.ПустаяСсылка());
	ВыборНоменклатуры.Параметры.УстановитьЗначениеПараметра("Производство", Производство);
	
	Для каждого Элем Из Параметры.Комплектация Цикл
		
		НоваяСтрока = Выбранные.Добавить();
		НоваяСтрока.Номенклатура = Элем.Номенклатура;
		НоваяСтрока.Количество = Элем.Количество;
		НоваяСтрока.Кратность = Элем.Номенклатура.Кратность;
		Если Элем.НомерИзделия <> 0 Тогда
			НоваяСтрока.НомерИзделия = Элем.НомерИзделия;
		КонецЕсли;
		
		НоваяСтрока.ЕдиницаИзмерения = Элем.ЕдиницаИзмерения;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	РабочийКаталог = ФайловыеФункцииСлужебныйКлиент.ВыбратьПутьККаталогуДанныхПользователя();
	ВыбранныеОбновитьСтроки();
	
КонецПроцедуры

&НаКлиенте
Процедура КаталогНоменклатурыПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.КаталогНоменклатуры.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		ТекстЗапроса="ВЫБРАТЬ
		|	НоменклатураПодразделений.Номенклатура КАК Номенклатура,
		|	НоменклатураПодразделений.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	НоменклатураПодразделений.Номенклатура.Кратность КАК Кратность
		|ИЗ
		|	РегистрСведений.НоменклатураПодразделений КАК НоменклатураПодразделений
		|ГДЕ
		|	НоменклатураПодразделений.Номенклатура.Родитель = &ТипНоменклатуры
		|	И НоменклатураПодразделений.Подразделение = &Производство
		|	И НЕ НоменклатураПодразделений.Номенклатура.ЭтоГруппа
		|	И НоменклатураПодразделений.Доступность
		|	И НоменклатураПодразделений.Номенклатура.Базовый
		|	И НЕ НоменклатураПодразделений.Номенклатура.МатериалЗаказчика";
		
		// { Васильев Александр Леонидович [09.09.2014]
		// Нельзя так. Делай без серверного вызова.
		ВыборНоменклатурыУстановитьТекстЗапроса(ТекстЗапроса);
		// } Васильев Александр Леонидович [09.09.2014]
		
		ГруппаНоменклатуры = ТекущиеДанные.Ссылка;
		ВыборНоменклатуры.Параметры.УстановитьЗначениеПараметра("ТипНоменклатуры", ГруппаНоменклатуры);
		ВыборНоменклатуры.Параметры.УстановитьЗначениеПараметра("Производство", Производство);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборНоменклатурыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка=Ложь;
	Если ВводКоличества Тогда
		ПоказатьФормуВводаКоличества(Элемент.ТекущиеДанные);
	Иначе
		Данные = ЛексСервер.ЗначенияРеквизитовОбъекта(Элемент.ТекущиеДанные.Номенклатура, "Номенклатура,ЕдиницаИзмерения,Кратность");
		ВыбранныеДобавитьЭлемент(1, Данные.Номенклатура, Данные.ЕдиницаИзмерения, Данные.Кратность);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КаталогНоменклатурыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранныеПослеУдаления(Элемент)
	
	ВыбранныеОбновитьСтроки();
	
КонецПроцедуры

&НаКлиенте
Процедура СтрокаПоискаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	ПолучитьДанныеПоиска(Текст);
	Элементы.ВыборНоменклатуры.Обновить();
	СтрокаПоиска = Текст;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Выбранные.Количество() > 0 И Модифицированность Тогда
		
		КодДиалога = Вопрос("Выбранные элементы не перенесены в документ. Перенести?", РежимДиалогаВопрос.ДаНетОтмена, 0);
		
		Если КодДиалога = КодВозвратаДиалога.Отмена Тогда
			Отказ = Истина;
		КонецЕсли;
		
		Если КодДиалога = КодВозвратаДиалога.Да Тогда
			Модифицированность = Ложь;
			ОповеститьОВыборе(ПолучитьАдресХранилища());
			Выбранные.Очистить();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборНоменклатурыПриАктивизацииСтроки(Элемент)
	
	Элементы.ВыборНоменклатуры.ТекущаяСтрока=Неопределено;
	ПоказатьКартинку(Элементы.ВыборНоменклатуры.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранныеПриАктивизацииСтроки(Элемент)
	
	Элементы.Выбранные.ТекущаяСтрока=Неопределено;
	ПоказатьКартинку(Элементы.Выбранные.ТекущиеДанные);
	
КонецПроцедуры

/// ДОПОЛНИТЕЛЬНЫЕ ФУНКЦИИ /////////////////////////////////////////////////////////////////////
&НаСервере
Функция ПолучитьАдресХранилища()
	
	АдресТаблицы = ПоместитьВоВременноеХранилище(Выбранные.Выгрузить());
	СтруктураОповещения = Новый Структура;
	СтруктураОповещения.Вставить("АдресТаблицы", АдресТаблицы);
	СтруктураОповещения.Вставить("Таблица", "ПодборКомплектации");
	
	Возврат СтруктураОповещения;
	
КонецФункции

&НаКлиенте
Процедура ПоказатьФормуВводаКоличества(Ссылка)
	
	Пар = Новый Структура("ВыбранныйЭлементСсылка",Ссылка);
	ОткрытьФорму("Документ.Спецификация.Форма.ФормаВводаКоличества", Пар, ЭтаФорма, Неопределено, Неопределено, Неопределено, Неопределено, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранныеОбновитьСтроки()
	
	НомСтроки = 1;
	Для каждого Ном Из Выбранные Цикл
		
		Ном.Номер = НомСтроки;
		НомСтроки = НомСтроки+1;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранныеДобавитьЭлемент(Кол, Элем, ЕдиницаИзмерения, Кратность) Экспорт
	
	Модифицированность=Истина;
	ФлагСуществования=0;
	Для каждого Ном Из Выбранные Цикл
		
		Если Ном.Номенклатура=Элем И НЕ ЗначениеЗаполнено(Ном.НомерИзделия) Тогда
			
			Ном.Количество=Ном.Количество+Кол;
			ФлагСуществования=1;
			Прервать;
			
		КонецЕсли; 
		
	КонецЦикла;
	
	Если ФлагСуществования = 0 Тогда
		НоваяСтрока = Выбранные.Вставить(0);
		НоваяСтрока.Номенклатура = Элем;
		НоваяСтрока.Количество = Кол;
		НоваяСтрока.ЕдиницаИзмерения = ЕдиницаИзмерения;
		НоваяСтрока.Кратность = Кратность;
		ВыбранныеОбновитьСтроки();
		Элементы.Выбранные.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьДанныеПоиска(Текст)
	
	МассивСтрок = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Текст," ",Истина);
	
	ТекстЗапроса="ВЫБРАТЬ
	|	НоменклатураПодразделений.Номенклатура КАК Номенклатура,
	|	НоменклатураПодразделений.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	НоменклатураПодразделений.Номенклатура.Кратность КАК Кратность
	|ИЗ
	|	РегистрСведений.НоменклатураПодразделений КАК НоменклатураПодразделений
	|ГДЕ
	|	НоменклатураПодразделений.Подразделение = &Производство
	|	И НЕ НоменклатураПодразделений.Номенклатура.ЭтоГруппа
	|	И НоменклатураПодразделений.Доступность
	|	И НоменклатураПодразделений.Номенклатура.Базовый
	|	И НЕ НоменклатураПодразделений.Номенклатура.МатериалЗаказчика";
	
	Инд = 0;
	Для каждого Стр Из МассивСтрок Цикл
		
		ТекстЗапроса = ТекстЗапроса+" И НоменклатураПодразделений.Номенклатура.Наименование ПОДОБНО &Пар"+Инд;
		Инд = Инд+1;
		
	КонецЦикла;
	
	ВыборНоменклатурыУстановитьТекстЗапроса(ТекстЗапроса);
	
	Инд2 = 0;
	Пока Инд2 < Инд Цикл
		
		ВыборНоменклатуры.Параметры.УстановитьЗначениеПараметра("Пар" + Инд2, "%"+МассивСтрок[Инд2]+"%");
		Инд2 = Инд2+1;
		
	КонецЦикла;
	
	ВыборНоменклатуры.Параметры.УстановитьЗначениеПараметра("Производство", Производство);
	
КонецПроцедуры

&НаСервере
Процедура ВыборНоменклатурыУстановитьТекстЗапроса(Текст)
	
	ВыборНоменклатуры.ТекстЗапроса=Текст;
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВДокумент(Команда)
	
	Модифицированность=Ложь;
	ОповеститьОВыборе(ПолучитьАдресХранилища());
	Выбранные.Очистить();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьКодНоменклатуры(Ссылка)
	
	Возврат Ссылка.Код;
	
КонецФункции

&НаКлиенте
Процедура ПоказатьКартинку(Элемент)
	
	Если Элемент = Неопределено Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Попытка
		
		Код=ПолучитьКодНоменклатуры(Элемент.Номенклатура);
		Код = СокрЛП(Код);
		ПутьКИзображению = РабочийКаталог + Код + ".jpg";
		ФайлНаДиске = Новый Файл(ПутьКИзображению);
		
		Если ФайлНаДиске.Существует() Тогда
			Изображение = "<html><head><META HTTP-EQUIV=""Pragma"" CONTENT=""no-cache""></head><body bottommargin=""0"" topmargin=""0"" leftmargin=""0"" rightmargin=""0""><img src="""+ПутьКИзображению+""" height=100% /></body></html>";
		Иначе
			Изображение = "";
		КонецЕсли;
		
	Исключение;
		
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранныеКоличествоПриИзменении(Элемент)
	
	Кратн=Элементы.Выбранные.ТекущиеДанные.Кратность;
	
	Если Кратн <> 0 Тогда
		Кол=Элементы.Выбранные.ТекущиеДанные.Количество;
		Кол = Кратн*Цел(Кол/Кратн);
		Элементы.Выбранные.ТекущиеДанные.Количество=Кол;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранныеПриИзменении(Элемент)
	
	Модифицированность=Истина;
	
КонецПроцедуры
