
&НаКлиенте
Процедура ИзделиеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущийЭлементИзделия = ПредопределенноеЗначение("Перечисление.ВидыИзделийПоКаталогу." + ЭтаФорма.ТекущийЭлемент.Имя);	
	НашаСтрока = Детали.НайтиСтроки(Новый Структура("Элемент", ТекущийЭлементИзделия));
	ЕстьСтрока = НашаСтрока.Количество() = 1;
	
	Если ЕстьСтрока Тогда
		Элементы.Детали.ТекущаяСтрока = Детали.Индекс(НашаСтрока[0]);
		ТекущиеДанные = Элементы.Детали.ТекущиеДанные;
		
		Если ТекущиеДанные <> Неопределено Тогда
			Если НЕ ЗначениеЗаполнено(ТекущиеДанные.НоменклатураЛДСП) И ЗначениеЗаполнено(ШапкаОсновныхНастроек.НоменклатураЛДСП) Тогда			
				ТекущиеДанные.НоменклатураЛДСП = ШапкаОсновныхНастроек.НоменклатураЛДСП;		
			ИначеЕсли ЗначениеЗаполнено(ТекущиеДанные.НоменклатураЛДСП) Тогда
				ШапкаОсновныхНастроек.Вставить("НоменклатураЛДСП", ТекущиеДанные.НоменклатураЛДСП);
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(ТекущиеДанные.КромкаЛДСП) И ЗначениеЗаполнено(ШапкаОсновныхНастроек.КромкаЛДСП) Тогда			
				ТекущиеДанные.КромкаЛДСП = ШапкаОсновныхНастроек.КромкаЛДСП;		
			ИначеЕсли ЗначениеЗаполнено(ТекущиеДанные.КромкаЛДСП) Тогда
				ШапкаОсновныхНастроек.Вставить("КромкаЛДСП", ТекущиеДанные.КромкаЛДСП);
			КонецЕсли;
		КонецЕсли;	
		
		УстановитьОграничения(НашаСтрока[0]);
	Иначе
		Элементы.Детали.ТекущаяСтрока = Неопределено;
	КонецЕсли;
	
	УстановитьДоступность(ЕстьСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборИзделия(Команда)
	
	ЭлементИзделия = ЭтаФорма.ТекущийЭлемент.Имя;
	ТекущийЭлементИзделия = ПредопределенноеЗначение("Перечисление.ВидыИзделийПоКаталогу." + ЭтаФорма.ТекущийЭлемент.Имя);
	
	Изделие = ОткрытьФормуМодально("Справочник.КаталогИзделий.ФормаВыбора", Новый Структура("ВидИзделия", ТекущийЭлементИзделия), ЭтаФорма);
	
	Если ЗначениеЗаполнено(Изделие) Тогда	
		СтруктураИзделия = ПолучитьСтруктуруИзделия(Изделие, РабочийКаталог);
		
		НашаСтрока = Детали.НайтиСтроки(Новый Структура("Элемент", ТекущийЭлементИзделия));
		ЕстьСтрока = НашаСтрока.Количество() = 1;
		
		Если ЕстьСтрока Тогда
			НоваяСтрока = НашаСтрока[0];
		Иначе
			НоваяСтрока = Детали.Добавить();
		КонецЕсли;
		
		НоваяСтрока.Изделие = Изделие;
		НоваяСтрока.ВысотаИзделия = СтруктураИзделия.ВысотаИзделия;
		НоваяСтрока.ШиринаИзделия = СтруктураИзделия.ШиринаИзделия;
		НоваяСтрока.ГлубинаИзделия = СтруктураИзделия.ГлубинаИзделия;
		НоваяСтрока.ВысотаИзделияМин = СтруктураИзделия.ВысотаИзделияМин;
		НоваяСтрока.ШиринаИзделияМин = СтруктураИзделия.ШиринаИзделияМин;
		НоваяСтрока.ГлубинаИзделияМин = СтруктураИзделия.ГлубинаИзделияМин;
		НоваяСтрока.ВысотаИзделияМакс = СтруктураИзделия.ВысотаИзделияМакс;
		НоваяСтрока.ШиринаИзделияМакс = СтруктураИзделия.ШиринаИзделияМакс;
		НоваяСтрока.ГлубинаИзделияМакс = СтруктураИзделия.ГлубинаИзделияМакс;
		НоваяСтрока.Элемент = ТекущийЭлементИзделия;
		
		Отказ = ПроверкаДвери(НоваяСтрока);
		
		Если НЕ Отказ Тогда
			УстановитьОграничения(НоваяСтрока);
			УстановитьДоступность(Истина);	
			ОтобразитьКартинку(СтруктураИзделия.АдресЭлемента, ЭлементИзделия);	
		КонецЕсли;
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОграничения(СтрокаИзделия)
	
	//Элементы.ГлубинаИзделия.Подсказка = Строка(СтрокаИзделия.ГлубинаИзделияМин) + " - " + Строка(СтрокаИзделия.ГлубинаИзделияМакс);
	//Элементы.ГлубинаИзделия.МаксимальноеЗначение = СтрокаИзделия.ГлубинаИзделияМакс;
	//Элементы.ГлубинаИзделия.МинимальноеЗначение = СтрокаИзделия.ГлубинаИзделияМин;
	//Элементы.ШиринаИзделия.Подсказка = Строка(СтрокаИзделия.ШиринаИзделияМин) + " - " + Строка(СтрокаИзделия.ШиринаИзделияМакс);
	//Элементы.ШиринаИзделия.МаксимальноеЗначение = СтрокаИзделия.ШиринаИзделияМакс;
	//Элементы.ШиринаИзделия.МинимальноеЗначение = СтрокаИзделия.ШиринаИзделияМин;
	//Элементы.ВысотаИзделия.Подсказка = Строка(СтрокаИзделия.ВысотаИзделияМин) + " - " + Строка(СтрокаИзделия.ВысотаИзделияМакс);
	//Элементы.ВысотаИзделия.МаксимальноеЗначение = СтрокаИзделия.ВысотаИзделияМакс;
	//Элементы.ВысотаИзделия.МинимальноеЗначение = СтрокаИзделия.ВысотаИзделияМин;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступность(Флаг)
	
	Элементы.ДеталиНоменклатураЛДСП.Доступность = Флаг;
	Элементы.ДеталиКромкаЛДСП.Доступность = Флаг;
	
КонецПроцедуры

&НаКлиенте
Процедура Очистить(Команда)
	
	ЭлементИзделия = ЭтаФорма.ТекущийЭлемент.Имя;	
	ТекущийЭлементИзделия = ПредопределенноеЗначение("Перечисление.ВидыИзделийПоКаталогу." + ЭлементИзделия);
 	НашаСтрока = Детали.НайтиСтроки(Новый Структура("Элемент", ТекущийЭлементИзделия));
	ЕстьСтрока = НашаСтрока.Количество() = 1;
	
	Если ЕстьСтрока Тогда
		Детали.Удалить(НашаСтрока[0]);
		ЭтаФорма[ЭлементИзделия] = "";
		Если ТекущийЭлементИзделия = ПредопределенноеЗначение("Перечисление.ВидыИзделийПоКаталогу.ОсновнойЭлемент")
			И ЗначениеЗаполнено(Двери) Тогда
			Двери = Неопределено;
			ВысотаПроемаДвери = 0;
			ШиринаПроемаДвери = 0;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Поле дверей очищено", , "Двери");	
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСтруктуруИзделия(Изделие, Каталог)
	
	Структура = Новый Структура;
	Структура.Вставить("АдресЭлемента", Каталог + "Изделие" + Изделие.Код);
	Структура.Вставить("ВысотаИзделия", Изделие.ВысотаИзделия);
	Структура.Вставить("ШиринаИзделия", Изделие.ШиринаИзделия);
	Структура.Вставить("ГлубинаИзделия", Изделие.ГлубинаИзделия);
	Структура.Вставить("ВысотаИзделияМин", Изделие.ВысотаИзделияМин);
	Структура.Вставить("ШиринаИзделияМин", Изделие.ШиринаИзделияМин);
	Структура.Вставить("ГлубинаИзделияМин", Изделие.ГлубинаИзделияМин);
	Структура.Вставить("ВысотаИзделияМакс", Изделие.ВысотаИзделияМакс);
	Структура.Вставить("ШиринаИзделияМакс", Изделие.ШиринаИзделияМакс);
	Структура.Вставить("ГлубинаИзделияМакс", Изделие.ГлубинаИзделияМакс);
	
	Возврат Структура;
	
КонецФункции

&НаКлиенте
Процедура ОтобразитьКартинку(АдресЭлемента, ЭлементИзделия)
		
	АдресВХранилище = "";
	Если ЗначениеЗаполнено(АдресЭлемента) Тогда
		ИмяФайла = АдресЭлемента;
		ФайлИзображения = Новый Файл(ИмяФайла);
		Если ФайлИзображения.Существует() Тогда
			ПоместитьФайл(АдресВХранилище, ИмяФайла, , Ложь, ЭтаФорма.УникальныйИдентификатор);
		КонецЕсли;
	КонецЕсли;
	
	ЭтаФорма[ЭлементИзделия] = АдресВХранилище;
	
КонецПроцедуры	

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	РабочийКаталог = ФайловыеФункцииСлужебныйКлиент.ВыбратьПутьККаталогуДанныхПользователя();
		
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Подразделение = Параметры.Подразделение;
	
	#Область Заполнение_номенклатурных_групп	
	СписокНоменклатурныхГрупп = Новый СписокЗначений;
	
	СписокНоменклатурныхГрупп.Добавить(Справочники.НоменклатурныеГруппы.ЛДСП16);
	СписокНоменклатурныхГрупп.Добавить(Справочники.НоменклатурныеГруппы.КантТ);
	СписокНоменклатурныхГрупп.Добавить(Справочники.НоменклатурныеГруппы.Кромка045_19);
	СписокНоменклатурныхГрупп.Добавить(Справочники.НоменклатурныеГруппы.Кромка2_19);
	СписокНоменклатурныхГрупп.Добавить(Справочники.НоменклатурныеГруппы.Кромка2_35);
	СписокНоменклатурныхГрупп.Добавить(Справочники.НоменклатурныеГруппы.ДВП);
	
	МассивыНоменклатурныхГрупп = ЛексСервер.ОтборНоменклатурныхГрупп(СписокНоменклатурныхГрупп, Подразделение);
	
	ДобавитьДопГруппы();
	
	Элементы.ДеталиНоменклатураЛДСП.СписокВыбора.ЗагрузитьЗначения(МассивыНоменклатурныхГрупп.ЛДСП16);
	Элементы.ДеталиКромкаЛДСП.СписокВыбора.ЗагрузитьЗначения(МассивыНоменклатурныхГрупп.Кромка);
	Элементы.ЗадняяСтенка.СписокВыбора.ЗагрузитьЗначения(ПолучитьСписокСтенок());	
	#КонецОбласти
	
	#Область ШапкаОсновныхНастроек
	ШапкаОсновныхНастроек = Новый Структура;
	
	ШапкаОсновныхНастроек.Вставить("НоменклатураЛДСП", "");
	ШапкаОсновныхНастроек.Вставить("КромкаЛДСП", "");
	#КонецОбласти
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьДопГруппы() 
	
	Кромка2 = Новый Массив;
	Для каждого Элемент Из МассивыНоменклатурныхГрупп.Кромка2_19 Цикл
		Кромка2.Добавить(Элемент);
	КонецЦикла;
	Для каждого Элемент Из МассивыНоменклатурныхГрупп.Кромка2_35 Цикл
		Кромка2.Добавить(Элемент);
	КонецЦикла;
	МассивыНоменклатурныхГрупп.Вставить("Кромка2мм", Кромка2);
	
	Кромка 	= Новый Массив;
	Для каждого Элемент Из МассивыНоменклатурныхГрупп.Кромка045_19 Цикл
		Кромка.Добавить(Элемент);
	КонецЦикла;
	Для каждого Элемент Из МассивыНоменклатурныхГрупп.Кромка2мм Цикл
		Кромка.Добавить(Элемент);
	КонецЦикла;
	Для каждого Элемент Из МассивыНоменклатурныхГрупп.КантТ Цикл
		Кромка.Добавить(Элемент);
	КонецЦикла;
	МассивыНоменклатурныхГрупп.Вставить("Кромка", Кромка);
		
КонецПроцедуры

&НаСервере
Функция ПолучитьСписокСтенок() 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КаталогИзделий.Ссылка
	|ИЗ
	|	Справочник.КаталогИзделий КАК КаталогИзделий
	|ГДЕ
	|	КаталогИзделий.ВидИзделия = ЗНАЧЕНИЕ(Перечисление.ВидыИзделийПоКаталогу.ЗадняяСтенка)";
		
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
			
КонецФункции

&НаКлиенте
Процедура ЗадняяСтенкаПриИзменении(Элемент)
	
	ТекущийЭлементИзделия = ПредопределенноеЗначение("Перечисление.ВидыИзделийПоКаталогу.ЗадняяСтенка");
	
	УстановитьДоступность(Ложь);
	
	Если ЗначениеЗаполнено(ЗадняяСтенка) Тогда
		
		МатериалСтенки = ПолучитьМатериалСтенки(ЗадняяСтенка);
		
		Если МатериалСтенки = "16 ЛДСП" Тогда
			Элементы.НоменклатураСтенка.СписокВыбора.ЗагрузитьЗначения(МассивыНоменклатурныхГрупп.ЛДСП16);
		ИначеЕсли МатериалСтенки = "ДВП" Тогда
			Элементы.НоменклатураСтенка.СписокВыбора.ЗагрузитьЗначения(МассивыНоменклатурныхГрупп.ДВП);
		Иначе
			Элементы.НоменклатураСтенка.СписокВыбора.Очистить();
		КонецЕсли;
		
		Стенка = ПредопределенноеЗначение("Перечисление.ВидыИзделийПоКаталогу.ЗадняяСтенка"); 
		НашаСтрока = Детали.НайтиСтроки(Новый Структура("Элемент", Стенка));
		ЕстьСтрока = НашаСтрока.Количество() = 1;
		
		Если ЕстьСтрока Тогда
			НоваяСтрока = НашаСтрока[0];
		Иначе
			НоваяСтрока = Детали.Добавить();
		КонецЕсли;
		
		НоваяСтрока.Изделие = ЗадняяСтенка;
		НоваяСтрока.Элемент = Стенка;
	Иначе
		НоменклатураСтенка = Неопределено;
		Элементы.НоменклатураСтенка.СписокВыбора.Очистить();
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ЗадняяСтенкаОчистка(Элемент, СтандартнаяОбработка)

 	НашаСтрока = Детали.НайтиСтроки(Новый Структура("Элемент", ПредопределенноеЗначение("Перечисление.ВидыИзделийПоКаталогу.ЗадняяСтенка")));
	ЕстьСтрока = НашаСтрока.Количество() = 1;
	
	Если ЕстьСтрока Тогда
		Детали.Удалить(НашаСтрока[0]);
	КонецЕсли;	
	 	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьМатериалСтенки(ЗадняяСтенка)
	
	//Изделие по каталогу Задняя стенка должна иметь 1 деталь - саму стенку, определяем ее материал
	Если ЗначениеЗаполнено(ЗадняяСтенка) Тогда
		Если ЗадняяСтенка.СписокМатериалы.Количество() > 0 Тогда
			Возврат ЗадняяСтенка.СписокМатериалы[0].Материал;
		КонецЕсли;
	КонецЕсли;
КонецФункции

&НаКлиенте
Процедура ДвериНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СписокКнопок = Новый СписокЗначений;	
	СписокКнопок.Добавить(КодВозвратаДиалога.Да, "Создать");
	СписокКнопок.Добавить(КодВозвратаДиалога.Нет, "Выбрать");
	СписокКнопок.Добавить(КодВозвратаДиалога.Отмена, "Отмена");
	
	Ответ = Вопрос("Создать новую или выбрать существующую дверь?",СписокКнопок,,);
	
	РазмерыОсновногоЭлемента = ПолучитьРазмерыОсновногоЭлемента();
	Если РазмерыОсновногоЭлемента <> Неопределено Тогда
		Если Ответ = КодВозвратаДиалога.Да Тогда
			
			ПараметрыФормы = Новый Структура("Подразделение", Подразделение);
			ПараметрыФормы.Вставить("ВысотаПроема", РазмерыОсновногоЭлемента.Высота);
			ПараметрыФормы.Вставить("ШиринаПроема", РазмерыОсновногоЭлемента.Ширина);
			
			Форма = ПолучитьФорму("Справочник.Двери.Форма.ФормаЭлемента", ПараметрыФормы, ЭтаФорма);
			Форма.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
			Форма.Открыть();	
			
		ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
			
			ПараметрыФормы = Новый Структура("ШиринаПроема", РазмерыОсновногоЭлемента.Ширина);
			ПараметрыФормы.Вставить("ВысотаПроема", РазмерыОсновногоЭлемента.Высота);
			Форма = ПолучитьФорму("Справочник.Двери.Форма.ФормаВыбора", ПараметрыФормы, ЭтаФорма);
			Форма.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
			Форма.Открыть();
			
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция ПолучитьРазмерыОсновногоЭлемента()
	
	НашОсновнойЭлемент = Детали.НайтиСтроки(Новый Структура("Элемент", ПредопределенноеЗначение("Перечисление.ВидыИзделийПоКаталогу.ОсновнойЭлемент")));
	ЕстьОсновной = НашОсновнойЭлемент.Количество() > 0;
	Если ЕстьОсновной Тогда
		Размеры = Новый Структура;
		Размеры.Вставить("Глубина", ГлубинаИтог);
		
		ШиринаОсновногоЭлемента = ШиринаИтог;
		ВысотаОсновногоЭлемента = ВысотаИтог;
		
		Для Каждого Строка Из Детали Цикл
			Если Строка.Элемент = ПредопределенноеЗначение("Перечисление.ВидыИзделийПоКаталогу.Крыша") Тогда
				ВысотаОсновногоЭлемента = ВысотаОсновногоЭлемента - Строка.ВысотаИзделия;	
			ИначеЕсли Строка.Элемент = ПредопределенноеЗначение("Перечисление.ВидыИзделийПоКаталогу.Пол") Тогда
				ВысотаОсновногоЭлемента = ВысотаОсновногоЭлемента - Строка.ВысотаИзделия; 	
			ИначеЕсли Строка.Элемент = ПредопределенноеЗначение("Перечисление.ВидыИзделийПоКаталогу.ЛевыйБоковойЭлемент") Тогда
				ШиринаОсновногоЭлемента = ШиринаОсновногоЭлемента - Строка.ШиринаИзделия;	
			ИначеЕсли Строка.Элемент = ПредопределенноеЗначение("Перечисление.ВидыИзделийПоКаталогу.ПравыйБоковойЭлемент") Тогда
				ШиринаОсновногоЭлемента = ШиринаОсновногоЭлемента - Строка.ШиринаИзделия;	
			КонецЕсли;	
		КонецЦикла;
		
		Размеры.Вставить("Ширина", ШиринаОсновногоЭлемента);
		Размеры.Вставить("Высота", ВысотаОсновногоЭлемента);
		
		Возврат Размеры;
	Иначе
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Выберите основной элемент", , "ОсновнойЭлемент");
		
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ПолучитьИтоговыеРазмеры(ВысотаОсновного, ШиринаОсновного)
		
	ШиринаИтог = ШиринаОсновного;
	ВысотаИтог = ВысотаОсновного;
	
	Для Каждого Строка Из Детали Цикл
		Если Строка.Элемент = ПредопределенноеЗначение("Перечисление.ВидыИзделийПоКаталогу.Крыша") Тогда
		ВысотаИтог = ВысотаИтог + Строка.ВысотаИзделия;	
		ИначеЕсли Строка.Элемент = ПредопределенноеЗначение("Перечисление.ВидыИзделийПоКаталогу.Пол") Тогда
		ВысотаИтог = ВысотаИтог + Строка.ВысотаИзделия; 	
		ИначеЕсли Строка.Элемент = ПредопределенноеЗначение("Перечисление.ВидыИзделийПоКаталогу.ЛевыйБоковойЭлемент") Тогда
		ШиринаИтог = ШиринаИтог + Строка.ШиринаИзделия;	
		ИначеЕсли Строка.Элемент = ПредопределенноеЗначение("Перечисление.ВидыИзделийПоКаталогу.ПравыйБоковойЭлемент") Тогда
		ШиринаИтог = ШиринаИтог + Строка.ШиринаИзделия;	
		КонецЕсли;	
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.Двери") И ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		
		ДвериПриИзмененииНаКлиенте(ВыбранноеЗначение);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста                                
Функция ПолучитьРазмерыДвери(Дверь)
	
	Структура = Новый Структура;                          
	Структура.Вставить("ВысотаПроема", Дверь.ВысотаПроема);
	Структура.Вставить("ШиринаПроема", Дверь.ШиринаПроема);
	
	Возврат Структура;
	
КонецФункции

&НаКлиенте
Процедура ПеренестиВДокумент(Команда)
	
	РазмерыОсновногоЭлемента = ПолучитьРазмерыОсновногоЭлемента();
	Если РазмерыОсновногоЭлемента <> Неопределено Тогда 
		Модифицированность = Ложь;
		ТаблицаДетали = ПроверитьИУстановитьРазмерыПередСохранением(РазмерыОсновногоЭлемента);
		Если ТаблицаДетали <> Неопределено Тогда 
			ОповеститьОВыборе(ТаблицаДетали);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ПроверитьИУстановитьРазмерыПередСохранением(РазмерыОсновного)
	
	Отказ = Ложь;
	
	Для Каждого Строка Из Детали Цикл
		Строка.ГлубинаИзделия = ГлубинаИтог;
		Если Строка.Элемент = Перечисления.ВидыИзделийПоКаталогу.Крыша Тогда 
			Строка.ШиринаИзделия = ШиринаИтог;
		ИначеЕсли Строка.Элемент = Перечисления.ВидыИзделийПоКаталогу.Пол Тогда
			Строка.ШиринаИзделия = ШиринаИтог; 	
		ИначеЕсли Строка.Элемент = Перечисления.ВидыИзделийПоКаталогу.ЛевыйБоковойЭлемент Тогда
			Строка.ВысотаИзделия = РазмерыОсновного.Высота;	
		ИначеЕсли Строка.Элемент = Перечисления.ВидыИзделийПоКаталогу.ПравыйБоковойЭлемент Тогда
			Строка.ВысотаИзделия = РазмерыОсновного.Высота;
		ИначеЕсли Строка.Элемент = Перечисления.ВидыИзделийПоКаталогу.ОсновнойЭлемент Тогда
			Строка.ШиринаИзделия = РазмерыОсновного.Ширина;
			Строка.ВысотаИзделия = РазмерыОсновного.Высота;
		ИначеЕсли Строка.Элемент = Перечисления.ВидыИзделийПоКаталогу.ЗадняяСтенка Тогда
			Строка.ШиринаИзделия = ШиринаИтог;
			Строка.ВысотаИзделия = ВысотаИтог;
			Если НоменклатураСтенка.НоменклатурнаяГруппа = Справочники.НоменклатурныеГруппы.ЛДСП16 Тогда
				Строка.НоменклатураЛДСП = НоменклатураСтенка;
			ИначеЕсли НоменклатураСтенка.НоменклатурнаяГруппа = Справочники.НоменклатурныеГруппы.ДВП Тогда
				Строка.НоменклатураДВП = НоменклатураСтенка;
			КонецЕсли;
			НашаСтенка = Строка.Изделие;
			Строка.ШиринаИзделияМакс = НашаСтенка.ШиринаИзделияМакс;
			Строка.ВысотаИзделияМакс = НашаСтенка.ВысотаИзделияМакс;
			Строка.ГлубинаИзделияМакс = НашаСтенка.ГлубинаИзделияМакс;
			Строка.ШиринаИзделияМин = НашаСтенка.ШиринаИзделияМин;
			Строка.ВысотаИзделияМин = НашаСтенка.ВысотаИзделияМин;
			Строка.ГлубинаИзделияМин = НашаСтенка.ГлубинаИзделияМин;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Строка.НоменклатураЛДСП) Тогда
			Элементы.Детали.ТекущаяСтрока = Строка.ПолучитьИдентификатор();
			Если Строка.Элемент = Перечисления.ВидыИзделийПоКаталогу.ЗадняяСтенка Тогда
				Текст = "Не выбрана номенклатура стенки";
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст, , "НоменклатураСтенка");
			Иначе
				Текст = "Не выбрана номенклатура ЛДСП";
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст, , "Элементы.Детали.ТекущиеДанные.НоменклатураЛДСП");
			КонецЕсли;				
			Отказ = Истина;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Строка.КромкаЛДСП)
			И Строка.Элемент <> Перечисления.ВидыИзделийПоКаталогу.ЗадняяСтенка Тогда
			Элементы.Детали.ТекущаяСтрока = Строка.ПолучитьИдентификатор();
			Текст = "Не выбрана кромка";
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст, , "Элементы.Детали.ТекущиеДанные.КромкаЛДСП");
			Отказ = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Если НЕ Отказ Тогда
		Структура = Новый Структура;
		Структура.Вставить("Детали", Детали.Выгрузить());
		Структура.Вставить("Двери", Двери);
		
		Возврат ПоместитьВоВременноеХранилище(Структура);
	КонецЕсли;
КонецФункции

&НаКлиенте
Процедура ДеталиНоменклатураЛДСППриИзменении(Элемент)
	
	ШапкаОсновныхНастроек.Вставить("НоменклатураЛДСП", Элементы.Детали.ТекущиеДанные.НоменклатураЛДСП);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеталиКромкаЛДСППриИзменении(Элемент)
	
	ШапкаОсновныхНастроек.Вставить("КромкаЛДСП", Элементы.Детали.ТекущиеДанные.КромкаЛДСП);	
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураСтенкаПриИзменении(Элемент)
	
	ТекущийЭлементИзделия = ПредопределенноеЗначение("Перечисление.ВидыИзделийПоКаталогу.ЗадняяСтенка");
	УстановитьДоступность(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ВысотаШиринаПриИзменении(Элемент)
	
	ПроверкаДвери();
	
КонецПроцедуры

&НаКлиенте
Функция ПроверкаДвери(СтрокаДетали = Неопределено)
	
	Отказ = Ложь;
	
	Если ЗначениеЗаполнено(Двери) Тогда
		РазмерыОсновногоЭлемента = ПолучитьРазмерыОсновногоЭлемента();
		Если РазмерыОсновногоЭлемента <> Неопределено Тогда
			Если ВысотаПроемаДвери <> РазмерыОсновногоЭлемента.Высота 
				ИЛИ ШиринаПроемаДвери <> РазмерыОсновногоЭлемента.Ширина Тогда
				
				СписокКнопок = Новый СписокЗначений;	
				СписокКнопок.Добавить(КодВозвратаДиалога.Да, "Редактировать");
				СписокКнопок.Добавить(КодВозвратаДиалога.Нет, "Отмена");
				
				Ответ = Вопрос("     Размеры основного элемента изменились," 
				+ Символы.ПС + "редактировать дверь или оставить старые размеры ?",СписокКнопок,,);
				
				Если Ответ = КодВозвратаДиалога.Да Тогда
					
					ПараметрыФормы = Новый Структура("Ключ", Двери);
					ПараметрыФормы.Вставить("ВысотаПроема", РазмерыОсновногоЭлемента.Высота);
					ПараметрыФормы.Вставить("ШиринаПроема", РазмерыОсновногоЭлемента.Ширина);
					ПараметрыФормы.Вставить("Редактирование", Истина);
					Форма = ПолучитьФорму("Справочник.Двери.Форма.ФормаЭлемента", ПараметрыФормы, ЭтаФорма);
					Форма.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
					Форма.Открыть();	
					
				ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
					
					Отказ = Истина;
					Если СтрокаДетали <> Неопределено Тогда
						Детали.Удалить(СтрокаДетали);
					КонецЕсли;	
					ПолучитьИтоговыеРазмеры(ВысотаПроемаДвери, ШиринаПроемаДвери);
					
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Отказ;
	
КонецФункции

&НаКлиенте
Процедура ДвериПриИзменении(Элемент)
	
	ДвериПриИзмененииНаКлиенте(Двери);
	
КонецПроцедуры

&НаКлиенте
Процедура ДвериПриИзмененииНаКлиенте(Значение)
	
	Если ЗначениеЗаполнено(Значение) Тогда
		РазмерыДвери = ПолучитьРазмерыДвери(Значение);
		ВысотаПроемаДвери = РазмерыДвери.ВысотаПроема;
		ШиринаПроемаДвери = РазмерыДвери.ШиринаПроема;
	КонецЕсли;
	
	РазмерыОсновногоЭлемента = ПолучитьРазмерыОсновногоЭлемента();
	Если РазмерыОсновногоЭлемента <> Неопределено Тогда
		Если ВысотаПроемаДвери = РазмерыОсновногоЭлемента.Высота 
			И ШиринаПроемаДвери = РазмерыОсновногоЭлемента.Ширина 
			Тогда
			
			Двери = Значение;
			
		Иначе
			
			Двери = Неопределено;
			ВысотаПроемаДвери = 0;
			ШиринаПроемаДвери = 0;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Двери не подходят по размерам", , "Двери");
			
		КонецЕсли;
	Иначе
		Двери = Неопределено;
	КонецЕсли;
КонецПроцедуры





