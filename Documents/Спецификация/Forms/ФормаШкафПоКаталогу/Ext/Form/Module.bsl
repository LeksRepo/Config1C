////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаСервере
Процедура ДобавитьДопГруппы() 
	
	Кромка2 = Новый Массив;
	Для каждого Элемент Из МассивыНоменклатурныхГрупп.Кромка2_19 Цикл
		Кромка2.Добавить(Элемент);
	КонецЦикла;
	Для каждого Элемент Из МассивыНоменклатурныхГрупп.Кромка2_35 Цикл
		Кромка2.Добавить(Элемент);
	КонецЦикла;
	МассивыНоменклатурныхГрупп.Вставить("Кромка2мм", Кромка2);
	
	Кромка 	= Новый Массив;
	Для каждого Элемент Из МассивыНоменклатурныхГрупп.Кромка045_19 Цикл
		Кромка.Добавить(Элемент);
	КонецЦикла;
	Для каждого Элемент Из МассивыНоменклатурныхГрупп.Кромка2мм Цикл
		Кромка.Добавить(Элемент);
	КонецЦикла;
	Для каждого Элемент Из МассивыНоменклатурныхГрупп.КантТ Цикл
		Кромка.Добавить(Элемент);
	КонецЦикла;
	МассивыНоменклатурныхГрупп.Вставить("Кромка", Кромка);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьСписокСтенок() 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КаталогИзделий.Ссылка
	|ИЗ
	|	Справочник.КаталогИзделий КАК КаталогИзделий
	|ГДЕ
	|	КаталогИзделий.ВидИзделия = ЗНАЧЕНИЕ(Перечисление.ВидыИзделийПоКаталогу.ЗадняяСтенка)";
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

&НаСервере
Процедура ПолучитьИтоговыеРазмеры(ВысотаОсновного, ШиринаОсновного)
	
	ШиринаИтог = ШиринаОсновного;
	ВысотаИтог = ВысотаОсновного;
	
	Для Каждого Строка Из Детали Цикл
		Если (Строка.ВидИзделия = Перечисления.ВидыИзделийПоКаталогу.Крыша 
			ИЛИ Строка.ВидИзделия = Перечисления.ВидыИзделийПоКаталогу.Пол) И НЕ Строка.НеВлияетНаОсновной Тогда
			ВысотаИтог = ВысотаИтог + Строка.ВысотаИзделия;	 	
		ИначеЕсли (Строка.ВидИзделия = Перечисления.ВидыИзделийПоКаталогу.ЛевыйБоковойЭлемент
			ИЛИ Строка.ВидИзделия = Перечисления.ВидыИзделийПоКаталогу.ПравыйБоковойЭлемент) И НЕ Строка.НеВлияетНаОсновной Тогда
			ШиринаИтог = ШиринаИтог + Строка.ШиринаИзделия;	
		КонецЕсли;	
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПроверитьИУстановитьРазмерыПередСохранением(РазмерыОсновного)
	
	Отказ = Ложь;
	
	ЕстьКрыша = Ложь;
	ЕстьКрышаНаВсе = Ложь;
	ЕстьПол = Ложь;
	ЕстьОсновной = Ложь;
	ЕстьЛевыйБоковой = Ложь;
	ЕстьПравыйБоковой = Ложь;
	                                           
	Для Каждого Строка Из Детали Цикл
		
		Строка.ГлубинаИзделия = ГлубинаИтог;
		Строка.Ручка = РучкиЯщиков;
		
		Если Строка.ВидИзделия = Перечисления.ВидыИзделийПоКаталогу.Крыша Тогда 
						
			Если Строка.Изделие.НаВсеИзделие Тогда
				ЕстьКрышаНаВсе = Истина;
				Строка.ШиринаИзделия = ШиринаИтог;
			Иначе
				ЕстьКрыша = Истина;
				Строка.ШиринаИзделия = РазмерыОсновного.Ширина;
			КонецЕсли;
			
		ИначеЕсли Строка.ВидИзделия = Перечисления.ВидыИзделийПоКаталогу.Пол Тогда
			Строка.ШиринаИзделия = РазмерыОсновного.Ширина;
			ЕстьПол = Истина;
		ИначеЕсли Строка.ВидИзделия = Перечисления.ВидыИзделийПоКаталогу.ЛевыйБоковойЭлемент Тогда
			Строка.ВысотаИзделия = РазмерыОсновного.ВысотаБоковой;
			ЕстьЛевыйБоковой = Истина;
		ИначеЕсли Строка.ВидИзделия = Перечисления.ВидыИзделийПоКаталогу.ПравыйБоковойЭлемент Тогда
			Строка.ВысотаИзделия = РазмерыОсновного.ВысотаБоковой;
			ЕстьПравыйБоковой = Истина;
		ИначеЕсли Строка.ВидИзделия = Перечисления.ВидыИзделийПоКаталогу.ОсновнойЭлемент Тогда
			Строка.ШиринаИзделия = РазмерыОсновного.Ширина;
			Строка.ВысотаИзделия = РазмерыОсновного.Высота;
			ЕстьОсновной = Истина;
		ИначеЕсли Строка.ВидИзделия = Перечисления.ВидыИзделийПоКаталогу.ЗадняяСтенка Тогда
			Строка.ШиринаИзделия = РазмерыОсновного.Ширина + 30;
			Строка.ВысотаИзделия = РазмерыОсновного.Высота + 30;
			Если НоменклатураСтенка.НоменклатурнаяГруппа = Справочники.НоменклатурныеГруппы.ЛДСП16 Тогда
				Строка.НоменклатураЛДСП = НоменклатураСтенка;
			ИначеЕсли НоменклатураСтенка.НоменклатурнаяГруппа = Справочники.НоменклатурныеГруппы.ДВП Тогда
				Строка.НоменклатураДВП = НоменклатураСтенка;
			КонецЕсли;
			НашаСтенка = Строка.Изделие;
			Строка.ШиринаИзделияМакс = НашаСтенка.ШиринаИзделияМакс;
			Строка.ВысотаИзделияМакс = НашаСтенка.ВысотаИзделияМакс;
			Строка.ГлубинаИзделияМакс = НашаСтенка.ГлубинаИзделияМакс;
			Строка.ШиринаИзделияМин = НашаСтенка.ШиринаИзделияМин;
			Строка.ВысотаИзделияМин = НашаСтенка.ВысотаИзделияМин;
			Строка.ГлубинаИзделияМин = НашаСтенка.ГлубинаИзделияМин;
			
			Если НЕ ЗначениеЗаполнено(НоменклатураСтенка) Тогда
				Текст = "Не выбрана номенклатура задней стенки";
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст, , "НоменклатураСтенка");				
				Отказ = Истина;
			КонецЕсли;
			
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Строка.НоменклатураЛДСП) И (Строка.ВидИзделия <> Перечисления.ВидыИзделийПоКаталогу.ЗадняяСтенка) Тогда
			Если ЗначениеЗаполнено(ШапкаОсновныхНастроек.НоменклатураЛДСП) Тогда
				Строка.НоменклатураЛДСП = ШапкаОсновныхНастроек.НоменклатураЛДСП;
			Иначе	
				Элементы.Детали.ТекущаяСтрока = Строка.ПолучитьИдентификатор();
				Текст = "Не выбрана номенклатура ЛДСП у " + Строка.ВидИзделия;
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст, , "Элементы.Детали.ТекущиеДанные.НоменклатураЛДСП");				
				Отказ = Истина;
			КонецЕсли;	
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Строка.КромкаЛДСП) И (Строка.ВидИзделия <> Перечисления.ВидыИзделийПоКаталогу.ЗадняяСтенка) Тогда
			Если ЗначениеЗаполнено(ШапкаОсновныхНастроек.КромкаЛДСП) Тогда
				Строка.КромкаЛДСП = ШапкаОсновныхНастроек.КромкаЛДСП;
			Иначе
				Элементы.Детали.ТекущаяСтрока = Строка.ПолучитьИдентификатор();
				Текст = "Не выбрана кромка у " + Строка.ВидИзделия;
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст, , "Элементы.Детали.ТекущиеДанные.КромкаЛДСП");
				Отказ = Истина;
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(РучкиЯщиков) И Строка.ЕстьЯщики Тогда
			Текст = "Не выбраны ручки у " + Строка.ВидИзделия;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст, , "РучкиЯщиков");
			Отказ = Истина;
		КонецЕсли;	
		
		Если Строка.ШиринаИзделия > Строка.ШиринаИзделияМакс ИЛИ Строка.ШиринаИзделия < Строка.ШиринаИзделияМин Тогда
			Текст = Строка(Строка.ВидИзделия) + " не подходит по ширине. Ширина изделия = " + Строка.ШиринаИзделия + " (Мин = " + Строка.ШиринаИзделияМин + ", Макс = " + Строка.ШиринаИзделияМакс + ")";
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст, , "ШиринаИтог");
			Отказ = Истина;	
		КонецЕсли;	
		
		Если Строка.ВысотаИзделия > Строка.ВысотаИзделияМакс ИЛИ Строка.ВысотаИзделия < Строка.ВысотаИзделияМин Тогда
			Текст = Строка(Строка.ВидИзделия) + " не подходит по высоте. Высота изделия = " + Строка.ВысотаИзделия + " (Мин = " + Строка.ВысотаИзделияМин + ", Макс = " + Строка.ВысотаИзделияМакс + ")";
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст, , "ВысотаИтог");
			Отказ = Истина;
		КонецЕсли;
		
		Если Строка.ГлубинаИзделия > Строка.ГлубинаИзделияМакс ИЛИ Строка.ГлубинаИзделия < Строка.ГлубинаИзделияМин Тогда
			Текст = Строка(Строка.ВидИзделия) + " не подходит по глубине. Глубина изделия = " + Строка.ГлубинаИзделия + " (Мин = " + Строка.ГлубинаИзделияМин + ", Макс = " + Строка.ГлубинаИзделияМакс + ")";
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст, , "ГлубинаИтог");
			Отказ = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Если НЕ Отказ Тогда
		
		# Область СозданиеСтрокиПрипусков
		
		Для Каждого Элемент Из Детали Цикл
			
			Элемент.СтрокаПрипусков = "";
			
			Если Элемент.ВидИзделия = Перечисления.ВидыИзделийПоКаталогу.Крыша Тогда 
				
				Если НЕ ЕстьЛевыйБоковой Тогда
					Элемент.СтрокаПрипусков = Элемент.СтрокаПрипусков + "ПрипускСлева";
				КонецЕсли;
				Если НЕ ЕстьПравыйБоковой Тогда
					Элемент.СтрокаПрипусков = Элемент.СтрокаПрипусков + "ПрипускСправа";
				КонецЕсли;
				
			ИначеЕсли Элемент.ВидИзделия = Перечисления.ВидыИзделийПоКаталогу.Пол Тогда
				
				Если НЕ ЕстьЛевыйБоковой Тогда
					Элемент.СтрокаПрипусков = Элемент.СтрокаПрипусков + "ПрипускСлева";
				КонецЕсли;
				Если НЕ ЕстьПравыйБоковой Тогда
					Элемент.СтрокаПрипусков = Элемент.СтрокаПрипусков + "ПрипускСправа";
				КонецЕсли;
				
			ИначеЕсли Элемент.ВидИзделия = Перечисления.ВидыИзделийПоКаталогу.ЛевыйБоковойЭлемент
				ИЛИ Элемент.ВидИзделия = Перечисления.ВидыИзделийПоКаталогу.ПравыйБоковойЭлемент Тогда
				
				Если НЕ ЕстьКрышаНаВсе Тогда
					Элемент.СтрокаПрипусков = Элемент.СтрокаПрипусков + "ПрипускСверху";
				КонецЕсли;
				Элемент.СтрокаПрипусков = Элемент.СтрокаПрипусков + "ПрипускСнизу";
								
			ИначеЕсли Элемент.ВидИзделия = Перечисления.ВидыИзделийПоКаталогу.ОсновнойЭлемент Тогда
				
				Если НЕ ЕстьКрыша И НЕ ЕстьКрышаНаВсе Тогда
					Элемент.СтрокаПрипусков = Элемент.СтрокаПрипусков + "ПрипускСверху";
				КонецЕсли;
				Если НЕ ЕстьЛевыйБоковой Тогда
					Элемент.СтрокаПрипусков = Элемент.СтрокаПрипусков + "ПрипускСлева";
				КонецЕсли;
				Если НЕ ЕстьПол Тогда
					Элемент.СтрокаПрипусков = Элемент.СтрокаПрипусков + "ПрипускСнизу";
				КонецЕсли;
				Если НЕ ЕстьПравыйБоковой Тогда
					Элемент.СтрокаПрипусков = Элемент.СтрокаПрипусков + "ПрипускСправа";
				КонецЕсли;
				
			КонецЕсли;			
		КонецЦикла;
		
		# КонецОбласти
		
		Структура = Новый Структура;
		Структура.Вставить("Детали", Детали.Выгрузить());
		Структура.Вставить("Двери", Двери);
		
		Возврат ПоместитьВоВременноеХранилище(Структура);
		
	КонецЕсли;
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьСтруктуруИзделия(Изделие)
	
	Структура = Новый Структура;
	Структура.Вставить("АдресЭлемента", "Изделие" + Изделие.Код);
	Структура.Вставить("ВысотаИзделия", Изделие.ВысотаИзделия);
	Структура.Вставить("ШиринаИзделия", Изделие.ШиринаИзделия);
	Структура.Вставить("ГлубинаИзделия", Изделие.ГлубинаИзделия);
	Структура.Вставить("ВысотаИзделияМин", Изделие.ВысотаИзделияМин);
	Структура.Вставить("ШиринаИзделияМин", Изделие.ШиринаИзделияМин);
	Структура.Вставить("ГлубинаИзделияМин", Изделие.ГлубинаИзделияМин);
	Структура.Вставить("ВысотаИзделияМакс", Изделие.ВысотаИзделияМакс);
	Структура.Вставить("ШиринаИзделияМакс", Изделие.ШиринаИзделияМакс);
	Структура.Вставить("ГлубинаИзделияМакс", Изделие.ГлубинаИзделияМакс);
	ЕстьЯщики = Изделие.СписокЯщики.Количество() > 0;
	Структура.Вставить("ЕстьЯщики", ЕстьЯщики);
	Структура.Вставить("НаВсеИзделие", Изделие.НаВсеИзделие);
	Структура.Вставить("НеВлияетНаОсновной", Изделие.НеВлияетНаОсновной);
	
	Возврат Структура;                                   
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьМатериалСтенки(ЗадняяСтенка)
	
	//Изделие по каталогу Задняя стенка должна иметь 1 деталь - саму стенку, определяем ее материал
	Если ЗначениеЗаполнено(ЗадняяСтенка) Тогда
		Если ЗадняяСтенка.СписокМатериалы.Количество() > 0 Тогда
			Возврат ЗадняяСтенка.СписокМатериалы[0].Материал;
		КонецЕсли;
	КонецЕсли;
КонецФункции

&НаКлиенте
Функция ПолучитьРазмерыОсновногоЭлемента()
	
	НашОсновнойЭлемент = Детали.НайтиСтроки(Новый Структура("ВидИзделия", ПредопределенноеЗначение("Перечисление.ВидыИзделийПоКаталогу.ОсновнойЭлемент")));
	ЕстьОсновной = НашОсновнойЭлемент.Количество() > 0;
	Если ЕстьОсновной Тогда
		Размеры = Новый Структура;
		Размеры.Вставить("Глубина", ГлубинаИтог);
		
		ШиринаОсновногоЭлемента = ШиринаИтог;
		ВысотаОсновногоЭлемента = ВысотаИтог;
		ВысотаПол = 0;
		ВысотаКрыша = 0;
		
		Для Каждого Строка Из Детали Цикл
			Если Строка.ВидИзделия = ПредопределенноеЗначение("Перечисление.ВидыИзделийПоКаталогу.Крыша") Тогда
				Если НЕ Строка.НаВсеИзделие Тогда
					ВысотаКрыша = Строка.ВысотаИзделия;
				КонецЕсли;
				Если НЕ Строка.НеВлияетНаОсновной Тогда
					ВысотаОсновногоЭлемента = ВысотаОсновногоЭлемента - Строка.ВысотаИзделия;	
				КонецЕсли;
			ИначеЕсли Строка.ВидИзделия = ПредопределенноеЗначение("Перечисление.ВидыИзделийПоКаталогу.Пол") Тогда
				ВысотаПол = Строка.ВысотаИзделия;
				Если НЕ Строка.НеВлияетНаОсновной Тогда
					ВысотаОсновногоЭлемента = ВысотаОсновногоЭлемента - ВысотаПол;
				КонецЕсли;
			ИначеЕсли (Строка.ВидИзделия = ПредопределенноеЗначение("Перечисление.ВидыИзделийПоКаталогу.ЛевыйБоковойЭлемент")
				ИЛИ Строка.ВидИзделия = ПредопределенноеЗначение("Перечисление.ВидыИзделийПоКаталогу.ПравыйБоковойЭлемент"))
				И НЕ Строка.НеВлияетНаОсновной Тогда
				ШиринаОсновногоЭлемента = ШиринаОсновногоЭлемента - Строка.ШиринаИзделия;	
			КонецЕсли;	
		КонецЦикла;
		
		Размеры.Вставить("Ширина", ШиринаОсновногоЭлемента);
		Размеры.Вставить("Высота", ВысотаОсновногоЭлемента);
		Размеры.Вставить("ВысотаБоковой", ВысотаОсновногоЭлемента + ВысотаПол + ВысотаКрыша);
		
		Возврат Размеры;
	Иначе
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Выберите основной элемент", , "ОсновнойЭлемент");
		
	КонецЕсли;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ УПРАВЛЕНИЯ ВНЕШНИМ ВИДОМ ФОРМЫ

&НаКлиенте
Процедура УстановитьДоступность(Флаг, ЕстьЯщики = Ложь)
	
	Элементы.ДеталиНоменклатураЛДСП.Доступность = Флаг;
	Элементы.ДеталиКромкаЛДСП.Доступность = Флаг;
	Элементы.РучкиЯщиков.Доступность = ЕстьЯщики;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьКартинку(АдресЭлемента, ЭлементИзделия)
	
	АдресЭлемента = РабочийКаталог + АдресЭлемента;
	АдресВХранилище = "";
	Если ЗначениеЗаполнено(АдресЭлемента) Тогда
		ИмяФайла = АдресЭлемента;
		ФайлИзображения = Новый Файл(ИмяФайла);
		Если ФайлИзображения.Существует() Тогда
			ПоместитьФайл(АдресВХранилище, ИмяФайла, , Ложь, ЭтаФорма.УникальныйИдентификатор);
		КонецЕсли;
	КонецЕсли;
	
	ЭтаФорма[ЭлементИзделия] = АдресВХранилище;
	
КонецПроцедуры	

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	РабочийКаталог = ФайловыеФункцииСлужебныйКлиент.ВыбратьПутьККаталогуДанныхПользователя();
	
	Если ЗначениеЗаполнено(Двери) Тогда		
		РазмерыДвери = ОпределитьРазмерыДвери();
		ВысотаПроемаДвери = РазмерыДвери.ВысотаПроема;
		ШиринаПроемаДвери = РазмерыДвери.ШиринаПроема;
	КонецЕсли;
	
	//Очень криво обращаться по имени, надо чтото придумать
	МассивЭлементов = Новый Массив;
	МассивЭлементов.Добавить("Крыша");
	МассивЭлементов.Добавить("ЛевыйБоковойЭлемент");
	МассивЭлементов.Добавить("ОсновнойЭлемент");
	МассивЭлементов.Добавить("Пол");
	МассивЭлементов.Добавить("ПравыйБоковойЭлемент");
	
	Для Каждого Строка Из МассивЭлементов Цикл
		Если ЗначениеЗаполнено(ЭтаФорма[Строка]) Тогда
			ОтобразитьКартинку(ЭтаФорма[Строка], Строка);
		КонецЕсли;	
	КонецЦикла;
	
	Элементы.РучкиЯщиков.Доступность = ЗначениеЗаполнено(РучкиЯщиков);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Подразделение = Параметры.Подразделение;
	
	#Область Заполнение_номенклатурных_групп	
	СписокНоменклатурныхГрупп = Новый СписокЗначений;
	
	СписокНоменклатурныхГрупп.Добавить(Справочники.НоменклатурныеГруппы.ЛДСП16);
	СписокНоменклатурныхГрупп.Добавить(Справочники.НоменклатурныеГруппы.КантТ);
	СписокНоменклатурныхГрупп.Добавить(Справочники.НоменклатурныеГруппы.Кромка045_19);
	СписокНоменклатурныхГрупп.Добавить(Справочники.НоменклатурныеГруппы.Кромка2_19);
	СписокНоменклатурныхГрупп.Добавить(Справочники.НоменклатурныеГруппы.Кромка2_35);
	СписокНоменклатурныхГрупп.Добавить(Справочники.НоменклатурныеГруппы.ДВП);
	СписокНоменклатурныхГрупп.Добавить(Справочники.НоменклатурныеГруппы.Ручка);
	
	МассивыНоменклатурныхГрупп = ЛексСервер.ОтборНоменклатурныхГрупп(СписокНоменклатурныхГрупп, Подразделение);
	ДобавитьДопГруппы();
	
	Элементы.ДеталиНоменклатураЛДСП.СписокВыбора.ЗагрузитьЗначения(МассивыНоменклатурныхГрупп.ЛДСП16);
	Элементы.ДеталиКромкаЛДСП.СписокВыбора.ЗагрузитьЗначения(МассивыНоменклатурныхГрупп.Кромка);
	Элементы.РучкиЯщиков.СписокВыбора.ЗагрузитьЗначения(МассивыНоменклатурныхГрупп.Ручка);	
	Элементы.ЗадняяСтенка.СписокВыбора.ЗагрузитьЗначения(ПолучитьСписокСтенок());	
	#КонецОбласти
	                             
	#Область ШапкаОсновныхНастроек
	ШапкаОсновныхНастроек = Новый Структура;
	
	ШапкаОсновныхНастроек.Вставить("НоменклатураЛДСП", "");
	ШапкаОсновныхНастроек.Вставить("КромкаЛДСП", "");
	#КонецОбласти
	
	СтруктураФормы = ПолучитьИзВременногоХранилища(Параметры.АдресТаблицы);
	
	Двери = СтруктураФормы.Двери;
	Детали.Загрузить(СтруктураФормы.Детали.Выгрузить());
	
	Для Каждого Строка Из Детали Цикл
		НашеИзделие = Строка.Изделие;
		Если Строка.ВидИзделия = Перечисления.ВидыИзделийПоКаталогу.ЗадняяСтенка Тогда
			ЗадняяСтенка = НашеИзделие;			
			МатериалСтенки = ПолучитьМатериалСтенки(ЗадняяСтенка);
			
			Если МатериалСтенки = "16 ЛДСП" Тогда
				Элементы.НоменклатураСтенка.СписокВыбора.ЗагрузитьЗначения(МассивыНоменклатурныхГрупп.ЛДСП16);
				НоменклатураСтенка = Строка.НоменклатураЛДСП;
			ИначеЕсли МатериалСтенки = "ДВП" Тогда
				Элементы.НоменклатураСтенка.СписокВыбора.ЗагрузитьЗначения(МассивыНоменклатурныхГрупп.ДВП);
				НоменклатураСтенка = Строка.НоменклатураДВП;
			Иначе
				Элементы.НоменклатураСтенка.СписокВыбора.Очистить();
			КонецЕсли;
			
		ИначеЕсли Строка.ВидИзделия = Перечисления.ВидыИзделийПоКаталогу.ОсновнойЭлемент Тогда
			ГлубинаИтог = Строка.ГлубинаИзделия;
			ВысотаОсновного = Строка.ВысотаИзделия;
			ШиринаОсновного = Строка.ШиринаИзделия;
			ОсновнойЭлемент = "Изделие" + НашеИзделие.Код;
			РучкиЯщиков = Строка.Ручка;
			ШапкаОсновныхНастроек.Вставить("НоменклатураЛДСП", Строка.НоменклатураЛДСП);
			ШапкаОсновныхНастроек.Вставить("КромкаЛДСП", Строка.КромкаЛДСП);
		ИначеЕсли Строка.ВидИзделия = Перечисления.ВидыИзделийПоКаталогу.ЛевыйБоковойЭлемент Тогда
			ЛевыйБоковойЭлемент = "Изделие" + НашеИзделие.Код;
			РучкиЯщиков = Строка.Ручка;
		ИначеЕсли Строка.ВидИзделия = Перечисления.ВидыИзделийПоКаталогу.Пол Тогда
			Пол = "Изделие" + НашеИзделие.Код;
		ИначеЕсли Строка.ВидИзделия = Перечисления.ВидыИзделийПоКаталогу.ПравыйБоковойЭлемент Тогда
			ПравыйБоковойЭлемент = "Изделие" + НашеИзделие.Код;
			РучкиЯщиков = Строка.Ручка;
		ИначеЕсли Строка.ВидИзделия = Перечисления.ВидыИзделийПоКаталогу.Крыша Тогда
			Крыша = "Изделие" + НашеИзделие.Код;
		КонецЕСли;
		
		Строка.ШиринаИзделияМакс = НашеИзделие.ШиринаИзделияМакс;
		Строка.ВысотаИзделияМакс = НашеИзделие.ВысотаИзделияМакс;
		Строка.ГлубинаИзделияМакс = НашеИзделие.ГлубинаИзделияМакс;
		Строка.ШиринаИзделияМин = НашеИзделие.ШиринаИзделияМин;
		Строка.ВысотаИзделияМин = НашеИзделие.ВысотаИзделияМин;
		Строка.ГлубинаИзделияМин = НашеИзделие.ГлубинаИзделияМин;
		ЕстьЯщики = НашеИзделие.СписокЯщики.Количество() > 0;
		Строка.ЕстьЯщики = ЕстьЯщики;
		Строка.НаВсеИзделие = НашеИзделие.НаВсеИзделие;
		Строка.НеВлияетНаОсновной = НашеИзделие.НеВлияетНаОсновной;
		
	КонецЦикла;
	
	ПолучитьИтоговыеРазмеры(ВысотаОсновного, ШиринаОсновного);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.Двери") И ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		
		ДвериПриИзмененииНаКлиенте(ВыбранноеЗначение);
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

&НаКлиенте
Процедура ВыборИзделия(Команда)
	
	ЭлементИзделия = ЭтаФорма.ТекущийЭлемент.Имя;
	ТекущийЭлементИзделия = ПредопределенноеЗначение("Перечисление.ВидыИзделийПоКаталогу." + ЭтаФорма.ТекущийЭлемент.Имя);
	
	МассивИзделий = Новый Массив;
	
	Для Каждого Строка Из Детали Цикл
		
		МассивИзделий.Добавить(Строка.Изделие);
		
	КонецЦикла;	
	
	Изделие = ОткрытьФормуМодально("Справочник.КаталогИзделий.ФормаВыбора", Новый Структура("ВидИзделия, ИсключитьНесовместимые, МассивИзделий", ТекущийЭлементИзделия, Истина, МассивИзделий), ЭтаФорма);
	
	Если ЗначениеЗаполнено(Изделие) Тогда	
		СтруктураИзделия = ПолучитьСтруктуруИзделия(Изделие);
		
		НашаСтрока = Детали.НайтиСтроки(Новый Структура("ВидИзделия", ТекущийЭлементИзделия));
		ЕстьСтрока = НашаСтрока.Количество() = 1;
		
		Если ЕстьСтрока Тогда
			НоваяСтрока = НашаСтрока[0];
		Иначе
			НоваяСтрока = Детали.Добавить();
		КонецЕсли;
		
		НоваяСтрока.Изделие = Изделие;
		НоваяСтрока.ВысотаИзделия = СтруктураИзделия.ВысотаИзделия;
		НоваяСтрока.ШиринаИзделия = СтруктураИзделия.ШиринаИзделия;
		НоваяСтрока.ГлубинаИзделия = СтруктураИзделия.ГлубинаИзделия;
		НоваяСтрока.ВысотаИзделияМин = СтруктураИзделия.ВысотаИзделияМин;
		НоваяСтрока.ШиринаИзделияМин = СтруктураИзделия.ШиринаИзделияМин;
		НоваяСтрока.ГлубинаИзделияМин = СтруктураИзделия.ГлубинаИзделияМин;
		НоваяСтрока.ВысотаИзделияМакс = СтруктураИзделия.ВысотаИзделияМакс;
		НоваяСтрока.ШиринаИзделияМакс = СтруктураИзделия.ШиринаИзделияМакс;
		НоваяСтрока.ГлубинаИзделияМакс = СтруктураИзделия.ГлубинаИзделияМакс;
		НоваяСтрока.ВидИзделия = ТекущийЭлементИзделия;
		НоваяСтрока.НоменклатураЛДСП = ШапкаОсновныхНастроек.НоменклатураЛДСП;
		НоваяСтрока.КромкаЛДСП = ШапкаОсновныхНастроек.КромкаЛДСП;
		НоваяСтрока.ЕстьЯщики = СтруктураИзделия.ЕстьЯщики;
		НоваяСтрока.НаВсеИзделие = СтруктураИзделия.НаВсеИзделие;
		НоваяСтрока.НеВлияетНаОсновной = СтруктураИзделия.НеВлияетНаОсновной;
				
		УстановитьДоступность(Истина, СтруктураИзделия.ЕстьЯщики);	
		ОтобразитьКартинку(СтруктураИзделия.АдресЭлемента, ЭлементИзделия);	
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура Очистить(Команда)
	
	ЭлементИзделия = ЭтаФорма.ТекущийЭлемент.Имя;	
	ТекущийЭлементИзделия = ПредопределенноеЗначение("Перечисление.ВидыИзделийПоКаталогу." + ЭлементИзделия);
	НашаСтрока = Детали.НайтиСтроки(Новый Структура("ВидИзделия", ТекущийЭлементИзделия));
	ЕстьСтрока = НашаСтрока.Количество() = 1;
	
	Если ЕстьСтрока Тогда
		Детали.Удалить(НашаСтрока[0]);
		ЭтаФорма[ЭлементИзделия] = "";	
	КонецЕсли;
	
	УстановитьДоступность(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВДокумент(Команда)
	
	РазмерыОсновногоЭлемента = ПолучитьРазмерыОсновногоЭлемента();
	Отказ = Ложь;
	Если РазмерыОсновногоЭлемента <> Неопределено Тогда 
		Модифицированность = Ложь;
		СтруктураФормы = ПроверитьИУстановитьРазмерыПередСохранением(РазмерыОсновногоЭлемента);
		
		Если ЗначениеЗаполнено(Двери) Тогда
			
			Отказ = ПроверкаДвери();
			
		КонецЕсли;	
		
		Если (СтруктураФормы <> Неопределено) И (НЕ Отказ) Тогда 
			ОповеститьОВыборе(СтруктураФормы);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ОБРАБОТКИ СВОЙСТВ И КАТЕГОРИЙ

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура ИзделиеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущийЭлементИзделия = ПредопределенноеЗначение("Перечисление.ВидыИзделийПоКаталогу." + ЭтаФорма.ТекущийЭлемент.Имя);	
	НашаСтрока = Детали.НайтиСтроки(Новый Структура("ВидИзделия", ТекущийЭлементИзделия));
	ЕстьСтрока = НашаСтрока.Количество() = 1;
	ЕстьЯщики = Ложь;
	
	Если ЕстьСтрока Тогда
		Элементы.Детали.ТекущаяСтрока = Детали.Индекс(НашаСтрока[0]);
		ТекущиеДанные = Элементы.Детали.ТекущиеДанные;
		ЕстьЯщики = ТекущиеДанные.ЕстьЯщики;
		
		Если ТекущиеДанные <> Неопределено Тогда
			Если НЕ ЗначениеЗаполнено(ТекущиеДанные.НоменклатураЛДСП) И ЗначениеЗаполнено(ШапкаОсновныхНастроек.НоменклатураЛДСП) Тогда			
				ТекущиеДанные.НоменклатураЛДСП = ШапкаОсновныхНастроек.НоменклатураЛДСП;		
			ИначеЕсли ЗначениеЗаполнено(ТекущиеДанные.НоменклатураЛДСП) Тогда
				ШапкаОсновныхНастроек.Вставить("НоменклатураЛДСП", ТекущиеДанные.НоменклатураЛДСП);
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(ТекущиеДанные.КромкаЛДСП) И ЗначениеЗаполнено(ШапкаОсновныхНастроек.КромкаЛДСП) Тогда			
				ТекущиеДанные.КромкаЛДСП = ШапкаОсновныхНастроек.КромкаЛДСП;		
			ИначеЕсли ЗначениеЗаполнено(ТекущиеДанные.КромкаЛДСП) Тогда
				ШапкаОсновныхНастроек.Вставить("КромкаЛДСП", ТекущиеДанные.КромкаЛДСП);
			КонецЕсли;
		КонецЕсли;	
	Иначе
		Элементы.Детали.ТекущаяСтрока = Неопределено;
	КонецЕсли;
	
	УстановитьДоступность(ЕстьСтрока, ЕстьЯщики);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗадняяСтенкаПриИзменении(Элемент)
	
	ТекущийЭлементИзделия = ПредопределенноеЗначение("Перечисление.ВидыИзделийПоКаталогу.ЗадняяСтенка");
	
	УстановитьДоступность(Ложь);
	
	Если ЗначениеЗаполнено(ЗадняяСтенка) Тогда
		
		МатериалСтенки = ПолучитьМатериалСтенки(ЗадняяСтенка);
		
		Если МатериалСтенки = "16 ЛДСП" Тогда
			Элементы.НоменклатураСтенка.СписокВыбора.ЗагрузитьЗначения(МассивыНоменклатурныхГрупп.ЛДСП16);
		ИначеЕсли МатериалСтенки = "ДВП" Тогда
			Элементы.НоменклатураСтенка.СписокВыбора.ЗагрузитьЗначения(МассивыНоменклатурныхГрупп.ДВП);
		Иначе
			Элементы.НоменклатураСтенка.СписокВыбора.Очистить();
		КонецЕсли;
		
		Стенка = ПредопределенноеЗначение("Перечисление.ВидыИзделийПоКаталогу.ЗадняяСтенка"); 
		НашаСтрока = Детали.НайтиСтроки(Новый Структура("ВидИзделия", Стенка));
		ЕстьСтрока = НашаСтрока.Количество() = 1;
		
		Если ЕстьСтрока Тогда
			НоваяСтрока = НашаСтрока[0];
		Иначе
			НоваяСтрока = Детали.Добавить();
		КонецЕсли;
		
		НоваяСтрока.Изделие = ЗадняяСтенка;
		НоваяСтрока.ВидИзделия = Стенка;
		НоменклатураСтенка = Неопределено;
	Иначе
		НоменклатураСтенка = Неопределено;
		Элементы.НоменклатураСтенка.СписокВыбора.Очистить();
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ЗадняяСтенкаОчистка(Элемент, СтандартнаяОбработка)
	
	НашаСтрока = Детали.НайтиСтроки(Новый Структура("ВидИзделия", ПредопределенноеЗначение("Перечисление.ВидыИзделийПоКаталогу.ЗадняяСтенка")));
	ЕстьСтрока = НашаСтрока.Количество() = 1;
	
	Если ЕстьСтрока Тогда
		Детали.Удалить(НашаСтрока[0]);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ДеталиНоменклатураЛДСППриИзменении(Элемент)
	
	ШапкаОсновныхНастроек.Вставить("НоменклатураЛДСП", Элементы.Детали.ТекущиеДанные.НоменклатураЛДСП);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеталиКромкаЛДСППриИзменении(Элемент)
	
	ШапкаОсновныхНастроек.Вставить("КромкаЛДСП", Элементы.Детали.ТекущиеДанные.КромкаЛДСП);	
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураСтенкаПриИзменении(Элемент)
	
	ТекущийЭлементИзделия = ПредопределенноеЗначение("Перечисление.ВидыИзделийПоКаталогу.ЗадняяСтенка");
	УстановитьДоступность(Ложь);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ПОЛЯ ДВЕРИ

&НаКлиенте
Функция ПроверкаДвери()
	
	Отказ = Ложь;
	
	РазмерыДвери = ОпределитьРазмерыДвери();
	
	Если ВысотаПроемаДвери <> РазмерыДвери.ВысотаПроема ИЛИ ШиринаПроемаДвери <> РазмерыДвери.ШиринаПроема Тогда
		
		СписокКнопок = Новый СписокЗначений;	
		СписокКнопок.Добавить(КодВозвратаДиалога.Да, "Редактировать");
		СписокКнопок.Добавить(КодВозвратаДиалога.Нет, "Отмена");
		
		Ответ = Вопрос("Размеры двери не совпадают с размерами изделия.", СписокКнопок,,);
		
		Если Ответ = КодВозвратаДиалога.Да Тогда
			
			ПараметрыФормы = Новый Структура("Ключ", Двери);
			ПараметрыФормы.Вставить("ВысотаПроема", РазмерыДвери.ВысотаПроема);
			ПараметрыФормы.Вставить("ШиринаПроема", РазмерыДвери.ШиринаПроема);
			ПараметрыФормы.Вставить("Редактирование", Истина);
			Форма = ПолучитьФорму("Справочник.Двери.Форма.ФормаЭлемента", ПараметрыФормы, ЭтаФорма);
			Форма.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
			Форма.Открыть();
			
		КонецЕсли;
		
		Отказ = Истина;
		
	КонецЕсли;
	
	Возврат Отказ;
	
КонецФункции

&НаКлиенте
Процедура ДвериНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	РазмерыДвери = ОпределитьРазмерыДвери();
	Если РазмерыДвери.ВысотаПроема > 0 И РазмерыДвери.ШиринаПроема > 0  Тогда
		
		СписокКнопок = Новый СписокЗначений;	
		СписокКнопок.Добавить(КодВозвратаДиалога.Да, "Создать");
		СписокКнопок.Добавить(КодВозвратаДиалога.Нет, "Выбрать");
		СписокКнопок.Добавить(КодВозвратаДиалога.Отмена, "Отмена");
		
		Ответ = Вопрос("Создать новую или выбрать существующую дверь?",СписокКнопок,,);
		
		Если Ответ = КодВозвратаДиалога.Да Тогда
			
			ПараметрыФормы = Новый Структура("Подразделение", Подразделение);
			ПараметрыФормы.Вставить("ВысотаПроема", РазмерыДвери.ВысотаПроема);
			ПараметрыФормы.Вставить("ШиринаПроема", РазмерыДвери.ШиринаПроема);
			
			Форма = ПолучитьФорму("Справочник.Двери.Форма.ФормаЭлемента", ПараметрыФормы, ЭтаФорма);
			Форма.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
			Форма.Открыть();	
			
		ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
			
			ПараметрыФормы = Новый Структура("ШиринаПроема", РазмерыДвери.ШиринаПроема);
			ПараметрыФормы.Вставить("ВысотаПроема", РазмерыДвери.ВысотаПроема);
			Форма = ПолучитьФорму("Справочник.Двери.Форма.ФормаВыбора", ПараметрыФормы, ЭтаФорма);
			Форма.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
			Форма.Открыть();
			
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДвериПриИзменении(Элемент)
	
	ДвериПриИзмененииНаКлиенте(Двери);
	
КонецПроцедуры

&НаКлиенте
Процедура ДвериПриИзмененииНаКлиенте(Значение)
	
	Если ЗначениеЗаполнено(Значение) Тогда
		РазмерыДвери = ПолучитьРазмерыВыбраннойДвери(Значение);
		РазмерыПроема = ОпределитьРазмерыДвери();
		
		Если РазмерыПроема.ВысотаПроема = РазмерыДвери.ВысотаПроема И РазмерыПроема.ШиринаПроема = РазмерыДвери.ШиринаПроема Тогда
			
			Двери = Значение;
			ВысотаПроемаДвери = РазмерыПроема.ВысотаПроема;
			ШиринаПроемаДвери = РазмерыПроема.ШиринаПроема;
			
		Иначе
			
			Двери = Неопределено;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Двери не подходят по размерам", , "Двери");
			
		КонецЕсли;
		
	Иначе
		
		Двери = Неопределено;
		
	КонецЕсли;

КонецПроцедуры

//Размеры двери при выборе уже существующей двери
&НаСервереБезКонтекста                                
Функция ПолучитьРазмерыВыбраннойДвери(Дверь)
	
	Структура = Новый Структура;
	
	Структура.Вставить("ВысотаПроема", Дверь.ВысотаПроема);
	Структура.Вставить("ШиринаПроема", Дверь.ШиринаПроема);
	
	Возврат Структура;
	
КонецФункции

//Размеры двери с учетом размеров элементов шкафа-купе
&НаКлиенте                                
Функция ОпределитьРазмерыДвери()
	
	ВысотаПроема = 0;
	ШиринаПроема = 0;
	
	РазмерыОсновногоЭлемента = ПолучитьРазмерыОсновногоЭлемента();
	Если РазмерыОсновногоЭлемента <> Неопределено Тогда
		
		ВысотаПроема = РазмерыОсновногоЭлемента.Высота;
		ШиринаПроема = РазмерыОсновногоЭлемента.Ширина;
		
		Если ВысотаПроема > 0 И ШиринаПроема > 0  Тогда
			Для Каждого Элемент Из Детали Цикл
				
				Если Элемент.НеВлияетНаОсновной И 
					(Элемент.ВидИзделия = ПредопределенноеЗначение("Перечисление.ВидыИзделийПоКаталогу.ЛевыйБоковойЭлемент") ИЛИ
					Элемент.ВидИзделия = ПредопределенноеЗначение("Перечисление.ВидыИзделийПоКаталогу.ПравыйБоковойЭлемент")) Тогда
					
					ШиринаПроема = ШиринаПроема - Элемент.ШиринаИзделия;
					
				ИначеЕсли Элемент.НеВлияетНаОсновной И 
					(Элемент.ВидИзделия = ПредопределенноеЗначение("Перечисление.ВидыИзделийПоКаталогу.Крыша") ИЛИ
					Элемент.ВидИзделия = ПредопределенноеЗначение("Перечисление.ВидыИзделийПоКаталогу.Пол")) Тогда
					
					ВысотаПроема = ВысотаПроема - Элемент.ВысотаИзделия;
					
				КонецЕсли;
				
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Структура = Новый Структура;
	
	Структура.Вставить("ВысотаПроема", ВысотаПроема);
	Структура.Вставить("ШиринаПроема", ШиринаПроема);
	
	Возврат Структура;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ


