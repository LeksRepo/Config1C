////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаСервере
Процедура ПроверитьКромкиЛДСП(Ошибки, ЛДСП)
	
	Структура = ЛексСервер.ПолучитьСтруктуруПодставляемойНоменклатурыПоЦветуЛДСП(ЛДСП, Подразделение);
	
	Если НЕ ЗначениеЗаполнено(Структура.Кромка045_19) Тогда
		Текст = "Не задана кромка 0.45мм по умолчанию для " + ЛДСП + ". Обратитесь в ИРЦ";
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, , Текст);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Структура.Кромка2_19) Тогда
		Текст = "Не задана кромка 2мм по умолчанию для " + ЛДСП + ". Обратитесь в ИРЦ";
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, , Текст);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОпределитьКорневойВидИзделия(ВидИзделия)
	
	//Справочник наш, при условии что 2 уровня в иерархии
	Возврат ?(ВидИзделия.Родитель.Пустая(), ВидИзделия, ВидИзделия.Родитель);
	
КонецФункции

&НаСервере
Процедура ЗагрузкаАдресаПредопределенных()
	
	//Получаем Таблицу значений предопределенных изделий(Стены) их адреса на картинку и список несовместимых изделий с ними
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КаталогИзделий.Ссылка КАК Изделие,
	|	ВЫБОР
	|		КОГДА КаталогИзделий.Ссылка = ЗНАЧЕНИЕ(Справочник.КаталогИзделий.ПраваяСтена)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыИзделийПоКаталогу.ПравыйБоковойЭлемент)
	|		КОГДА КаталогИзделий.ВидИзделияПоКаталогу.Родитель = ЗНАЧЕНИЕ(Справочник.ВидыИзделийПоКаталогу.ПустаяСсылка)
	|			ТОГДА КаталогИзделий.ВидИзделияПоКаталогу
	|		ИНАЧЕ КаталогИзделий.ВидИзделияПоКаталогу.Родитель
	|	КОНЕЦ КАК ВидИзделия,
	|	НесовместимостьИзделийПоКаталогу.НесовместимостимоеИзделие КАК НесовместимостимоеИзделие,
	|	КаталогИзделий.ВидИзделияПоКаталогу
	|ИЗ
	|	Справочник.КаталогИзделий КАК КаталогИзделий
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НесовместимостьИзделийПоКаталогу КАК НесовместимостьИзделийПоКаталогу
	|		ПО (НесовместимостьИзделийПоКаталогу.Изделие = КаталогИзделий.Ссылка)
	|ГДЕ
	|	(КаталогИзделий.Ссылка = ЗНАЧЕНИЕ(Справочник.КаталогИзделий.ЛеваяСтена)
	|			ИЛИ КаталогИзделий.Ссылка = ЗНАЧЕНИЕ(Справочник.КаталогИзделий.ПолЗаказчика)
	|			ИЛИ КаталогИзделий.Ссылка = ЗНАЧЕНИЕ(Справочник.КаталогИзделий.ПотолокЗаказчика)
	|			ИЛИ КаталогИзделий.Ссылка = ЗНАЧЕНИЕ(Справочник.КаталогИзделий.ПраваяСтена))
	|ИТОГИ ПО
	|	Изделие";
	
	Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = АдресаПредопределенных.Добавить();
		НоваяСтрока.Изделие = Выборка.Изделие;
		НоваяСтрока.ВидИзделия = Выборка.ВидИзделия;
		НоваяСтрока.ВидИзделияПоКаталогу = Выборка.ВидИзделияПоКаталогу;
		
		ВыборкаПоИзделиям = Выборка.Выбрать();
		Пока ВыборкаПоИзделиям.Следующий() Цикл
			НоваяСтрока.СписокНесовместимых.Добавить(ВыборкаПоИзделиям.НесовместимостимоеИзделие);
		КонецЦикла;
		СтруктураАдреса = ПолучитьАдрес(НоваяСтрока.Изделие, НоваяСтрока.ВидИзделия);
		НоваяСтрока.АдресИзделия = СтруктураАдреса.АдресЭлемента;
		НоваяСтрока.АдресКрыши = СтруктураАдреса.АдресКрыши;
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьАдрес(Изделие, ВидИзделия)
	
	Структура = Новый Структура;
	
	Если ВидИзделия = Справочники.ВидыИзделийПоКаталогу.ЛевыйБоковойЭлемент Тогда
		Расположение = Строка(Перечисления.ВидыПрисоединенныхФайлов.КартинкаЛевая);
		ПоложениеКрыши = Строка(Перечисления.ВидыПрисоединенныхФайлов.КрышаЛевая); 
	ИначеЕсли ВидИзделия = Справочники.ВидыИзделийПоКаталогу.ПравыйБоковойЭлемент Тогда
		Расположение = Строка(Перечисления.ВидыПрисоединенныхФайлов.КартинкаПравая);
		ПоложениеКрыши = Строка(Перечисления.ВидыПрисоединенныхФайлов.КрышаПравая);
	Иначе
		Расположение = Строка(Перечисления.ВидыПрисоединенныхФайлов.ОсновнаяКартинка);
		ПоложениеКрыши = "";
	КонецЕсли;
	
	АдресКрыши = ?(ЗначениеЗаполнено(ПоложениеКрыши), ПоложениеКрыши + Изделие.Код, ПоложениеКрыши);
	
	Структура.Вставить("АдресЭлемента", Расположение + Изделие.Код);
	Структура.Вставить("АдресКрыши", АдресКрыши);
	
	Возврат Структура;
	
КонецФункции

&НаСервереБезКонтекста
Функция ОпределитьСтруктурыЛДСП(ЛДСП, Подразделение, СтруктураНоменклатураМатериала)
	
	Структура = Новый Структура;
	
	Структура.Вставить("НоменклатураМатериала", СтруктураНоменклатураМатериала(СтруктураНоменклатураМатериала, ЛДСП));
	Структура.Вставить("СтруктураПодставляяемойНоменклатуры", ЛексСервер.ПолучитьСтруктуруПодставляемойНоменклатурыПоЦветуЛДСП(ЛДСП, Подразделение));
	
	Возврат Структура;
	
КонецФункции

&НаСервереБезКонтекста
Функция СтруктураНоменклатураМатериала(Структура, Номенклатура) 
	
	Структура.Вставить("ДлинаДетали", Номенклатура.ДлинаДетали);
	Структура.Вставить("ШиринаДетали ", Номенклатура.ШиринаДетали);
	Структура.Вставить("ГлубинаДетали ", Номенклатура.ГлубинаДетали);
	
	Возврат Структура;
	
КонецФункции

&НаСервере
Процедура ПолучитьИтоговыеРазмеры(ВысотаОсновного, ШиринаОсновного)
	
	ШиринаИтог = ШиринаОсновного;
	ВысотаИтог = ВысотаОсновного;
	
	Для Каждого Строка Из Детали Цикл
		Если (Строка.ВидИзделия = Справочники.ВидыИзделийПоКаталогу.Потолок 
			ИЛИ Строка.ВидИзделия = Справочники.ВидыИзделийПоКаталогу.Пол) И НЕ Строка.НеВлияетНаОсновной Тогда
			ВысотаИтог = ВысотаИтог + Строка.ВысотаИзделия;	 	
		ИначеЕсли (Строка.ВидИзделия = Справочники.ВидыИзделийПоКаталогу.ЛевыйБоковойЭлемент
			ИЛИ Строка.ВидИзделия = Справочники.ВидыИзделийПоКаталогу.ПравыйБоковойЭлемент) И НЕ Строка.НеВлияетНаОсновной Тогда
			ШиринаИтог = ШиринаИтог + Строка.ШиринаИзделия;	
		КонецЕсли;	
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПроверитьИУстановитьРазмерыПередСохранением(РазмерыОсновного)
	
	Ошибки = Неопределено;
	
	ЕстьКрыша = Ложь;
	ЕстьКрышаНаВсе = Ложь;
	ЕстьПол = Ложь;
	ЕстьЛевыйБоковой = Ложь;
	ЕстьПравыйБоковой = Ложь;
	
	НеВлияетНаОсновнойСнизу = Ложь;
	НеВлияетНаОсновнойСверху = Ложь;
	НеВлияетНаОсновнойСлева = Ложь;
	НеВлияетНаОсновнойСправа = Ложь;
	
	МинимальнаяГлубина = 0;
	МаксимальнаяГлубина = 0;
	
	СтруктураДиапазонов = Новый Структура;
	
	СтруктураДиапазонов.Вставить("ВысотаМинЛевый", 0);
	СтруктураДиапазонов.Вставить("ВысотаМаксЛевый", 0);
	СтруктураДиапазонов.Вставить("ВысотаМинЦентр", 0);
	СтруктураДиапазонов.Вставить("ВысотаМаксЦентр", 0);
	СтруктураДиапазонов.Вставить("ВысотаМинПравый", 0);
	СтруктураДиапазонов.Вставить("ВысотаМаксПравый", 0);
	
	СтруктураДиапазонов.Вставить("ШиринаМинВерхний", 0);
	СтруктураДиапазонов.Вставить("ШиринаМаксВерхний", 0);
	СтруктураДиапазонов.Вставить("ШиринаМинЦентр", 0);
	СтруктураДиапазонов.Вставить("ШиринаМаксЦентр", 0);
	СтруктураДиапазонов.Вставить("ШиринаМинНижний", 0);
	СтруктураДиапазонов.Вставить("ШиринаМаксНижний", 0);	
	
	Для Каждого Строка Из Детали Цикл
		
		ВидИзделияПоКаталогу = Строка.Изделие.ВидИзделияПоКаталогу;
		
		Если НЕ ЗначениеЗаполнено(Строка.Завал) И НЕ Корпусный Тогда
			
			Текст = "Заполните завал у " + Строка.ВидИзделия;
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "Детали", Текст);
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Строка.Завал) И Число(Строка.Завал) >= 10 
			И (ВидИзделияПоКаталогу = Справочники.ВидыИзделийПоКаталогу.СтенаСверху
			ИЛИ ВидИзделияПоКаталогу = Справочники.ВидыИзделийПоКаталогу.СтенаСлева
			ИЛИ ВидИзделияПоКаталогу = Справочники.ВидыИзделийПоКаталогу.СтенаСнизу
			ИЛИ ВидИзделияПоКаталогу = Справочники.ВидыИзделийПоКаталогу.СтенаСправа
			ИЛИ ВидИзделияПоКаталогу = Справочники.ВидыИзделийПоКаталогу.ФальшПанельСлева
			ИЛИ ВидИзделияПоКаталогу = Справочники.ВидыИзделийПоКаталогу.ФальшПанельСправа
			ИЛИ ВидИзделияПоКаталогу = Справочники.ВидыИзделийПоКаталогу.ПодложкаПодТрекСверху
			ИЛИ ВидИзделияПоКаталогу = Справочники.ВидыИзделийПоКаталогу.ПодложкаПодТрекСнизу) Тогда
			
			Текст = "" + Строка.ВидИзделия + ", ошибка установки завала или элемента (для завала более 10 мм. не подходят стены и фальш панели)";
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "Детали", Текст);
			
		КонецЕсли;
		
		Если Строка.Предопределенный Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		Строка.ГлубинаИзделия = ГлубинаИтог;
		Строка.Ручка = РучкиЯщиков;
		
		Если Строка.ВидИзделия = Справочники.ВидыИзделийПоКаталогу.Потолок Тогда 
			
			НеВлияетНаОсновнойСверху = Строка.Изделие.НеВлияетНаОсновной;
			
			СтруктураДиапазонов.ВысотаМинЦентр = СтруктураДиапазонов.ВысотаМинЦентр + Строка.ВысотаИзделияМин;
			СтруктураДиапазонов.ВысотаМаксЦентр = СтруктураДиапазонов.ВысотаМаксЦентр + Строка.ВысотаИзделияМакс;
			
			СтруктураДиапазонов.ШиринаМинВерхний = СтруктураДиапазонов.ШиринаМинВерхний + Строка.ШиринаИзделияМин;
			СтруктураДиапазонов.ШиринаМаксВерхний = СтруктураДиапазонов.ШиринаМаксВерхний + Строка.ШиринаИзделияМакс;
			
			Если Строка.Изделие.НаВсеИзделие Тогда
				
				ЕстьКрышаНаВсе = Истина;
				Строка.ШиринаИзделия = ШиринаИтог;
				
				СтруктураДиапазонов.ВысотаМинЛевый = СтруктураДиапазонов.ВысотаМинЛевый + Строка.ВысотаИзделияМин;
				СтруктураДиапазонов.ВысотаМаксЛевый = СтруктураДиапазонов.ВысотаМаксЛевый + Строка.ВысотаИзделияМакс;
				СтруктураДиапазонов.ВысотаМинПравый = СтруктураДиапазонов.ВысотаМинПравый + Строка.ВысотаИзделияМин;
				СтруктураДиапазонов.ВысотаМаксПравый = СтруктураДиапазонов.ВысотаМаксПравый + Строка.ВысотаИзделияМакс;
				
				СтруктураДиапазонов.ШиринаМинВерхний = Строка.ШиринаИзделияМин;
				СтруктураДиапазонов.ШиринаМаксВерхний = Строка.ШиринаИзделияМакс;
				
			Иначе
				
				ЕстьКрыша = Истина;
				Строка.ШиринаИзделия = РазмерыОсновного.Ширина;
				
			КонецЕсли;
			
		ИначеЕсли Строка.ВидИзделия = Справочники.ВидыИзделийПоКаталогу.Пол Тогда
			
			СтруктураДиапазонов.ВысотаМинЦентр = СтруктураДиапазонов.ВысотаМинЦентр + Строка.ВысотаИзделияМин;
			СтруктураДиапазонов.ВысотаМаксЦентр = СтруктураДиапазонов.ВысотаМаксЦентр + Строка.ВысотаИзделияМакс;
			
			СтруктураДиапазонов.ШиринаМинНижний = СтруктураДиапазонов.ШиринаМинНижний + Строка.ШиринаИзделияМин;
			СтруктураДиапазонов.ШиринаМаксНижний = СтруктураДиапазонов.ШиринаМаксНижний + Строка.ШиринаИзделияМакс;
			
			Строка.ШиринаИзделия = РазмерыОсновного.Ширина;
			ЕстьПол = Истина;
			НеВлияетНаОсновнойСнизу = Строка.Изделие.НеВлияетНаОсновной;
			
		ИначеЕсли Строка.ВидИзделия = Справочники.ВидыИзделийПоКаталогу.ЛевыйБоковойЭлемент Тогда
			
			СтруктураДиапазонов.ВысотаМинЛевый = СтруктураДиапазонов.ВысотаМинЛевый + Строка.ВысотаИзделияМин;
			СтруктураДиапазонов.ВысотаМаксЛевый = СтруктураДиапазонов.ВысотаМаксЛевый + Строка.ВысотаИзделияМакс;
			
			СтруктураДиапазонов.ШиринаМинВерхний = ?(ЕстьКрышаНаВсе, СтруктураДиапазонов.ШиринаМинВерхний, СтруктураДиапазонов.ШиринаМинВерхний + Строка.ШиринаИзделияМин);
			СтруктураДиапазонов.ШиринаМаксВерхний = ?(ЕстьКрышаНаВсе, СтруктураДиапазонов.ШиринаМаксВерхний, СтруктураДиапазонов.ШиринаМаксВерхний + Строка.ШиринаИзделияМакс);
			СтруктураДиапазонов.ШиринаМинЦентр = СтруктураДиапазонов.ШиринаМинЦентр + Строка.ШиринаИзделияМин;
			СтруктураДиапазонов.ШиринаМаксЦентр = СтруктураДиапазонов.ШиринаМаксЦентр + Строка.ШиринаИзделияМакс;
			СтруктураДиапазонов.ШиринаМинНижний = СтруктураДиапазонов.ШиринаМинНижний + Строка.ШиринаИзделияМин;
			СтруктураДиапазонов.ШиринаМаксНижний = СтруктураДиапазонов.ШиринаМаксНижний + Строка.ШиринаИзделияМакс;
			
			Строка.ВысотаИзделия = РазмерыОсновного.ВысотаБоковой;
			ЕстьЛевыйБоковой = Истина;
			НеВлияетНаОсновнойСлева = Строка.Изделие.НеВлияетНаОсновной;
			
		ИначеЕсли Строка.ВидИзделия = Справочники.ВидыИзделийПоКаталогу.ПравыйБоковойЭлемент Тогда
			
			СтруктураДиапазонов.ВысотаМинПравый = СтруктураДиапазонов.ВысотаМинПравый + Строка.ВысотаИзделияМин;
			СтруктураДиапазонов.ВысотаМаксПравый = СтруктураДиапазонов.ВысотаМаксПравый + Строка.ВысотаИзделияМакс;			
			
			СтруктураДиапазонов.ШиринаМинВерхний = ?(ЕстьКрышаНаВсе, СтруктураДиапазонов.ШиринаМинВерхний, СтруктураДиапазонов.ШиринаМинВерхний + Строка.ШиринаИзделияМин);
			СтруктураДиапазонов.ШиринаМаксВерхний = ?(ЕстьКрышаНаВсе, СтруктураДиапазонов.ШиринаМаксВерхний, СтруктураДиапазонов.ШиринаМаксВерхний + Строка.ШиринаИзделияМакс);			
			СтруктураДиапазонов.ШиринаМинЦентр = СтруктураДиапазонов.ШиринаМинЦентр + Строка.ШиринаИзделияМин;
			СтруктураДиапазонов.ШиринаМаксЦентр = СтруктураДиапазонов.ШиринаМаксЦентр + Строка.ШиринаИзделияМакс;
			СтруктураДиапазонов.ШиринаМинНижний = СтруктураДиапазонов.ШиринаМинНижний + Строка.ШиринаИзделияМин;
			СтруктураДиапазонов.ШиринаМаксНижний = СтруктураДиапазонов.ШиринаМаксНижний + Строка.ШиринаИзделияМакс;
			
			Строка.ВысотаИзделия = РазмерыОсновного.ВысотаБоковой;
			ЕстьПравыйБоковой = Истина;
			НеВлияетНаОсновнойСправа = Строка.Изделие.НеВлияетНаОсновной;
			
		ИначеЕсли Строка.ВидИзделия = Справочники.ВидыИзделийПоКаталогу.ОсновнойЭлемент Тогда
			
			СтруктураДиапазонов.ВысотаМинЦентр = СтруктураДиапазонов.ВысотаМинЦентр + Строка.ВысотаИзделияМин;
			СтруктураДиапазонов.ВысотаМаксЦентр = СтруктураДиапазонов.ВысотаМаксЦентр + Строка.ВысотаИзделияМакс;
			
			СтруктураДиапазонов.ШиринаМинЦентр = СтруктураДиапазонов.ШиринаМинЦентр + Строка.ШиринаИзделияМин;
			СтруктураДиапазонов.ШиринаМаксЦентр = СтруктураДиапазонов.ШиринаМаксЦентр + Строка.ШиринаИзделияМакс;
			
			Строка.ШиринаИзделия = РазмерыОсновного.Ширина;
			Строка.ВысотаИзделия = РазмерыОсновного.Высота;
			
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Строка.НоменклатураЛДСП) Тогда
			Если ЗначениеЗаполнено(ШапкаОсновныхНастроек.НоменклатураЛДСП) Тогда
				Строка.НоменклатураЛДСП = ШапкаОсновныхНастроек.НоменклатураЛДСП;
				ПроверитьКромкиЛДСП(Ошибки, Строка.НоменклатураЛДСП);
			Иначе
				Элементы.Детали.ТекущаяСтрока = Строка.ПолучитьИдентификатор();
				Текст = "Не выбрана номенклатура ЛДСП у " + Строка.ВидИзделия;
				ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "Элементы.Детали.ТекущиеДанные.НоменклатураЛДСП", Текст);
			КонецЕсли;
		Иначе	
			ПроверитьКромкиЛДСП(Ошибки, Строка.НоменклатураЛДСП);
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Строка.КромкаЛДСП) Тогда
			Если ЗначениеЗаполнено(ШапкаОсновныхНастроек.КромкаЛДСП) Тогда
				Строка.КромкаЛДСП = ШапкаОсновныхНастроек.КромкаЛДСП;
			Иначе
				Элементы.Детали.ТекущаяСтрока = Строка.ПолучитьИдентификатор();
				Текст = "Не выбрана кромка у " + Строка.ВидИзделия;
				ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "Элементы.Детали.ТекущиеДанные.КромкаЛДСП", Текст);
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(РучкиЯщиков) И Строка.ЕстьРучки Тогда
			Текст = "Не выбраны ручки у " + Строка.ВидИзделия;
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "РучкиЯщиков", Текст);
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Строка.Крючки) И Строка.ЕстьКрючки Тогда
			Текст = "Не выбраны крючки у " + Строка.ВидИзделия;
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "Элементы.Детали.ТекущиеДанные.Крючки", Текст);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Строка.НоменклатураЛДСПДоп) Тогда	
			ПроверитьКромкиЛДСП(Ошибки, Строка.НоменклатураЛДСПДоп);
		КонецЕсли;
		
		МинимальнаяГлубина = ?(ЗначениеЗаполнено(МинимальнаяГлубина), Макс(МинимальнаяГлубина, Строка.ГлубинаИзделияМин), Строка.ГлубинаИзделияМин);
		МаксимальнаяГлубина = ?(ЗначениеЗаполнено(МаксимальнаяГлубина), Мин(МаксимальнаяГлубина, Строка.ГлубинаИзделияМакс), Строка.ГлубинаИзделияМакс);
		
	КонецЦикла;
	
	Если ГлубинаИтог > МаксимальнаяГлубина ИЛИ ГлубинаИтог < МинимальнаяГлубина Тогда
		Текст = "Шкаф не подходит по глубине. Диапазон глубины изделия Мин = " + МинимальнаяГлубина + ", Макс = " + МаксимальнаяГлубина;
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "ГлубинаИтог", Текст);
	КонецЕсли;
	
	МинимальнаяВысота = Макс(СтруктураДиапазонов.ВысотаМинЛевый, СтруктураДиапазонов.ВысотаМинЦентр, СтруктураДиапазонов.ВысотаМинПравый);
	МаксимальнаяВысота = СтруктураДиапазонов.ВысотаМаксЦентр;	
	Если ЕстьЛевыйБоковой И ЕстьПравыйБоковой Тогда
		МаксимальнаяВысота = Мин(СтруктураДиапазонов.ВысотаМаксЛевый, МаксимальнаяВысота, СтруктураДиапазонов.ВысотаМаксПравый);
	ИначеЕсли ЕстьЛевыйБоковой Тогда 						
		МаксимальнаяВысота = Мин(СтруктураДиапазонов.ВысотаМаксЛевый, МаксимальнаяВысота);
	ИначеЕсли ЕстьПравыйБоковой Тогда
		МаксимальнаяВысота = Мин(СтруктураДиапазонов.ВысотаМаксПравый, МаксимальнаяВысота);
	КонецЕсли;
	
	Если ВысотаИтог > МаксимальнаяВысота ИЛИ ВысотаИтог < МинимальнаяВысота Тогда
		Текст = "Шкаф не подходит по высоте. Диапазон высоты изделия Мин = " + МинимальнаяВысота + ", Макс = " + МаксимальнаяВысота;
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "ВысотаИтог", Текст);
	КонецЕсли;
	
	МинимальнаяШирина = Макс(СтруктураДиапазонов.ШиринаМинВерхний, СтруктураДиапазонов.ШиринаМинЦентр, СтруктураДиапазонов.ШиринаМинНижний);
	МаксимальнаяШирина = СтруктураДиапазонов.ШиринаМаксЦентр;	
	Если ЗначениеЗаполнено(СтруктураДиапазонов.ШиринаМаксВерхний) И ЗначениеЗаполнено(СтруктураДиапазонов.ШиринаМаксНижний) Тогда
		МаксимальнаяВысота = Мин(СтруктураДиапазонов.ШиринаМаксВерхний, МаксимальнаяШирина, СтруктураДиапазонов.ШиринаМаксНижний);
	ИначеЕсли ЗначениеЗаполнено(СтруктураДиапазонов.ШиринаМаксВерхний) Тогда 						
		МаксимальнаяВысота = Мин(СтруктураДиапазонов.ШиринаМаксВерхний, МаксимальнаяШирина);
	ИначеЕсли ЗначениеЗаполнено(СтруктураДиапазонов.ШиринаМаксНижний) Тогда
		МаксимальнаяВысота = Мин(СтруктураДиапазонов.ШиринаМаксНижний, МаксимальнаяШирина);
	КонецЕсли;
	
	Если ШиринаИтог > МаксимальнаяШирина ИЛИ ШиринаИтог < МинимальнаяШирина Тогда
		Текст = "Шкаф не подходит по ширине. Диапазон ширины изделия Мин = " + МинимальнаяШирина + ", Макс = " + МаксимальнаяШирина;
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "ШиринаИтог", Текст);
	КонецЕсли;
	
	Если Ошибки <> Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки);
		Возврат Неопределено;
	КонецЕсли;
	
	# Область СозданиеСтрокиПрипусков
	
	Для Каждого Элемент Из Детали Цикл
		
		Элемент.СтрокаПрипусков = "";
		Элемент.СтрокаЗавалов = "";
		
		Если Элемент.ВидИзделия = Справочники.ВидыИзделийПоКаталогу.Потолок Тогда 
			
			Если НЕ ЕстьЛевыйБоковой ИЛИ НеВлияетНаОсновнойСлева Тогда
				Элемент.СтрокаПрипусков = Элемент.СтрокаПрипусков + "ПрипускСлева";
			КонецЕсли;
			Если НЕ ЕстьПравыйБоковой ИЛИ НеВлияетНаОсновнойСправа Тогда
				Элемент.СтрокаПрипусков = Элемент.СтрокаПрипусков + "ПрипускСправа";
			КонецЕсли;
			
			Элемент.СтрокаЗавалов = "ЗавалСтенкаЗавалПотолок";
			
		ИначеЕсли Элемент.ВидИзделия = Справочники.ВидыИзделийПоКаталогу.Пол Тогда
			
			Если НЕ ЕстьЛевыйБоковой ИЛИ НеВлияетНаОсновнойСлева Тогда
				Элемент.СтрокаПрипусков = Элемент.СтрокаПрипусков + "ПрипускСлева";
			КонецЕсли;
			Если НЕ ЕстьПравыйБоковой ИЛИ НеВлияетНаОсновнойСправа Тогда
				Элемент.СтрокаПрипусков = Элемент.СтрокаПрипусков + "ПрипускСправа";
			КонецЕсли;
			
			Элемент.СтрокаЗавалов = "ЗавалСтенкаЗавалПол";
			
		ИначеЕсли Элемент.ВидИзделия = Справочники.ВидыИзделийПоКаталогу.ЛевыйБоковойЭлемент Тогда
			
			Если НЕ ЕстьКрышаНаВсе Тогда
				Элемент.СтрокаПрипусков = Элемент.СтрокаПрипусков + "ПрипускСверху";
			КонецЕсли;
			Элемент.СтрокаПрипусков = Элемент.СтрокаПрипусков + "ПрипускСнизу";
			
			Элемент.СтрокаЗавалов = "ЗавалСтенкаЗавалЛевый";
			
		ИначеЕсли Элемент.ВидИзделия = Справочники.ВидыИзделийПоКаталогу.ПравыйБоковойЭлемент Тогда
			
			Если НЕ ЕстьКрышаНаВсе Тогда
				Элемент.СтрокаПрипусков = Элемент.СтрокаПрипусков + "ПрипускСверху";
			КонецЕсли;
			Элемент.СтрокаПрипусков = Элемент.СтрокаПрипусков + "ПрипускСнизу";
			
			Элемент.СтрокаЗавалов = "ЗавалСтенкаЗавалПравый";
			
		ИначеЕсли Элемент.ВидИзделия = Справочники.ВидыИзделийПоКаталогу.ОсновнойЭлемент Тогда
			
			Если (НЕ ЕстьКрыша И НЕ ЕстьКрышаНаВсе) ИЛИ НеВлияетНаОсновнойСверху Тогда
				Элемент.СтрокаПрипусков = Элемент.СтрокаПрипусков + "ПрипускСверху";
				Элемент.СтрокаЗавалов = Элемент.СтрокаЗавалов + "ЗавалПотолок";
			КонецЕсли;
			Если НЕ ЕстьЛевыйБоковой ИЛИ НеВлияетНаОсновнойСлева Тогда
				Элемент.СтрокаПрипусков = Элемент.СтрокаПрипусков + "ПрипускСлева";
				Элемент.СтрокаЗавалов = Элемент.СтрокаЗавалов + "ЗавалЛевый";
			КонецЕсли;
			Если НЕ ЕстьПол ИЛИ НеВлияетНаОсновнойСнизу Тогда
				Элемент.СтрокаПрипусков = Элемент.СтрокаПрипусков + "ПрипускСнизу";
				Элемент.СтрокаЗавалов = Элемент.СтрокаЗавалов + "ЗавалПол";
			КонецЕсли;
			Если НЕ ЕстьПравыйБоковой ИЛИ НеВлияетНаОсновнойСправа Тогда
				Элемент.СтрокаПрипусков = Элемент.СтрокаПрипусков + "ПрипускСправа";
				Элемент.СтрокаЗавалов = Элемент.СтрокаЗавалов + "ЗавалПравый";
			КонецЕсли;
			Элемент.СтрокаЗавалов = Элемент.СтрокаЗавалов + "ЗавалСтенка";
			
		КонецЕсли;
		
	КонецЦикла;
	
	# КонецОбласти
	
	Структура = Новый Структура;
	Структура.Вставить("Детали", Детали.Выгрузить());
	Структура.Вставить("Двери", ?(ЗначениеЗаполнено(Двери), Двери, Неопределено));
	Структура.Вставить("Модифицированность", Модифицированность);
	
	Возврат ПоместитьВоВременноеХранилище(Структура);
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьСтруктуруИзделия(Изделие, ВидИзделия)
	
	ЕстьРучкиФасада = Ложь;
	ЕстьЯщики = Изделие.СписокЯщики.Количество() > 0;
	ЕстьЛДСПДоп = ЗначениеЗаполнено(Изделие.СписокМатериалы.Найти(Истина, "ВторойЦвет"));
	ЕстьКрючки = ЗначениеЗаполнено(Изделие.СписокНоменклатуры.Найти(Справочники.НоменклатурныеГруппы.Крючок, "Номенклатура"));
	
	СпрДВП = Справочники.ВидыДеталей.НайтиПоНаименованию("ДВП");
	СпрЛДСП = Справочники.ВидыДеталей.НайтиПоНаименованию("ФасадЛДСП");
	
	ЕстьДВП = ЗначениеЗаполнено(Изделие.СписокМатериалы.Найти(СпрДВП , "ВидДетали"));
	
	МассивФасадов = Изделие.СписокМатериалы.НайтиСтроки(Новый Структура("ВидДетали", СпрЛДСП));
	
	Если МассивФасадов.Количество() > 0 Тогда
		
		Для Каждого Элемент Из МассивФасадов Цикл
			
			Если ЗначениеЗаполнено(Элемент.РасположениеПазовИРучкиНаФасадах) Тогда
				ЕстьРучкиФасада = НЕ Элемент.РасположениеПазовИРучкиНаФасадах.БезРучки;
			КонецЕсли;
			
			Если ЕстьРучкиФасада Тогда
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	СтруктураАдреса = ПолучитьАдрес(Изделие, ВидИзделия);
	
	Структура = Новый Структура;
	Структура.Вставить("АдресЭлемента", СтруктураАдреса.АдресЭлемента);
	Структура.Вставить("АдресКрыши", СтруктураАдреса.АдресКрыши);
	Структура.Вставить("ВысотаИзделия", Изделие.ВысотаИзделия);
	Структура.Вставить("ШиринаИзделия", Изделие.ШиринаИзделия);
	Структура.Вставить("ГлубинаИзделия", Изделие.ГлубинаИзделия);
	Структура.Вставить("ВысотаИзделияМин", Изделие.ВысотаИзделияМин);
	Структура.Вставить("ШиринаИзделияМин", Изделие.ШиринаИзделияМин);
	Структура.Вставить("ГлубинаИзделияМин", Изделие.ГлубинаИзделияМин);
	Структура.Вставить("ВысотаИзделияМакс", Изделие.ВысотаИзделияМакс);
	Структура.Вставить("ШиринаИзделияМакс", Изделие.ШиринаИзделияМакс);
	Структура.Вставить("ГлубинаИзделияМакс", Изделие.ГлубинаИзделияМакс);
	Структура.Вставить("ВысотаИзделияСтрока", Изделие.ВысотаИзделияСтрока);
	Структура.Вставить("ШиринаИзделияСтрока", Изделие.ШиринаИзделияСтрока);
	Структура.Вставить("ГлубинаИзделияСтрока", Изделие.ГлубинаИзделияСтрока);
	Структура.Вставить("ГлубинаИзделияСтрока", Изделие.ГлубинаИзделияСтрока);
	Структура.Вставить("ВидИзделияПоКаталогу", Изделие.ВидИзделияПоКаталогу); 
	Структура.Вставить("ЕстьЯщики", ЕстьЯщики);
	Структура.Вставить("ЕстьДВП", ЕстьДВП);
	Структура.Вставить("ЕстьЛДСПДоп", ЕстьЛДСПДоп);
	Структура.Вставить("НаВсеИзделие", Изделие.НаВсеИзделие);
	Структура.Вставить("НеВлияетНаОсновной", Изделие.НеВлияетНаОсновной);
	Структура.Вставить("ЕстьРучки", ЕстьЯщики ИЛИ ЕстьРучкиФасада);
	Структура.Вставить("ЕстьКрючки", ЕстьКрючки);
	
	Возврат Структура;
	
КонецФункции

&НаКлиенте
Функция ПолучитьРазмерыОсновногоЭлемента()
	
	НашОсновнойЭлемент = Детали.НайтиСтроки(Новый Структура("ВидИзделия", ВидыИзделий.ОсновнойЭлемент));
	ЕстьОсновной = НашОсновнойЭлемент.Количество() > 0;
	Если ЕстьОсновной Тогда
		Размеры = Новый Структура;
		Размеры.Вставить("Глубина", ГлубинаИтог);
		
		ШиринаОсновногоЭлемента = ШиринаИтог;
		ВысотаОсновногоЭлемента = ВысотаИтог;
		ВысотаКрыша = 0;
		
		Для Каждого Строка Из Детали Цикл
			Если Строка.ВидИзделия = ВидыИзделий.Потолок Тогда
				Если Строка.НаВсеИзделие Тогда
					ВысотаКрыша = Строка.ВысотаИзделия;
				КонецЕсли;
				Если НЕ Строка.НеВлияетНаОсновной Тогда
					ВысотаОсновногоЭлемента = ВысотаОсновногоЭлемента - Строка.ВысотаИзделия;	
				КонецЕсли;
			ИначеЕсли Строка.ВидИзделия = ВидыИзделий.Пол И НЕ Строка.НеВлияетНаОсновной Тогда
				ВысотаОсновногоЭлемента = ВысотаОсновногоЭлемента - Строка.ВысотаИзделия;
			ИначеЕсли (Строка.ВидИзделия = ВидыИзделий.ЛевыйБоковойЭлемент
				ИЛИ Строка.ВидИзделия = ВидыИзделий.ПравыйБоковойЭлемент)
				И НЕ Строка.НеВлияетНаОсновной Тогда
				ШиринаОсновногоЭлемента = ШиринаОсновногоЭлемента - Строка.ШиринаИзделия;	
			КонецЕсли;	
		КонецЦикла;                                                      
		
		Размеры.Вставить("Ширина", ШиринаОсновногоЭлемента);
		Размеры.Вставить("Высота", ВысотаОсновногоЭлемента);
		Размеры.Вставить("ВысотаБоковой", ВысотаИтог - ВысотаКрыша);
		
		Возврат Размеры;
	Иначе
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Выберите основной элемент", , "ОсновнойЭлемент");
		
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура УстановитьДоступность(ВидИзделия)
	
	СтрокаДетали = Детали.НайтиСтроки(Новый Структура("ВидИзделия", ВидИзделия));
	ЕстьИзделие = Истина;
	
	Если СтрокаДетали.Количество() = 1 Тогда
		
		НашаДеталь = СтрокаДетали[0];
		
		ЭтоБоковаяПанель = НашаДеталь.ВидИзделияПоКаталогу = ВидыИзделий.БоковаяПанельСлева ИЛИ
		НашаДеталь.ВидИзделияПоКаталогу = ВидыИзделий.БоковаяПанельСправа;
		
		Элементы.ДеталиЗавал.ТолькоПросмотр = ЭтоБоковаяПанель;
		
		МассивВидСтены = Новый Массив;
		МассивВидСтены.Добавить("Бетон");
		Если ВидИзделия = ВидыИзделий.Пол Тогда
			МассивВидСтены.Добавить("Ламинат");
		Иначе
			МассивВидСтены.Добавить("Гипс");
		КонецЕсли;
		Если ВидИзделия = ВидыИзделий.Потолок Тогда
			МассивВидСтены.Добавить("Натяжной");
		КонецЕсли;
		Элементы.ДеталиВидСтены.СписокВыбора.Очистить();
		Элементы.ДеталиВидСтены.СписокВыбора.ЗагрузитьЗначения(МассивВидСтены);
		Элементы.ДеталиВидСтены.ТолькоПросмотр = ЭтоБоковаяПанель;
		
	Иначе
		
		ЕстьИзделие = Ложь;
		
	КонецЕсли;
	
	ИзделиеДоступно = ЕстьИзделие И НЕ НашаДеталь.Предопределенный;
	
	Элементы.ДеталиНоменклатураЛДСП.Доступность = ИзделиеДоступно;
	Элементы.ДеталиКромкаЛДСП.Доступность = ИзделиеДоступно;
	Элементы.РучкиЯщиков.Доступность = ИзделиеДоступно И НашаДеталь.ЕстьРучки;
	Элементы.ДеталиКрючки.Доступность = ИзделиеДоступно И НашаДеталь.ЕстьКрючки; 
	Элементы.ДеталиНоменклатураЛДСПДоп.Доступность = ИзделиеДоступно И НашаДеталь.ЕстьЛДСПДоп;
	Элементы.ДеталиКромкаЛДСПДоп.Доступность = ИзделиеДоступно И НашаДеталь.ЕстьЛДСПДоп;
	Элементы.ДеталиНоменклатураДВП.Доступность = ИзделиеДоступно И НашаДеталь.ЕстьДВП;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьКартинку(АдресЭлемента, ЭлементИзделия, АдресКрыши, ТолькоКрыша = Ложь)
	
	Если ЗначениеЗаполнено(АдресЭлемента) Тогда
		
		Если НЕ ТолькоКрыша Тогда
			ПутьЭлемента = РабочийКаталог + АдресЭлемента;
			АдресВХранилище = "";
			Если ЗначениеЗаполнено(ПутьЭлемента) Тогда
				ИмяФайла = ПутьЭлемента;
				ФайлИзображения = Новый Файл(ИмяФайла);
				Если ФайлИзображения.Существует() Тогда
					ПоместитьФайл(АдресВХранилище, ИмяФайла, , Ложь, ЭтаФорма.УникальныйИдентификатор);
				КонецЕсли;
			КонецЕсли;
			
			ЭтаФорма[ЭлементИзделия] = АдресВХранилище;
		КонецЕсли;
		
		ЕстьКрыша = Детали.НайтиСтроки(Новый Структура("ВидИзделияПоКаталогу", ВидыИзделий.Крыша)).Количество()>0;
		
		Если ЗначениеЗаполнено(АдресКрыши) И ЕстьКрыша Тогда
			
			ПутьКрыши = РабочийКаталог + АдресКрыши;
			АдресВХранилище = "";
			Если ЗначениеЗаполнено(ПутьКрыши) Тогда
				ИмяФайла = ПутьКрыши;
				ФайлИзображения = Новый Файл(ИмяФайла);
				Если ФайлИзображения.Существует() Тогда
					ПоместитьФайл(АдресВХранилище, ИмяФайла, , Ложь, ЭтаФорма.УникальныйИдентификатор);
				КонецЕсли;
			КонецЕсли;
			
			Если Найти(ЭлементИзделия, "Прав") > 0 Тогда
				ЭтаФорма.ПотолокПравый = АдресВХранилище;
			Иначе
				ЭтаФорма.ПотолокЛевый = АдресВХранилище;
			КонецЕсли;
			
		КонецЕсли;
		
		Если СтенаСверху И (ЗначениеЗаполнено(ПотолокЛевый) ИЛИ ЗначениеЗаполнено(ПотолокПравый)) ИЛИ НЕ ЕстьКрыша Тогда
			ПотолокЛевый = "";
			ПотолокПравый = "";
		ИначеЕсли НЕ СтенаСверху И ЭлементИзделия = "Потолок" Тогда
			
			Для Каждого Элемент Из Детали Цикл
				Если Элемент.ВидИзделия = ВидыИзделий.ПравыйБоковойЭлемент
					ИЛИ Элемент.ВидИзделия = ВидыИзделий.ЛевыйБоковойЭлемент Тогда
					ОтобразитьКартинку(Элемент.АдресЭлемента, СтрЗаменить(Строка(Элемент.ВидИзделия), " ", ""), Элемент.АдресКрыши, Истина);
				КонецЕсли;				
			КонецЦикла;
			
		КонецЕсли;
		
	Иначе
		ВызватьИсключение "Ошибка 735: Критическая ошибка каталога (нет изображения изделия)!!!";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСтроку(НашаСтрока, ВидИзделия, Изделие = Неопределено, СтруктураИзделия = Неопределено, ЭлементИзделия = Неопределено)
	
	ЭтоПредопределенный = Ложь;
	Ошибки = Неопределено;
	АдресЭлемента = "";
	АдресКрыши = "";
	
	Если Изделие = Неопределено Тогда
		СтрокаПредопределенных = АдресаПредопределенных.НайтиСтроки(Новый Структура("ВидИзделия", ВидИзделия));
		Если СтрокаПредопределенных.Количество() = 1 Тогда
			ЭтоПредопределенный = Истина;
			
			ТекущаяСтрока = СтрокаПредопределенных[0];
			Изделие	= ТекущаяСтрока.Изделие;
			АдресЭлемента = ТекущаяСтрока.АдресИзделия;
			АдресКрыши = ТекущаяСтрока.АдресКрыши;
			ВидИзделияПоКаталогу = ТекущаяСтрока.ВидИзделияПоКаталогу;
			
			СписокНесовместимых = ТекущаяСтрока.СписокНесовместимых;
			Для Каждого СтрокаДетали Из Детали Цикл
				Если СписокНесовместимых.НайтиПоЗначению(СтрокаДетали.Изделие) <> Неопределено Тогда
					Текст = "Изделие """ + Изделие + """ несовместимо с """ + СтрокаДетали.Изделие + """";
					ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, , Текст);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Если Ошибки <> Неопределено Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки);
		Возврат;
	КонецЕсли;
	
	Если Изделие <> Неопределено Тогда
		НашаСтрока.Изделие = Изделие;
		НашаСтрока.ВидИзделия = ВидИзделия;
		Если СтруктураИзделия <> Неопределено Тогда
			ЗаполнитьЗначенияСвойств(НашаСтрока, СтруктураИзделия);
		Иначе
			НашаСтрока.ВидИзделияПоКаталогу = ВидИзделияПоКаталогу;
			НашаСтрока.ВысотаИзделия = 0;
			НашаСтрока.ШиринаИзделия = 0;
			НашаСтрока.ГлубинаИзделия = 0;
			НашаСтрока.ВысотаИзделияМин = 0;
			НашаСтрока.ШиринаИзделияМин = 0;
			НашаСтрока.ГлубинаИзделияМин = 0;
			НашаСтрока.ВысотаИзделияМакс = 0;
			НашаСтрока.ШиринаИзделияМакс = 0;
			НашаСтрока.ГлубинаИзделияМакс = 0;
			НашаСтрока.ВысотаИзделияСтрока = "";
			НашаСтрока.ШиринаИзделияСтрока = "";
			НашаСтрока.ГлубинаИзделияСтрока = "";
			НашаСтрока.ЕстьЯщики = Ложь;
			НашаСтрока.ЕстьРучки = Ложь;
			НашаСтрока.ЕстьКрючки = Ложь;
			НашаСтрока.ЕстьДВП = Ложь;
			НашаСтрока.НаВсеИзделие = Ложь;
			НашаСтрока.НеВлияетНаОсновной = Ложь;
		КонецЕсли;
		
		НашаСтрока.Предопределенный = ЭтоПредопределенный;
		
		Если НЕ Корпусный Тогда
			НашаСтрока.ВидСтены = ?(ЗначениеЗаполнено(НашаСтрока.ВидСтены), НашаСтрока.ВидСтены, "Бетон");
			ЭтоБоковаяПанель = НашаСтрока.ВидИзделияПоКаталогу = ВидыИзделий.БоковаяПанельСлева ИЛИ
			НашаСтрока.ВидИзделияПоКаталогу = ВидыИзделий.БоковаяПанельСправа;
			НашаСтрока.Завал = ?(ЭтоБоковаяПанель, 0, НашаСтрока.Завал);	
		КонецЕсли;
		
		Элементы.Детали.ТекущаяСтрока = Детали.Индекс(НашаСтрока);
		
		Если ВидИзделия = ВидыИзделий.Потолок Тогда
			СтенаСверху = Изделие = ВидыИзделий.ПотолокЗаказчика;
		КонецЕсли;
		
		НашаСтрока.АдресЭлемента = ?(СтруктураИзделия <> Неопределено, СтруктураИзделия.АдресЭлемента, АдресЭлемента);
		НашаСтрока.АдресКрыши = ?(СтруктураИзделия <> Неопределено, СтруктураИзделия.АдресКрыши, АдресКрыши);
		ОтобразитьКартинку(НашаСтрока.АдресЭлемента, ЭлементИзделия, НашаСтрока.АдресКрыши);
		
		Если ВидИзделия = ВидыИзделий.ОсновнойЭлемент Тогда
			НашаСтрока.ПредставлениеВидаИзделия = "Основной";
		КонецЕсли;
		
		Если ЗначениеЗаполнено(НашаСтрока.ВысотаИзделияСтрока) ИЛИ ЗначениеЗаполнено(НашаСтрока.ШиринаИзделияСтрока) ИЛИ ЗначениеЗаполнено(НашаСтрока.ГлубинаИзделияСтрока) Тогда
			
			ЗавалПол = 0;
			ЗавалЛевый = 0;
			ЗавалПравый = 0;
			ЗавалПотолок = 0;
			ЗавалСтенка = 0;
			
			ПолучитьЗавалы(ЗавалПол, ЗавалЛевый, ЗавалПравый, ЗавалПотолок, ЗавалСтенка, НашаСтрока);
			
			НоменклатураМатериала = НашаСтрока.НоменклатураМатериала;
			
			Если ЗначениеЗаполнено(НашаСтрока.ВысотаИзделияСтрока) Тогда
				Попытка
					Выполнить("НашаСтрока.ВысотаИзделия = " + НашаСтрока.ВысотаИзделияСтрока);
				Исключение
					Текст = "Ошибка при формировании формул для Высоты каталога изделий " + НашаСтрока.Изделие;
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст);
					ЗаполнитьСтроку(НашаСтрока, ТекущийЭлементИзделия,,, ЭлементИзделия);
				КонецПопытки;	
			КонецЕсли;
			
			Если ЗначениеЗаполнено(НашаСтрока.ШиринаИзделияСтрока) Тогда
				Попытка
					Выполнить("НашаСтрока.ШиринаИзделия = " + НашаСтрока.ШиринаИзделияСтрока);
				Исключение
					Текст = "Ошибка при формировании формул для Ширины каталога изделий " + НашаСтрока.Изделие;
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст);
					ЗаполнитьСтроку(НашаСтрока, ТекущийЭлементИзделия,,, ЭлементИзделия);
				КонецПопытки;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(НашаСтрока.ГлубинаИзделияСтрока) Тогда
				Попытка
					Выполнить("НашаСтрока.ГлубинаИзделия = " + НашаСтрока.ГлубинаИзделияСтрока);
				Исключение
					Текст = "Ошибка при формировании формул для Глубины каталога изделий " + НашаСтрока.Изделие;
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст);
					ЗаполнитьСтроку(НашаСтрока, ТекущийЭлементИзделия,,, ЭлементИзделия);
				КонецПопытки;
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		Детали.Удалить(НашаСтрока);
		ЭтаФорма[ЭлементИзделия] = "";
	КонецЕсли;
	
	УстановитьДоступность(ВидИзделия);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	РабочийКаталог = ФайловыеФункцииСлужебныйКлиент.ВыбратьПутьККаталогуДанныхПользователя();
	
	Если ЗначениеЗаполнено(Двери) Тогда		
		РазмерыДвери = ОпределитьРазмерыДвери();
		ВысотаПроемаДвери = РазмерыДвери.ВысотаПроема;
		ШиринаПроемаДвери = РазмерыДвери.ШиринаПроема;
	КонецЕсли;
	
	Если Детали.Количество() > 1 Тогда
		
		Если ЗначениеЗаполнено(Детали[1].НоменклатураЛДСП) Тогда
			
			СтруктураПодставляяемойНоменклатуры = ЛексСервер.ПолучитьСтруктуруПодставляемойНоменклатурыПоЦветуЛДСП(Детали[1].НоменклатураЛДСП, Подразделение);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Для Каждого Элемент Из Детали Цикл
		
		ОтобразитьКартинку(Элемент.АдресЭлемента, СтрЗаменить(Строка(Элемент.ВидИзделия), " ", ""), Элемент.АдресКрыши);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Подразделение = Параметры.Подразделение;
	Спецификация = Параметры.Спецификация;
	ИзделиеСпец = Параметры.Изделие;
	Корпусный = ИзделиеСпец = Справочники.Изделия.КорпусныйШкафКупеПоКаталогу;
	
	Если Параметры.Свойство("Офис") И ЗначениеЗаполнено(Параметры.ОФис) Тогда
		НаценкаОфиса = Параметры.ОФис.КоэффициентМатериалы;
	Иначе
		НаценкаОфиса = 1;
	КонецЕсли;
		
	Дилерский = Спецификация.Дилерский;
	
	Элементы.ДеталиЗавал.Видимость = НЕ Корпусный;
	Элементы.ДеталиВидСтены.Видимость = НЕ Корпусный;
	
	ЗагрузкаАдресаПредопределенных();
	
	ЗаполнитьВидыИзделий();
	ЗаполнитьНомГруппы();
	ЗаполнитьШапкуОсновныхНастроек();
	
	ПерезаполнитьСтруктуруИзделий();	
			
КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьСтруктуруИзделий()
	
	СтруктураФормы = ПолучитьИзВременногоХранилища(Параметры.АдресТаблицы);
	Двери = СтруктураФормы.Двери;
	
	Если ЗначениеЗаполнено(СтруктураФормы.Детали) Тогда
		Детали.Загрузить(СтруктураФормы.Детали.Выгрузить());
	КонецЕсли;
	
	Если Детали.Количество() = 0 Тогда
		Для Каждого ЗначениеЭлемента Из АдресаПредопределенных Цикл
			НоваяСтрока = Детали.Добавить();
			НоваяСтрока.Изделие = ЗначениеЭлемента.Изделие;
			НоваяСтрока.ВидИзделия = ЗначениеЭлемента.ВидИзделия;
		КонецЦикла;
	КонецЕсли;
	
	НоменклатураМатериала = Новый Структура;
	НоменклатураМатериала.Вставить("ДлинаДетали", 0);
	НоменклатураМатериала.Вставить("ШиринаДетали ", 0);
	НоменклатураМатериала.Вставить("ГлубинаДетали ", 0);
	
	Для Каждого Строка Из Детали Цикл

		НашеИзделие = Строка.Изделие;
		НашВидИзделия = ОпределитьКорневойВидИзделия(Строка.ВидИзделия);
		
		СтруктураИзделия = ПолучитьСтруктуруИзделия(НашеИзделие, НашВидИзделия);
		
		Строка.НоменклатураМатериала = НоменклатураМатериала;
		Строка.НоменклатураМатериала.Вставить("ДлинаДетали", Строка.НоменклатураЛДСП.ДлинаДетали);
		Строка.НоменклатураМатериала.Вставить("ШиринаДетали", Строка.НоменклатураЛДСП.ШиринаДетали);
		Строка.НоменклатураМатериала.Вставить("ГлубинаДетали", Строка.НоменклатураЛДСП.ГлубинаДетали);
		            
		Если НашВидИзделия = Справочники.ВидыИзделийПоКаталогу.ОсновнойЭлемент Тогда
			
			ГлубинаИтог = Строка.ГлубинаИзделия;
			
			ВысотаОсновного = Строка.ВысотаИзделия;
			ШиринаОсновного = Строка.ШиринаИзделия;
			
			РучкиЯщиков = Строка.Ручка;
			
			ШапкаОсновныхНастроек.Вставить("НоменклатураЛДСП", Строка.НоменклатураЛДСП);
			ШапкаОсновныхНастроек.Вставить("КромкаЛДСП", Строка.КромкаЛДСП);
			ШапкаОсновныхНастроек.Вставить("НоменклатураМатериала", НоменклатураМатериала);
			
			Строка.ПредставлениеВидаИзделия = "Основной";
			
		ИначеЕсли НашВидИзделия = Справочники.ВидыИзделийПоКаталогу.ЛевыйБоковойЭлемент Тогда
			
			РучкиЯщиков = Строка.Ручка;
			Строка.ПредставлениеВидаИзделия = "Левый";
			
		ИначеЕсли НашВидИзделия = Справочники.ВидыИзделийПоКаталогу.Пол Тогда
			
			Строка.ПредставлениеВидаИзделия = "Пол";
			
		ИначеЕсли НашВидИзделия = Справочники.ВидыИзделийПоКаталогу.ПравыйБоковойЭлемент Тогда
			
			РучкиЯщиков = Строка.Ручка;
			Строка.ПредставлениеВидаИзделия = "Правый";
			
		ИначеЕсли НашВидИзделия = Справочники.ВидыИзделийПоКаталогу.Потолок Тогда
			
			Строка.ПредставлениеВидаИзделия = "Потолок";
			СтенаСверху = НашеИзделие = Справочники.КаталогИзделий.ПотолокЗаказчика;
			
		КонецЕсли;
		
		Строка.АдресЭлемента = СтруктураИзделия.АдресЭлемента;
		Строка.АдресКрыши = СтруктураИзделия.АдресКрыши;
		Строка.ШиринаИзделияМакс = СтруктураИзделия.ШиринаИзделияМакс;
		Строка.ВысотаИзделияМакс = СтруктураИзделия.ВысотаИзделияМакс;
		Строка.ГлубинаИзделияМакс = СтруктураИзделия.ГлубинаИзделияМакс;
		Строка.ШиринаИзделияМин = СтруктураИзделия.ШиринаИзделияМин;
		Строка.ВысотаИзделияМин = СтруктураИзделия.ВысотаИзделияМин;
		Строка.ГлубинаИзделияМин = СтруктураИзделия.ГлубинаИзделияМин;
		Строка.ШиринаИзделияСтрока = СтруктураИзделия.ШиринаИзделияСтрока;
		Строка.ВысотаИзделияСтрока = СтруктураИзделия.ВысотаИзделияСтрока;
		Строка.ГлубинаИзделияСтрока = СтруктураИзделия.ГлубинаИзделияСтрока;
		Строка.НаВсеИзделие = СтруктураИзделия.НаВсеИзделие;
		Строка.НеВлияетНаОсновной = СтруктураИзделия.НеВлияетНаОсновной;
		Строка.Предопределенный = НашеИзделие.Предопределенный;
		Строка.ВидИзделияПоКаталогу = НашеИзделие.ВидИзделияПоКаталогу;
		Строка.ЕстьЯщики = СтруктураИзделия.ЕстьЯщики;
		Строка.ЕстьДВП = СтруктураИзделия.ЕстьДВП;
		Строка.ЕстьЛДСПДоп = СтруктураИзделия.ЕстьЛДСПДоп;
		Строка.ЕстьРучки = СтруктураИзделия.ЕстьРучки;
		Строка.ЕстьКрючки = СтруктураИзделия.ЕстьКрючки;
		Строка.ВидСтены = ?(ЗначениеЗаполнено(Строка.ВидСтены), Строка.ВидСтены, "Бетон");
		
	КонецЦикла;
	
	ПолучитьИтоговыеРазмеры(ВысотаОсновного, ШиринаОсновного);	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьШапкуОсновныхНастроек()
	
	НоменклатураМатериала = Новый Структура;
	НоменклатураМатериала.Вставить("ДлинаДетали", 0);
	НоменклатураМатериала.Вставить("ШиринаДетали ", 0);
	НоменклатураМатериала.Вставить("ГлубинаДетали ", 0);
	
	ШапкаОсновныхНастроек = Новый Структура;
	
	ШапкаОсновныхНастроек.Вставить("НоменклатураЛДСП", "");
	ШапкаОсновныхНастроек.Вставить("КромкаЛДСП", "");
	ШапкаОсновныхНастроек.Вставить("НоменклатураМатериала", НоменклатураМатериала);
	ШапкаОсновныхНастроек.Вставить("НоменклатураЛДСПДоп", "");
	ШапкаОсновныхНастроек.Вставить("КромкаЛДСПДоп", "");
		
КонецПроцедуры


&НаСервере
Процедура ЗаполнитьНомГруппы()
	
	МассивНомГрупп = Новый Структура;
	
	МассивНомГрупп.Вставить("ЛДСП16", Справочники.НоменклатурныеГруппы.ЛДСП16);
	МассивНомГрупп.Вставить("КантТ", Справочники.НоменклатурныеГруппы.КантТ);
	МассивНомГрупп.Вставить("Кромка2_19", Справочники.НоменклатурныеГруппы.Кромка2_19);
	МассивНомГрупп.Вставить("ДВП", Справочники.НоменклатурныеГруппы.ДВП);
	МассивНомГрупп.Вставить("Ручка", Справочники.НоменклатурныеГруппы.Ручка);
	МассивНомГрупп.Вставить("Крючок", Справочники.НоменклатурныеГруппы.Крючок);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВидыИзделий()
	
	ВидыИзделий = Новый Структура;
		
	ВидыИзделий.Вставить("ОсновнойЭлемент", Справочники.ВидыИзделийПоКаталогу.ОсновнойЭлемент);
	
	ВидыИзделий.Вставить("ЛевыйБоковойЭлемент", Справочники.ВидыИзделийПоКаталогу.ЛевыйБоковойЭлемент);
	ВидыИзделий.Вставить("ПравыйБоковойЭлемент", Справочники.ВидыИзделийПоКаталогу.ПравыйБоковойЭлемент);
	
	ВидыИзделий.Вставить("БоковаяПанельСлева", Справочники.ВидыИзделийПоКаталогу.БоковаяПанельСлева);
	ВидыИзделий.Вставить("БоковаяПанельСправа", Справочники.ВидыИзделийПоКаталогу.БоковаяПанельСправа);
	
	ВидыИзделий.Вставить("БоковаяПанельЧерезОтбойнуюПланкуСлева", Справочники.ВидыИзделийПоКаталогу.БоковаяПанельЧерезОтбойнуюПланкуСлева);
	ВидыИзделий.Вставить("БоковаяПанельЧерезОтбойнуюПланкуСправа", Справочники.ВидыИзделийПоКаталогу.БоковаяПанельЧерезОтбойнуюПланкуСправа);
	
	ВидыИзделий.Вставить("КоробБоковойСлева", Справочники.ВидыИзделийПоКаталогу.КоробБоковойСлева);
	ВидыИзделий.Вставить("КоробБоковойСправа", Справочники.ВидыИзделийПоКаталогу.КоробБоковойСправа);
	
	ВидыИзделий.Вставить("ОтбойнаяПланкаСлева", Справочники.ВидыИзделийПоКаталогу.ОтбойнаяПланкаСлева);
	ВидыИзделий.Вставить("ОтбойнаяПланкаСправа", Справочники.ВидыИзделийПоКаталогу.ОтбойнаяПланкаСправа);
	
	ВидыИзделий.Вставить("СтенаСлева", Справочники.ВидыИзделийПоКаталогу.СтенаСлева);
	ВидыИзделий.Вставить("СтенаСправа", Справочники.ВидыИзделийПоКаталогу.СтенаСправа);
	
	ВидыИзделий.Вставить("ФальшПанельСлева", Справочники.ВидыИзделийПоКаталогу.ФальшПанельСлева);
	ВидыИзделий.Вставить("ФальшПанельСправа", Справочники.ВидыИзделийПоКаталогу.ФальшПанельСправа);
	
	ВидыИзделий.Вставить("Пол", Справочники.ВидыИзделийПоКаталогу.Пол);
	ВидыИзделий.Вставить("Потолок", Справочники.ВидыИзделийПоКаталогу.Потолок);
	
	ВидыИзделий.Вставить("ПотолокЗаказчика", Справочники.КаталогИзделий.ПотолокЗаказчика);
	
	ВидыИзделий.Вставить("Крыша", Справочники.ВидыИзделийПоКаталогу.Крыша);
		
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.Двери") И ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		ДвериПриИзмененииНаКлиенте(ВыбранноеЗначение);
	КонецЕсли;
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.КаталогИзделий") И ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		ИзделиеПриИзменении(ВыбранноеЗначение);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

&НаКлиенте
Процедура ВыборИзделия(Команда)
	
	ЭлементИзделия = ЭтаФорма.ТекущийЭлемент.Имя;
	ТекущийЭлементИзделия = ВидыИзделий[ЭлементИзделия];
	
	МассивИзделий = Новый Массив;
	
	Для Каждого Строка Из Детали Цикл
		
		МассивИзделий.Добавить(Строка.Изделие);
		
		ВидИзделия = Строка.ВидИзделияПоКаталогу;
		
		Если Строка.ВидИзделия = ВидыИзделий.ПравыйБоковойЭлемент Тогда
			
			Если ВидИзделия = ВидыИзделий.БоковаяПанельСлева Тогда
				ВидИзделия = ВидыИзделий.БоковаяПанельСправа;
			ИначеЕсли ВидИзделия = ВидыИзделий.БоковаяПанельЧерезОтбойнуюПланкуСлева Тогда
				ВидИзделия = ВидыИзделий.БоковаяПанельЧерезОтбойнуюПланкуСправа;
			ИначеЕсли ВидИзделия = ВидыИзделий.КоробБоковойСлева Тогда
				ВидИзделия = ВидыИзделий.КоробБоковойСправа;
			ИначеЕсли ВидИзделия = ВидыИзделий.ОтбойнаяПланкаСлева Тогда
				ВидИзделия = ВидыИзделий.ОтбойнаяПланкаСправа;
			ИначеЕсли ВидИзделия = ВидыИзделий.СтенаСлева Тогда
				ВидИзделия = ВидыИзделий.СтенаСправа;
			ИначеЕсли ВидИзделия = ВидыИзделий.ФальшПанельСлева Тогда
				ВидИзделия = ВидыИзделий.ФальшПанельСправа;
			КонецЕсли;
			
		КонецЕсли;
		
		МассивИзделий.Добавить(ВидИзделия);
		
	КонецЦикла;	
	
	Пар = Новый Структура();
	Пар.Вставить("Изделие", ИзделиеСпец);
	Пар.Вставить("ВидИзделияПоКаталогу", ТекущийЭлементИзделия);
	Пар.Вставить("МассивИзделий", МассивИзделий);
	
	Если Дилерский Тогда
		Пар.Вставить("АктивныйДляДилера", Истина);	
	Иначе
		Пар.Вставить("Активный", Истина);
	КонецЕсли;
	
	Пар.Вставить("Корпусный", Корпусный);
	
	ОткрытьФормуМодально("Справочник.КаталогИзделий.ФормаВыбора",Пар,ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзделиеПриИзменении(Изделие)
	
	Если ЗначениеЗаполнено(Изделие) Тогда	
		
		ЭлементИзделия = ЭтаФорма.ТекущийЭлемент.Имя;
		ТекущийЭлементИзделия = ВидыИзделий[ЭлементИзделия];
		
		СтруктураИзделия = ПолучитьСтруктуруИзделия(Изделие, ТекущийЭлементИзделия);
		НашаСтрока = Детали.НайтиСтроки(Новый Структура("ВидИзделия", ТекущийЭлементИзделия));
		
		Если НашаСтрока.Количество() = 1 Тогда
			НоваяСтрока = НашаСтрока[0];
		Иначе
			НоваяСтрока = Детали.Добавить();
		КонецЕсли;
		
		НоваяСтрока.НоменклатураЛДСП = ШапкаОсновныхНастроек.НоменклатураЛДСП;
		НоваяСтрока.КромкаЛДСП = ШапкаОсновныхНастроек.КромкаЛДСП;
		НоваяСтрока.НоменклатураМатериала = ШапкаОсновныхНастроек.НоменклатураМатериала;
		
		ЗаполнитьСтроку(НоваяСтрока, ТекущийЭлементИзделия, Изделие, СтруктураИзделия, ЭлементИзделия);
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура Очистить(Команда)
	
	ЭлементИзделия = ЭтаФорма.ТекущийЭлемент.Имя;
	ТекущийЭлементИзделия = ВидыИзделий[ЭлементИзделия];
	НашаСтрока = Детали.НайтиСтроки(Новый Структура("ВидИзделия", ТекущийЭлементИзделия));
	
	Если НашаСтрока.Количество() = 1 Тогда
		ЗаполнитьСтроку(НашаСтрока[0], ТекущийЭлементИзделия,,, ЭлементИзделия);	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВДокумент(Команда)
	
	ОчиститьСообщения();
	
	РазмерыОсновногоЭлемента = ПолучитьРазмерыОсновногоЭлемента();
	Отказ = Ложь;
	
	Если РазмерыОсновногоЭлемента <> Неопределено Тогда 
			
		СтруктураФормы = ПроверитьИУстановитьРазмерыПередСохранением(РазмерыОсновногоЭлемента);
		
		Если ЗначениеЗаполнено(Двери) Тогда
			
			Отказ = ПроверкаДвери();
			
		КонецЕсли;
		
		Если (СтруктураФормы <> Неопределено) И (НЕ Отказ) Тогда
			Модифицированность = Ложь;
			ОповеститьОВыборе(СтруктураФормы);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ОБРАБОТКИ СВОЙСТВ И КАТЕГОРИЙ

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура ИзделиеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.Детали.ТекущиеДанные;
	ТекущийЭлементИзделия = ВидыИзделий[ЭтаФорма.ТекущийЭлемент.Имя];	
	НашаСтрока = Детали.НайтиСтроки(Новый Структура("ВидИзделия", ТекущийЭлементИзделия));
	ЭтоПредопределенный = Ложь;
	
	ЕстьЯщики = Ложь;
	ЕстьРучки = Ложь;
	ЕстьКрючки = Ложь;
	
	Если НашаСтрока.Количество() = 1 Тогда
		
		Элементы.Детали.ТекущаяСтрока = Детали.Индекс(НашаСтрока[0]);
		СтрокаПредопределенных = АдресаПредопределенных.НайтиСтроки(Новый Структура("Изделие",  ТекущиеДанные.Изделие));
		ЭтоПредопределенный = СтрокаПредопределенных.Количество() <> 0;
		
		Если НЕ ЭтоПредопределенный Тогда
			ЕстьЯщики = ТекущиеДанные.ЕстьЯщики;
			ЕстьРучки = ТекущиеДанные.ЕстьРучки;
			ЕстьКрючки = ТекущиеДанные.ЕстьКрючки;
			Если НЕ ЗначениеЗаполнено(ТекущиеДанные.НоменклатураЛДСП) И ЗначениеЗаполнено(ШапкаОсновныхНастроек.НоменклатураЛДСП) Тогда
				ТекущиеДанные.НоменклатураЛДСП = ШапкаОсновныхНастроек.НоменклатураЛДСП;
			ИначеЕсли ЗначениеЗаполнено(ТекущиеДанные.НоменклатураЛДСП) Тогда
				ШапкаОсновныхНастроек.Вставить("НоменклатураЛДСП", ТекущиеДанные.НоменклатураЛДСП);
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(ТекущиеДанные.КромкаЛДСП) И ЗначениеЗаполнено(ШапкаОсновныхНастроек.КромкаЛДСП) Тогда
				ТекущиеДанные.КромкаЛДСП = ШапкаОсновныхНастроек.КромкаЛДСП;
			ИначеЕсли ЗначениеЗаполнено(ТекущиеДанные.КромкаЛДСП) Тогда
				ШапкаОсновныхНастроек.Вставить("КромкаЛДСП", ТекущиеДанные.КромкаЛДСП);
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		Элементы.Детали.ТекущаяСтрока = Неопределено;
		
	КонецЕсли;
	
	УстановитьДоступность(ТекущийЭлементИзделия);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеталиНоменклатураЛДСППриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Детали.ТекущиеДанные;
	ЛДСП = ТекущиеДанные.НоменклатураЛДСП;
	ШапкаОсновныхНастроек.Вставить("НоменклатураЛДСП", ЛДСП);
	
	Структура = ОпределитьСтруктурыЛДСП(ЛДСП, Подразделение, ТекущиеДанные.НоменклатураМатериала);
	
	ТекущиеДанные.НоменклатураМатериала = Структура.НоменклатураМатериала;
	ШапкаОсновныхНастроек.Вставить("НоменклатураМатериала", ТекущиеДанные.НоменклатураМатериала);
	
	Если Найти(ТекущиеДанные.ВысотаИзделияСтрока, "НоменклатураМатериала") > 0 
		ИЛИ Найти(ТекущиеДанные.ШиринаИзделияСтрока, "НоменклатураМатериала") > 0
		ИЛИ Найти(ТекущиеДанные.ГлубинаИзделияСтрока, "НоменклатураМатериала") > 0 Тогда
		
		НоменклатураМатериала = ТекущиеДанные.НоменклатураМатериала;
		
		ЗавалПол = 0;
		ЗавалЛевый = 0;
		ЗавалПравый = 0;
		ЗавалПотолок = 0;
		ЗавалСтенка = 0;
		
		ПолучитьЗавалы(ЗавалПол, ЗавалЛевый, ЗавалПравый, ЗавалПотолок, ЗавалСтенка, ТекущиеДанные);
		
		Если Найти(ТекущиеДанные.ВысотаИзделияСтрока, "НоменклатураМатериала") Тогда
			Попытка
				Выполнить("ТекущиеДанные.ВысотаИзделия = " + ТекущиеДанные.ВысотаИзделияСтрока);
			Исключение
				Текст = "Ошибка при формировании формул для Высоты каталога изделий " + ТекущиеДанные.Изделие;
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст);
			КонецПопытки;	
		КонецЕсли;
		
		Если Найти(ТекущиеДанные.ШиринаИзделияСтрока, "НоменклатураМатериала") Тогда
			Попытка
				Выполнить("ТекущиеДанные.ШиринаИзделия = " + ТекущиеДанные.ШиринаИзделияСтрока);
			Исключение
				Текст = "Ошибка при формировании формул для Ширины каталога изделий " + ТекущиеДанные.Изделие;
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст);
			КонецПопытки;
		КонецЕсли;
		
		Если Найти(ТекущиеДанные.ГлубинаИзделияСтрока, "НоменклатураМатериала") Тогда
			Попытка
				Выполнить("ТекущиеДанные.ГлубинаИзделия = " + ТекущиеДанные.ГлубинаИзделияСтрока);
			Исключение
				Текст = "Ошибка при формировании формул для Глубины каталога изделий " + ТекущиеДанные.Изделие;
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст);
			КонецПопытки;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ТекущиеДанные.ЕстьЛДСПДоп Тогда
		
		ТекущиеДанные.НоменклатураЛДСПДоп = ЛДСП;	
		ШапкаОсновныхНастроек.Вставить("НоменклатураЛДСПДоп", ТекущиеДанные.НоменклатураЛДСПДоп);
		
	КонецЕсли;
	
	ТекущиеДанные.КромкаЛДСП = Структура.СтруктураПодставляяемойНоменклатуры.Кромка2_19;
	УстановитьПараметрыКромки(ТекущиеДанные)
	
КонецПроцедуры

&НаКлиенте
Процедура ДеталиКромкаЛДСППриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Детали.ТекущиеДанные;
	УстановитьПараметрыКромки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПараметрыКромки(ТекущиеДанные)
	
	ШапкаОсновныхНастроек.Вставить("КромкаЛДСП", ТекущиеДанные.КромкаЛДСП);
	
	Если ТекущиеДанные.ЕстьЛДСПДоп Тогда
		
		ТекущиеДанные.КромкаЛДСПДоп = ТекущиеДанные.КромкаЛДСП;	
		ШапкаОсновныхНастроек.Вставить("КромкаЛДСПДоп", ТекущиеДанные.КромкаЛДСПДоп);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеталиНоменклатураЛДСПДопПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Детали.ТекущиеДанные;
	НоменклатураЛДСПДоп = ТекущиеДанные.НоменклатураЛДСПДоп;	
	ШапкаОсновныхНастроек.Вставить("НоменклатураЛДСПДоп", НоменклатураЛДСПДоп);
	
	СтруктураПодставляяемойНоменклатуры = ЛексСервер.ПолучитьСтруктуруПодставляемойНоменклатурыПоЦветуЛДСП(НоменклатураЛДСПДоп, Подразделение);	
	ТекущиеДанные.КромкаЛДСПДоп = СтруктураПодставляяемойНоменклатуры.Кромка2_19;	
	ШапкаОсновныхНастроек.Вставить("КромкаЛДСПДоп", ТекущиеДанные.КромкаЛДСПДоп);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеталиКромкаЛДСПДопПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Детали.ТекущиеДанные;
	ТекущиеДанные.КромкаЛДСПДоп = ТекущиеДанные.КромкаЛДСП;	
	ШапкаОсновныхНастроек.Вставить("КромкаЛДСПДоп", ТекущиеДанные.КромкаЛДСПДоп);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ПОЛЯ ДВЕРИ

&НаКлиенте
Функция ПроверкаДвери()
	
	Отказ = Ложь;
	
	РазмерыДвери = ОпределитьРазмерыДвери();
	
	Если ВысотаПроемаДвери <> РазмерыДвери.ВысотаПроема ИЛИ ШиринаПроемаДвери <> РазмерыДвери.ШиринаПроема Тогда
		
		СписокКнопок = Новый СписокЗначений;	
		СписокКнопок.Добавить(КодВозвратаДиалога.Да, "Редактировать");
		СписокКнопок.Добавить(КодВозвратаДиалога.Нет, "Отмена");
		
		Ответ = Вопрос("Размеры двери не совпадают с размерами изделия.", СписокКнопок,,);
		
		Если Ответ = КодВозвратаДиалога.Да Тогда
			
			ПараметрыФормы = Новый Структура("Ключ", Двери);
			ПараметрыФормы.Вставить("ВысотаПроема", РазмерыДвери.ВысотаПроема);
			ПараметрыФормы.Вставить("ШиринаПроема", РазмерыДвери.ШиринаПроема);
			ПараметрыФормы.Вставить("Редактирование", Истина);
			Форма = ПолучитьФорму("Справочник.Двери.Форма.ФормаЭлемента", ПараметрыФормы, ЭтаФорма);
			Форма.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
			Форма.Открыть();
			
		КонецЕсли;
		
		Отказ = Истина;
		
	КонецЕсли;
	
	Возврат Отказ;
	
КонецФункции

&НаСервере
Функция ПолучитьКоличествоДверей(Изделие)
	
	Характеристики = Изделие.СписокХарактеристик.НайтиСтроки(Новый Структура("Характеристика", ПланыВидовХарактеристик.ХарактеристикиШкафовКупе.КоличествоДверей));
	
	Возврат Характеристики[0].ЗначениеХарактеристики;	
	
КонецФункции

&НаКлиенте
Процедура ДвериНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	РазмерыДвери = ОпределитьРазмерыДвери();
	Если РазмерыДвери.ВысотаПроема > 0 И РазмерыДвери.ШиринаПроема > 0  Тогда
		
		СписокКнопок = Новый СписокЗначений;
		СписокКнопок.Добавить(КодВозвратаДиалога.Да, "Создать");
		СписокКнопок.Добавить(КодВозвратаДиалога.Нет, "Выбрать");
		СписокКнопок.Добавить(КодВозвратаДиалога.Отмена, "Отмена");
		
		НашОсновнойЭлемент = Детали.НайтиСтроки(Новый Структура("ВидИзделия", ВидыИзделий.ОсновнойЭлемент));
		КоличествоДверей = ПолучитьКоличествоДверей(НашОсновнойЭлемент[0].Изделие);	
		
		Ответ = Вопрос("Создать новую или выбрать существующую дверь?",СписокКнопок,,);
		
		Если Ответ = КодВозвратаДиалога.Да Тогда
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("ВысотаПроема", РазмерыДвери.ВысотаПроема);
			ПараметрыФормы.Вставить("ШиринаПроема", РазмерыДвери.ШиринаПроема);
			ПараметрыФормы.Вставить("ЗначенияЗаполнения", Новый Структура("Спецификация", Спецификация));
			ПараметрыФормы.Вставить("КоличествоДверей", КоличествоДверей);
			
			Форма = ПолучитьФорму("Справочник.Двери.Форма.ФормаЭлемента", ПараметрыФормы, ЭтаФорма);
			Форма.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
			Форма.Открыть();
			
		ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
			
			ПараметрыФормы = Новый Структура("ШиринаПроема", РазмерыДвери.ШиринаПроема);
			ПараметрыФормы.Вставить("ВысотаПроема", РазмерыДвери.ВысотаПроема);
			ПараметрыФормы.Вставить("Спецификация", Спецификация);
			ПараметрыФормы.Вставить("КоличествоДверей", КоличествоДверей);
			Форма = ПолучитьФорму("Справочник.Двери.Форма.ФормаВыбора", ПараметрыФормы, ЭтаФорма);
			Форма.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
			Форма.Открыть();
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДвериПриИзменении(Элемент)
	
	ДвериПриИзмененииНаКлиенте(Двери);
	
КонецПроцедуры

&НаКлиенте
Процедура ДвериПриИзмененииНаКлиенте(Значение)
	
	Если ЗначениеЗаполнено(Значение) Тогда
		РазмерыДвери = ПолучитьРазмерыВыбраннойДвери(Значение);
		РазмерыПроема = ОпределитьРазмерыДвери();
		
		Если РазмерыПроема.ВысотаПроема = РазмерыДвери.ВысотаПроема И РазмерыПроема.ШиринаПроема = РазмерыДвери.ШиринаПроема Тогда
			
			Двери = Значение;
			ВысотаПроемаДвери = РазмерыПроема.ВысотаПроема;
			ШиринаПроемаДвери = РазмерыПроема.ШиринаПроема;
			
		Иначе
			
			Двери = Неопределено;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Двери не подходят по размерам", , "Двери");
			
		КонецЕсли;
		
	Иначе
		
		Двери = Неопределено;
		
	КонецЕсли;
	
КонецПроцедуры

//Размеры двери при выборе уже существующей двери
&НаСервереБезКонтекста
Функция ПолучитьРазмерыВыбраннойДвери(Дверь)
	
	Структура = Новый Структура;
	
	Структура.Вставить("ВысотаПроема", Дверь.ВысотаПроема);
	Структура.Вставить("ШиринаПроема", Дверь.ШиринаПроема);
	
	Возврат Структура;
	
КонецФункции

//Размеры двери с учетом размеров элементов шкафа-купе
&НаКлиенте
Функция ОпределитьРазмерыДвери()
	
	ВысотаПроема = 0;
	ШиринаПроема = 0;
	
	РазмерыОсновногоЭлемента = ПолучитьРазмерыОсновногоЭлемента();
	Если РазмерыОсновногоЭлемента <> Неопределено Тогда
		
		ВысотаПроема = РазмерыОсновногоЭлемента.Высота;
		ШиринаПроема = РазмерыОсновногоЭлемента.Ширина;
		
		Если ВысотаПроема > 0 И ШиринаПроема > 0  Тогда
			Для Каждого Элемент Из Детали Цикл
				
				Если Элемент.ВидИзделия = ВидыИзделий.ЛевыйБоковойЭлемент ИЛИ
					
					Элемент.ВидИзделия = ВидыИзделий.ПравыйБоковойЭлемент Тогда
					
					Если Элемент.НеВлияетНаОсновной Тогда
						ШиринаПроема = ШиринаПроема - Элемент.ШиринаИзделия;
					КонецЕсли;

				ИначеЕсли Элемент.ВидИзделия = ВидыИзделий.Потолок ИЛИ
					
					Элемент.ВидИзделия = ВидыИзделий.Пол Тогда
					
					Если Элемент.НеВлияетНаОсновной Тогда
						ВысотаПроема = ВысотаПроема - Элемент.ВысотаИзделия;
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Структура = Новый Структура;
	
	Структура.Вставить("ВысотаПроема", ВысотаПроема);
	Структура.Вставить("ШиринаПроема", ШиринаПроема);
	
	Возврат Структура;
	
КонецФункции

&НаКлиенте
Процедура ДеталиПриАктивизацииСтроки(Элемент)
	
	ТекущийЭлементИзделия = Элемент.ТекущиеДанные.ВидИзделия;
	УстановитьДоступность(ТекущийЭлементИзделия);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеталиПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеталиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеталиЗавалПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Детали.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.ВысотаИзделияСтрока) ИЛИ ЗначениеЗаполнено(ТекущиеДанные.ШиринаИзделияСтрока) ИЛИ ЗначениеЗаполнено(ТекущиеДанные.ГлубинаИзделияСтрока) Тогда
		//Если есть формульные значения размеров, определяем значеия завалов
		
		ВидИзделия = ТекущиеДанные.ВидИзделия; 
		
		ЗавалПол = 0;
		ЗавалЛевый = 0;
		ЗавалПравый = 0;
		ЗавалПотолок = 0;
		ЗавалСтенка = 0;
		
		ПолучитьЗавалы(ЗавалПол, ЗавалЛевый, ЗавалПравый, ЗавалПотолок, ЗавалСтенка, ТекущиеДанные);
		
		//RonEXI: Костыль, в формуле используется переменная НоменклатураМатериала, тут она называется НоменклатураЛДСП
		НоменклатураМатериала = ЛексСервер.ЗначенияРеквизитовОбъекта(ТекущиеДанные.НоменклатураЛДСП,"ДлинаДетали,ШиринаДетали,ГлубинаДетали");

		Если ЗначениеЗаполнено(ТекущиеДанные.ВысотаИзделияСтрока) Тогда
			Попытка
				Выполнить("ТекущиеДанные.ВысотаИзделия = " + ТекущиеДанные.ВысотаИзделияСтрока);
			Исключение
				Текст = "Ошибка при формировании формул для Высоты каталога изделий " + ТекущиеДанные.Изделие;
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст);
			КонецПопытки;	
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТекущиеДанные.ШиринаИзделияСтрока) Тогда
			Попытка
				Выполнить("ТекущиеДанные.ШиринаИзделия = " + ТекущиеДанные.ШиринаИзделияСтрока);
			Исключение
				Текст = "Ошибка при формировании формул для Ширины каталога изделий " + ТекущиеДанные.Изделие;
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст);
			КонецПопытки;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТекущиеДанные.ГлубинаИзделияСтрока) Тогда
			Попытка
				Выполнить("ТекущиеДанные.ГлубинаИзделия = " + ТекущиеДанные.ГлубинаИзделияСтрока);
			Исключение
				Текст = "Ошибка при формировании формул для Глубины каталога изделий " + ТекущиеДанные.Изделие;
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст);
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьЗавалы(ЗавалПол, ЗавалЛевый, ЗавалПравый, ЗавалПотолок, ЗавалСтенка, ТекущиеДанные)
		
	ЗавалПол = 0;
	ЗавалЛевый = 0;
	ЗавалПравый = 0;
	ЗавалПотолок = 0;
	ЗавалСтенка = 0;
	
	ВидИзделия = ТекущиеДанные.ВидИзделия;
			
	Если ВидИзделия = ВидыИзделий.ОсновнойЭлемент Тогда
		ЗавалСтенка = ?(ЗначениеЗаполнено(ТекущиеДанные.Завал),Число(ТекущиеДанные.Завал),0);
	ИначеЕсли ВидИзделия = ВидыИзделий.ПравыйБоковойЭлемент Тогда
		ЗавалПравый = ?(ЗначениеЗаполнено(ТекущиеДанные.Завал),Число(ТекущиеДанные.Завал),0);
	ИначеЕсли ВидИзделия = ВидыИзделий.ЛевыйБоковойЭлемент Тогда
		ЗавалЛевый = ?(ЗначениеЗаполнено(ТекущиеДанные.Завал),Число(ТекущиеДанные.Завал),0);
	ИначеЕсли ВидИзделия = ВидыИзделий.Пол Тогда
		ЗавалПол = ?(ЗначениеЗаполнено(ТекущиеДанные.Завал),Число(ТекущиеДанные.Завал),0);
	ИначеЕсли ВидИзделия = ВидыИзделий.Потолок Тогда
		ЗавалПотолок = ?(ЗначениеЗаполнено(ТекущиеДанные.Завал),Число(ТекущиеДанные.Завал),0);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуПодбора(НомГруппы, ЭлементФормы)
	
	Пар = Новый Структура();
	Пар.Вставить("НомГруппы", НомГруппы);
	Пар.Вставить("Подразделение", Подразделение);
	Пар.Вставить("НаценкаОфиса",НаценкаОфиса);
	
	ОткрытьФорму("Справочник.Номенклатура.Форма.ФормаПодбора", Пар, ЭлементФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеталиНоменклатураЛДСПНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	НомГруппы = Новый СписокЗначений;
	НомГруппы.Добавить(МассивНомГрупп.ЛДСП16);
	
	ОткрытьФормуПодбора(НомГруппы, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеталиКромкаЛДСПНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	НомГруппы = Новый СписокЗначений;
	НомГруппы.Добавить(МассивНомГрупп.Кромка2_19);
	НомГруппы.Добавить(МассивНомГрупп.КантТ);
	
	ОткрытьФормуПодбора(НомГруппы, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеталиНоменклатураЛДСПДопНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	НомГруппы = Новый СписокЗначений;
	НомГруппы.Добавить(МассивНомГрупп.ЛДСП16);
	
	ОткрытьФормуПодбора(НомГруппы, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеталиКромкаЛДСПДопНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	НомГруппы = Новый СписокЗначений;
	НомГруппы.Добавить(МассивНомГрупп.Кромка2_19);
	НомГруппы.Добавить(МассивНомГрупп.КантТ);
	
	ОткрытьФормуПодбора(НомГруппы, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеталиНоменклатураДВПНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	НомГруппы = Новый СписокЗначений;
	НомГруппы.Добавить(МассивНомГрупп.ДВП);
	
	ОткрытьФормуПодбора(НомГруппы, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура РучкиЯщиковНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	НомГруппы = Новый СписокЗначений;
	НомГруппы.Добавить(МассивНомГрупп.Ручка);
	
	ОткрытьФормуПодбора(НомГруппы, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ПотолокЛевыйПравыйНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеталиКрючкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	НомГруппы = Новый СписокЗначений;
	НомГруппы.Добавить(МассивНомГрупп.Крючок);
	
	ОткрытьФормуПодбора(НомГруппы, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеталиЗавалНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Ошибки = Неопределено;
	Изделие = Элементы.Детали.ТекущиеДанные;
	
	Если НЕ Изделие.Предопределенный И НЕ ЗначениеЗаполнено(Изделие.НоменклатураЛДСП) Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Текст = "Сначала заполните номенклатуру ЛДСП.";
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "Элементы.Детали.ТекущиеДанные.НоменклатураЛДСП", Текст);
		
	КонецЕсли;
	
	Если Ошибки <> Неопределено Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДвериОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ЗначениеЗаполнено(Двери) Тогда
	
		РазмерыДвери = ОпределитьРазмерыДвери();
		
		Если РазмерыДвери.ВысотаПроема > 0 И РазмерыДвери.ШиринаПроема > 0  Тогда
			
			НашОсновнойЭлемент = Детали.НайтиСтроки(Новый Структура("ВидИзделия", ВидыИзделий.ОсновнойЭлемент));
			КоличествоДверей = ПолучитьКоличествоДверей(НашОсновнойЭлемент[0].Изделие);	
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("ВысотаПроема", РазмерыДвери.ВысотаПроема);
			ПараметрыФормы.Вставить("ШиринаПроема", РазмерыДвери.ШиринаПроема);
			ПараметрыФормы.Вставить("ЗначенияЗаполнения", Новый Структура("Спецификация", Спецификация));
			ПараметрыФормы.Вставить("КоличествоДверей", КоличествоДверей);
			ПараметрыФормы.Вставить("Ключ", Двери);
			
			Форма = ПолучитьФорму("Справочник.Двери.Форма.ФормаЭлемента", ПараметрыФормы, ЭтаФорма);
			Форма.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
			Форма.Открыть();

		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ


