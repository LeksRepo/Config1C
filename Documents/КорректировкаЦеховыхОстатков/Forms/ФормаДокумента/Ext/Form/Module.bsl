
&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СписатьБезСпецификаций(Команда)
	
	ЗаполнитьСписокНоменклатуры(Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьОтрицательными(Команда)
	
	ЗаполнитьСписокНоменклатуры(Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСписокНоменклатуры(Команда)
	
	Если Объект.СписокНоменклатуры.Количество() > 0 Тогда
		
		Режим = РежимДиалогаВопрос.ДаНет;
		Текст = "Табличные части будут изменены." + Символы.ПС + "Продолжить?";
		
		Если Вопрос(Текст, Режим, 0) = КодВозвратаДиалога.Нет Тогда
			
			Возврат;
			
		КонецЕсли;
		
	КонецЕсли;
	
	// { Васильев Александр Леонидович [06.07.2015]
	// Спорный момент определения по имени команды...
	// } Васильев Александр Леонидович [06.07.2015]
	Если Команда.Имя = "ЗаполнитьОтрицательными" Тогда
		ЗаполнитьОтрицательнымиСервер();
	ИначеЕсли Команда.Имя = "СписатьБезСпецификаций" Тогда
		СписатьБезСпецификацийНаСервере();
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьСписокНоменклатуры()

&НаСервере
Процедура ЗаполнитьОтрицательнымиСервер()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Подразделение", Объект.Подразделение);
	Запрос.УстановитьПараметр("Дата", КонецДня(Объект.Дата));
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОстаткиВЦеху.Субконто1 КАК Номенклатура,
	|	-ОстаткиВЦеху.КоличествоОстатокДт КАК Оприходовать
	|ИЗ
	|	РегистрБухгалтерии.Управленческий.Остатки(&Дата, Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ОсновноеПроизводство), , Подразделение = &Подразделение) КАК ОстаткиВЦеху
	|ГДЕ
	|	ОстаткиВЦеху.КоличествоОстатокДт < 0";
	
	РезультатЗапроса = Запрос.Выполнить();
	Объект.СписокНоменклатуры.Загрузить(РезультатЗапроса.Выгрузить());
	
КонецПроцедуры

&НаСервере
Процедура СписатьБезСпецификацийНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Подразделение", Объект.Подразделение);
	Запрос.УстановитьПараметр("Дата", Объект.Дата);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОстаткиВЦеху.Субконто1 КАК Номенклатура,
	|	ОстаткиВЦеху.КоличествоОстатокДт КАК КоличествоОстаток
	|ПОМЕСТИТЬ втОстатки
	|ИЗ
	|	РегистрБухгалтерии.Управленческий.Остатки(&Дата, Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ОсновноеПроизводство), , Подразделение = &Подразделение) КАК ОстаткиВЦеху
	|ГДЕ
	|	ОстаткиВЦеху.Субконто1.НоменклатурнаяГруппа <> ЗНАЧЕНИЕ(Справочник.НоменклатурныеГруппы.РасходныйМатериал)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(СпецификацияСписокНоменклатуры.Количество) КАК Количество,
	|	СпецификацияСписокНоменклатуры.Номенклатура
	|ПОМЕСТИТЬ втНоменклатураСпецификаций
	|ИЗ
	|	Документ.Спецификация.СписокНоменклатуры КАК СпецификацияСписокНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусСпецификации.СрезПоследних КАК СтатусСпецификацииСрезПоследних
	|		ПО СпецификацияСписокНоменклатуры.Ссылка = СтатусСпецификацииСрезПоследних.Спецификация
	|ГДЕ
	|	НЕ СпецификацияСписокНоменклатуры.ПредоставитЗаказчик
	|	И СтатусСпецификацииСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСпецификации.ПереданВЦех)
	|	И СпецификацияСписокНоменклатуры.Ссылка.Подразделение = &Подразделение
	|	И СпецификацияСписокНоменклатуры.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Материал)
	|
	|СГРУППИРОВАТЬ ПО
	|	СпецификацияСписокНоменклатуры.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОстатки.Номенклатура,
	|	втОстатки.КоличествоОстаток - втНоменклатураСпецификаций.Количество КАК Списать,
	|	втОстатки.КоличествоОстаток КАК ОстатокВЦеху,
	|	втНоменклатураСпецификаций.Количество КАК ЕстьВСпецификациях
	|ИЗ
	|	втОстатки КАК втОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ втНоменклатураСпецификаций КАК втНоменклатураСпецификаций
	|		ПО втОстатки.Номенклатура = втНоменклатураСпецификаций.Номенклатура
	|ГДЕ
	|	втОстатки.КоличествоОстаток - втНоменклатураСпецификаций.Количество > 0";
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Объект.СписокНоменклатуры.Добавить();
		НоваяСтрока.Номенклатура = Выборка.Номенклатура;
		НоваяСтрока.Списать = Выборка.Списать;
		Описание = "В цеху %1, в спецификациях %2";
		Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Описание, Выборка.ОстатокВЦеху, Выборка.ЕстьВСпецификациях);
		НоваяСтрока.Описание = Описание;
	КонецЦикла;
	
КонецПроцедуры
