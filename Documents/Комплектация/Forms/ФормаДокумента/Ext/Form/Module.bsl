
&НаКлиенте
Процедура СпецификацияПриИзменении(Элемент)
	
	Спецификация = Объект.Спецификация;
	ПерезаполнитьНаСервере(Спецификация);
	ПерезаполнитьРасчетныеДанные();
	
КонецПроцедуры

&НаКлиенте
Процедура Перезаполнить(Команда)
	
	Ошибки = Неопределено;
	Отказ = Ложь;
	Спецификация = Объект.Спецификация;
	
	Если НЕ ЗначениеЗаполнено(Спецификация) Тогда
		
		ТекстСообщения = "Не выбрана спецификация";
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "Объект.Спецификация", ТекстСообщения);
		
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки, Отказ);
	
	Если НЕ Отказ Тогда
		ПерезаполнитьНаСервере(Спецификация);
		ПерезаполнитьРасчетныеДанные();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьОстаткиКомплектацииСпецификаций(СсылкаСпецификация)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", СсылкаСпецификация);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УправленческийОстатки.Субконто3 КАК МестоОбработки,
	|	УправленческийОстатки.Субконто2 КАК Номенклатура,
	|	ВЫРАЗИТЬ(УправленческийОстатки.Субконто2 КАК Справочник.Номенклатура).Наименование КАК Наименование,
	|	УправленческийОстатки.КоличествоОстаток КАК Количество,
	|	СписокПодЗаказ.Комментарий КАК Комментарий
	|ИЗ
	|	РегистрБухгалтерии.Управленческий.Остатки(, Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.КомплектацииСпецификаций), , Субконто1 = &Ссылка) КАК УправленческийОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Спецификация.СписокМатериаловПодЗаказ КАК СписокПодЗаказ
	|		ПО ((ВЫРАЗИТЬ(УправленческийОстатки.Субконто2 КАК Справочник.Номенклатура)) = СписокПодЗаказ.Номенклатура)
	|			И (СписокПодЗаказ.Ссылка = &Ссылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	МестоОбработки УБЫВ,
	|	Наименование";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат Выборка;
	
КонецФункции

&НаСервере
Процедура ПерезаполнитьНаСервере(СпецификацияСсылка)
	
	Объект.СписокНоменклатуры.Очистить();
	
	Если ЭтоПерваяКомплектация(СпецификацияСсылка) Тогда
		
		Выборка = Документы.Спецификация.ПерезаполнитьКомплектацию(СпецификацияСсылка);
		
		Пока Выборка.Следующий() Цикл
			
			НоваяСтрока = Объект.СписокНоменклатуры.Добавить();
			НоваяСтрока.Номенклатура = Выборка.Номенклатура;
			НоваяСтрока.КоличествоСклад = Выборка.КоличествоСклад;
			НоваяСтрока.КоличествоЦех = Выборка.КоличествоЦех;
			НоваяСтрока.ЗатребованоСклад = Выборка.КоличествоСклад;
			НоваяСтрока.ЗатребованоЦех = Выборка.КоличествоЦех;
			НоваяСтрока.Комментарий = Выборка.Комментарий;
		КонецЦикла;
		
	Иначе
		
		Выборка = ПолучитьОстаткиКомплектацииСпецификаций(СпецификацияСсылка);
		
		Пока Выборка.Следующий() Цикл
			
			НоваяСтрока = Объект.СписокНоменклатуры.Добавить();
			НоваяСтрока.Номенклатура = Выборка.Номенклатура;
			
			Если Выборка.МестоОбработки = Перечисления.МестоОбработки.Отгрузка Тогда
				
				НоваяСтрока.КоличествоСклад = Выборка.Количество;
				НоваяСтрока.КоличествоЦех = 0;
				
			Иначе
				
				НоваяСтрока.КоличествоСклад = 0;
				НоваяСтрока.КоличествоЦех = Выборка.Количество;
				
			КонецЕсли;
			
			НоваяСтрока.ЗатребованоСклад = НоваяСтрока.КоличествоСклад;
			НоваяСтрока.ЗатребованоЦех = НоваяСтрока.КоличествоЦех;
			
			НоваяСтрока.Комментарий = Выборка.Комментарий; 
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЭтоПерваяКомплектация(Спецификация)
	
	Первая = Истина;
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ТекущаяКомплектация", Объект.Ссылка);
	Запрос.УстановитьПараметр("Спецификация", Спецификация);
	Запрос.Текст=
	"ВЫБРАТЬ
	|	Комплектация.Ссылка
	|ИЗ
	|	Документ.Комплектация КАК Комплектация
	|ГДЕ
	|	Комплектация.Спецификация = &Спецификация
	|	И НЕ Комплектация.ПометкаУдаления
	|	И Комплектация.Ссылка <> &ТекущаяКомплектация";
	
	Результат = Запрос.Выполнить();
	Первая = Результат.Пустой();
	
	Возврат Первая;
	
КонецФункции

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Спецификация = Объект.Спецификация;
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.Дата = ТекущаяДата();
	КонецЕсли;
	
	Если НЕ ЭтаФорма.ТолькоПросмотр Тогда
		ЭтаФорма.ТолькоПросмотр = ЛексСервер.ДоступностьФормыСкладскиеДокументы(Объект.Ссылка);
	КонецЕсли;
		
	Если Объект.Ссылка = Документы.Комплектация.ПустаяСсылка() И ЗначениеЗаполнено(Объект.Спецификация) И ЗначениеЗаполнено(Объект.Подразделение) Тогда
		ПерезаполнитьНаСервере(Спецификация);
	КонецЕсли;
	
	ПерезаполнитьРасчетныеДанные();
	
КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьРасчетныеДанные()
	
	Для Каждого Стр ИЗ Объект.СписокНоменклатуры Цикл
		
		Стр.ОбщееКоличество = Стр.КоличествоСклад + Стр.КоличествоЦех;
		Стр.НеВыданоСклад = Стр.ЗатребованоСклад - Стр.КоличествоСклад;
		Стр.НеВыданоЦех = Стр.ЗатребованоЦех - Стр.КоличествоЦех;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Если НЕ ВладелецФормы = Неопределено Тогда
		
		ЗакрыватьПриВыборе = Ложь;
		Оповестить("ПоявиласьКомплектация", Объект.Спецификация);    //  ОВыборе
		
	КонецЕсли;
	
	ПерезаполнитьРасчетныеДанные();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыКоличествоСкладПриИзменении(Элемент)
	
	ОбновитьТекущиеДанные();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыКоличествоЦехПриИзменении(Элемент)
	
	ОбновитьТекущиеДанные();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьТекущиеДанные()
	
	Данные = Элементы.СписокНоменклатуры.ТекущиеДанные;
	
	Данные.ОбщееКоличество = Данные.КоличествоСклад + Данные.КоличествоЦех;
	Данные.НеВыданоСклад = Данные.ЗатребованоСклад - Данные.КоличествоСклад;
	Данные.НеВыданоЦех = Данные.ЗатребованоЦех - Данные.КоличествоЦех;
	
КонецПроцедуры

&НаКлиенте
Процедура СравнитьСоСкладом(Команда)
	
	Если ЗначениеЗаполнено(Объект.Склад) Тогда
		СравнитьСоСкладомСервер();
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Заполните Склад");	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СравнитьСоСкладомСервер()
	
	ВидыСубконто = Новый Массив;
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура);
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Склады);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МоментВремени", ТекущаяДата());
	Запрос.УстановитьПараметр("Подразделение", Объект.Подразделение);
	Запрос.УстановитьПараметр("Склад", Объект.Склад);
	Запрос.УстановитьПараметр("ТЗ", Объект.СписокНоменклатуры.Выгрузить());
	Запрос.УстановитьПараметр("ВидыСубконто", ВидыСубконто);

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СписокМатериалов.НомерСтроки КАК НомерСтроки,
	|	ВЫРАЗИТЬ(СписокМатериалов.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	|	СписокМатериалов.КоличествоСклад + СписокМатериалов.КоличествоЦех КАК Количество
	|ПОМЕСТИТЬ СписокМатериалов
	|ИЗ
	|	&ТЗ КАК СписокМатериалов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Список.Номенклатура КАК Номенклатура,
	|	СУММА(Список.Количество) КАК Количество
	|ПОМЕСТИТЬ втКоличествоМатериала
	|ИЗ
	|	СписокМатериалов КАК Список
	|
	|СГРУППИРОВАТЬ ПО
	|	Список.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписокМатериалов.Номенклатура
	|ПОМЕСТИТЬ втНехватка
	|ИЗ
	|	втКоличествоМатериала КАК СписокМатериалов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
	|				&МоментВремени,
	|				Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.МатериалыНаСкладе),
	|				&ВидыСубконто,
	|				Подразделение = &Подразделение
	|					И Субконто2 = &Склад) КАК УправленческийОстатки
	|		ПО СписокМатериалов.Номенклатура = УправленческийОстатки.Субконто1
	|ГДЕ
	|	НЕ СписокМатериалов.Номенклатура.Неноменклатурный
	|	И ЕСТЬNULL(УправленческийОстатки.КоличествоОстаток, 0) - СписокМатериалов.Количество < 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Список.НомерСтроки
	|ИЗ
	|	СписокМатериалов КАК Список
	|		ЛЕВОЕ СОЕДИНЕНИЕ втНехватка КАК Нехватка
	|		ПО Список.Номенклатура = Нехватка.Номенклатура
	|ГДЕ
	|	НЕ Нехватка.Номенклатура ЕСТЬ NULL";
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Строка = Объект.СписокНоменклатуры[Выборка.НомерСтроки-1];
		Строка.КоличествоСклад = 0;
		Строка.КоличествоЦех = 0;
		Строка.ОбщееКоличество = Строка.КоличествоСклад + Строка.КоличествоЦех;
		Строка.НеВыданоСклад = Строка.ЗатребованоСклад - Строка.КоличествоСклад;
		Строка.НеВыданоЦех = Строка.ЗатребованоЦех - Строка.КоличествоЦех;
		
	КонецЦикла;
	
КонецПроцедуры
