
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	//RonEXI: Печатаем комплектацию из комплектации а картинки из спецификации, поэтому 2 раза ВыполнитьКомандуПечати.  
	
	Проведены = ПроверитьПроведение(ПараметрКоманды);
	
	Если НЕ Проведены Тогда
		Возврат;
	КонецЕсли;
	
	Массив = ПараметрКоманды;
	
	ПараметрыПечати = Новый Структура;
	ПараметрыПечати.Вставить("ФиксированныйКомплект", Ложь);
	ПараметрыПечати.Вставить("ПереопределитьПользовательскиеНастройкиКоличества", Истина);
	
	МассивМакетов = Новый Массив;
	МассивМакетов.Добавить("Комплектация");
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати("Документ.Комплектация", СтроковыеФункцииКлиентСервер.ПолучитьСтрокуИзМассиваПодстрок(МассивМакетов), Массив, ПараметрыВыполненияКоманды.Источник, ПараметрыПечати);
	
	Спецификации = ПолучитьСпецификации(ПараметрКоманды);
	
	Для Каждого Спецификация Из Спецификации Цикл
		
		Массив = Новый Массив;
		Массив.Добавить(Спецификация);
					
		МассивМакетов = ОпределитьМакеты(Спецификация);
		
		Если ЗначениеЗаполнено(МассивМакетов) Тогда
			УправлениеПечатьюКлиент.ВыполнитьКомандуПечати("Документ.Спецификация", СтроковыеФункцииКлиентСервер.ПолучитьСтрокуИзМассиваПодстрок(МассивМакетов), Массив, ПараметрыВыполненияКоманды.Источник, ПараметрыПечати);	
		КонецЕсли;
			
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьСпецификации(МассивКомплектаций)
	
	МассивСпец = Новый Массив();
	
	Для Каждого Док ИЗ МассивКомплектаций Цикл
		
		Если Док.Проведен И ЗначениеЗаполнено(Док.Спецификация) Тогда
			МассивСпец.Добавить(Док.Спецификация);
		КонецЕсли;
		 
	КонецЦикла;
	
	Возврат МассивСпец;
	
КонецФункции

&НаСервере
Функция ПроверитьПроведение(МассивДок)
	
	Проведены = Истина;
	
	Для Каждого Комплектация ИЗ МассивДок Цикл
		
		Если НЕ Комплектация.Проведен Тогда
			Проведены = Ложь;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("" + Комплектация + " не проведена.");
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Проведены; 
	
КонецФункции

&НаСервере
Функция ОпределитьМакеты(Документ)
	
	МассивМакетов = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Документ", Документ);
	Запрос.УстановитьПараметр("ДокументОснование", Документ.ДокументОснование);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Файлы.Вид КАК ВидФайла
	|ИЗ
	|	Справочник.ФайлыЛекс КАК Файлы
	|ГДЕ
	|	((ВЫРАЗИТЬ(Файлы.ВладелецФайла КАК Документ.Спецификация)) = &Документ
	|			ИЛИ (ВЫРАЗИТЬ(Файлы.ВладелецФайла КАК Документ.Замер)) = &ДокументОснование)
	|	И (Файлы.Вид = ЗНАЧЕНИЕ(Перечисление.ВидыПрисоединенныхФайлов.Эскиз)
	|			ИЛИ Файлы.Вид = ЗНАЧЕНИЕ(Перечисление.ВидыПрисоединенныхФайлов.ЗамерныйЛист)
	|			ИЛИ Файлы.Вид = ЗНАЧЕНИЕ(Перечисление.ВидыПрисоединенныхФайлов.МонтажнаяСхема))
	|	И Файлы.ПометкаУдаления = ЛОЖЬ";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ДобавитьЭскиз = Ложь;
	ДобавитьЗамерныйЛист = Ложь;
	ДобавитьМонтажнаяСхема = Ложь;
	
	ВидЭскиз = ПредопределенноеЗначение("Перечисление.ВидыПрисоединенныхФайлов.Эскиз");
	ВидЗамерныйЛист = ПредопределенноеЗначение("Перечисление.ВидыПрисоединенныхФайлов.ЗамерныйЛист");
	ВидМонтажнаяСхема = ПредопределенноеЗначение("Перечисление.ВидыПрисоединенныхФайлов.МонтажнаяСхема");
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.ВидФайла = ВидЭскиз Тогда
			ДобавитьЭскиз = Истина; 
		КонецЕсли;
		
		Если Выборка.ВидФайла = ВидЗамерныйЛист Тогда
			ДобавитьЗамерныйЛист = Истина;
		КонецЕсли;
		
		Если Выборка.ВидФайла = ВидМонтажнаяСхема Тогда
			ДобавитьМонтажнаяСхема = Истина;
		КонецЕсли;
		
	КонецЦикла; 
	
	Если ДобавитьЭскиз Тогда	
		МассивМакетов.Добавить("Эскиз");
	КонецЕсли;
	
	Если ДобавитьЗамерныйЛист Тогда	
		МассивМакетов.Добавить("ЗамерныйЛист");
	КонецЕсли;
	
	Если ДобавитьМонтажнаяСхема Тогда	
		МассивМакетов.Добавить("МонтажнаяСхема");
	КонецЕсли;
	
	//МассивМакетов.Добавить("Раскрой");
	
	Возврат МассивМакетов;
	
КонецФункции


