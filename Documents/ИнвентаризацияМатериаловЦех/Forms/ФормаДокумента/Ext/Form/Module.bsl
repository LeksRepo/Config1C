
&НаСервере
Функция СформироватьОстаткиЦех(Запрос)
	
	Запрос.УстановитьПараметр("Подразделение", Объект.Подразделение);
	Запрос.УстановитьПараметр("Период", Объект.Дата);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	втСписокНоменклатуры.Номенклатура КАК Номенклатура,
	|	втСписокНоменклатуры.КоличествоФакт,
	|	ЕСТЬNULL(УправленческийОстатки.КоличествоОстаток, 0) КАК КоличествоУчетное,
	|	ЕСТЬNULL(УправленческийОстатки.СуммаОстаток, 0) КАК СтоимостьУчетная
	|ПОМЕСТИТЬ втОстаткиНоменклатуры
	|ИЗ
	|	втСписокНоменклатуры КАК втСписокНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
	|				&Период,
	|				Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ОсновноеПроизводство),
	|				,
	|				Подразделение = &Подразделение
	|					И Субконто1 В
	|						(ВЫБРАТЬ
	|							т.Номенклатура
	|						ИЗ
	|							втСписокНоменклатуры КАК т)) КАК УправленческийОстатки
	|		ПО втСписокНоменклатуры.Номенклатура = УправленческийОстатки.Субконто1
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

&НаСервере
Функция ЗапросПоНоменклатуреЦех()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Подразделение", Объект.Подразделение);
	Запрос.УстановитьПараметр("ГруппаНоменклатуры", ГруппаНоменклатуры);
	Запрос.УстановитьПараметр("Период", Объект.Дата);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УправленческийОстатки.Субконто1 КАК Номенклатура,
	|	0 КАК КоличествоФакт
	|ИЗ
	|	РегистрБухгалтерии.Управленческий.Остатки(
	|			&Период,
	|			Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ОсновноеПроизводство),
	|			,
	|			Подразделение = &Подразделение
	|				И ВЫБОР
	|					КОГДА &ГруппаНоменклатуры = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|						ТОГДА ИСТИНА
	|					ИНАЧЕ Субконто1 В ИЕРАРХИИ (&ГруппаНоменклатуры)
	|				КОНЕЦ) КАК УправленческийОстатки
	|
	|УПОРЯДОЧИТЬ ПО
	|	УправленческийОстатки.Субконто1.Родитель.Наименование,
	|	УправленческийОстатки.Субконто1.Наименование";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

&НаКлиенте
Функция ЗаполнитьСписокНоменклатуры()
	
	Если Объект.СписокНоменклатуры.Количество() > 0 Тогда
		
		Если рфПерезаполнение Тогда
			ТекстВопроса = "Учетные данные будут перезаполнены.";
		Иначе
			ТекстВопроса = "Табличная часть будет очищена.";
		КонецЕсли;

		Режим = РежимДиалогаВопрос.ДаНет;
		Ответ = Вопрос(ТекстВопроса + Символы.ВК + "Продолжить?", Режим, 0);
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьСписокНоменклатурыОстатками();
	
КонецФункции

&НаСервере
Процедура ЗаполнитьСписокНоменклатурыОстатками()
	
	Если НЕ рфПерезаполнение Тогда
		
		Объект.СписокНоменклатуры.Очистить();
		тзНоменклатура = ЗапросПоНоменклатуреЦех();
		
	Иначе
		
		тзНоменклатура = Объект.СписокНоменклатуры.Выгрузить();
		
	КонецЕсли;
	
	Запрос = Новый Запрос;

	Запрос.УстановитьПараметр("СписокНоменклатуры", тзНоменклатура);
	Запрос.УстановитьПараметр("Период", Объект.Дата);
	Запрос.УстановитьПараметр("ГруппаНоменклатуры", ГруппаНоменклатуры);
	Запрос.УстановитьПараметр("Подразделение", Объект.Подразделение);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(СписокНоменклатуры.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	|	СписокНоменклатуры.КоличествоФакт
	|ПОМЕСТИТЬ тзСписокНоменклатуры
	|ИЗ
	|	&СписокНоменклатуры КАК СписокНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втСписокНоменклатуры.Номенклатура КАК Номенклатура,
	|	СУММА(втСписокНоменклатуры.КоличествоФакт) КАК КоличествоФакт
	|ПОМЕСТИТЬ втСписокНоменклатуры
	|ИЗ
	|	тзСписокНоменклатуры КАК втСписокНоменклатуры
	|
	|СГРУППИРОВАТЬ ПО
	|	втСписокНоменклатуры.Номенклатура
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втСписокНоменклатуры.Номенклатура КАК Номенклатура,
	|	втСписокНоменклатуры.КоличествоФакт,
	|	ЕСТЬNULL(УправленческийОстатки.КоличествоОстаток, 0) КАК КоличествоУчетное,
	|	ВЫБОР
	|		КОГДА втСписокНоменклатуры.Номенклатура.Базовый
	|			ТОГДА ЦеныНоменклатурыСрезПоследних.ПлановаяЗакупочная
	|		ИНАЧЕ ЦеныНоменклатурыСрезПоследних.ПлановаяЗакупочная * втСписокНоменклатуры.Номенклатура.КоэффициентБазовых
	|	КОНЕЦ КАК ПлановаяЦена
	|ПОМЕСТИТЬ втНоменклатураОстатки
	|ИЗ
	|	втСписокНоменклатуры КАК втСписокНоменклатуры
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
	|				&Период,
	|				Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ОсновноеПроизводство),
	|				,
	|				Подразделение = &Подразделение
	|				И ВЫБОР
	|				       КОГДА &ГруппаНоменклатуры = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|					   ТОГДА ИСТИНА
	|					   ИНАЧЕ Субконто1 В ИЕРАРХИИ (&ГруппаНоменклатуры)
	|				  КОНЕЦ) КАК УправленческийОстатки
	|		ПО втСписокНоменклатуры.Номенклатура = УправленческийОстатки.Субконто1
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатурыПоПодразделениям.СрезПоследних(&Период, Подразделение = &Подразделение) КАК ЦеныНоменклатурыСрезПоследних
	|		ПО (втСписокНоменклатуры.Номенклатура = ЦеныНоменклатурыСрезПоследних.Номенклатура
	|				ИЛИ втСписокНоменклатуры.Номенклатура.БазоваяНоменклатура = ЦеныНоменклатурыСрезПоследних.Номенклатура)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втНоменклатураОстатки.Номенклатура,
	|	втНоменклатураОстатки.КоличествоФакт,
	|	втНоменклатураОстатки.КоличествоУчетное,
	|	втНоменклатураОстатки.ПлановаяЦена * втНоменклатураОстатки.КоличествоУчетное КАК СтоимостьУчетная,
	|	втНоменклатураОстатки.ПлановаяЦена * втНоменклатураОстатки.КоличествоФакт КАК СтоимостьФакт,
	|	втНоменклатураОстатки.ПлановаяЦена КАК Цена,
	|	втНоменклатураОстатки.КоличествоФакт - втНоменклатураОстатки.КоличествоУчетное КАК Отклонение
	|ИЗ
	|	втНоменклатураОстатки КАК втНоменклатураОстатки
	|ГДЕ
	|   втНоменклатураОстатки.КоличествоУчетное > 0
	|УПОРЯДОЧИТЬ ПО
	|	втНоменклатураОстатки.Номенклатура.Родитель.Наименование,
	|	втНоменклатураОстатки.Номенклатура.Наименование";
	
	Объект.СписокНоменклатуры.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаКлиенте
Процедура ПерезаполнитьУчетныеДанные(Команда)
	
	ГруппаНоменклатуры = Неопределено;
	рфПерезаполнение = Истина;
	ЗаполнитьСписокНоменклатуры();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьОстаткамиПоЦеху(Команда)
	
	ГруппаНоменклатуры = Неопределено;
	рфПерезаполнение = Ложь;
	ЗаполнитьСписокНоменклатуры();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьФактическиеДанныеУчетными(Команда)
	
	Для каждого Строка Из Объект.СписокНоменклатуры Цикл
		
		Строка.КоличествоФакт = Строка.КоличествоУчетное;
		Строка.Отклонение = 0;
		Строка.СтоимостьУчетная = Строка.КоличествоУчетное * Строка.Цена;
		Строка.СтоимостьФакт = Строка.КоличествоФакт * Строка.Цена;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьУчетнымиДаннымиГруппой(Команда)
	
	ГруппаНоменклатуры = ОткрытьФормуМодально("Справочник.Номенклатура.ФормаВыбораГруппы");
	Если ЗначениеЗаполнено(ГруппаНоменклатуры) Тогда
		рфПерезаполнение = Ложь;
		ЗаполнитьСписокНоменклатуры();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыКоличествоФактПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.СписокНоменклатуры.ТекущиеДанные;
	ТекущиеДанные.Отклонение = ТекущиеДанные.КоличествоФакт - ТекущиеДанные.КоличествоУчетное;
	ТекущиеДанные.СтоимостьФакт = ТекущиеДанные.КоличествоФакт * ТекущиеДанные.Цена;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыНоменклатураПриИзменении(Элемент)
	
	ТекДанные = Элементы.СписокНоменклатуры.ТекущиеДанные;
	ПараметрыФункции = Новый Структура;
	ПараметрыФункции.Вставить("Дата", Объект.Дата);
	ПараметрыФункции.Вставить("Подразделение", Объект.Подразделение);
	ПараметрыФункции.Вставить("Номенклатура", ТекДанные.Номенклатура);
	
	ЗаполнитьЗначенияСвойств(ТекДанные, ПолучитьОстатокЦену(ПараметрыФункции));
	ТекДанные.Отклонение = ТекДанные.КоличествоФакт - ТекДанные.КоличествоУчетное;
	ТекДанные.СтоимостьФакт = ТекДанные.КоличествоФакт * ТекДанные.Цена;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьОстатокЦену(ПараметрыФункции)
	
	Структура = Новый Структура;
	Структура.Вставить("Цена");
	Структура.Вставить("КоличествоУчетное");
	Структура.Вставить("СтоимостьУчетная");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Подразделение", ПараметрыФункции.Подразделение);
	Запрос.УстановитьПараметр("Период", ПараметрыФункции.Дата);
	Запрос.УстановитьПараметр("Номенклатура", ПараметрыФункции.Номенклатура);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	спрНоменклатура.Ссылка КАК Номенклатура,
	|	ЕСТЬNULL(УправленческийОстатки.КоличествоОстаток, 0) КАК КоличествоУчетное,
	|	ВЫБОР
	|		КОГДА спрНоменклатура.Базовый
	|			ТОГДА ЦеныНоменклатурыСрезПоследних.ПлановаяЗакупочная
	|		ИНАЧЕ ЦеныНоменклатурыСрезПоследних.ПлановаяЗакупочная * спрНоменклатура.КоэффициентБазовых
	|	КОНЕЦ КАК ПлановаяЦена
	|ПОМЕСТИТЬ втНоменклатураОстатки
	|ИЗ
	|	Справочник.Номенклатура КАК спрНоменклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
	|				&Период,
	|				Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ОсновноеПроизводство),
	|				,
	|				Подразделение = &Подразделение
	|					И Субконто1 = &Номенклатура) КАК УправленческийОстатки
	|		ПО спрНоменклатура.Ссылка = УправленческийОстатки.Субконто1
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатурыПоПодразделениям.СрезПоследних(&Период, Подразделение = &Подразделение) КАК ЦеныНоменклатурыСрезПоследних
	|		ПО (спрНоменклатура.Ссылка = ЦеныНоменклатурыСрезПоследних.Номенклатура
	|				ИЛИ спрНоменклатура.Ссылка.БазоваяНоменклатура = ЦеныНоменклатурыСрезПоследних.Номенклатура)
	|ГДЕ
	|	спрНоменклатура.Ссылка = &Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втНоменклатураОстатки.КоличествоУчетное,
	|	втНоменклатураОстатки.ПлановаяЦена * втНоменклатураОстатки.КоличествоУчетное КАК СтоимостьУчетная,
	|	втНоменклатураОстатки.ПлановаяЦена КАК Цена
	|ИЗ
	|	втНоменклатураОстатки КАК втНоменклатураОстатки
	|ГДЕ
	|	втНоменклатураОстатки.КоличествоУчетное > 0";
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(Структура, Выборка);
	КонецЕсли;
	
	Возврат Структура;
	
КонецФункции

