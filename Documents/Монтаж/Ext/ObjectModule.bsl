
Процедура ОбработкаПроведения(Отказ, Режим)
	
	РеквизитыСпецификации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Спецификация, "УслугаМонтаж, Контрагент");
	
	Если РеквизитыСпецификации.УслугаМонтаж И ПервыйМонтаж(РеквизитыСпецификации.Контрагент) Тогда
		
		Запись = Движения.СвободныеМонтажники.Добавить();
		Запись.Город = Спецификация.Офис.Город;
		Запись.Период = Дата;
		Запись.КоличествоМонтажей = 1;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ОснованиеДоговор 			= ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.Договор");
	ОснованиеСпецификация 	= ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.Спецификация");
	
	Если ОснованиеДоговор или ОснованиеСпецификация Тогда
		
		Спецификация 		= ?(ОснованиеДоговор, ДанныеЗаполнения.Спецификация, ДанныеЗаполнения);
		
		Дата 						= Спецификация.ДатаДоставки;
		Подразделение 		= Спецификация.Производство;
		ДатаМонтажа 			= Спецификация.ДатаМонтажа;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	РеквизитыСпецификации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Спецификация, "Доставка, УслугаМонтаж");
	
	Если НЕ РеквизитыСпецификации.УслугаМонтаж Тогда
		
		Если ЗначениеЗаполнено(Монтажник) Тогда
			
			Отказ = Истина;
			ТекстСообщения = "Спецификация к договору без монтажа. Уберите монтажника";
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "Монтажник");
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПервыйМонтаж(Контрагент)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Монтаж.Ссылка,
	|	Монтаж.Дата,
	|	Монтаж.Спецификация.Контрагент
	|ИЗ
	|	Документ.Монтаж КАК Монтаж
	|ГДЕ
	|	Монтаж.Подразделение = &Подразделение
	|	И Монтаж.Спецификация.Контрагент = &Контрагент
	|	И НАЧАЛОПЕРИОДА(Монтаж.Дата, ДЕНЬ) = НАЧАЛОПЕРИОДА(&Дата, ДЕНЬ)
	|	И Монтаж.Ссылка <> &Ссылка
	|	И Монтаж.Проведен";
	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Возврат Запрос.Выполнить().Пустой();
	
КонецФункции // ПовторныйМонтаж()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	МонтажСсылка= Документы.Спецификация.ПолучитьМонтаж(Спецификация);
	
	Если ЗначениеЗаполнено(МонтажСсылка) И Ссылка <> МонтажСсылка Тогда
		Отказ = Истина;
		ТекстСообщения = "К " + Ссылка + " уже введен " + МонтажСсылка;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, МонтажСсылка);
	КонецЕсли;

КонецПроцедуры
