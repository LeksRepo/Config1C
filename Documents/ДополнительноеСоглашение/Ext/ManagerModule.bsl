
Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка) Экспорт
 
 ЛексСервер.ПолучитьПредставлениеДокумента(Данные, Представление, СтандартнаяОбработка);
 
КонецПроцедуры

Функция ПечатьДопСоглашение(Выборка, ОбъектыПечати) Экспорт
	
	ТабДок 			= Новый ТабличныйДокумент;
	
	ТабДок.ИмяПараметровПечати 	= "ПараметрыПечати_Соглашение";
	ТабДок.АвтоМасштаб 					= Истина;
	ТабДок.ОтображатьСетку 			= Ложь;                    
	ТабДок.Защита 							= Истина;
	ТабДок.ТолькоПросмотр 				= Истина;
	ТабДок.ОтображатьЗаголовки 		= Ложь;
	
	Макет = Документы.ДополнительноеСоглашение.ПолучитьМакет("Соглашение");
	МакетРеквизитов = ПолучитьОбщийМакет("РеквизитыСторон");
	
	ОбластьТело = Макет.ПолучитьОбласть("Тело");
	
	Пока Выборка.Следующий() Цикл
		
		НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1;
		//ОбластьТело.Параметры.Заполнить(ЛексСервер.ПолучитьРеквизитыКлиента(Выборка.Контрагент));
		//ОбластьТело.Параметры.Заполнить(ЛексСервер.ПолучитьРеквизитыОрганизации(Выборка.Организация));
		ОбластьТело.Параметры.Заполнить(Выборка);
		
		Если Выборка.ФизЮрЛицо Тогда
			
			НаименованиеПокупателя = Выборка.Контрагент.ПолноеНаименование;
			ОрганизацияДляДоговора = Константы.ОрганизацияДляДоговоровСЮрлицами.Получить();
			ОбластьРеквизиты = МакетРеквизитов.ПолучитьОбласть("РеквизитыЮрЛицо");
			
		Иначе
			
			НаименованиеПокупателя = Выборка.Контрагент.Наименование;
			ОрганизацияДляДоговора = Выборка.Подразделение.Организация;
			ОбластьРеквизиты = МакетРеквизитов.ПолучитьОбласть("РеквизитыФизЛицо");
			
		КонецЕсли;
		
		
		ОбластьТело.Параметры.НаименованиеПокупателя = НаименованиеПокупателя;
		ОбластьТело.Параметры.ДатаДоговора = Формат(Выборка.ДатаДоговора, "ДЛФ=DD");
		ОбластьТело.Параметры.ДатаДокумента = Формат(Выборка.ДатаДокумента, "ДЛФ=DD");
		
		ТабДок.Вывести(ОбластьТело);
		
		
		ОбластьРеквизиты.Параметры.Заполнить(ЛексСервер.ПолучитьРеквизитыОрганизации(Выборка.Организация));
		ТабДок.Вывести(ОбластьРеквизиты);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, НомерСтрокиНачало, ОбъектыПечати, Выборка.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабДок;
КонецФункции

Функция ПолучитьСуммуПлатежа(ДопСоглашениеСсылка) Экспорт
	
	// { Васильев Александр Леонидович [06.09.2013]
	// Лёха, вынеси в общий модуль. И в договоре тоже используй эту функцию
	// } Васильев Александр Леонидович [06.09.2013]
	
	Ответ = 0;
	
	СвойстваДоговора = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(ДопСоглашениеСсылка.Договор, "Контрагент, Подразделение");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДопСоглашение", ДопСоглашениеСсылка);
	Запрос.УстановитьПараметр("Контрагент", СвойстваДоговора.Контрагент);
	Запрос.УстановитьПараметр("Подразделение", СвойстваДоговора.Подразделение);
	Запрос.УстановитьПараметр("Период", КонецДня(ДопСоглашениеСсылка.Дата));
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	УправленческийОстатки.СуммаОстатокКт,
	|	УправленческийОстатки.Субконто1,
	|	УправленческийОстатки.Субконто2,
	|	УправленческийОстатки.Подразделение
	|ИЗ
	|	РегистрБухгалтерии.Управленческий.Остатки(
	|			&Период,
	|			Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ВзаиморасчетыСПокупателями),
	|			,
	|			Подразделение = &Подразделение
	|				И Субконто1 = &Контрагент
	|				И Субконто2 = &ДопСоглашение) КАК УправленческийОстатки";
	
	Результат = Запрос.Выполнить();
	
	Если НЕ Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Ответ = Выборка.СуммаОстатокКт;
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции 

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Выборка = Запрос(МассивОбъектов);
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Соглашение") Тогда
		
		ПодготовитьПечатнуюФорму("Соглашение", "Соглашение", "Документ.ДополнительноеСоглашение.Соглашение",
		МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода, Выборка);
		
	КонецЕсли;
	
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
КонецПроцедуры

Процедура ПодготовитьПечатнуюФорму(Знач ИмяМакета, ПредставлениеМакета, ПолныйПутьКМакету = "", МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода, Выборка)
	
	Если ИмяМакета = "Соглашение" Тогда
		
		ТабДок = ПечатьДопСоглашение(Выборка, ОбъектыПечати);
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, ИмяМакета, 
		ПредставлениеМакета, ТабДок, , ПолныйПутьКМакету);
		
	КонецЕсли;
	
КонецПроцедуры

Функция Запрос(МассивОбъектов)
	
	Запрос 			= Новый Запрос;
	Запрос.Текст 	=
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДополнительноеСоглашение.Дата КАК ДатаДокумента,
	|	ДополнительноеСоглашение.Офис,
	|	ДополнительноеСоглашение.СуммаДокумента,
	|	ДополнительноеСоглашение.Договор.Номер КАК НомерДоговора,
	|	ДополнительноеСоглашение.Договор.Дата КАК ДатаДоговора,
	|	ДополнительноеСоглашение.Договор.Контрагент КАК Контрагент,
	|	ДополнительноеСоглашение.Договор.Организация КАК Организация,
	|	ДополнительноеСоглашение.Договор.Контрагент.ЮридическоеЛицо КАК ФизЮрЛицо,
	|	ДополнительноеСоглашение.Договор.Подразделение КАК Подразделение,
	|	ДополнительноеСоглашение.Офис.Город КАК ГородОфиса,
	|	ДополнительноеСоглашение.Офис.Телефон КАК ТелефонОфиса,
	|	ДополнительноеСоглашение.Офис.Адрес КАК АдресОФиса,
	|	ДополнительноеСоглашение.Ссылка
	|ИЗ
	|	Документ.ДополнительноеСоглашение КАК ДополнительноеСоглашение
	|ГДЕ
	|	ДополнительноеСоглашение.Ссылка В(&МассивСсылок)
	|	И ДополнительноеСоглашение.Проведен";
	
	Запрос.Параметры.Вставить("МассивСсылок", МассивОбъектов);
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции // Запрос()
