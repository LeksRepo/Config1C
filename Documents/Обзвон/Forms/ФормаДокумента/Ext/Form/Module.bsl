
#Область ПРОЦЕДУРЫ_И_ФУНКЦИИ_ОБЩЕГО_НАЗНАЧЕНИЯ

&НаКлиенте
Функция ПередЗаполнениемТаблицы(ИмяТаблицы)
	
	Результат = Истина;
	
	Если НЕ ЗначениеЗаполнено(Объект.Подразделение) Тогда
		Результат = Ложь;
		ТекстСообщения = "Укажите подразделение";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Объект.Подразделение");
	КонецЕсли;
	
	Таблица = Объект[ИмяТаблицы];
	КоличествоСтрок = Таблица.Количество();
	
	Если Результат И КоличествоСтрок > 0 Тогда
		
		Режим = РежимДиалогаВопрос.ДаНет;
		ТекстВопроса = "Таблица '%1' содержит %2 строк и будет очищена. %3 Продолжить?";
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстВопроса,
		Элементы[ИмяТаблицы].Заголовок,
		КоличествоСтрок,
		Символы.ПС);
		
		Ответ = Вопрос(ТекстВопроса, Режим, 0);
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Результат = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьПоля()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаДоговоров", Объект.СписокДоговоров.Выгрузить());
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаДоговоров.Договор
	|ПОМЕСТИТЬ ТаблицаДоговоров
	|ИЗ
	|	&ТаблицаДоговоров КАК ТаблицаДоговоров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДоговоров.Договор,
	|	ОбзвонСписокДоговоров.Ссылка.Дата,
	|	ОбзвонСписокДоговоров.Договор.Спецификация.ДатаМонтажа КАК ДатаМонтажа
	|ИЗ
	|	ТаблицаДоговоров КАК ТаблицаДоговоров
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Обзвон.СписокДоговоров КАК ОбзвонСписокДоговоров
	|		ПО ТаблицаДоговоров.Договор = ОбзвонСписокДоговоров.Договор
	|ГДЕ
	|	ОбзвонСписокДоговоров.Ссылка <> &Ссылка
	|	И НЕ ОбзвонСписокДоговоров.Ссылка.ПометкаУдаления";
	
	ТЗ = Запрос.Выполнить().Выгрузить();
	
	Если Объект.СписокДоговоров.Количество() > 0 Тогда
		
		Для Каждого Строка Из Объект.СписокДоговоров Цикл
			
			Строка.ПодсказкаДизайн = "Дизайн";
			Строка.ПодсказкаЗамер = "Замер";
			Строка.ПодсказкаМонтаж = "Монтаж";
			Строка.ПодсказкаДоставка = "Доставка";
			
			НайденнаяСтрока = ТЗ.Найти(Строка.Договор);
			Если НайденнаяСтрока <> Неопределено Тогда
				Строка.ДатаЗвонка = НайденнаяСтрока.Дата;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОБРАБОТЧИКИ_СОБЫТИЙ_ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьПоля();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ЗаполнитьПоля();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
КонецПроцедуры

#КонецОбласти

#Область КОМАНДЫ

&НаКлиенте
Процедура ЗаполнитьДоговоры(Команда)
	
	Если ПередЗаполнениемТаблицы("СписокДоговоров") Тогда
		ЗаполнитьДоговорыНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДоговорыНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Дата", НачалоДня(ТекущаяДата() - 8 * 86400));
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.УстановитьПараметр("Подразделение", Объект.Подразделение);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОбзвонСписокДоговоров.Договор,
	|	ОбзвонСписокДоговоров.Клиент КАК Клиент,
	|	ОбзвонСписокДоговоров.Телефон,
	|	МАКСИМУМ(ВЫРАЗИТЬ(ОбзвонСписокДоговоров.ОбщийКомментарий КАК СТРОКА(500))) КАК ОбщийКомментарий,
	|	ОбзвонСписокДоговоров.Клиент.Фамилия КАК Фамилия,
	|	МАКСИМУМ(ОбзвонСписокДоговоров.Ссылка.Дата) КАК ДатаЗвонка,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ОбзвонСписокДоговоров.Ссылка.Дата) КАК КоличествоПерезвонить,
	|	ОбзвонСписокДоговоров.Договор.Спецификация.ДатаМонтажа КАК ДатаМонтажа,
	|	ОбзвонСписокДоговоров.Договор.Спецификация.ДатаИзготовления КАК ДатаИзготовления,
	|	ОбзвонСписокДоговоров.Договор.Спецификация.ДатаОтгрузки КАК ДатаОтгрузки
	|ПОМЕСТИТЬ ВТ_Перезвон
	|ИЗ
	|	Документ.Обзвон.СписокДоговоров КАК ОбзвонСписокДоговоров
	|ГДЕ
	|	ОбзвонСписокДоговоров.ОбщаяОценка = ЗНАЧЕНИЕ(Перечисление.Оценки.Перезвонить)
	|	И НЕ ОбзвонСписокДоговоров.Договор В
	|				(ВЫБРАТЬ
	|					Дозвон.Договор
	|				ИЗ
	|					Документ.Обзвон.СписокДоговоров КАК Дозвон
	|				ГДЕ
	|					Дозвон.ОбщаяОценка <> ЗНАЧЕНИЕ(Перечисление.Оценки.Перезвонить))
	|	И ОбзвонСписокДоговоров.Ссылка <> &Ссылка
	|	И НЕ ОбзвонСписокДоговоров.Ссылка.ПометкаУдаления
	|	И ОбзвонСписокДоговоров.Ссылка.Подразделение = &Подразделение
	|
	|СГРУППИРОВАТЬ ПО
	|	ОбзвонСписокДоговоров.Клиент,
	|	ОбзвонСписокДоговоров.Договор,
	|	ОбзвонСписокДоговоров.Телефон,
	|	ОбзвонСписокДоговоров.Клиент.Фамилия,
	|	ОбзвонСписокДоговоров.Договор.Спецификация.ДатаМонтажа,
	|	ОбзвонСписокДоговоров.Договор.Спецификация.ДатаИзготовления,
	|	ОбзвонСписокДоговоров.Договор.Спецификация.ДатаОтгрузки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Перезвон.Договор,
	|	ВТ_Перезвон.Клиент,
	|	ВТ_Перезвон.Договор.Спецификация.АдресМонтажа КАК Адрес,
	|	ВТ_Перезвон.Телефон,
	|	ВТ_Перезвон.ОбщийКомментарий,
	|	ВТ_Перезвон.Фамилия,
	|	""Дизайн"" КАК ПодсказкаДизайн,
	|	""Замер"" КАК ПодсказкаЗамер,
	|	""Монтаж"" КАК ПодсказкаМонтаж,
	|	""Доставка"" КАК ПодсказкаДоставка,
	|	ВТ_Перезвон.ДатаЗвонка,
	|	ВТ_Перезвон.Договор.Офис КАК Офис,
	|	"""" КАК ПакетУслуг,
	|	ВТ_Перезвон.Договор.Представление КАК ПредставлениеДоговора,
	|	ВТ_Перезвон.ДатаМонтажа КАК ДатаМонтажа,
	|	ВТ_Перезвон.ДатаИзготовления КАК ДатаИзготовления,
	|	ВТ_Перезвон.ДатаОтгрузки КАК ДатаОтгрузки
	|ИЗ
	|	ВТ_Перезвон КАК ВТ_Перезвон
	|ГДЕ
	|	ВТ_Перезвон.КоличествоПерезвонить < 2
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	докДоговор.Ссылка,
	|	докДоговор.Спецификация.Контрагент,
	|	докДоговор.Спецификация.АдресМонтажа,
	|	докДоговор.Спецификация.Контрагент.Телефон + "", "" + докДоговор.Спецификация.Контрагент.ТелефонДополнительный,
	|	докДоговор.Спецификация.Изделие.Наименование,
	|	докДоговор.Спецификация.Контрагент.Фамилия,
	|	""Дизайн"",
	|	""Замер"",
	|	""Монтаж"",
	|	""Доставка"",
	|	NULL,
	|	докДоговор.Офис,
	|	докДоговор.Спецификация.ПакетУслуг,
	|	докДоговор.Представление,
	|	докДоговор.Спецификация.ДатаМонтажа,
	|	докДоговор.Спецификация.ДатаИзготовления,
	|	докДоговор.Спецификация.ДатаОтгрузки
	|ИЗ
	|	Документ.Договор КАК докДоговор
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Обзвон.СписокДоговоров КАК ОбзвонСписокДоговоров
	|		ПО (ОбзвонСписокДоговоров.Договор = докДоговор.Ссылка)
	|ГДЕ
	|	ОбзвонСписокДоговоров.Договор ЕСТЬ NULL
	|	И докДоговор.Дата >= ДАТАВРЕМЯ(2013, 10, 1, 0, 0, 0)
	|	И докДоговор.Проведен
	|	И докДоговор.Подразделение = &Подразделение
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТ_Перезвон.Фамилия";
	
	ТаблицаДоговоров = Запрос.Выполнить().Выгрузить();
	
	Для Каждого Строка Из ТаблицаДоговоров Цикл
		
		Если ЗначениеЗаполнено(Строка.ПакетУслуг) Тогда
			Строка.ОбщийКомментарий = Строка.ОбщийКомментарий + " (" + Строка.ПакетУслуг + ")";
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Строка.ДатаМонтажа) Тогда
			Строка.ОбщийКомментарий = Строка.ОбщийКомментарий + " (Монтаж: " + Формат(Строка.ДатаМонтажа, "ДФ=dd.MM.yyyy") + ")";
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Строка.ДатаИзготовления) Тогда
			Строка.ОбщийКомментарий = Строка.ОбщийКомментарий + " (Дата изготовления: " + Формат(Строка.ДатаИзготовления, "ДФ=dd.MM.yyyy") + ")";
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Строка.ДатаОтгрузки) Тогда
			Строка.ОбщийКомментарий = Строка.ОбщийКомментарий + " (Дата отгрузки: " + Формат(Строка.ДатаОтгрузки, "ДФ=dd.MM.yyyy") + ")";
		КонецЕсли;
		
		Строка.ПредставлениеДоговора = Строка.ПредставлениеДоговора + " (" + Строка.Офис + ")";
		
	КонецЦикла;
	
	Объект.СписокДоговоров.Загрузить(ТаблицаДоговоров);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДилеров(Команда)
	
	Если ПередЗаполнениемТаблицы("СписокДилеров") Тогда
		ЗаполнитьДилеровНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДилеровНаСервере()
	
	Объект.СписокДилеров.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Период", Объект.Дата);
	Запрос.УстановитьПараметр("Подразделение", Объект.Подразделение);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Спецификация.Контрагент
	|ИЗ
	|	Документ.Спецификация КАК Спецификация
	|ГДЕ
	|	Спецификация.Проведен
	|	И Спецификация.Дилерский
	|	И Спецификация.Подразделение = &Подразделение
	|	И Спецификация.Дата МЕЖДУ ДОБАВИТЬКДАТЕ(&Период, ДЕНЬ, -30) И &Период";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = Объект.СписокДилеров.Добавить();
		НоваяСтрока.Дилер = Выборка.Контрагент;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСпецификации(Команда)
	
	Если ПередЗаполнениемТаблицы("СписокСпецификаций") Тогда
		ЗаполнитьСпецификацииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСпецификацииНаСервере()
	
	Объект.СписокСпецификаций.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаОкончания", НачалоДня(ТекущаяДата()));
	Запрос.УстановитьПараметр("Подразделение", Объект.Подразделение);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЕСТЬNULL(Обзвон.Дата, ДОБАВИТЬКДАТЕ(&ДатаОкончания, ДЕНЬ, -8)) КАК Дата
	|ПОМЕСТИТЬ ВТ_ПоследняяДата
	|ИЗ
	|	Документ.Обзвон КАК Обзвон
	|ГДЕ
	|	Обзвон.Проведен
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата УБЫВ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 15
	|	докСпецификация.Ссылка КАК Спецификация,
	|	докСпецификация.Контрагент.Телефон КАК Телефон,
	|	докСпецификация.ДатаОтгрузки КАК ДатаОтгрузки
	|ИЗ
	|	Документ.Спецификация КАК докСпецификация
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Договор КАК докДоговор
	|		ПО (докДоговор.Спецификация = докСпецификация.Ссылка),
	|	ВТ_ПоследняяДата КАК ВТ_ПоследняяДата
	|ГДЕ
	|	докДоговор.Спецификация ЕСТЬ NULL
	|	И докСпецификация.Проведен
	|	И НЕ докСпецификация.Дилерский
	|	И докСпецификация.СуммаДокумента > 5000
	|	И НЕ докСпецификация.Изделие.ЭтоПеределка
	|	И НЕ докСпецификация.Изделие.ЭтоДопСоглашение
	|	И докСпецификация.Подразделение = &Подразделение
	|	И докСпецификация.ДатаОтгрузки МЕЖДУ ВТ_ПоследняяДата.Дата И ДОБАВИТЬКДАТЕ(&ДатаОкончания, ДЕНЬ, -1)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаОтгрузки УБЫВ";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Объект.СписокСпецификаций.Добавить();
		НоваяСтрока.Спецификация = Выборка.Спецификация;
		НоваяСтрока.Телефон = Выборка.Телефон;
		НоваяСтрока.Комментарий = НоваяСтрока.Комментарий + "(Отгрузка: " + Формат(Выборка.ДатаОтгрузки, "ДФ=dd.MM.yyyy") + ")";
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СОБЫТИЯ_ТАБЛИЧНЫХ_ЧАСТЕЙ

&НаКлиенте
Процедура СписокДоговоровПередУдалением(Элемент, Отказ)
	
	Режим = РежимДиалогаВопрос.ДаНет;
	Текст = "Удалить обзвон по данному договору?";
	
	Если Объект.СписокДоговоров.Количество() > 0
		И Вопрос(Текст, Режим, 0) = КодВозвратаДиалога.Нет Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокСпецификацийПередУдалением(Элемент, Отказ)
	
	Режим = РежимДиалогаВопрос.ДаНет;
	Текст = "Удалить обзвон по данной спецификации?";
	
	Если Объект.СписокСпецификаций.Количество() > 0
		И Вопрос(Текст, Режим, 0) = КодВозвратаДиалога.Нет Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти




