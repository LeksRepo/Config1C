
&НаКлиенте
Процедура СписокНоменклатурыКоличествоПриИзменении(Элемент)
	
	РассчитатьСумму();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыЦенаПриИзменении(Элемент)
	
	РассчитатьСумму();
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСумму()
	
	Стр = Элементы.СписокНоменклатуры.ТекущиеДанные;
	Стр.Сумма = Стр.Количество * Стр.Цена;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыСуммаПриИзменении(Элемент)
	
	Стр = Элементы.СписокНоменклатуры.ТекущиеДанные;
	Если Стр.Количество <> 0 Тогда
		Стр.Цена = Стр.Сумма / Стр.Количество;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РазрешеноИзменятьЦену = ПроверитьПользователя();
	ЭтаФорма.Элементы.СписокНоменклатурыЦена.ТолькоПросмотр = НЕ РазрешеноИзменятьЦену;
	ЭтаФорма.Элементы.СписокНоменклатурыСумма.ТолькоПросмотр = НЕ РазрешеноИзменятьЦену;
	
КонецПроцедуры

&НаСервере
Функция ПроверитьПользователя()

	Ответ = Ложь;
	
	СписокПравРазрешеноИзменятьЦену = Новый СписокЗначений;
	СписокПравРазрешеноИзменятьЦену.Добавить(Метаданные.Роли.АдминистраторСистемы);
	СписокПравРазрешеноИзменятьЦену.Добавить(Метаданные.Роли.Администрирование);
	
	Для каждого ы Из СписокПравРазрешеноИзменятьЦену Цикл
		Ответ = Ответ ИЛИ РольДоступна(ы.Значение);
	КонецЦикла;
	
	Возврат Ответ;

КонецФункции // ПроверитьПользователя()

&НаКлиенте
Процедура СписокНоменклатурыНоменклатураПриИзменении(Элемент)
	
	ЗаполнитьЦену(Элементы.СписокНоменклатуры.ТекущиеДанные);
	РассчитатьСумму();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЦену(ТекущиеДанные)
	
	Парам = Новый Структура;
	Парам.Вставить("Подразделение", Объект.Подразделение);
	Парам.Вставить("Номенклатура", ТекущиеДанные.Номенклатура);
	Парам.Вставить("Дата", Объект.Дата);
	
	ТекущиеДанные.Цена = ПолучитьЦенуНоменклатуры(Парам);
	РассчитатьСумму();
	
КонецПроцедуры // ЗаполнитьЦену()

&НаСервереБезКонтекста
Функция ПолучитьЦенуНоменклатуры(Парам)
	
	Если Парам.Номенклатура.Базовый Тогда
		БазоваяНоменклатура = Парам.Номенклатура;
		Коэффициент = 1;
	Иначе
		БазоваяНоменклатура = Парам.Номенклатура.БазоваяНоменклатура;
		Коэффициент = Парам.Номенклатура.КоэффициентБазовых;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Дата", Парам.Дата);
	Запрос.УстановитьПараметр("Регион", Парам.Подразделение.Регион);
	Запрос.УстановитьПараметр("БазоваяНоменклатура", БазоваяНоменклатура);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЦеныНоменклатурыСрезПоследних.Розничная
	|ИЗ
	|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
	|			&Дата,
	|			Номенклатура = &БазоваяНоменклатура
	|				И Регион = &Регион) КАК ЦеныНоменклатурыСрезПоследних";
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		
		Ответ = 0;
		
	Иначе
		
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Ответ = Выборка.Розничная * Коэффициент;
		
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции // ПолучитьЦенуНоменклатуры()
