
Процедура ОбработкаПроведения(Отказ, Режим)
	
	Режим = РежимПроведенияДокумента.Неоперативный;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.УстановитьПараметр("Дата", КонецМесяца(Дата));
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЗакрытиеМесяца.Ссылка
	|ИЗ
	|	Документ.ЗакрытиеМесяца КАК ЗакрытиеМесяца
	|ГДЕ
	|	ЗакрытиеМесяца.Подразделение = &Подразделение
	|	И ЗакрытиеМесяца.Дата = &Дата
	|	И НЕ ЗакрытиеМесяца.Ссылка = &Ссылка";
	
	Результат = Запрос.Выполнить().Выбрать();
	
	Если Результат.Количество() = 0 Тогда
		
		Движения.Управленческий.Записывать = Истина;
		
		Для Каждого ТекСтрокаСписок Из Список Цикл
			
			Если ТекСтрокаСписок.Доход > 0 Тогда
				
				Движение = Движения.Управленческий.Добавить();
				Движение.Период = КонецМесяца(Дата);
				Движение.Подразделение = Подразделение;
				
				Движение.СчетКт = ПланыСчетов.Управленческий.ФинансовыйРезультат;
				
				Движение.СчетДт = ПланыСчетов.Управленческий.Доходы;
				Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.СтатьиДР] = ТекСтрокаСписок.Статья;
				
				Движение.Сумма =  ТекСтрокаСписок.Доход;
				
			КонецЕсли;
			
			Если ТекСтрокаСписок.Расход > 0 Тогда
				
				Движение = Движения.Управленческий.Добавить();
				Движение.Период = КонецМесяца(Дата);
				Движение.Подразделение = Подразделение;
				
				Движение.СчетКт = ПланыСчетов.Управленческий.Расходы;
				Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.СтатьиДР] = ТекСтрокаСписок.Статья;
				
				Движение.СчетДт = ПланыСчетов.Управленческий.ФинансовыйРезультат;
				
				Движение.Сумма = ТекСтрокаСписок.Расход;
				
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		Отказ = Истина;
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "Документ с такими параметрами уже существует. Измените Период или Подразделение.";
		Сообщение.Сообщить();
		
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДатыЗапретаИзменения.ПроверитьДатуЗапретаИзмененияПередЗаписьюДокумента(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	
КонецПроцедуры
