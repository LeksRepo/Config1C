
&НаКлиенте
Процедура Сформировать(Команда)
	
	ЗаполнитьДанными();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанными()
	
	Если ЗначениеЗаполнено(Объект.Подразделение) Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Подразделение", Объект.Подразделение);
		Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Объект.Дата));
		Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Объект.Дата));
		Запрос.УстановитьПараметр("Регистратор", Объект.Ссылка);
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	УправленческийОбороты.Субконто1 КАК ОборотСтатья,
		|	СУММА(УправленческийОбороты.СуммаОборотДт) КАК ОборотДт,
		|	СУММА(УправленческийОбороты.СуммаОборотКт) КАК ОборотКт
		|ПОМЕСТИТЬ ОборотыБезПроведенных
		|ИЗ
		|	РегистрБухгалтерии.Управленческий.Обороты(&НачалоПериода, &КонецПериода, Регистратор, Счет В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрибыльУбытки)), , Подразделение = &Подразделение, , ) КАК УправленческийОбороты
		|ГДЕ
		|	УправленческийОбороты.Регистратор <> &Регистратор
		|СГРУППИРОВАТЬ ПО
		|	УправленческийОбороты.Субконто1
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ISNULL(Остатки.Субконто1,ОборотыБезПроведенных.ОборотСтатья) КАК Статья,
		|	ВЫБОР
		|		КОГДА ОборотыБезПроведенных.ОборотКт ЕСТЬ NULL 			
		|       ТОГДА ISNULL(Остатки.СуммаОстатокКт, 0)
		|		ИНАЧЕ ВЫБОР
		|				КОГДА Остатки.СуммаОстатокКт ЕСТЬ NULL 
		|					ТОГДА ОборотыБезПроведенных.ОборотКт
		|				ИНАЧЕ Остатки.СуммаОстатокКт + ОборотыБезПроведенных.ОборотКт
		|			КОНЕЦ
		|	КОНЕЦ КАК Доход,
		|	ВЫБОР
		|		КОГДА ОборотыБезПроведенных.ОборотДт ЕСТЬ NULL 
		|		ТОГДА ISNULL(Остатки.СуммаОстатокДт, 0)	
		|		ИНАЧЕ ВЫБОР
		|				КОГДА Остатки.СуммаОстатокДт ЕСТЬ NULL 
		|					ТОГДА ОборотыБезПроведенных.ОборотДт
		|				ИНАЧЕ Остатки.СуммаОстатокДт + ОборотыБезПроведенных.ОборотДт
		|			КОНЕЦ
		|	КОНЕЦ КАК Расход
		|ИЗ
		|	РегистрБухгалтерии.Управленческий.Остатки(&НачалоПериода, Счет В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрибыльУбытки)), , Подразделение = &Подразделение) КАК Остатки
		|		ПОЛНОЕ СОЕДИНЕНИЕ ОборотыБезПроведенных КАК ОборотыБезПроведенных
		|		ПО Остатки.Субконто1 = ОборотыБезПроведенных.ОборотСтатья
		|УПОРЯДОЧИТЬ ПО
		|	Расход";
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Объект.Список.Загрузить(Результат.Выгрузить());
		
		Объект.Сумма = Объект.Список.Итог("Доход") - Объект.Список.Итог("Расход")
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	
	Объект.Дата = КонецМесяца(Объект.Дата);
	ЗаполнитьДанными();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеПриИзменении(Элемент)
	
	ЗаполнитьДанными();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Отказ = ПередЗаписьюНаСервере();
	
	Если Отказ Тогда
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "Документ с такими параметрами уже существует. Измените Период или Подразделение.";
		Сообщение.Сообщить();
	
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Функция ПередЗаписьюНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Подразделение", Объект.Подразделение);
	Запрос.УстановитьПараметр("Дата", КонецМесяца(Объект.Дата));
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЗакрытиеМесяца.Ссылка
	|ИЗ
	|	Документ.ЗакрытиеМесяца КАК ЗакрытиеМесяца
	|ГДЕ
	|	ЗакрытиеМесяца.Подразделение = &Подразделение
	|	И ЗакрытиеМесяца.Дата = &Дата
	|	И НЕ ЗакрытиеМесяца.Ссылка = &Ссылка";
	
	Результат = Запрос.Выполнить().Выбрать();
	
	Отказ = Ложь;
	
	Если Результат.Количество() > 0 Тогда
		  Отказ = Истина;
	КонецЕсли;
	  
	Возврат Отказ;  
	  
КонецФункции
