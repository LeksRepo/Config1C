////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем
СчетКт,
СчетМатериалы,
СчетВзаиморасчетыСПоставщиками,
ПВХСклад,
ПВХКонтрагент,
ПВХОфисы,
ПВХСтатьиДР,
Запрос;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

Функция ИнициализироватьПеременныеМодуля()
	
	СчетМатериалы = ПланыСчетов.Управленческий.МатериалыНаСкладе;
	СчетВзаиморасчетыСПоставщиками = ПланыСчетов.Управленческий.ВзаиморасчетыСПоставщиками;
	СчетРасходы = ПланыСчетов.Управленческий.Расходы;
	
	ПВХСклад = ПланыВидовХарактеристик.ВидыСубконто.Склады;
	ПВХКонтрагент = ПланыВидовХарактеристик.ВидыСубконто.Контрагенты;
	ПВХОфисы = ПланыВидовХарактеристик.ВидыСубконто.Офисы;
	ПВХСтатьиДР = ПланыВидовХарактеристик.ВидыСубконто.СтатьиДР;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
КонецФункции

Функция СформироватьДвиженияМатериалы(фСчетКт)
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МИНИМУМ(ПоступлениеМатериаловУслугМатериалы.НомерСтроки) КАК НомерСтроки,
	|	ПоступлениеМатериаловУслугМатериалы.Номенклатура,
	|	СУММА(ПоступлениеМатериаловУслугМатериалы.Количество) КАК Количество,
	|	СУММА(ПоступлениеМатериаловУслугМатериалы.Сумма) КАК Сумма,
	|	МИНИМУМ(ПоступлениеМатериаловУслугМатериалы.Содержание) КАК Содержание
	|ИЗ
	|	Документ.ПоступлениеМатериаловУслуг.Материалы КАК ПоступлениеМатериаловУслугМатериалы
	|ГДЕ
	|	ПоступлениеМатериаловУслугМатериалы.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПоступлениеМатериаловУслугМатериалы.Номенклатура";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Проводка = Движения.Управленческий.Добавить();
		Проводка.Период = Дата;
		Проводка.Подразделение = Подразделение;
		Проводка.Сумма = Выборка.Сумма;
		Проводка.СчетДт = СчетМатериалы;
		Проводка.СубконтоДт[ПВХСклад] = Склад;
		Проводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
		Проводка.КоличествоДт = Выборка.Количество;
		Проводка.СчетКТ = фСчетКт;
		
		Если фСчетКт = ПланыСчетов.Управленческий.Доходы Тогда
			Проводка.СубконтоКт[ПВХСтатьиДР] = Справочники.СтатьиДоходовРасходов.ОприходованиеМатериалов;
		Иначе
			Проводка.СубконтоКт[ПВХКонтрагент] = Контрагент;
		КонецЕсли;
		
		Проводка.Содержание = Выборка.Содержание;
		
	КонецЦикла;
	
КонецФункции

Функция СформироватьДвиженияУслуги()
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПоступлениеМатериаловУслугУслуги.Номенклатура,
	|	СУММА(ПоступлениеМатериаловУслугУслуги.Сумма) КАК Сумма,
	|	МИНИМУМ(ПоступлениеМатериаловУслугУслуги.Содержание) КАК Содержание,
	|	ПоступлениеМатериаловУслугУслуги.Офис,
	|	ПоступлениеМатериаловУслугУслуги.Номенклатура.СтатьяДоходаРасхода СтатьяДоходаРасхода
	|ИЗ
	|	Документ.ПоступлениеМатериаловУслуг.Услуги КАК ПоступлениеМатериаловУслугУслуги
	|ГДЕ
	|	ПоступлениеМатериаловУслугУслуги.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПоступлениеМатериаловУслугУслуги.Номенклатура,
	|	ПоступлениеМатериаловУслугУслуги.Офис";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Проводка = Движения.Управленческий.Добавить();
		Проводка.Период = Дата;
		Проводка.Подразделение = Подразделение;
		Проводка.Сумма = Выборка.Сумма;
		Проводка.СчетДт = ПланыСчетов.Управленческий.Расходы;
		Проводка.СубконтоДт[ПВХСтатьиДР] = Выборка.СтатьяДоходаРасхода;
		
		Проводка.СчетКТ = СчетВзаиморасчетыСПоставщиками;
		Проводка.СубконтоКт[ПВХКонтрагент] = Контрагент;
		Проводка.Содержание = Выборка.Содержание;
		
	КонецЦикла;
	
КонецФункции

Функция СформироватьДвиженияОсновныеСредства()
	
	Сформировать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТЧОсновныеСредства", ОсновныеСредства.Выгрузить());
	Запрос.УстановитьПараметр("МассивОС", ОсновныеСредства.ВыгрузитьКолонку("ОсновноеСредство"));
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПоступлениеМатериаловУслугОсновныеСредстваТЗ.ОсновноеСредство КАК ОсновноеСредствоКоличество,
	|	ПоступлениеМатериаловУслугОсновныеСредстваТЗ.ОсновноеСредство КАК ОсновноеСредство
	|ПОМЕСТИТЬ ВТ_ОсновныеСредства
	|ИЗ
	|	&ТЧОсновныеСредства КАК ПоступлениеМатериаловУслугОсновныеСредстваТЗ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(ВТ_ОсновныеСредства.ОсновноеСредствоКоличество) КАК Количество,
	|	ВТ_ОсновныеСредства.ОсновноеСредство КАК ОсновноеСредство,
	|	СУММА(УправленческийОстатки.КоличествоОстаток) КАК КоличествоОстаток,
	|	МАКСИМУМ(ОсновныеСредства.Код) КАК ИнвентарныйНомер
	|ИЗ
	|	ВТ_ОсновныеСредства КАК ВТ_ОсновныеСредства
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОсновныеСредства КАК ОсновныеСредства
	|		ПО ВТ_ОсновныеСредства.ОсновноеСредство = ОсновныеСредства.Ссылка,
	|	РегистрБухгалтерии.Управленческий.Остатки(, Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПервоначальнаяСтоимостьОС), , Субконто1 В (&МассивОС)) КАК УправленческийОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ОсновныеСредства.ОсновноеСредство";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если Выборка.Количество > 1 ИЛИ ЗначениеЗаполнено(Выборка.КоличествоОстаток) И Выборка.КоличествоОстаток > 0 Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Основное средство %1 инв. номер %2 уже присутствует в табличной части или оприходовано, каждое основное средство должно быть уникальным", Выборка.ОсновноеСредство, Выборка.ИнвентарныйНомер);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			Сформировать = Ложь;
			Возврат Сформировать;
		КонецЕсли;
	КонецЦикла;
	
	// сюда бы контроль повторного оприходования ОС
	// и здесь же добавить проводки для начисления амортизации
	Для каждого СтрокаОС Из ОсновныеСредства Цикл
		
		Проводка = Движения.Управленческий.Добавить();
		Если СтрокаОС.НачислятьАмортизацию Тогда			
			Проводка.Период = КонецМесяца(Дата) + 1; //Начислять амортизацию со следующего месяца
		Иначе							
			Проводка.Период = Дата;				
		КонецЕсли;
		Проводка.Подразделение = Подразделение;
		Проводка.Сумма = СтрокаОС.Стоимость;
		Проводка.СчетДт = ПланыСчетов.Управленческий.ПервоначальнаяСтоимостьОС;
		Проводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.ОсновныеСредства] = СтрокаОС.ОсновноеСредство;
		Проводка.КоличествоДт = 1;
		
		Проводка.СчетКТ = СчетВзаиморасчетыСПоставщиками;
		Проводка.СубконтоКт[ПВХКонтрагент] = Контрагент;
		Проводка.Содержание = СтрокаОС.Содержание;
		
		//////////////////////////////////////////////
		/// ДВИЖЕНИЯ ПО РЕГИСТРУ ОСНОВНЫЕ СРЕДСТВА ///
		Если Дата > '2014.10.01' Тогда
			
			Проводка = Движения.ОсновныеСредства.Добавить();
			Проводка.Период = Дата;
			Проводка.ОсновноеСредство = СтрокаОС.ОсновноеСредство;
			Проводка.Стоимость = СтрокаОС.Стоимость;
			Проводка.СрокАмортизации = СтрокаОС.Срок;
			Проводка.МОЛ = СтрокаОС.МОЛ;
			Проводка.НачислятьАмортизацию = СтрокаОС.НачислятьАмортизацию;
			
		КонецЕсли;
		
		/// ДВИЖЕНИЯ ПО РЕГИСТРУ ОСНОВНЫЕ СРЕДСТВА ///
		//////////////////////////////////////////////
		
	КонецЦикла;
	
	Возврат Сформировать;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если РольДоступна("ВводОприходованияКладовщиком") Тогда
		
		ВидОперации = Перечисления.ХозяйственнаяОперация.ОприходованиеКладовщиком;
		
	Иначе
		
		ВидОперации = Перечисления.ХозяйственнаяОперация.ЗакупкаУПоставщика;
		
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ИнвентаризацияМатериалов") Тогда
		
		ВидОперации = Перечисления.ХозяйственнаяОперация.ОприходованиеБухгалтером;
		Подразделение = ДанныеЗаполнения.Подразделение;
		Склад = ДанныеЗаполнения.Склад;
		
		Для каждого Строка Из ДанныеЗаполнения.СписокНоменклатуры Цикл
			Если Строка.Отклонение > 0 Тогда
				НоваяСтрока = Материалы.Добавить();
				НоваяСтрока.Номенклатура = Строка.Номенклатура;
				НоваяСтрока.Количество = Строка.Отклонение;
				НоваяСтрока.Цена = Строка.Цена;
				НоваяСтрока.Сумма = Строка.Цена * НоваяСтрока.Количество;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.Управленческий.Записывать = Истина;
	ИнициализироватьПеременныеМодуля();
	
	Если ВидОперации = Перечисления.ХозяйственнаяОперация.ЗакупкаУПоставщика Тогда
		// оприходование бухгалтером
		// по полной программе
		
		СформироватьДвиженияМатериалы(ПланыСчетов.Управленческий.ВзаиморасчетыСПоставщиками);
		СформироватьДвиженияУслуги();
		Если НЕ СформироватьДвиженияОсновныеСредства() Тогда
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
	ИначеЕсли ВидОперации = Перечисления.ХозяйственнаяОперация.ОприходованиеКладовщиком Тогда
		
		// оприходование кладовщиком
		// принимаем только материалы на временный счет
		СформироватьДвиженияМатериалы(ПланыСчетов.Управленческий.ПромежуточныеВзаиморасчетыСПоставщиками);
		
	ИначеЕсли ВидОперации = Перечисления.ХозяйственнаяОперация.ОприходованиеБухгалтером Тогда
		
		// оприходование бухгалтером
		// принимаем материалы ниоткуда (сразу прибыль)
		СформироватьДвиженияМатериалы(ПланыСчетов.Управленческий.Доходы);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив();
	// реквизит Склад не всегда должен быть заполнен
	// Получаем только основные средства или только услуги
	Если Материалы.Количество() = 0 Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Склад");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ВидОперации = Неопределено;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДатыЗапретаИзменения.ПроверитьДатуЗапретаИзмененияПередЗаписьюДокумента(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	СуммаДокумента = Материалы.Итог("Сумма") + Услуги.Итог("Сумма") + ОсновныеСредства.Итог("Стоимость");
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
