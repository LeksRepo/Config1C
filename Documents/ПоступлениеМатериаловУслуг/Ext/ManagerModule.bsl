
Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	ЛексСервер.ПолучитьПредставлениеДокумента(Данные, Представление, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ТабДок = ПечатьПоступлениеМатериалов(МассивОбъектов, ОбъектыПечати);
	УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ПечатьПоступлениеМатериаловУслуг", "ПечатьПоступлениеМатериаловУслуг", ТабДок,, "Документ.ПоступлениеМатериаловУслуг.ПечатьПоступлениеМатериаловУслуг");
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
КонецПроцедуры

Функция ПечатьПоступлениеМатериалов(МассивОбъектов, ОбъектыПечати) Экспорт
	
	ТабДок = Новый ТабличныйДокумент;
	
	ФормСтрока = "Л = ru_RU; ДП = Истина";
	ПарПредмета="рубль, рубля, рублей, м, копейка, копейки, копеек, ж, 0";
	
	Макет = Документы.ПоступлениеМатериаловУслуг.ПолучитьМакет("ПечатьПоступлениеМатериалов");
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("МассивОбъектов", МассивОбъектов);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПоступлениеМатериаловУслуг.Автор,
	|	ПоступлениеМатериаловУслуг.Дата,
	|	ПоступлениеМатериаловУслуг.Номер,
	|	ПоступлениеМатериаловУслуг.Подразделение,
	|	ПоступлениеМатериаловУслуг.Склад,
	|	ПоступлениеМатериаловУслуг.СуммаДокумента,
	|	ПоступлениеМатериаловУслуг.Материалы.(
	|		НомерСтроки КАК НомерСтроки,
	|		Номенклатура,
	|		Количество,
	|		Цена,
	|		Сумма
	|	),
	|	ПоступлениеМатериаловУслуг.Услуги.(
	|		НомерСтроки,
	|		Номенклатура,
	|		Офис,
	|		Содержание,
	|		Сумма
	|	),
	|	ПоступлениеМатериаловУслуг.ОсновныеСредства.(
	|		НомерСтроки,
	|		МОЛ,
	|		НачислятьАмортизацию,
	|		ОсновноеСредство,
	|		Срок,
	|		Стоимость,
	|		Содержание
	|	),
	|	ПоступлениеМатериаловУслуг.Ссылка
	|ИЗ
	|	Документ.ПоступлениеМатериаловУслуг КАК ПоступлениеМатериаловУслуг
	|ГДЕ
	|	ПоступлениеМатериаловУслуг.Ссылка В(&МассивОбъектов)
	|	И ПоступлениеМатериаловУслуг.Проведен
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	Шапка = Макет.ПолучитьОбласть("Шапка");
	Подвал = Макет.ПолучитьОбласть("Подвал");
	ОбластьМатериалыШапка = Макет.ПолучитьОбласть("МатериалыШапка");
	ОбластьМатериалыСтрока = Макет.ПолучитьОбласть("МатериалыСтрока");
	ОбластьУслугиШапка = Макет.ПолучитьОбласть("УслугиШапка");
	ОбластьУслугиСтрока = Макет.ПолучитьОбласть("УслугиСтрока");
	ОбластьОсновныеСредстваШапка = Макет.ПолучитьОбласть("ОсновныеСредстваШапка");
	ОбластьОсновныеСредстваСтрока = Макет.ПолучитьОбласть("ОсновныеСредстваСтрока");
	
	ВставлятьРазделительСтраниц = Ложь;
	
	Пока Выборка.Следующий() Цикл
		
		НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1;
		
		Если ВставлятьРазделительСтраниц Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ТабДок.Вывести(ОбластьЗаголовок);
		
		Шапка.Параметры.Заполнить(Выборка);
		ТабДок.Вывести(Шапка, Выборка.Уровень());
		
		ВывестиТабличнуюЧасть("Материалы", ТабДок, Выборка, ОбластьМатериалыШапка, ОбластьМатериалыСтрока);
		ВывестиТабличнуюЧасть("Услуги", ТабДок, Выборка, ОбластьУслугиШапка, ОбластьУслугиСтрока);
		ВывестиТабличнуюЧасть("ОсновныеСредства", ТабДок, Выборка, ОбластьОсновныеСредстваШапка, ОбластьОсновныеСредстваСтрока);
		
		Подвал.Параметры.Заполнить(Выборка);
		Подвал.Параметры.СуммаДокумента = Выборка.СуммаДокумента;
		Подвал.Параметры.СуммаДокументаПрописью = ЧислоПрописью(Выборка.СуммаДокумента, ФормСтрока, ПарПредмета);
		ТабДок.Вывести(Подвал);
		
		ВставлятьРазделительСтраниц = Истина;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, НомерСтрокиНачало, ОбъектыПечати, Выборка.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции

Функция ВывестиТабличнуюЧасть(ИмяТЧ, ТабДок, Выборка, ОбластьШапка, ОбластьСтрока)
	
		ВыборкаСтроки = Выборка[ИмяТЧ].Выбрать();
		
		Если ВыборкаСтроки.Количество() > 0 Тогда
			ТабДок.Вывести(ОбластьШапка);
			Пока ВыборкаСтроки.Следующий() Цикл
				ОбластьСтрока.Параметры.Заполнить(ВыборкаСтроки);
				ТабДок.Вывести(ОбластьСтрока);
			КонецЦикла;
		КонецЕсли;
	
КонецФункции

Процедура ПриИзмененииРеквизитовВТЧСервер(СтруктураТЧ, СтруктураДействий) Экспорт
	
	Перем ЗначениеИзСтруктуры;
	
	СоответствиеДействий = Новый Соответствие;
	
	Если СтруктураТЧ.Свойство("СтрокиТЧ", ЗначениеИзСтруктуры) Тогда
		Для Каждого ТекущаяСтрока Из ЗначениеИзСтруктуры Цикл
			ТекСтруктураДействий = СоответствиеДействий[ТекущаяСтрока];
			Если ТекСтруктураДействий <> Неопределено Тогда
				ОбработатьСтрокуТЧСервер(ТекущаяСтрока, ТекСтруктураДействий);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	Если СтруктураТЧ.Свойство("ТекущаяСтрока", ЗначениеИзСтруктуры) Тогда
		ОбработатьСтрокуТЧСервер(ЗначениеИзСтруктуры, СтруктураДействий);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьСтрокуТЧСервер(ТекущаяСтрока, СтруктураДействий) Экспорт
	
	Перем ПараметрДействия;
	
	Если СтруктураДействий.Свойство("ЗаполнитьКоэффициент", ПараметрДействия) Тогда
		ТекущаяСтрока.Коэффициент = ТекущаяСтрока.ЕдиницаХранения.Коэффициент;
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ЗаполнитьКоличество", ПараметрДействия) Тогда
		ТекущаяСтрока.Количество = 1;
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ПересчитатьСумму", ПараметрДействия) Тогда
		ТекущаяСтрока.Сумма = ТекущаяСтрока.Цена*ТекущаяСтрока.Количество;
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ПересчитатьЦену", ПараметрДействия) Тогда
		Если ТекущаяСтрока.Количество<>0 Тогда
			ТекущаяСтрока.Цена = ТекущаяСтрока.Сумма/ТекущаяСтрока.Количество;
		Иначе
			ТекущаяСтрока.Цена = 0;
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры
