
Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	ЛексСервер.ПолучитьПредставлениеДокумента(Данные, Представление, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "МакетПоступлениеМатериаловУслуг") Тогда
		
		ПодготовитьПечатнуюФорму("МакетПоступлениеМатериаловУслуг", "Поступление материалов услуг", "Документ.ПоступлениеМатериаловУслуг.МакетПоступлениеМатериаловУслуг",
		МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
		
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "МакетПоступлениеОС") Тогда
		
		ПодготовитьПечатнуюФорму("МакетПоступлениеОС", "Поступление основных средств", "Документ.ПоступлениеМатериаловУслуг.МакетПоступлениеОС",
		МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "МакетОС1") Тогда
		
		ПодготовитьПечатнуюФорму("МакетОС1", "Поступление основных средств (ОС-1)", "Документ.ПоступлениеМатериаловУслуг.МакетОС1",
		МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПечатьПоступлениеМатериалов(МассивОбъектов, ОбъектыПечати) Экспорт
	
	ТабДок = Новый ТабличныйДокумент;
	
	ФормСтрока = "Л = ru_RU; ДП = Истина";
	ПарПредмета="рубль, рубля, рублей, м, копейка, копейки, копеек, ж, 0";
	
	Макет = Документы.ПоступлениеМатериаловУслуг.ПолучитьМакет("МакетПоступлениеМатериаловУслуг");
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("МассивОбъектов", МассивОбъектов);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПоступлениеМатериаловУслуг.Автор,
	|	ПоступлениеМатериаловУслуг.Дата,
	|	ПоступлениеМатериаловУслуг.Номер,
	|	ПоступлениеМатериаловУслуг.Подразделение,
	|	ПоступлениеМатериаловУслуг.Склад,
	|	ПоступлениеМатериаловУслуг.СуммаДокумента,
	|	ПоступлениеМатериаловУслуг.Материалы.(
	|		НомерСтроки КАК НомерСтроки,
	|		Номенклатура,
	|		Количество,
	|		Цена,
	|		Сумма
	|	),
	|	ПоступлениеМатериаловУслуг.Услуги.(
	|		НомерСтроки,
	|		Номенклатура,
	|		Офис,
	|		Содержание,
	|		Сумма
	|	),
	|	ПоступлениеМатериаловУслуг.Ссылка
	|ИЗ
	|	Документ.ПоступлениеМатериаловУслуг КАК ПоступлениеМатериаловУслуг
	|ГДЕ
	|	ПоступлениеМатериаловУслуг.Ссылка В(&МассивОбъектов)
	|	И ПоступлениеМатериаловУслуг.Проведен
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	Шапка = Макет.ПолучитьОбласть("Шапка");
	Подвал = Макет.ПолучитьОбласть("Подвал");
	ОбластьМатериалыШапка = Макет.ПолучитьОбласть("МатериалыШапка");
	ОбластьМатериалыСтрока = Макет.ПолучитьОбласть("МатериалыСтрока");
	ОбластьУслугиШапка = Макет.ПолучитьОбласть("УслугиШапка");
	ОбластьУслугиСтрока = Макет.ПолучитьОбласть("УслугиСтрока");
	
	ВставлятьРазделительСтраниц = Ложь;
	
	Пока Выборка.Следующий() Цикл
		
		НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1;
		
		Если ВставлятьРазделительСтраниц Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ОбластьЗаголовок.Параметры.НомерДокумента = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(Выборка.Номер);
		ОбластьЗаголовок.Параметры.ДатаДокумента = Формат(Выборка.Дата, "ДЛФ=DD");
		ТабДок.Вывести(ОбластьЗаголовок);
		
		Шапка.Параметры.Заполнить(Выборка);
		ТабДок.Вывести(Шапка, Выборка.Уровень());
		
		СуммаМатериалы = ВывестиТабличнуюЧасть("Материалы", ТабДок, Выборка, ОбластьМатериалыШапка, ОбластьМатериалыСтрока);
		СуммаУслуги = ВывестиТабличнуюЧасть("Услуги", ТабДок, Выборка, ОбластьУслугиШапка, ОбластьУслугиСтрока);
		
		Подвал.Параметры.Заполнить(Выборка);
		Подвал.Параметры.СуммаДокумента = СуммаМатериалы + СуммаУслуги; // Когда-нибудь придется разделить
		Подвал.Параметры.СуммаДокументаПрописью = ЧислоПрописью(СуммаМатериалы + СуммаУслуги, ФормСтрока, ПарПредмета);
		ТабДок.Вывести(Подвал);
		
		ВставлятьРазделительСтраниц = Истина;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, НомерСтрокиНачало, ОбъектыПечати, Выборка.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции

Функция ПечатьОС(МассивОбъектов, ОбъектыПечати) Экспорт
	
	ТабДок = Новый ТабличныйДокумент;
	
	ФормСтрока = "Л = ru_RU; ДП = Истина";
	ПарПредмета="рубль, рубля, рублей, м, копейка, копейки, копеек, ж, 0";
	
	Макет = Документы.ПоступлениеМатериаловУслуг.ПолучитьМакет("МакетПоступлениеОС");
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("МассивОбъектов", МассивОбъектов);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПоступлениеМатериаловУслугОсновныеСредства.Ссылка.Автор КАК Автор,
	|	ПоступлениеМатериаловУслугОсновныеСредства.Ссылка.Дата КАК Дата,
	|	ПоступлениеМатериаловУслугОсновныеСредства.Ссылка.Номер КАК Номер,
	|	ПоступлениеМатериаловУслугОсновныеСредства.Ссылка.Подразделение КАК Подразделение,
	|	ПоступлениеМатериаловУслугОсновныеСредства.Ссылка.Склад КАК Склад,
	|	ПоступлениеМатериаловУслугОсновныеСредства.Ссылка.СуммаДокумента КАК СуммаДокумента,
	|	ПоступлениеМатериаловУслугОсновныеСредства.Ссылка КАК Ссылка,
	|	ПоступлениеМатериаловУслугОсновныеСредства.НомерСтроки,
	|	ПоступлениеМатериаловУслугОсновныеСредства.МОЛ КАК МОЛ,
	|	ПоступлениеМатериаловУслугОсновныеСредства.НачислятьАмортизацию,
	|	ПоступлениеМатериаловУслугОсновныеСредства.ОсновноеСредство КАК ОС,
	|	ПоступлениеМатериаловУслугОсновныеСредства.Срок КАК КоличествоМесяцев,
	|	ПоступлениеМатериаловУслугОсновныеСредства.Стоимость КАК ПервоначальнаяСтоимость
	|ИЗ
	|	Документ.ПоступлениеМатериаловУслуг.ОсновныеСредства КАК ПоступлениеМатериаловУслугОсновныеСредства
	|ГДЕ
	|	ПоступлениеМатериаловУслугОсновныеСредства.Ссылка В(&МассивОбъектов)
	|	И ПоступлениеМатериаловУслугОсновныеСредства.Ссылка.Проведен
	|ИТОГИ ПО
	|	Ссылка,
	|	МОЛ";
	
	ВыборкаДокументы = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	Шапка = Макет.ПолучитьОбласть("Шапка");
	Подвал = Макет.ПолучитьОбласть("Подвал");
	ОбластьОСШапка = Макет.ПолучитьОбласть("ОСШапка");
	ОбластьОССтрока = Макет.ПолучитьОбласть("ОССтрока");
	
	ВставлятьРазделительСтраниц = Ложь;
	
	Пока ВыборкаДокументы.Следующий() Цикл
		
		ВыборкаМОЛ = ВыборкаДокументы.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаМОЛ.Следующий() Цикл
		НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1;
		
		Если ВставлятьРазделительСтраниц Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ОбластьЗаголовок.Параметры.НомерДокумента = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(ВыборкаДокументы.Номер);
		ОбластьЗаголовок.Параметры.ДатаДокумента = Формат(ВыборкаДокументы.Дата, "ДЛФ=DD");
		ТабДок.Вывести(ОбластьЗаголовок);
		
		Шапка.Параметры.Заполнить(ВыборкаДокументы);
		Шапка.Параметры.МОЛ = ВыборкаМОЛ.МОЛ;
		ТабДок.Вывести(Шапка, ВыборкаДокументы.Уровень());
		
		СуммаОС = 0;
		
		ВыборкаОС = ВыборкаМОЛ.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		ТабДок.Вывести(ОбластьОСШапка);
		
		Пока ВыборкаОС.Следующий() Цикл
			
			СуммаОС = СуммаОС + ВыборкаОС.ПервоначальнаяСтоимость;
			ОбластьОССтрока.Параметры.Заполнить(ВыборкаОС);
			ТабДок.Вывести(ОбластьОССтрока);
			
		КонецЦикла;
		
			Подвал.Параметры.Заполнить(ВыборкаДокументы);
			Подвал.Параметры.СуммаДокумента = СуммаОС;
			Подвал.Параметры.МОЛ = ВыборкаМОЛ.МОЛ;
			Подвал.Параметры.СуммаДокументаПрописью = ЧислоПрописью(СуммаОС, ФормСтрока, ПарПредмета);
			ТабДок.Вывести(Подвал);
			
		ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
			
		КонецЦикла;
			
		ВставлятьРазделительСтраниц = Истина;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, НомерСтрокиНачало, ОбъектыПечати, ВыборкаДокументы.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции

Функция ПечатьОС1(МассивОбъектов, ОбъектыПечати) Экспорт
	
	ТабДок = Новый ТабличныйДокумент;
	
	ФормСтрока = "Л = ru_RU; ДП = Истина";
	ПарПредмета="рубль, рубля, рублей, м, копейка, копейки, копеек, ж, 0";
	
	Макет = Документы.ПоступлениеМатериаловУслуг.ПолучитьМакет("МакетОС1");
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("МассивОбъектов", МассивОбъектов);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПоступлениеМатериаловУслугОсновныеСредства.Ссылка,
	|	ПоступлениеМатериаловУслугОсновныеСредства.Ссылка.Дата КАК Дата,
	|	ПоступлениеМатериаловУслугОсновныеСредства.Ссылка.Номер КАК Номер,
	|	ПоступлениеМатериаловУслугОсновныеСредства.НомерСтроки,
	|	ПоступлениеМатериаловУслугОсновныеСредства.МОЛ,
	|	ПоступлениеМатериаловУслугОсновныеСредства.НачислятьАмортизацию,
	|	ПоступлениеМатериаловУслугОсновныеСредства.ОсновноеСредство.Наименование КАК НаименованиеОС,
	|	ПоступлениеМатериаловУслугОсновныеСредства.Срок КАК СрокЭкспл,
	|	ПоступлениеМатериаловУслугОсновныеСредства.Срок КАК СрокПолезнИспПриПеред,
	|	ПоступлениеМатериаловУслугОсновныеСредства.Стоимость КАК ОстСтоимость,
	|	ПоступлениеМатериаловУслугОсновныеСредства.Содержание,
	|	ПоступлениеМатериаловУслугОсновныеСредства.ОсновноеСредство.ИнвентарныйНомер КАК ИнвНомер,
	|	ПоступлениеМатериаловУслугОсновныеСредства.Ссылка.Контрагент КАК НаимСдатчика,
	|	ПоступлениеМатериаловУслугОсновныеСредства.Ссылка.Подразделение КАК ПодрПолучателя
	|ИЗ
	|	Документ.ПоступлениеМатериаловУслуг.ОсновныеСредства КАК ПоступлениеМатериаловУслугОсновныеСредства
	|ГДЕ
	|	ПоступлениеМатериаловУслугОсновныеСредства.Ссылка В(&МассивОбъектов)
	|	И ПоступлениеМатериаловУслугОсновныеСредства.Ссылка.Проведен";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ОбластьТело = Макет.ПолучитьОбласть("Тело");
	
	ВставлятьРазделительСтраниц = Ложь;
	
	Пока Выборка.Следующий() Цикл
		
		НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1;
		
		Если ВставлятьРазделительСтраниц Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ОбластьТело.Параметры.НомерДокументаСуффикс = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(Выборка.Номер) + "-" + Выборка.НомерСтроки;
		ОбластьТело.Параметры.ДатаДокумента = Формат(Выборка.Дата, "ДЛФ=DD");
		
		ОбластьТело.Параметры.Заполнить(Выборка);
		ТабДок.Вывести(ОбластьТело);
		
		ВставлятьРазделительСтраниц = Истина;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, НомерСтрокиНачало, ОбъектыПечати, Выборка.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции

Процедура ПодготовитьПечатнуюФорму(Знач ИмяМакета, ПредставлениеМакета, ПолныйПутьКМакету = "", МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода)
	
	ВывестиДокумент = Ложь;
	
	Если ИмяМакета = "МакетПоступлениеМатериаловУслуг" Тогда
		
		ТабДок = ПечатьПоступлениеМатериалов(МассивОбъектов, ОбъектыПечати);
		ВывестиДокумент = Истина;
		
	ИначеЕсли ИмяМакета = "МакетОС1" Тогда
		
		ТабДок = ПечатьОС1(МассивОбъектов, ОбъектыПечати);
		ВывестиДокумент = Истина;
		
	ИначеЕсли ИмяМакета = "МакетПоступлениеОС" Тогда
		
		ТабДок = ПечатьОС(МассивОбъектов, ОбъектыПечати);
		ВывестиДокумент = Истина;
		
	КонецЕсли;
	
	Если ВывестиДокумент Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, ИмяМакета, ПредставлениеМакета, ТабДок, , ПолныйПутьКМакету);
	КонецЕсли;
	
КонецПроцедуры

Функция ВывестиТабличнуюЧасть(ИмяТЧ, ТабДок, Выборка, ОбластьШапка, ОбластьСтрока)
	
	Сумма = 0;
	
	ВыборкаСтроки = Выборка[ИмяТЧ].Выбрать();
	
	Если ВыборкаСтроки.Количество() > 0 Тогда
		ТабДок.Вывести(ОбластьШапка);
		Пока ВыборкаСтроки.Следующий() Цикл
			Сумма = Сумма + ВыборкаСтроки.Сумма;
			ОбластьСтрока.Параметры.Заполнить(ВыборкаСтроки);
			ТабДок.Вывести(ОбластьСтрока);
		КонецЦикла;
	КонецЕсли;
	
	Возврат Сумма;
	
КонецФункции

Процедура ПриИзмененииРеквизитовВТЧСервер(СтруктураТЧ, СтруктураДействий) Экспорт
	
	Перем ЗначениеИзСтруктуры;
	
	СоответствиеДействий = Новый Соответствие;
	
	Если СтруктураТЧ.Свойство("СтрокиТЧ", ЗначениеИзСтруктуры) Тогда
		Для Каждого ТекущаяСтрока Из ЗначениеИзСтруктуры Цикл
			ТекСтруктураДействий = СоответствиеДействий[ТекущаяСтрока];
			Если ТекСтруктураДействий <> Неопределено Тогда
				ОбработатьСтрокуТЧСервер(ТекущаяСтрока, ТекСтруктураДействий);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если СтруктураТЧ.Свойство("ТекущаяСтрока", ЗначениеИзСтруктуры) Тогда
		ОбработатьСтрокуТЧСервер(ЗначениеИзСтруктуры, СтруктураДействий);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьСтрокуТЧСервер(ТекущаяСтрока, СтруктураДействий) Экспорт
	
	Перем ПараметрДействия;
	
	Если СтруктураДействий.Свойство("ЗаполнитьКоэффициент", ПараметрДействия) Тогда
		ТекущаяСтрока.Коэффициент = ТекущаяСтрока.ЕдиницаХранения.Коэффициент;
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ЗаполнитьКоличество", ПараметрДействия) Тогда
		ТекущаяСтрока.Количество = 1;
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ПересчитатьСумму", ПараметрДействия) Тогда
		ТекущаяСтрока.Сумма = ТекущаяСтрока.Цена*ТекущаяСтрока.Количество;
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ПересчитатьЦену", ПараметрДействия) Тогда
		Если ТекущаяСтрока.Количество<>0 Тогда
			ТекущаяСтрока.Цена = ТекущаяСтрока.Сумма/ТекущаяСтрока.Количество;
		Иначе
			ТекущаяСтрока.Цена = 0;
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры

