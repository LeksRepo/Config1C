
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВидОперации = Параметры.ВидОперации;
	СформироватьОтчет(Параметры.Период, Параметры.Дизайнер);
	
КонецПроцедуры

&НаСервере
Функция СформироватьОтчет(Период, Дизайнер)
	
	УстановитьПривилегированныйРежим(Истина);
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.ИмяПараметровПечати = "ПараметрыПечати_ЗаработнаяПлата";
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Истина;
	ТабДок.ТолькоПросмотр = Истина;
	ТабДок.ОтображатьЗаголовки = Ложь;
	
	Макет = Обработки.ЗаработнаяПлатаДизайнера.ПолучитьМакет("МакетРасшифровкиРегистратор");
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	Подразделение = Дизайнер.Подразделение;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	УправленческийОбороты.Регистратор.Ссылка КАК Регистратор,
		|	УправленческийОбороты.СуммаОборотКт КАК Сумма,
		|	УправленческийОбороты.Регистратор.Комментарий КАК Комментарий
		|ИЗ
		|	РегистрБухгалтерии.Управленческий.Обороты(
		|			&ДатаНачала,
		|			&ДатаОкончания,
		|			Регистратор,
		|			Счет = ЗНАЧЕНИЕ(ПланСчетов.управленческий.ВзаиморасчетыССотрудниками),
		|			,
		|			Подразделение = &Подразделение
		|				И Субконто1 = &Дизайнер,
		|			,
		|			) КАК УправленческийОбороты";
	
	Запрос.УстановитьПараметр("ДатаНачала", Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", Период.ДатаОкончания);
	Запрос.УстановитьПараметр("Дизайнер", Дизайнер);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	
	Если ВидОперации = "Выплаты" Тогда
	
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "УправленческийОбороты.СуммаОборотКт КАК Сумма", "УправленческийОбороты.СуммаОборотДт КАК Сумма");
	
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		ОбластьЗаголовок.Параметры.Период = Период;
		ОбластьЗаголовок.Параметры.Подразделение = Подразделение;
		ТабДок.Вывести(ОбластьЗаголовок);
		ТабДок.Вывести(ОбластьШапка);
		
		ОбщаяСумма = 0;
		НомерСтроки = 1;
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			ОбластьСтрока.Параметры.Заполнить(ВыборкаДетальныеЗаписи);
			ОбластьСтрока.Параметры.НомерСтроки =НомерСтроки;
			ТабДок.Вывести(ОбластьСтрока);
			ОбщаяСумма = ОбщаяСумма + ВыборкаДетальныеЗаписи.Сумма;
			НомерСтроки = НомерСтроки +1;
			
		КонецЦикла;
		
		ОбластьПодвал.Параметры.ИтогСумма = ОбщаяСумма;
		ТабДок.Вывести(ОбластьПодвал);
		
	КонецЕсли;
	
	Подробности = ТабДок;
	
КонецФункции // СформироватьОтчет()

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Подробности.Показать("Подробности");
	
КонецПроцедуры
