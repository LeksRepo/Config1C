
Функция СформироватьОтчет(ПараметрыОтчета) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.АвтоМасштаб = Истина;
	
	Макет = Отчеты.СкладскаяВедомость.ПолучитьМакет("Макет");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	Склад = ПараметрыОтчета.Подразделение.ОсновнойСклад;
	
	ОбластьШапка.Параметры.МестоХранения = Склад;
	ОбластьШапка.Параметры.Период = ПредставлениеПериода(ПараметрыОтчета.ПериодОтчета.ДатаНачала, ПараметрыОтчета.ПериодОтчета.ДатаОкончания);
	ТабДок.Вывести(ОбластьШапка);
	
	НомерСтроки = 1;
	
	СуммаНачалоИтог = 0;
	СуммаПриходИтог = 0;
	СуммаРасходИтог = 0;
	СуммаКонецИтог = 0;
	КоличествоНачалоИтог = 0;
	КоличествоПриходИтог = 0;
	КоличествоРасходИтог = 0;
	КоличествоКонецИтог = 0;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Подразделение", Справочники.Подразделения.Логистика);
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("ГруппаНоменклатуры", ПараметрыОтчета.ГруппаНоменклатуры);
	Запрос.УстановитьПараметр("НачалоПериода", ПараметрыОтчета.ПериодОтчета.ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода", ПараметрыОтчета.ПериодОтчета.ДатаОкончания);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УправленческийОстаткиИОбороты.Субконто2 КАК Номенклатура,
	|	УправленческийОстаткиИОбороты.Субконто2.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	УправленческийОстаткиИОбороты.СуммаНачальныйОстатокДт КАК НачалоСумма,
	|	УправленческийОстаткиИОбороты.СуммаКонечныйОстатокДт КАК ОкончаниеСумма,
	|	УправленческийОстаткиИОбороты.СуммаОборотДт КАК ПриходСумма,
	|	УправленческийОстаткиИОбороты.СуммаОборотКт КАК РасходСумма,
	|	УправленческийОстаткиИОбороты.КоличествоОборотДт КАК ПриходКоличество,
	|	УправленческийОстаткиИОбороты.КоличествоОборотКт КАК РасходКоличество,
	|	УправленческийОстаткиИОбороты.КоличествоНачальныйОстатокДт КАК НачалоКоличество,
	|	УправленческийОстаткиИОбороты.КоличествоКонечныйОстатокДт КАК ОкончаниеКоличество,
	|	УправленческийОстаткиИОбороты.Субконто2.Родитель КАК Родитель
	|ИЗ
	|	РегистрБухгалтерии.Управленческий.ОстаткиИОбороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			,
	|			,
	|			Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.МатериалыНаСкладе),
	|			,
	|			Субконто1 = &Склад
	|				И ВЫБОР
	|					КОГДА &ГруппаНоменклатуры = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|						ТОГДА ИСТИНА
	|					ИНАЧЕ Субконто2.Родитель В ИЕРАРХИИ (&ГруппаНоменклатуры)
	|				КОНЕЦ) КАК УправленческийОстаткиИОбороты";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ОбластьСтрока.Параметры.Заполнить(Выборка);
		ОбластьСтрока.Параметры.НомерСтроки = НомерСтроки;
		ТабДок.Вывести(ОбластьСтрока);
		НомерСтроки = 1 + НомерСтроки;
		
		СуммаНачалоИтог = СуммаНачалоИтог + Выборка.НачалоСумма;
		СуммаПриходИтог = СуммаПриходИтог + Выборка.ПриходСумма;
		СуммаРасходИтог = СуммаРасходИтог + Выборка.РасходСумма;
		СуммаКонецИтог = СуммаКонецИтог + Выборка.ОкончаниеСумма;
		КоличествоНачалоИтог = КоличествоНачалоИтог + Выборка.НачалоКоличество;
		КоличествоПриходИтог = КоличествоПриходИтог + Выборка.ПриходКоличество;
		КоличествоРасходИтог = КоличествоРасходИтог + Выборка.РасходКоличество;
		КоличествоКонецИтог = КоличествоКонецИтог + Выборка.ОкончаниеКоличество;
		
	КонецЦикла;
	
	ОбластьПодвал.Параметры.СуммаНачалоИтог = СуммаНачалоИтог;
	ОбластьПодвал.Параметры.СуммаПриходИтог = СуммаПриходИтог;
	ОбластьПодвал.Параметры.СуммаРасходИтог = СуммаРасходИтог;
	ОбластьПодвал.Параметры.СуммаКонецИтог = СуммаКонецИтог;
	
	ОбластьПодвал.Параметры.КоличествоНачалоИтог = КоличествоНачалоИтог;
	ОбластьПодвал.Параметры.КоличествоПриходИтог = КоличествоПриходИтог;
	ОбластьПодвал.Параметры.КоличествоРасходИтог = КоличествоРасходИтог;
	ОбластьПодвал.Параметры.КоличествоКонецИтог = КоличествоКонецИтог;
	
	ТабДок.Вывести(ОбластьПодвал);
	
	Возврат ТабДок;
	
КонецФункции
