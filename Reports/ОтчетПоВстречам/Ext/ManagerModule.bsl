
Функция СформироватьОтчет(ПараметрыОтчета) Экспорт
	
	ТабДокТаблица = Новый ТабличныйДокумент;
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.АвтоМасштаб = Истина;
	
	Макет = Отчеты.ОтчетПоВстречам.ПолучитьМакет("Макет");
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьШапкаТаблица = Макет.ПолучитьОбласть("ШапкаТаблица|Таблица");
	ОбластьСтрока = Макет.ПолучитьОбласть("СтрокаОтчета|Таблица");
	ОбластьПустаяСтрока = Макет.ПолучитьОбласть("ПустаяСтрока");
	
	ОбластьШапка.Параметры.Подразделение = ПараметрыОтчета.Подразделение;
	ОбластьШапка.Параметры.Ответственный = ПараметрыОтчета.Ответственный;
	ОбластьШапка.Параметры.ПериодОтчета = ПредставлениеПериода(ПараметрыОтчета.ПериодОтчета.ДатаНачала, ПараметрыОтчета.ПериодОтчета.ДатаОкончания);
	ТабДок.Вывести(ОбластьШапка);
	
	НачальнаяДата = Дата(1, 1, 1);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Подразделение", ПараметрыОтчета.Подразделение);
	Запрос.УстановитьПараметр("Ответственный", ПараметрыОтчета.Ответственный);
	Запрос.УстановитьПараметр("ДатаНачала", ПараметрыОтчета.ПериодОтчета.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ПараметрыОтчета.ПериодОтчета.ДатаОкончания);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВстречаСКлиентом.Дата КАК Время,
	|	ВстречаСКлиентом.Ссылка КАК Ссылка,
	|	ВстречаСКлиентом.Основание.ИмяЗаказчика + "" / "" + ВстречаСКлиентом.Основание.АдресЗамера КАК ИмяАдрес
	|ИЗ
	|	Документ.ВстречаСКлиентом КАК ВстречаСКлиентом
	|ГДЕ
	|	ВстречаСКлиентом.Ответственный = &Ответственный
	|	И ВстречаСКлиентом.Подразделение = &Подразделение
	|	И ВстречаСКлиентом.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|
	|УПОРЯДОЧИТЬ ПО
	|	Время
	|ИТОГИ
	|	МАКСИМУМ(Ссылка),
	|	МАКСИМУМ(ИмяАдрес)
	|ПО
	|	Время ПЕРИОДАМИ(ЧАС, &ДатаНачала, &ДатаОкончания)";
	
	Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Время", "ВСЕ");
	Сч = 0;
	Пока Выборка.Следующий() Цикл
		
		Сч = ?(Сч = 4, 0, Сч);
		
		Если НачалоДня(Выборка.Время) <> НачальнаяДата Тогда
			НачальнаяДата = НачалоДня(Выборка.Время);
			ОбластьШапкаТаблица.Параметры.Дата = Выборка.Время;
			ТабДокТаблица.Вывести(ОбластьШапкаТаблица);
		КонецЕсли;	
		                                                         
		ЧасВремени = Час(Выборка.Время); 
		                                                                       
		Если ЧасВремени >= 9 И ЧасВремени <= 18 Тогда
			
			ОбластьСтрока.Параметры.Время = Выборка.Время;
			ОбластьСтрока.Параметры.ИмяАдрес = Выборка.ИмяАдрес;
			ОбластьСтрока.Параметры.Расшифровка = Новый Структура("Время, Ссылка, Основание, Подразделение, Ответственный", Выборка.Время, Выборка.Ссылка, ПараметрыОтчета.Основание, ПараметрыОтчета.Подразделение, ПараметрыОтчета.Ответственный); 					
			ТабДокТаблица.Вывести(ОбластьСтрока);
			
		ИначеЕсли ЧасВремени = 23 Тогда	
			
			Если Сч = 0 Тогда
				ТабДок.Вывести(ОбластьПустаяСтрока);
			КонецЕсли; 
			
			ТабДок.Присоединить(ТабДокТаблица);
			ТабДокТаблица.Очистить();
			
			Сч = Сч + 1;
			
		КонецЕсли;			
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции
