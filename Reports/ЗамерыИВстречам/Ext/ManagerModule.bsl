
Функция СформироватьОтчет(ПараметрыОтчета) Экспорт
	
	ТабДокТаблица = Новый ТабличныйДокумент;
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабДок.ФиксацияСверху = 9;
	
	Макет = Отчеты.ЗамерыИВстречам.ПолучитьМакет("Макет");
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьШапкаТаблица = Макет.ПолучитьОбласть("ШапкаТаблица|Таблица");
	ОбластьСтрока = Макет.ПолучитьОбласть("СтрокаОтчета|Таблица");
	ОбластьСтрокаСотрудник = Макет.ПолучитьОбласть("СтрокаСотрудник|Таблица");
	
	ОбластьШапка.Параметры.Подразделение = ПараметрыОтчета.Подразделение;
	//ОбластьШапка.Параметры.Ответственный = ПараметрыОтчета.Ответственный;
	ОбластьШапка.Параметры.ПериодОтчета = ПредставлениеПериода(ПараметрыОтчета.ПериодОтчета.ДатаНачала, ПараметрыОтчета.ПериодОтчета.ДатаОкончания);
	ОбластьШапка.Параметры.Основание = ПараметрыОтчета.Основание;
	ТабДок.Вывести(ОбластьШапка);
		
	НачальнаяДата = Дата(1, 1, 1);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Подразделение", ПараметрыОтчета.Подразделение);
	Запрос.УстановитьПараметр("Ответственный", ПараметрыОтчета.Ответственный);
	Запрос.УстановитьПараметр("ДатаНачала", ПараметрыОтчета.ПериодОтчета.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ПараметрыОтчета.ПериодОтчета.ДатаОкончания);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДолжностиСотрудниковСрезПоследних.ФизЛицо
	|ПОМЕСТИТЬ ВТ_Сотрудники
	|ИЗ
	|	РегистрСведений.ДолжностиСотрудников.СрезПоследних(
	|			&ДатаНачала,
	|			Работает
	|				И Подразделение = &Подразделение
	|				И (Должность = ЗНАЧЕНИЕ(Справочник.Должности.СтаршийДизайнер)
	|					ИЛИ Должность = ЗНАЧЕНИЕ(Справочник.Должности.ДизайнерКонсультант))) КАК ДолжностиСотрудниковСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВстречаСКлиентом.Ссылка,
	|	ВстречаСКлиентом.Дата,
	|	ВстречаСКлиентом.Ответственный,
	|	ВстречаСКлиентом.Основание.ИмяЗаказчика + "" / "" + ВстречаСКлиентом.Основание.АдресЗамера КАК ИмяАдрес,
	|	""В"" КАК ВидСсылки
	|ПОМЕСТИТЬ ВТ_ВстречиЗамеры
	|ИЗ
	|	Документ.ВстречаСКлиентом КАК ВстречаСКлиентом
	|ГДЕ
	|	ВстречаСКлиентом.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ВстречаСКлиентом.Подразделение = &Подразделение
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Замер.Ссылка,
	|	Замер.Дата,
	|	Замер.Замерщик,
	|	Замер.ИмяЗаказчика + "" / "" + Замер.АдресЗамера,
	|	""З""
	|ИЗ
	|	Документ.Замер КАК Замер
	|ГДЕ
	|	Замер.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И Замер.Подразделение = &Подразделение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Сотрудники.ФизЛицо КАК Ответственный,
	|	ВТ_ВстречиЗамеры.Ссылка КАК Ссылка,
	|	ВТ_ВстречиЗамеры.Дата КАК Время,
	|	ВТ_ВстречиЗамеры.ИмяАдрес КАК ИмяАдрес,
	|	ВТ_ВстречиЗамеры.ВидСсылки КАК ВидСсылки
	|ИЗ
	|	ВТ_Сотрудники КАК ВТ_Сотрудники
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВстречиЗамеры КАК ВТ_ВстречиЗамеры
	|		ПО ВТ_Сотрудники.ФизЛицо = ВТ_ВстречиЗамеры.Ответственный
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТ_Сотрудники.ФизЛицо.Фамилия,
	|	Время
	|ИТОГИ
	|	МАКСИМУМ(Ссылка),
	|	МАКСИМУМ(ИмяАдрес),
	|	МАКСИМУМ(ВидСсылки)
	|ПО
	|	Ответственный,
	|	Время ПЕРИОДАМИ(ЧАС, &ДатаНачала, &ДатаОкончания)";
	
	Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока Выборка.Следующий() Цикл

		ОбластьСтрокаСотрудник.Параметры.Заполнить(Выборка);
		ТабДокТаблица.Вывести(ОбластьСтрокаСотрудник);
		
		ВыборкаПоСотруднику = Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Время", "ВСЕ");
		Пока ВыборкаПоСотруднику.Следующий() Цикл
			Если ЗначениеЗаполнено(ВыборкаПоСотруднику.Время) Тогда
				Если НачалоДня(ВыборкаПоСотруднику.Время) <> НачальнаяДата Тогда
					НачальнаяДата = НачалоДня(ВыборкаПоСотруднику.Время);
					ОбластьШапкаТаблица.Параметры.Дата = ВыборкаПоСотруднику.Время;
					ТабДокТаблица.Вывести(ОбластьШапкаТаблица);
				КонецЕсли;
				
				ЧасВремени = Час(ВыборкаПоСотруднику.Время);
				
				Если ЧасВремени >= 9 И ЧасВремени <= 18 Тогда
					
					ОбластьСтрока.Параметры.Время = ВыборкаПоСотруднику.Время;
					ОбластьСтрока.Параметры.ИмяАдрес = ВыборкаПоСотруднику.ИмяАдрес;
					ОбластьСтрока.Параметры.ВидСсылки = ВыборкаПоСотруднику.ВидСсылки;
					ОбластьСтрока.Параметры.Расшифровка = Новый Структура("Время, Ссылка, Основание, Подразделение, Ответственный", ВыборкаПоСотруднику.Время, ВыборкаПоСотруднику.Ссылка, ПараметрыОтчета.Основание, ПараметрыОтчета.Подразделение, ВыборкаПоСотруднику.Ответственный); 					
					ТабДокТаблица.Вывести(ОбластьСтрока);
					
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		ТабДок.Присоединить(ТабДокТаблица);
		ТабДокТаблица.Очистить();
		
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции
