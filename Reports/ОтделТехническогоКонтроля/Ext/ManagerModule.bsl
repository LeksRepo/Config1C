
Функция СформироватьОтчет(ТабДок, Подразделение, КлиентскаяДата = Неопределено) Экспорт
	
	#Если Никогда Тогда
		ТабДок = Новый ТабличныйДокумент;
	#КонецЕсли
	
	ТабДок.Очистить();
	
	Макет = Отчеты.ОтделТехническогоКонтроля.ПолучитьМакет("МакетОтделТехническогоКонтроля");
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьВЦеху = Макет.ПолучитьОбласть("ВЦеху");
	ОбластьНаСкладе = Макет.ПолучитьОбласть("НаСкладе");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	ОбластьЗаметка = Макет.ПолучитьОбласть("Заметка");
	
	ОбластьЗаголовок.Параметры.Подразделение = Подразделение;
	Если ЗначениеЗаполнено(КлиентскаяДата) Тогда
		ДатаСоставления = КлиентскаяДата;
	Иначе 
		ДатаСоставления = ТекущаяДата();
	КонецЕсли;
	ОбластьЗаголовок.Параметры.ДатаСоставления = ДатаСоставления;
	
	ТабДок.Вывести(ОбластьЗаголовок);
	ТабДок.Вывести(ОбластьШапка);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	докСпецификация.Ссылка КАК Спецификация,
	|	НарядЗаданиеСписокСпецификаций.Ссылка КАК Наряд,
	|	докДоговор.Ссылка КАК Договор,
	|	СтатусСпецификацииСрезПоследних.Статус КАК Статус,
	|	ВЫБОР
	|		КОГДА докДоговор.Ссылка ЕСТЬ NULL 
	|			ТОГДА докСпецификация.Контрагент
	|		ИНАЧЕ докДоговор.Контрагент
	|	КОНЕЦ КАК Контрагент,
	|	докСпецификация.Технолог,
	|	докСпецификация.Технолог.ТелефонРабочий КАК ТелефонТехнолога,
	|	Заметки.Содержание КАК СодержаниеЗаметки,
	|	докСпецификация.ДатаОтгрузки,
	|	докСпецификация.ДатаИзготовления,
	|	докСпецификация.Изделие,
	|	докСпецификация.АдресМонтажа КАК Адрес,
	|	докСпецификация.Номер КАК НомерСпецификации,
	|	Заметки.Ссылка КАК Заметка,
	|	докСпецификация.ОсобыеУслуги,
	|	докСпецификация.ЦветЛДСПОсновной,
	|	докСпецификация.ЦветЛДСПДополнительный,
	|	докСпецификация.ЦветВертикальногоПрофиля,
	|	докСпецификация.КомплектацияСклад,
	|	докСпецификация.КомплектацияЦех,
	|	докСпецификация.Дилерский,
	|	докСпецификация.Контрагент.Телефон КАК ТелефонДилера,
	|	докСпецификация.Контрагент КАК Дилер,
	|	докСпецификация.Контрагент.ЮридическоеЛицо КАК ДилерЮридическоеЛицо,
	|	докСпецификация.Контрагент.Фамилия КАК ДилерФамилия,
	|	докСпецификация.Контрагент.Имя КАК ДилерИмя,
	|	докСпецификация.Контрагент.Отчество КАК ДилерОтчество
	|ИЗ
	|	Документ.Спецификация КАК докСпецификация
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусСпецификации.СрезПоследних КАК СтатусСпецификацииСрезПоследних
	|		ПО (СтатусСпецификацииСрезПоследних.Спецификация = докСпецификация.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.НарядЗадание.СписокСпецификаций КАК НарядЗаданиеСписокСпецификаций
	|		ПО (НарядЗаданиеСписокСпецификаций.Спецификация = докСпецификация.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Договор КАК докДоговор
	|		ПО (докДоговор.Спецификация = докСпецификация.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Заметки КАК Заметки
	|		ПО докСпецификация.Ссылка = Заметки.Предмет
	|ГДЕ
	|	докСпецификация.Подразделение = &Подразделение
	|	И (СтатусСпецификацииСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСпецификации.ПереданВЦех)
	|			ИЛИ СтатусСпецификацииСрезПоследних.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСпецификации.Изготовлен))
	|ИТОГИ ПО
	|	Статус,
	|	Наряд";
	
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаМесто = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаМесто.Следующий() Цикл
		
			Если ВыборкаМесто.Статус = Перечисления.СтатусыСпецификации.Изготовлен Тогда
				ТабДок.Вывести(ОбластьНаСкладе);
			КонецЕсли;
		
		ВыборкаНаряд = ВыборкаМесто.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаНаряд.Следующий() Цикл
			
			Если ВыборкаМесто.Статус = Перечисления.СтатусыСпецификации.ПереданВЦех Тогда
				
				ОбластьВЦеху.Параметры.НомерНаряда = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(ВыборкаНаряд.Наряд.Номер);
				ОбластьВЦеху.Параметры.ДатаИзготовленияНаряда = Формат(ВыборкаНаряд.Наряд.ДатаИзготовления, "ДЛФ=DD");
				ТабДок.Вывести(ОбластьВЦеху);
				
			КонецЕсли;
			
			ВыборкаДетальныеЗаписи = ВыборкаНаряд.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				
				ОбластьСтрока.Параметры.Заполнить(ВыборкаДетальныеЗаписи);
				
				ПризнакиСпецификации = "%1 / %2 / %3";
				ПризнакиСпецификации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПризнакиСпецификации,
				ВыборкаДетальныеЗаписи.ЦветЛДСПОсновной,
				ВыборкаДетальныеЗаписи.ЦветЛДСПДополнительный,
				ВыборкаДетальныеЗаписи.ЦветВертикальногоПрофиля);
				
				ОбластьСтрока.Параметры.ПризнакиСпецификации = ПризнакиСпецификации;
				ОбластьСтрока.Параметры.НомерСпецификации = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(ВыборкаДетальныеЗаписи.НомерСпецификации);
				
				Если НЕ ВыборкаДетальныеЗаписи.Дилерский Тогда
					ОбластьСтрока.Параметры.Технолог = ФизическиеЛица.ФамилияИнициалыФизЛица(ВыборкаДетальныеЗаписи.Технолог);
				Иначе
					ОбластьСтрока.Параметры.ТелефонТехнолога = ВыборкаДетальныеЗаписи.ТелефонДилера;
					Если НЕ ВыборкаДетальныеЗаписи.ДилерЮридическоеЛицо Тогда
						ОбластьСтрока.Параметры.Технолог = ФизическиеЛица.ФамилияИнициалыФизЛица("" + ВыборкаДетальныеЗаписи.Дилер);
					Иначе
						ОбластьСтрока.Параметры.Технолог = ВыборкаДетальныеЗаписи.Дилер;
					КонецЕсли;
					
				КонецЕсли;
				
				ТабДок.Вывести(ОбластьСтрока);
				
				Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Заметка) Тогда
					
					ОбластьЗаметка.Параметры.Спецификация = ВыборкаДетальныеЗаписи.Спецификация;
					ОбластьЗаметка.Параметры.Заметка = ВыборкаДетальныеЗаписи.Заметка;
					ОбластьЗаметка.Параметры.СодержаниеЗаметки = ВыборкаДетальныеЗаписи.СодержаниеЗаметки.Получить().ПолучитьТекст();
					
					ТабДок.Вывести(ОбластьЗаметка);
					
				КонецЕсли;
				
			КонецЦикла; // ВыборкаДетальныеЗаписи.Следующий()
			
		КонецЦикла; //ВыборкаНаряд.Следующий()
		
	КонецЦикла; // ВыборкаМесто.Следующий()
	
КонецФункции
