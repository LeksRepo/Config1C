Функция СформироватьОтчет(ПериодОтчета, Контрагент) Экспорт
	
	ТабДок = Новый ТабличныйДокумент;
	
	Макет = Отчеты.ДилерскиеДоговора.ПолучитьМакет("Макет");
	
	ОбластьСтрокаДоговор =  Макет.ПолучитьОбласть("СтрокаДоговор");
	ОбластьСтрокаПлатеж = Макет.ПолучитьОбласть("СтрокаПлатеж");
	ОбластьСтрокаДопСоглашение = Макет.ПолучитьОбласть("СтрокаДопСоглашение");
	ОбластьСтрокаПлатежДопСоглашение = Макет.ПолучитьОбласть("СтрокаПлатежДопСоглашение");
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьПустаяСтрока = Макет.ПолучитьОбласть("ПустаяСтрока");
	ОбластьРазделитель = Макет.ПолучитьОбласть("Разделитель");
	
	ТабДок.Вывести(ОбластьПустаяСтрока);
	
	Если ПериодОтчета.ДатаОкончания = Дата("01.01.0001 0:00:00") Тогда
		ПериодОтчета.ДатаОкончания = Дата("31.12.2099 23:59:59");
	КонецЕсли;
	
	Запрос = Новый Запрос;	
	Запрос.УстановитьПараметр("ДатаНачала", ПериодОтчета.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ПериодОтчета.ДатаОкончания);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДоговорДилера.Ссылка КАК ДоговорСсылка,
	|	ЕСТЬNULL(ДоговорДилера.СуммаДокумента, 0) КАК СуммаДоговор,
	|	ДоговорДилера.АдресДоговора,
	|	ДоговорДилера.ДатаМонтажа,
	|	ДоговорДилера.Изделие,
	|	ДоговорДилера.Комментарий,
	|	ПлатежДилера.Ссылка КАК ПлатежДилераСсылка,
	|	ЕСТЬNULL(ПлатежДилера.СуммаДокумента, 0) КАК СуммаПлатежДилера
	|ИЗ
	|	Документ.ДоговорДилера КАК ДоговорДилера
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПлатежДилера КАК ПлатежДилера
	|		ПО (ПлатежДилера.ДоговорДопСоглашение = ДоговорДилера.Ссылка)
	|ГДЕ
	|	ДоговорДилера.Автор = &Контрагент
	|	И ДоговорДилера.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДоговорДилера.Дата УБЫВ
	|ИТОГИ
	|	СУММА(СуммаПлатежДилера)
	|ПО
	|	ДоговорСсылка,
	|	ПлатежДилераСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДоговорДилера.Ссылка КАК ДоговорСсылка,
	|	ДопСоглашение.Ссылка КАК ДопСоглашениеСсылка,
	|	ЕСТЬNULL(ДопСоглашение.СуммаДокумента, 0) КАК СуммаДопСоглашение,
	|	ПлатежДилераКДопСоглашению.Ссылка КАК ПлатежДилераКДопСоглашениюСсылка,
	|	ЕСТЬNULL(ПлатежДилераКДопСоглашению.СуммаДокумента, 0) КАК СуммаПлатежДилераКДопСоглашению
	|ИЗ
	|	Документ.ДоговорДилера КАК ДоговорДилера
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ДополнительноеСоглашениеДилера КАК ДопСоглашение
	|		ПО ДоговорДилера.Ссылка = ДопСоглашение.Договор
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПлатежДилера КАК ПлатежДилераКДопСоглашению
	|		ПО (ПлатежДилераКДопСоглашению.ДоговорДопСоглашение = ДопСоглашение.Ссылка)
	|ГДЕ
	|	ДоговорДилера.Автор = &Контрагент
	|	И ДоговорДилера.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДоговорДилера.Дата УБЫВ
	|ИТОГИ
	|	СУММА(СуммаПлатежДилераКДопСоглашению)
	|ПО
	|	ДоговорСсылка,
	|	ДопСоглашениеСсылка,
	|	ПлатежДилераКДопСоглашениюСсылка";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ВыборкаДоговор = РезультатЗапроса[0].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ДоговорСсылка");
	ВыборкаДоговорДопСоглашение = РезультатЗапроса[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ДоговорСсылка");
	
	Пока ВыборкаДоговор.Следующий() Цикл
		
		ТабДок.Вывести(ОбластьШапка);
		
		ОбластьСтрокаДоговор.Параметры.Заполнить(ВыборкаДоговор);
		
		Договор = ВыборкаДоговор.ДоговорСсылка;
		
		СуммаДоговор = ВыборкаДоговор.СуммаДоговор;
		СуммаПлатежейДоговор = ВыборкаДоговор.СуммаПлатежДилера ;
		Долг = ?(СуммаДоговор > СуммаПлатежейДоговор,(СуммаПлатежейДоговор - СуммаДоговор)*-1,"");
		
		ОбластьСтрокаДоговор.Параметры.ДоговорСсылка = "Договор "+Договор.Номер+" от "+Формат(Договор.Дата,"ДФ=dd.MM.yy");
		ОбластьСтрокаДоговор.Параметры.ДатаМонтажа = Формат(ВыборкаДоговор.ДатаМонтажа,"ДЛФ=DD");
		ОбластьСтрокаДоговор.Параметры.РасшифровкаДоговора = ВыборкаДоговор.ДоговорСсылка;
		ОбластьСтрокаДоговор.Параметры.Долг = Долг; 
		
		ТабДок.Вывести(ОбластьСтрокаДоговор);
		
		ВыборкаПлатеж = ВыборкаДоговор.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ПлатежДилераСсылка");
		
		Пока ВыборкаПлатеж.Следующий() Цикл
			
			ПлатежДилера = ВыборкаПлатеж.ПлатежДилераСсылка;
			
			Если ЗначениеЗаполнено(ПлатежДилера) Тогда
			
				ОбластьСтрокаПлатеж.Параметры.Платеж = "Платеж "+ПлатежДилера.Номер+" от "+Формат(ПлатежДилера.Дата,"ДФ=dd.MM.yy");
				ОбластьСтрокаПлатеж.Параметры.РасшифровкаПлатежа = ВыборкаПлатеж.ПлатежДилераСсылка;
				ОбластьСтрокаПлатеж.Параметры.Сумма = ВыборкаПлатеж.СуммаПлатежДилера;
			
				ТабДок.Вывести(ОбластьСтрокаПлатеж);
				
			КонецЕсли;
				
		КонецЦикла;
		
		Если ВыборкаДоговорДопСоглашение.Следующий() Тогда

			ВыборкаДопСоглашение = ВыборкаДоговорДопСоглашение.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ДопСоглашениеСсылка");
			
			Пока ВыборкаДопСоглашение.Следующий() Цикл
				
				ДопСоглашение = ВыборкаДопСоглашение.ДопСоглашениеСсылка;
				
				Если ЗначениеЗаполнено(ДопСоглашение) Тогда
				
					СуммаДопСоглашение = ВыборкаДопСоглашение.СуммаДопСоглашение;
					СуммаПлатежейПодСоглашение = ВыборкаДопСоглашение.СуммаПлатежДилераКДопСоглашению;
					Долг = ?(СуммаДопСоглашение > СуммаПлатежейПодСоглашение,(СуммаПлатежейПодСоглашение - СуммаДопСоглашение)*-1,"");

					ОбластьСтрокаДопСоглашение.Параметры.ДопСоглашение = "Доп. соглашение "+ДопСоглашение.Номер+" от "+Формат(ДопСоглашение.Дата,"ДФ=dd.MM.yy");
					ОбластьСтрокаДопСоглашение.Параметры.РасшифровкаДопСоглашения = ВыборкаДопСоглашение.ДопСоглашениеСсылка;
					ОбластьСтрокаДопСоглашение.Параметры.Сумма = ВыборкаДопСоглашение.СуммаДопСоглашение;
					ОбластьСтрокаДопСоглашение.Параметры.Долг = Долг;

					ТабДок.Вывести(ОбластьСтрокаДопСоглашение);
					
					ВыборкаПлатежДопСоглашение = ВыборкаДопСоглашение.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ПлатежДилераКДопСоглашениюСсылка");
					
					Пока ВыборкаПлатежДопСоглашение.Следующий() Цикл
						
						ПлатежДопСоглашение = ВыборкаПлатежДопСоглашение.ПлатежДилераКДопСоглашениюСсылка;
						
						Если ЗначениеЗаполнено(ПлатежДопСоглашение) Тогда
							
								ОбластьСтрокаПлатежДопСоглашение.Параметры.ПлатежКДопСоглашению = "Платеж "+ПлатежДопСоглашение.Номер+" от "+Формат(ПлатежДопСоглашение.Дата,"ДФ=dd.MM.yy");
								ОбластьСтрокаПлатежДопСоглашение.Параметры.РасшифровкаПлатежКДопСоглашению = ВыборкаПлатежДопСоглашение.ПлатежДилераКДопСоглашениюСсылка;
								ОбластьСтрокаПлатежДопСоглашение.Параметры.Сумма = ВыборкаПлатежДопСоглашение.СуммаПлатежДилераКДопСоглашению; 
								
								ТабДок.Вывести(ОбластьСтрокаПлатежДопСоглашение);

						КонецЕсли;

					КонецЦикла;
					
				КонецЕсли;

			КонецЦикла;
			
		КонецЕсли;
		
		ТабДок.Вывести(ОбластьПустаяСтрока);
		ТабДок.Вывести(ОбластьРазделитель);
		ТабДок.Вывести(ОбластьПустаяСтрока);
		ТабДок.Вывести(ОбластьПустаяСтрока);
		
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции