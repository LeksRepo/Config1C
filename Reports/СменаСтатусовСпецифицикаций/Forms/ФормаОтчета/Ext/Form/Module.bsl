
&НаКлиенте
Процедура Сформировать(Команда)
	
	Если ЗначениеЗаполнено(Отчет.Подразделение) Тогда
		ТабличныйДокумент = СформироватьТабличныйДокумент();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СформироватьТабличныйДокумент()
	
	ТабДок = Новый ТабличныйДокумент();
	ТабДок.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабДок.Защита = Истина;
	
	Макет = Отчеты.СменаСтатусовСпецифицикаций.ПолучитьМакет("МакетСметаСтатусов");
	
	ОблШапка = Макет.ПолучитьОбласть("Шапка");
	ОблПустаяСтрока = Макет.ПолучитьОбласть("ПустаяСтрока");
	ОблЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОблДокумент = Макет.ПолучитьОбласть("Документ");
		
	ОблШапка.Параметры.Период = Формат(Отчет.Период, "ДЛФ=DD");
	ОблШапка.Параметры.Подразделение = Отчет.Подразделение;
	ТабДок.Вывести(ОблШапка);
	
	МассивСтатусовДляОтбора = Новый Массив();
	МассивСтатусовДляОтбора.Добавить(Перечисления.СтатусыСпецификации.Изготовлен);
	МассивСтатусовДляОтбора.Добавить(Перечисления.СтатусыСпецификации.Отгружен);
	МассивСтатусовДляОтбора.Добавить(Перечисления.СтатусыСпецификации.ПереданВЦех);
	МассивСтатусовДляОтбора.Добавить(Перечисления.СтатусыСпецификации.Размещен);
	МассивСтатусовДляОтбора.Добавить(Перечисления.СтатусыСпецификации.Установлен);
	МассивСтатусовДляОтбора.Добавить(Перечисления.СтатусыСпецификации.ОжиданиеМатериала);
	
	МассивСменыСтатусов = Новый Массив();
	МассивСменыСтатусов.Добавить(Новый Структура("Начальный, Конечный",Перечисления.СтатусыСпецификации.Сохранен, Перечисления.СтатусыСпецификации.Размещен));
	МассивСменыСтатусов.Добавить(Новый Структура("Начальный, Конечный",Перечисления.СтатусыСпецификации.Рассчитывается, Перечисления.СтатусыСпецификации.Размещен));
	МассивСменыСтатусов.Добавить(Новый Структура("Начальный, Конечный",Перечисления.СтатусыСпецификации.Рассчитывается, Перечисления.СтатусыСпецификации.ОжиданиеМатериала));
	МассивСменыСтатусов.Добавить(Новый Структура("Начальный, Конечный",Перечисления.СтатусыСпецификации.ОжиданиеМатериала, Перечисления.СтатусыСпецификации.Размещен));

	Первый = Истина;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Подразделение", Отчет.Подразделение);
	Запрос.УстановитьПараметр("НачалоПериода", ДобавитьМесяц(НачалоДня(Отчет.Период), -3));
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(Отчет.Период));
	Запрос.УстановитьПараметр("СтатусыДляОтбора", МассивСтатусовДляОтбора);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Спец.Ссылка КАК Спецификация,
	|	Статусы.Статус КАК ТекущийСтатус
	|ПОМЕСТИТЬ втСпецификации
	|ИЗ
	|	Документ.Спецификация КАК Спец
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусСпецификации.СрезПоследних(, ) КАК Статусы
	|		ПО Спец.Ссылка = Статусы.Спецификация
	|ГДЕ
	|	Спец.Подразделение = &Подразделение
	|	И Статусы.Статус В(&СтатусыДляОтбора)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Спец.Спецификация КАК Спецификация,
	|	Спец.ТекущийСтатус КАК ТекущийСтатус,
	|	Статусы.Статус КАК Статус,
	|	Статусы.Период КАК Период,
	|	Статусы.Автор КАК Автор
	|ИЗ
	|	РегистрСведений.СтатусСпецификации КАК Статусы
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСпецификации КАК Спец
	|		ПО Статусы.Спецификация = Спец.Спецификация
	|ГДЕ
	|	НЕ Спец.Спецификация ЕСТЬ NULL
	|	И Статусы.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И НЕ Статусы.Статус ЕСТЬ NULL
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период
	|ИТОГИ ПО
	|	Спецификация";
	
	Результат = Запрос.Выполнить();
	
	Если НЕ Результат.Пустой() Тогда
		
		ТабДок.Вывести(ОблПустаяСтрока);
		
		Выборка = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Для Каждого Эл ИЗ МассивСменыСтатусов Цикл
			
			Если НЕ Первый Тогда
				 ТабДок.Вывести(ОблПустаяСтрока);
			КонецЕсли;
			
			тзДанные = Новый ТаблицаЗначений();
			тзДанные.Колонки.Добавить("Документ", Новый ОписаниеТипов("ДокументСсылка.Спецификация"));
			тзДанные.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
			тзДанные.Колонки.Добавить("ТекущийСтатус", Новый ОписаниеТипов("Строка"));
			тзДанные.Колонки.Добавить("Рас", Новый ОписаниеТипов("ДокументСсылка.Спецификация"));
			тзДанные.Колонки.Добавить("Автор", Новый ОписаниеТипов("Строка"));
			
			Количество = 0;
			
			Выборка.Сбросить();
			ТабДок2 = Новый ТабличныйДокумент();
			
			Пока Выборка.Следующий() Цикл
				
				 ВыборкаСтатусы = Выборка.Выбрать();
				 
				 Статус1 = Неопределено;
				 Статус2 = Неопределено;
				 Период = Неопределено;
				 ТекущийСтатус = Неопределено;
				 Автор = Неопределено;
				 Инд = 0;

				 Пока ВыборкаСтатусы.Следующий() Цикл
					 
					 Если Эл.Начальный = ВыборкаСтатусы.Статус Тогда
						  Статус1 = ВыборкаСтатусы.Статус;
						  Статус2 = Неопределено;
						  Инд = 0;
					 КонецЕсли;
					  
					 Если Статус1 = Эл.Начальный И Эл.Конечный = ВыборкаСтатусы.Статус И Инд = 1 Тогда
						 
						  Статус2 = ВыборкаСтатусы.Статус;
						  Период = ВыборкаСтатусы.Период;
						  ТекущийСтатус = ВыборкаСтатусы.ТекущийСтатус;
						  Автор = ВыборкаСтатусы.Автор;

					 КонецЕсли;
					 
					 Инд = Инд + 1;
					 
				 КонецЦикла;
				 
				 Если ЗначениеЗаполнено(Статус1) И ЗначениеЗаполнено(Статус2) И НачалоДня(Период) = НачалоДня(Отчет.Период) Тогда
					 
					  СтрДанные = тзДанные.Добавить();
					  СтрДанные.Документ = Выборка.Спецификация;
					  СтрДанные.Период = Период;
					  СтрДанные.ТекущийСтатус = ТекущийСтатус;
					  СтрДанные.Рас = Выборка.Спецификация;
					  СтрДанные.Автор = Автор;
					  
					  Количество = Количество + 1;
					  
				 КонецЕсли;
				 
			КонецЦикла;
			
			Если Количество > 0 Тогда
			
				ОблЗаголовок.Параметры.Заголовок = Строка(Эл.Начальный) + " >>> " + Строка(Эл.Конечный);   
				ОблЗаголовок.Параметры.Количество = Количество;
				
				ТабДок.Вывести(ОблЗаголовок);
				
				тзДанные.Сортировать("Период Возр");
				Для Каждого Стр ИЗ тзДанные Цикл
					ОблДокумент.Параметры.Заполнить(Стр);
			  		ТабДок2.Вывести(ОблДокумент);
				КонецЦикла;
				
				ТабДок.Вывести(ТабДок2);
				
				Первый = Ложь;
				
			КонецЕсли;
			
		КонецЦикла;
	
	КонецЕсли;
	
	Возврат ТабДок;
	
КонецФункции
