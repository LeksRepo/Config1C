
&НаКлиенте
Процедура Сформировать(Команда)
	
	Если ЗначениеЗаполнено(Отчет.Подразделение) Тогда
		ТабличныйДокумент = СформироватьТабличныйДокумент();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СформироватьТабличныйДокумент()
	
	ТабДок = Новый ТабличныйДокумент();
	ТабДок.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабДок.Защита = Истина;
	
	Макет = Отчеты.СменаСтатусовСпецифицикаций.ПолучитьМакет("МакетСметаСтатусов");
	
	ОблШапка = Макет.ПолучитьОбласть("Шапка");
	ОблПустаяСтрока = Макет.ПолучитьОбласть("ПустаяСтрока");
	ОблЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОблДокумент = Макет.ПолучитьОбласть("Документ");
		
	ОблШапка.Параметры.Период = Формат(Отчет.Период.ДатаНачала, "ДЛФ=DD")+" - "+Формат(Отчет.Период.ДатаОкончания, "ДЛФ=DD");
	ОблШапка.Параметры.Подразделение = Отчет.Подразделение;
	ТабДок.Вывести(ОблШапка);
	
	МассивСтатусовДляОтбора = Новый Массив();
	МассивСтатусовДляОтбора.Добавить(Перечисления.СтатусыСпецификации.Изготовлен);
	МассивСтатусовДляОтбора.Добавить(Перечисления.СтатусыСпецификации.Отгружен);
	МассивСтатусовДляОтбора.Добавить(Перечисления.СтатусыСпецификации.ПереданВЦех);
	МассивСтатусовДляОтбора.Добавить(Перечисления.СтатусыСпецификации.Размещен);
	МассивСтатусовДляОтбора.Добавить(Перечисления.СтатусыСпецификации.Установлен);
	
	МассивСменыСтатусов = Новый Массив();
	МассивСменыСтатусов.Добавить(Новый Структура("Начальный, Конечный",Перечисления.СтатусыСпецификации.Рассчитывается, Перечисления.СтатусыСпецификации.Размещен));
	МассивСменыСтатусов.Добавить(Новый Структура("Начальный, Конечный",Перечисления.СтатусыСпецификации.Рассчитывается, Перечисления.СтатусыСпецификации.ОжиданиеМатериала));
	МассивСменыСтатусов.Добавить(Новый Структура("Начальный, Конечный",Перечисления.СтатусыСпецификации.ОжиданиеМатериала, Перечисления.СтатусыСпецификации.Размещен));

	Первый = Истина;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Подразделение", Отчет.Подразделение);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоДня(Отчет.Период.ДатаНачала));
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(Отчет.Период.ДатаОкончания));
	Запрос.УстановитьПараметр("СтатусыДляОтбора", МассивСтатусовДляОтбора);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Спец.Ссылка КАК Спецификация,
	|	Статусы.Статус КАК ТекущийСтатус
	|ПОМЕСТИТЬ втСпецификации
	|ИЗ
	|	Документ.Спецификация КАК Спец
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусСпецификации.СрезПоследних(, ) КАК Статусы
	|		ПО Спец.Ссылка = Статусы.Спецификация
	|ГДЕ
	|	Спец.Подразделение = &Подразделение
	|	И Статусы.Статус В(&СтатусыДляОтбора)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Спец.Спецификация КАК Спецификация,
	|	Спец.ТекущийСтатус КАК ТекущийСтатус,
	|	Статусы.Статус КАК Статус,
	|	Статусы.Период КАК Период
	|ИЗ
	|	РегистрСведений.СтатусСпецификации КАК Статусы
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСпецификации КАК Спец
	|		ПО Статусы.Спецификация = Спец.Спецификация
	|ГДЕ
	|	НЕ Спец.Спецификация ЕСТЬ NULL
	|	И Статусы.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И НЕ Статусы.Статус ЕСТЬ NULL
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период
	|ИТОГИ ПО
	|	Спецификация";
	
	Результат = Запрос.Выполнить();
	
	Если НЕ Результат.Пустой() Тогда
		
		ТабДок.Вывести(ОблПустаяСтрока);
		
		Выборка = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Для Каждого Эл ИЗ МассивСменыСтатусов Цикл
			
			Если НЕ Первый Тогда
				 ТабДок.Вывести(ОблПустаяСтрока);
			КонецЕсли;
			
			Количество = 0;
			
			Выборка.Сбросить();
			ТабДок2 = Новый ТабличныйДокумент();
			
			Пока Выборка.Следующий() Цикл
				
				 ВыборкаСтатусы = Выборка.Выбрать();
				 
				 Статус1 = Неопределено;
				 Статус2 = Неопределено;
				 Период = Неопределено;
				 ТекущийСтатус = Неопределено;
				 Инд = 0;

				 Пока ВыборкаСтатусы.Следующий() Цикл
					 
					 Если Эл.Начальный = ВыборкаСтатусы.Статус Тогда
						  Статус1 = ВыборкаСтатусы.Статус;
						  Статус2 = Неопределено;
						  Инд = 0;
					 КонецЕсли;
					  
					 Если Статус1 = Эл.Начальный И Эл.Конечный = ВыборкаСтатусы.Статус И Инд = 1 Тогда
						 
						  Статус2 = ВыборкаСтатусы.Статус;
						  Период = ВыборкаСтатусы.Период;
						  ТекущийСтатус = ВыборкаСтатусы.ТекущийСтатус;

					 КонецЕсли;
					 
					 Инд = Инд + 1;
					 
				 КонецЦикла;
				 
				 Если ЗначениеЗаполнено(Статус1) И  ЗначениеЗаполнено(Статус2) Тогда
					 
				 	  ОблДокумент.Параметры.Документ = Выборка.Спецификация;
					  ОблДокумент.Параметры.Период = Период;
					  ОблДокумент.Параметры.ТекущийСтатус = ТекущийСтатус;
					  ОблДокумент.Параметры.Рас = Выборка.Спецификация;
					  
					  ТабДок2.Вывести(ОблДокумент);
					  
					  Количество = Количество + 1;
					  
				 КонецЕсли;
				 
			КонецЦикла;
			
			Если Количество > 0 Тогда
			
				ОблЗаголовок.Параметры.Заголовок = Строка(Эл.Начальный) + " >>> " + Строка(Эл.Конечный);   
				ОблЗаголовок.Параметры.Количество = Количество;
				
				ТабДок.Вывести(ОблЗаголовок);
				ТабДок.Вывести(ТабДок2);
				
				Первый = Ложь;
				
			КонецЕсли;
			
		КонецЦикла;
	
	КонецЕсли;
	
	Возврат ТабДок;
	
КонецФункции
