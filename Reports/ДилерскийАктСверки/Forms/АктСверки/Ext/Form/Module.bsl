
&НаКлиенте
Процедура Сформировать(Команда)
	СформироватьНаСервере();
КонецПроцедуры

&НаСервере
Процедура СформироватьНаСервере()
	
	УстановитьПривилегированныйРежим(Истина);
	Макет = Отчеты.ДилерскийАктСверки.ПолучитьМакет("РасшифровкаОборота");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	ТабДок = Новый ТабличныйДокумент;
		
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", Период.ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода", Период.ДатаОкончания);
	Запрос.УстановитьПараметр("Контрагент", Дилер.ОбъектАвторизации);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УправленческийОстаткиИОбороты.Регистратор КАК Документ,
	|	NULL КАК Приход,
	|	ЕСТЬNULL(УправленческийОстаткиИОбороты.СуммаОборотКт, 0) КАК Расход,
	|	УправленческийОстаткиИОбороты.Период КАК Период,
	|	NULL КАК СуммаОстаток
	|ИЗ
	|	РегистрБухгалтерии.Управленческий.ОстаткиИОбороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			Регистратор,
	|			,
	|			Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ВзаиморасчетыСПокупателями),
	|			,
	|			Подразделение = &Подразделение
	|				И Субконто1 = &Контрагент
	|				И Субконто2 <> НЕОПРЕДЕЛЕНО) КАК УправленческийОстаткиИОбороты
	|ГДЕ
	|	УправленческийОстаткиИОбороты.Регистратор <> НЕОПРЕДЕЛЕНО
	|	И НЕ УправленческийОстаткиИОбороты.Регистратор ССЫЛКА Документ.РеализацияГотовойПродукции
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	УправленческийОстаткиИОбороты.Регистратор,
	|	ЕСТЬNULL(-УправленческийОстаткиИОбороты.СуммаОборот, 0),
	|	NULL,
	|	УправленческийОстаткиИОбороты.Период,
	|	NULL
	|ИЗ
	|	РегистрБухгалтерии.Управленческий.ОстаткиИОбороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			Регистратор,
	|			,
	|			Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ВзаиморасчетыСПокупателями),
	|			,
	|			Подразделение = &Подразделение
	|				И Субконто1 = &Контрагент) КАК УправленческийОстаткиИОбороты
	|ГДЕ
	|	УправленческийОстаткиИОбороты.Регистратор <> НЕОПРЕДЕЛЕНО
	|	И НЕ УправленческийОстаткиИОбороты.Регистратор ССЫЛКА Документ.РеализацияГотовойПродукции
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	NULL,
	|	NULL,
	|	NULL,
	|	ДАТАВРЕМЯ(1, 1, 1),
	|	УправленческийОстатки.СуммаОстаток
	|ИЗ
	|	РегистрБухгалтерии.Управленческий.Остатки(
	|			&НачалоПериода,
	|			Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ВзаиморасчетыСПокупателями),
	|			,
	|			Подразделение = &Подразделение
	|				И Субконто1 = &Контрагент
	|				И Субконто2 = НЕОПРЕДЕЛЕНО) КАК УправленческийОстатки
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Выборка.Следующий();
	Остаток = - Выборка.СуммаОстаток;
	ОбластьШапка.Параметры.НачальныйОстаток = Остаток;
	Выборка.Сбросить();
	
	ТабДок.Вывести(ОбластьШапка);
	
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Выборка.Документ) Тогда
			ОбластьСтрока.Параметры.Заполнить(Выборка);
			Остаток = Остаток + ?(ЗначениеЗаполнено(Выборка.Приход), Выборка.Приход, -Выборка.Расход);		
			ОбластьСтрока.Параметры.Остаток = Остаток;
			ТабДок.Вывести(ОбластьСтрока);
		КонецЕсли;
	КонецЦикла;
		 	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Дилер = Параметры.Дилер;
	Период = Параметры.Период;
	Подразделение = Параметры.Подразделение;
	
КонецПроцедуры
