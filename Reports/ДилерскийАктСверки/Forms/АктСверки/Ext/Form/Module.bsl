
&НаКлиенте
Процедура Сформировать(Команда)
	
	СформироватьНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура СформироватьНаСервере()
	
	УстановитьПривилегированныйРежим(Истина);
	Макет = Отчеты.ДилерскийАктСверки.ПолучитьМакет("РасшифровкаОборота");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьОбороты = Макет.ПолучитьОбласть("Обороты");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.ФиксацияСверху = 3;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", Период.ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода", Период.ДатаОкончания);
	Запрос.УстановитьПараметр("Контрагент", Дилер.ОбъектАвторизации);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.Текст =	
	"ВЫБРАТЬ
	|	УправленческийОстаткиИОбороты.Регистратор КАК Документ,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(УправленческийОстаткиИОбороты.СуммаОборотКт, 0) <> 0
	|			ТОГДА ВЫБОР
	|					КОГДА ЕСТЬNULL(УправленческийОстаткиИОбороты.СуммаОборотКт, 0) > 0
	|						ТОГДА ЕСТЬNULL(УправленческийОстаткиИОбороты.СуммаОборотКт, 0)
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		ИНАЧЕ ЕСТЬNULL(-УправленческийОстаткиИОбороты.СуммаОборотДт, 0)
	|	КОНЕЦ КАК Приход,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(УправленческийОстаткиИОбороты.СуммаОборотКт, 0) <> 0
	|			ТОГДА ВЫБОР
	|					КОГДА ЕСТЬNULL(УправленческийОстаткиИОбороты.СуммаОборотКт, 0) < 0
	|						ТОГДА ЕСТЬNULL(-УправленческийОстаткиИОбороты.СуммаОборотКт, 0)
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Расход,
	|	УправленческийОстаткиИОбороты.Период КАК Период,
	|	0 КАК СуммаОстаток
	|ИЗ
	|	РегистрБухгалтерии.Управленческий.ОстаткиИОбороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			Регистратор,
	|			,
	|			Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ВзаиморасчетыСПокупателями),
	|			,
	|			Подразделение = &Подразделение
	|				И Субконто1 = &Контрагент
	|				И Субконто2 = НЕОПРЕДЕЛЕНО) КАК УправленческийОстаткиИОбороты
	|ГДЕ
	|	УправленческийОстаткиИОбороты.Регистратор <> НЕОПРЕДЕЛЕНО
	|	И НЕ УправленческийОстаткиИОбороты.Регистратор ССЫЛКА Документ.РеализацияГотовойПродукции
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	0,
	|	0,
	|	0,
	|	&НачалоПериода,
	|	ЕСТЬNULL(-УправленческийОстатки.СуммаОстаток, 0)
	|ИЗ
	|	РегистрБухгалтерии.Управленческий.Остатки(
	|			&НачалоПериода,
	|			Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ВзаиморасчетыСПокупателями),
	|			,
	|			Подразделение = &Подразделение
	|				И Субконто1 = &Контрагент
	|				И Субконто2 = НЕОПРЕДЕЛЕНО) КАК УправленческийОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	0,
	|	0,
	|	0,
	|	&КонецПериода,
	|	ЕСТЬNULL(-УправленческийОстатки.СуммаОстаток, 0)
	|ИЗ
	|	РегистрБухгалтерии.Управленческий.Остатки(
	|			&КонецПериода,
	|			Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ВзаиморасчетыСПокупателями),
	|			,
	|			Подразделение = &Подразделение
	|				И Субконто1 = &Контрагент
	|				И Субконто2 = НЕОПРЕДЕЛЕНО) КАК УправленческийОстатки
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Выборка.Следующий();
	Остаток = Выборка.СуммаОстаток;
	ОбластьШапка.Параметры.НачалоПериода = Формат(Выборка.Период, "ДЛФ=DD");
	ОбластьШапка.Параметры.НачальныйОстаток = Остаток;
	Выборка.Сбросить();
	
	ТабДок.Вывести(ОбластьШапка);
	
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Выборка.Документ) Тогда
			ОбластьОбороты.Параметры.Заполнить(Выборка);
			Остаток = Остаток + ?(ЗначениеЗаполнено(Выборка.Приход), Выборка.Приход, -Выборка.Расход);
			ОбластьОбороты.Параметры.Остаток = Остаток;
			ТабДок.Вывести(ОбластьОбороты);
		ИначеЕсли Выборка.Период = Период.ДатаОкончания Тогда
			ОбластьПодвал.Параметры.КонецПериода = Формат(Выборка.Период, "ДЛФ=DD");
			ОбластьПодвал.Параметры.КонечныйОстаток = Выборка.СуммаОстаток;
			ТабДок.Вывести(ОбластьПодвал);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Дилер = Параметры.Дилер;
	Период = Параметры.Период;
	Подразделение = Параметры.Подразделение;
	
	СформироватьНаСервере();
	
КонецПроцедуры
