
Функция СформироватьОтчет(ПериодОтчета, СписокПодразделений) Экспорт
	
	ТабДок = Новый ТабличныйДокумент;
	
	Макет = Отчеты.ОтчетПоПродажам.ПолучитьМакет("Макет");
	ОбластьЗаголовок =  Макет.ПолучитьОбласть("Заголовок");
	ОбластьНаименованиеПоказателя = Макет.ПолучитьОбласть("НаименованиеПоказателя");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрокаГод = Макет.ПолучитьОбласть("Строка | ГодКолонка");
	ОбластьСтрокаМесяц = Макет.ПолучитьОбласть("Строка | МесяцКолонка");
	ОбластьПустаяСтрока = Макет.ПолучитьОбласть("ПустаяСтрока");
	
	Если ПериодОтчета.ДатаОкончания = Дата("01.01.0001 0:00:00") Тогда
		ПериодОтчета.ДатаОкончания = Дата("31.12.2099 23:59:59");
	КонецЕсли;
	
	ИтогиПоКолонкам = Новый Массив();
	МассивГодов = Новый Массив();
	
	МассивВидовЗапроса = Новый Массив();
	МассивВидовЗапроса.Добавить("Обороты");
	МассивВидовЗапроса.Добавить("Количество договоров");
	МассивВидовЗапроса.Добавить("Количество замеров");
	
	Для Каждого ВидЗапроса ИЗ МассивВидовЗапроса Цикл
	
		Запрос = Новый Запрос;	
		Запрос.УстановитьПараметр("ДатаНачала", НачалоГода(ПериодОтчета.ДатаНачала));
		Запрос.УстановитьПараметр("ДатаОкончания",КонецГода(ПериодОтчета.ДатаОкончания));
		Запрос.УстановитьПараметр("ВидЗапроса",ВидЗапроса);
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПродажиФактОбороты.Период КАК ПериодМесяц,
		|	ВЫБОР
		|		КОГДА &ВидЗапроса = ""Обороты""
		|			ТОГДА ПродажиФактОбороты.ВидПродажи
		|		ИНАЧЕ &ВидЗапроса
		|	КОНЕЦ КАК Показатель,
		|	СУММА(ВЫБОР
		|			КОГДА &ВидЗапроса = ""Обороты""
		|				ТОГДА ЕСТЬNULL(ПродажиФактОбороты.СуммаОборот, 0)
		|			ИНАЧЕ ЕСТЬNULL(ПродажиФактОбороты.КоличествоОборот, 0)
		|		КОНЕЦ) КАК Значение,
		|	ГОД(ПродажиФактОбороты.Период) КАК ПериодГод
		|ИЗ
		|	РегистрНакопления.ПродажиФакт.Обороты(&ДатаНачала, &ДатаОкончания, Месяц, Подразделение В (&СписокПодразделений)) КАК ПродажиФактОбороты
		|ГДЕ
		|	ВЫБОР
		|			КОГДА &ВидЗапроса = ""Обороты""
		|				ТОГДА ПродажиФактОбороты.ВидПродажи <> ЗНАЧЕНИЕ(Перечисление.ВидыПродаж.Замер)
		|			ИНАЧЕ ВЫБОР
		|					КОГДА &ВидЗапроса = ""Количество договоров""
		|						ТОГДА ПродажиФактОбороты.ВидПродажи = ЗНАЧЕНИЕ(Перечисление.ВидыПродаж.Договоры)
		|					ИНАЧЕ ПродажиФактОбороты.ВидПродажи = ЗНАЧЕНИЕ(Перечисление.ВидыПродаж.Замер)
		|				КОНЕЦ
		|		КОНЕЦ
		|
		|СГРУППИРОВАТЬ ПО
		|	ПродажиФактОбороты.Период,
		|	ПродажиФактОбороты.ВидПродажи
		|
		|УПОРЯДОЧИТЬ ПО
		|	Показатель,
		|	ПериодМесяц,
		|	ПериодГод
		|ИТОГИ ПО
		|	Показатель,
		|	ПериодГод,
		|	ПериодМесяц ПЕРИОДАМИ(МЕСЯЦ, &ДатаНачала, &ДатаОкончания) КАК ПериодМесяц";
		
		Если СписокПодразделений.Количество() = 1 и (СписокПодразделений[0].Значение = Неопределено или СписокПодразделений[0].Значение = Справочники.Подразделения.ПустаяСсылка()) Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "Подразделение В (&СписокПодразделений)", "");
		Иначе
			Запрос.УстановитьПараметр("СписокПодразделений", СписокПодразделений);
		КонецЕсли;
		
		РезультатЗапроса = Запрос.Выполнить();
		ВыборкаПоказатель = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Показатель");
		
		Если ВидЗапроса = "Обороты" Тогда
			ТабДок.Вывести(ОбластьЗаголовок);
			ВывестиДанныеВыборки(ВыборкаПоказатель,ТабДок,СписокПодразделений,ВидЗапроса,ИтогиПоКолонкам,МассивГодов);
		Иначе
			ВывестиДанныеВыборки(ВыборкаПоказатель,ТабДок,СписокПодразделений,ВидЗапроса);
		КонецЕсли;
		
	КонецЦикла;
	
	ОбластьНаименованиеПоказателя.Параметры.НаименованиеПоказателя = "Итоги";
	ТабДок.Вывести(ОбластьНаименованиеПоказателя);
	ТабДок.Вывести(ОбластьШапка);
	
	СчетчикГод = 0;
	
	Для Каждого Год Из МассивГодов Цикл
		
		ПострочныйРезультат = 0;
		
		ОбластьСтрокаГод.Параметры.Год = Год;
		ТабДок.Вывести(ОбластьСтрокаГод);
		
		Для Каждого Месяц ИЗ ИтогиПоКолонкам Цикл
			
			ОбластьСтрокаМесяц.Параметры.ЗначениеПоказателя = Месяц[СчетчикГод];
			ТабДок.Присоединить(ОбластьСтрокаМесяц);
			
			ПострочныйРезультат = ПострочныйРезультат + Месяц[СчетчикГод];
			
		КонецЦикла;
		
		ОбластьСтрокаМесяц.Параметры.ЗначениеПоказателя = ПострочныйРезультат;
		ТабДок.Присоединить(ОбластьСтрокаМесяц);

		СчетчикГод = СчетчикГод + 1;
		
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции

Процедура ВывестиДанныеВыборки(Выборка,ТабДок,СписокПодразделений,ВидЗапроса,ИтогиПоКолонкам=Неопределено,МассивГодов=Неопределено)
	
	Макет = Отчеты.ОтчетПоПродажам.ПолучитьМакет("Макет");
	ОбластьЗаголовок =  Макет.ПолучитьОбласть("Заголовок");
	ОбластьНаименованиеПоказателя = Макет.ПолучитьОбласть("НаименованиеПоказателя");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрокаГод = Макет.ПолучитьОбласть("Строка | ГодКолонка");
	ОбластьСтрокаМесяц = Макет.ПолучитьОбласть("Строка | МесяцКолонка");
	ОбластьПустаяСтрока = Макет.ПолучитьОбласть("ПустаяСтрока");
	
	Если МассивГодов = Неопределено Тогда
		МассивГодов = Новый Массив;	
	КонецЕсли;
	
	Пока Выборка.Следующий() Цикл
		
		ОбластьНаименованиеПоказателя.Параметры.НаименованиеПоказателя = Выборка.Показатель;
		ТабДок.Вывести(ОбластьНаименованиеПоказателя);
		
		ПериодГод = Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ПериодГод", "ВСЕ");
		ТабДок.Вывести(ОбластьШапка);
		
		СчетчикГод = 0;
		
		Пока ПериодГод.Следующий() Цикл
			
			ПострочныйРезультат = 0;
			СчетчикГод = СчетчикГод + 1;
			СчетчикМесяц = 0;
			
			ОбластьСтрокаГод.Параметры.Год = Формат(ПериодГод.ПериодГод,"ЧГ=");
			ТабДок.Вывести(ОбластьСтрокаГод);
			
			Если МассивГодов.Количество() < СчетчикГод Тогда
				МассивГодов.Добавить(Формат(ПериодГод.ПериодГод,"ЧГ="));
			КонецЕсли;
			
			ПериодМесяц = ПериодГод.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ПериодМесяц", "ВСЕ");
	
			Пока ПериодМесяц.Следующий() Цикл
				
				Если ПериодГод.ПериодГод = Число( Формат(ПериодМесяц.ПериодМесяц,"ДФ=гггг")) Тогда
					
					СчетчикМесяц = СчетчикМесяц +1;
					
					ОбластьСтрокаМесяц.Параметры.ЗначениеПоказателя = ПериодМесяц.Значение;
					
					Если ВидЗапроса = "Обороты" Тогда
						ОбластьСтрокаМесяц.Параметры.РасшифровкаПоказателя = Новый Структура("Период, ЗначениеПоказателя, Показатель, СписокПодразделений", ПериодМесяц.ПериодМесяц, ПериодМесяц.Значение, Выборка.Показатель, СписокПодразделений);
					КонецЕсли;
					
					ТабДок.Присоединить(ОбластьСтрокаМесяц);

					ПострочныйРезультат = ПострочныйРезультат + ?(ЗначениеЗаполнено(ПериодМесяц.Значение), ПериодМесяц.Значение, 0);
					
					Если ИтогиПоКолонкам <> Неопределено Тогда
					
						Если ИтогиПоКолонкам.Количество() < СчетчикМесяц Тогда
							МасГод = Новый Массив();
							ИтогиПоКолонкам.Добавить(МасГод);
						КонецЕсли;
						
						Если ИтогиПоКолонкам[СчетчикМесяц-1].Количество() < СчетчикГод Тогда
							ИтогиПоКолонкам[СчетчикМесяц-1].Добавить(0);
						КонецЕсли;
						
						ИтогиПоКолонкам[СчетчикМесяц-1][СчетчикГод-1] = ИтогиПоКолонкам[СчетчикМесяц-1][СчетчикГод-1] + ?(ЗначениеЗаполнено(ПериодМесяц.Значение), ПериодМесяц.Значение, 0);  
						
					КонецЕсли;	
											
				КонецЕсли;
				
			КонецЦикла;
			
			ОбластьСтрокаМесяц.Параметры.ЗначениеПоказателя = ПострочныйРезультат;
			ТабДок.Присоединить(ОбластьСтрокаМесяц);
	
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры