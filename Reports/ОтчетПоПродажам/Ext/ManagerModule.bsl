
Функция СформироватьОтчет(ПериодОтчета, СписокПодразделений) Экспорт
	
	ТабДок = Новый ТабличныйДокумент;
	
	Макет = Отчеты.ОтчетПоПродажам.ПолучитьМакет("Макет");
	ОбластьЗаголовок =  Макет.ПолучитьОбласть("Заголовок");
	ОбластьНаименованиеПоказателя = Макет.ПолучитьОбласть("НаименованиеПоказателя");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрокаГод = Макет.ПолучитьОбласть("Строка | ГодКолонка");
	ОбластьСтрокаМесяц = Макет.ПолучитьОбласть("Строка | МесяцКолонка");
	
	МассивВключаемыхПоказателей = Новый Массив;
	
	МассивВключаемыхПоказателей.Добавить(Перечисления.ВидыПоказателейСотрудников.СтоимостьЗаключенныхДоговоров);
	МассивВключаемыхПоказателей.Добавить(Перечисления.ВидыПоказателейСотрудников.СтоимостьПродажСерийнойМебели);
	МассивВключаемыхПоказателей.Добавить(Перечисления.ВидыПоказателейСотрудников.СтоимостьРазмещенныхСпецификаций);
	МассивВключаемыхПоказателей.Добавить(Перечисления.ВидыПоказателейСотрудников.СтоимостьРазмещенныхСпецификацийДилеров);
	МассивВключаемыхПоказателей.Добавить(Перечисления.ВидыПоказателейСотрудников.СтоимостьРеализованныхМатериалов);
	Запрос = Новый Запрос;
	Запрос.Текст = 
	
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	УправленческийОбороты.Период КАК ПериодМесяц,
	|	УправленческийОбороты.Субконто1 КАК Показатель,
	|	СУММА(ЕСТЬNULL(УправленческийОбороты.СуммаОборот, 0)) КАК Значение,
	|	ГОД(УправленческийОбороты.Период) КАК ПериодГод
	|ИЗ
	|	РегистрБухгалтерии.Управленческий.Обороты(&ДатаНачала, &ДатаОкончания, Месяц, Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПоказателиСотрудника), , Подразделение В (&СписокПодразделений), , ) КАК УправленческийОбороты
	|ГДЕ
	|	УправленческийОбороты.Субконто1 В(&МассивВключаемыхПоказателей)
	|
	|СГРУППИРОВАТЬ ПО
	|	УправленческийОбороты.Период,
	|	УправленческийОбороты.Субконто1
	|
	|УПОРЯДОЧИТЬ ПО
	|	Показатель,
	|	ПериодМесяц,
	|	ПериодГод
	|ИТОГИ ПО
	|	Показатель,
	|	ПериодГод,
	|	ПериодМесяц ПЕРИОДАМИ(МЕСЯЦ, &ДатаНачала, &ДатаОкончания) КАК ПериодМесяц";
	
	Запрос.УстановитьПараметр("ДатаНачала", ПериодОтчета.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ПериодОтчета.ДатаОкончания);
	Запрос.УстановитьПараметр("СписокПодразделений", СписокПодразделений);
	Запрос.УстановитьПараметр("МассивВключаемыхПоказателей", МассивВключаемыхПоказателей);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаПоказатель = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Показатель");
	
	ТабДок.Вывести(ОбластьЗаголовок);
	
	Пока ВыборкаПоказатель.Следующий() Цикл
		
		ОбластьНаименованиеПоказателя.Параметры.НаименованиеПоказателя = ВыборкаПоказатель.Показатель;
		ТабДок.Вывести(ОбластьНаименованиеПоказателя);
		
		ПериодГод = ВыборкаПоказатель.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ПериодГод", "ВСЕ");
		ТабДок.Вывести(ОбластьШапка);
		
		Пока ПериодГод.Следующий() Цикл
			
			ОбластьСтрокаГод.Параметры.Год = ПериодГод.ПериодГод;
			ТабДок.Вывести(ОбластьСтрокаГод);
			
			ПериодМесяц = ПериодГод.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ПериодМесяц", "ВСЕ");
			
			Пока ПериодМесяц.Следующий() Цикл
				
				Если ПериодГод.ПериодГод = Число( Формат(ПериодМесяц.ПериодМесяц,"ДФ=гггг")) Тогда
					
					ОбластьСтрокаМесяц.Параметры.ЗначениеПоказателя = ПериодМесяц.Значение;
					ТабДок.Присоединить(ОбластьСтрокаМесяц);
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции