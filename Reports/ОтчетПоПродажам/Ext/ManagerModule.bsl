
Функция СформироватьОтчет(ПериодОтчета, СписокПодразделений) Экспорт
	
	ТабДок = Новый ТабличныйДокумент;
	
	Макет = Отчеты.ОтчетПоПродажам.ПолучитьМакет("Макет");
	ОбластьЗаголовок =  Макет.ПолучитьОбласть("Заголовок");
	ОбластьНаименованиеПоказателя = Макет.ПолучитьОбласть("НаименованиеПоказателя");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрокаГод = Макет.ПолучитьОбласть("Строка | ГодКолонка");
	ОбластьСтрокаМесяц = Макет.ПолучитьОбласть("Строка | МесяцКолонка");
	ОбластьПустаяСтрока = Макет.ПолучитьОбласть("ПустаяСтрока");
	
	Если ПериодОтчета.ДатаОкончания = Дата("01.01.0001 0:00:00") Тогда
		ПериодОтчета.ДатаОкончания = Дата("31.12.2099 23:59:59");
	КонецЕсли;
	
	ИтогиПоКолонкам = Новый Массив();
	
	Запрос = Новый Запрос;	
	Запрос.УстановитьПараметр("ДатаНачала", НачалоГода(ПериодОтчета.ДатаНачала));
	Запрос.УстановитьПараметр("ДатаОкончания",КонецГода(ПериодОтчета.ДатаОкончания));
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПродажиФактОбороты.Период КАК ПериодМесяц,
	|	ПродажиФактОбороты.ВидПродажи КАК Показатель,
	|	СУММА(ЕСТЬNULL(ПродажиФактОбороты.СуммаОборот, 0)) КАК Значение,
	|	ГОД(ПродажиФактОбороты.Период) КАК ПериодГод
	|ИЗ
	|	РегистрНакопления.ПродажиФакт.Обороты(&ДатаНачала, &ДатаОкончания, Месяц, Подразделение В (&СписокПодразделений)) КАК ПродажиФактОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	ПродажиФактОбороты.Период,
	|	ПродажиФактОбороты.ВидПродажи
	|
	|УПОРЯДОЧИТЬ ПО
	|	Показатель,
	|	ПериодМесяц,
	|	ПериодГод
	|ИТОГИ ПО
	|	Показатель,
	|	ПериодГод,
	|	ПериодМесяц ПЕРИОДАМИ(МЕСЯЦ, &ДатаНачала, &ДатаОкончания) КАК ПериодМесяц";
	
	Если СписокПодразделений.Количество() = 1 и (СписокПодразделений[0].Значение = Неопределено или СписокПодразделений[0].Значение = Справочники.Подразделения.ПустаяСсылка()) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Подразделение В (&СписокПодразделений)", "");
	Иначе
		Запрос.УстановитьПараметр("СписокПодразделений", СписокПодразделений);
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	ТаблицаРезультатов = РезультатЗапроса.Выгрузить();
	ВыборкаПоказатель = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Показатель");
	
	ТабДок.Вывести(ОбластьЗаголовок);
	
	Пока ВыборкаПоказатель.Следующий() Цикл
		
		ОбластьНаименованиеПоказателя.Параметры.НаименованиеПоказателя = ВыборкаПоказатель.Показатель;
		ТабДок.Вывести(ОбластьНаименованиеПоказателя);
		
		ПериодГод = ВыборкаПоказатель.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ПериодГод", "ВСЕ");
		ТабДок.Вывести(ОбластьШапка);
		
		Пока ПериодГод.Следующий() Цикл
			
			ПострочныйРезультат = 0;
			Счетчик = 0;
			
			ОбластьСтрокаГод.Параметры.Год = Формат(ПериодГод.ПериодГод,"ЧГ=");
			ТабДок.Вывести(ОбластьСтрокаГод);
			
			ПериодМесяц = ПериодГод.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ПериодМесяц", "ВСЕ");
			
			Пока ПериодМесяц.Следующий() Цикл
				
				Если ПериодГод.ПериодГод = Число( Формат(ПериодМесяц.ПериодМесяц,"ДФ=гггг")) Тогда
					
					Счетчик = Счетчик +1;
					ОбластьСтрокаМесяц.Параметры.ЗначениеПоказателя = ПериодМесяц.Значение;
					ОбластьСтрокаМесяц.Параметры.РасшифровкаПоказателя = Новый Структура("Период, ЗначениеПоказателя, Показатель, СписокПодразделений", ПериодМесяц.ПериодМесяц, ПериодМесяц.Значение, ВыборкаПоказатель.Показатель, СписокПодразделений);
					ТабДок.Присоединить(ОбластьСтрокаМесяц);
					ПострочныйРезультат = ПострочныйРезультат + ?(ЗначениеЗаполнено(ПериодМесяц.Значение), ПериодМесяц.Значение, 0);
					
					Если ИтогиПоКолонкам.Количество() < Счетчик Тогда
						ИтогиПоКолонкам.Добавить(0);
					КонецЕсли;
					
					ИтогиПоКолонкам[Счетчик-1] = ИтогиПоКолонкам[Счетчик-1] + ?(ЗначениеЗаполнено(ПериодМесяц.Значение), ПериодМесяц.Значение, 0);  
					
					Если Счетчик = 12 Тогда
						ОбластьСтрокаМесяц.Параметры.ЗначениеПоказателя = ПострочныйРезультат;
						ТабДок.Присоединить(ОбластьСтрокаМесяц);
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ТабДок.Вывести(ОбластьПустаяСтрока);
	
	ОбластьСтрокаГод.Параметры.Год = "Итого:";
	ТабДок.Вывести(ОбластьСтрокаГод);
	
	ОбщийИтог = 0;
	
	Для Каждого Месяц ИЗ ИтогиПоКолонкам Цикл
		ОбластьСтрокаМесяц.Параметры.ЗначениеПоказателя = Месяц;
		ТабДок.Присоединить(ОбластьСтрокаМесяц);
		ОбщийИтог = ОбщийИтог + Месяц;
	КонецЦикла;
	
	ОбластьСтрокаМесяц.Параметры.ЗначениеПоказателя = ОбщийИтог;
	ТабДок.Присоединить(ОбластьСтрокаМесяц);
	
	Возврат ТабДок;
	
КонецФункции