
&НаКлиенте
Процедура Сформировать(Команда)
	
	Если ЗначениеЗаполнено(Отчет.Подразделение) Тогда	
		ТабличныйДокумент = СформироватьТабличныйДокумент();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СформироватьТабличныйДокумент()
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.ОриентацияСтраницы=ОриентацияСтраницы.Ландшафт;
	ТабДок.Защита=Истина;
	
	Макет = Отчеты.КТС1_КонтрольПроведенияДокументов.ПолучитьМакет("МакетКТС1");
	
	ОблШапка = Макет.ПолучитьОбласть("Шапка");
	ОблПустаяСтрока = Макет.ПолучитьОбласть("ПустаяСтрока");
	ОблНаряд = Макет.ПолучитьОбласть("Наряд");
	ОблУровень2 = Макет.ПолучитьОбласть("Уровень2");
	ОблУровень2Ошибка = Макет.ПолучитьОбласть("Уровень2Ошибка");
	ОблУровень3 = Макет.ПолучитьОбласть("Уровень3");
	ОблУровень3Ошибка = Макет.ПолучитьОбласть("Уровень3Ошибка");
	ОблУровень4 = Макет.ПолучитьОбласть("Уровень4");
	ОблУровень4Ошибка = Макет.ПолучитьОбласть("Уровень4Ошибка");
		
	ОблШапка.Параметры.Период = Формат(Отчет.НачалоПериода, "ДЛФ=DD")+" - "+Формат(Отчет.КонецПериода, "ДЛФ=DD");
	ОблШапка.Параметры.Подразделение = Отчет.Подразделение;
	ТабДок.Вывести(ОблШапка);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Подразделение", Отчет.Подразделение);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоДня(Отчет.НачалоПериода));
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(Отчет.КонецПериода));
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СписокСпецификаций.Ссылка КАК Наряд,
	|	СписокСпецификаций.Спецификация КАК Спецификация,
	|	ВЫБОР
	|		КОГДА КОЛИЧЕСТВО(СкладГотовойПродукции.Номенклатура) > 0
	|			ТОГДА ВЫБОР
	|					КОГДА Комплектация.Ссылка ЕСТЬ NULL 
	|						ТОГДА ""Комплектация не создана""
	|					ИНАЧЕ Комплектация.Ссылка
	|				КОНЕЦ
	|		ИНАЧЕ ""НеТребуется""
	|	КОНЕЦ КАК Комплектация,
	|	Комплектация.Проведен КАК КомплектацияПроведен
	|ИЗ
	|	Документ.НарядЗадание.СписокСпецификаций КАК СписокСпецификаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Комплектация КАК Комплектация
	|		ПО СписокСпецификаций.Спецификация = Комплектация.Спецификация
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Спецификация.СкладГотовойПродукции КАК СкладГотовойПродукции
	|		ПО СписокСпецификаций.Спецификация = СкладГотовойПродукции.Ссылка
	|ГДЕ
	|	СписокСпецификаций.Ссылка.ДатаИзготовления МЕЖДУ &НачалоПериода И &КонецПериода
	|	И СписокСпецификаций.Ссылка.Подразделение = &Подразделение
	|
	|СГРУППИРОВАТЬ ПО
	|	СписокСпецификаций.Ссылка,
	|	СписокСпецификаций.Спецификация,
	|	Комплектация.Ссылка,
	|	Комплектация.Проведен
	|
	|УПОРЯДОЧИТЬ ПО
	|	СписокСпецификаций.Ссылка.Номер,
	|	СписокСпецификаций.Спецификация.Номер
	|ИТОГИ ПО
	|	Наряд
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НарядЗадание.Ссылка КАК Наряд,
	|	ОперативныйЗакуп.Ссылка КАК Закуп
	|ИЗ
	|	Документ.НарядЗадание КАК НарядЗадание
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОперативныйЗакуп.НарядЗадания КАК ОперативныйЗакуп
	|		ПО НарядЗадание.Ссылка = ОперативныйЗакуп.Наряд
	|ГДЕ
	|	НарядЗадание.ДатаИзготовления МЕЖДУ &НачалоПериода И &КонецПериода
	|	И НарядЗадание.Подразделение = &Подразделение
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОперативныйЗакуп.Ссылка.Номер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НарядЗадание2.Ссылка КАК Наряд,
	|	Требование.Ссылка КАК Требование,
	|	Требование.Проведен КАК Проведен
	|ИЗ
	|	Документ.НарядЗадание КАК НарядЗадание2
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ТребованиеНакладная КАК Требование
	|		ПО НарядЗадание2.Ссылка = Требование.НарядЗадание
	|ГДЕ
	|	НарядЗадание2.ДатаИзготовления МЕЖДУ &НачалоПериода И &КонецПериода
	|	И НарядЗадание2.Подразделение = &Подразделение
	|
	|УПОРЯДОЧИТЬ ПО
	|	Требование.Номер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НарядЗадание.Ссылка КАК Наряд,
	|	ОперативныйЗакуп.Ссылка КАК Закуп,
	|	АвансовыйОтчет.Ссылка КАК Авансовый,
	|	АвансовыйОтчет.Проведен КАК Проведен
	|ИЗ
	|	Документ.НарядЗадание КАК НарядЗадание
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОперативныйЗакуп.НарядЗадания КАК ОперативныйЗакуп
	|		ПО НарядЗадание.Ссылка = ОперативныйЗакуп.Наряд
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.АвансовыйОтчет КАК АвансовыйОтчет
	|		ПО (АвансовыйОтчет.ДокументОснование = ОперативныйЗакуп.Ссылка)
	|ГДЕ
	|	НарядЗадание.ДатаИзготовления МЕЖДУ &НачалоПериода И &КонецПериода
	|	И НарядЗадание.Подразделение = &Подразделение
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОперативныйЗакуп.Ссылка.Номер";
		
	Результаты = Запрос.ВыполнитьПакет();
	РезультатОсновной = Результаты[0];
	
	Если НЕ РезультатОсновной.Пустой() Тогда
		
		ОперативныеЗакупы = Результаты[1].Выгрузить();
		Требования = Результаты[2].Выгрузить();
		Авансовые = Результаты[3].Выгрузить();
	
		ВыборкаНаряд = РезультатОсновной.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаНаряд.Следующий() Цикл
			
			ОблНаряд.Параметры.Заполнить(ВыборкаНаряд);
			ТабДок.Вывести(ОблПустаяСтрока);
			ТабДок.Вывести(ОблНаряд);
			
			Отбор = Новый Структура();
		    Отбор.Вставить("Наряд", ВыборкаНаряд.Наряд);

			Мас = ОперативныеЗакупы.НайтиСтроки(Отбор);
			ЕстьЗакупы = Ложь;
			
			Для Каждого Стр ИЗ Мас Цикл
				
				Если ЗначениеЗаполнено(Стр.Закуп) Тогда
				
					ОблСтрока = ОблУровень2;
					ОблСтрока.Параметры.Документ = Стр.Закуп;
					ТабДок.Вывести(ОблСтрока);
					ЕстьЗакупы = Истина;
					
					Отбор2 = Новый Структура();
		    		Отбор2.Вставить("Закуп", Стр.Закуп);
					
					Мас2 = Авансовые.НайтиСтроки(Отбор2);
					
					Для Каждого Стр2 ИЗ Мас2 Цикл
						
						Если ЗначениеЗаполнено(Стр2.Авансовый) Тогда

							 Если Стр2.Проведен Тогда
								ОблСтрока = ОблУровень3;	
							 Иначе
								ОблСтрока = ОблУровень3Ошибка;
							 КонецЕсли;
							
							 ОблСтрока.Параметры.Документ = Стр2.Авансовый;
							 ТабДок.Вывести(ОблСтрока);
							 
						 КонецЕсли;
						 
					КонецЦикла;
				
				КонецЕсли;
				
			КонецЦикла;
			
			Мас = Требования.НайтиСтроки(Отбор);
			ЕстьТребования = Ложь;
			
			Для Каждого Стр ИЗ Мас Цикл

				Если ЗначениеЗаполнено(Стр.Требование) Тогда
				
					Если Стр.Проведен Тогда
						ОблСтрока = ОблУровень2;	
					Иначе
						ОблСтрока = ОблУровень2Ошибка;
					КонецЕсли;
					
					ОблСтрока.Параметры.Документ = Стр.Требование;
					ТабДок.Вывести(ОблСтрока);
					
					ЕстьТребования = Истина;
					
				КонецЕсли;

			КонецЦикла;
			
			Если ЕстьЗакупы И НЕ ЕстьТребования Тогда
				
				ОблУровень2Ошибка.Параметры.Документ = "Требования накладные не созданы";
				ТабДок.Вывести(ОблУровень2Ошибка);
				
			КонецЕсли;
			
			ВыборкаСпец = ВыборкаНаряд.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаСпец.Следующий() Цикл
				
				 ОблУровень3.Параметры.Документ = ВыборкаСпец.Спецификация;
				 ТабДок.Вывести(ОблУровень3);
				 
				 Если НЕ (ВыборкаСпец.Комплектация = "НеТребуется") Тогда
				 
					Если ВыборкаСпец.Комплектация = "Комплектация не создана" Тогда
						ОблСтрока = ОблУровень4Ошибка; 	 
					Иначе
						ОблСтрока = ОблУровень4;
					КонецЕсли;
					
					ОблСтрока.Параметры.Документ = ВыборкаСпец.Комплектация;
					ТабДок.Вывести(ОблСтрока);
					
				 КонецЕсли;	
							
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;
			
	Возврат ТабДок;
	
КонецФункции

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Отчет.НачалоПериода = ТекущаяДата() - 24*60*60;
	Отчет.КонецПериода = ТекущаяДата();
	
КонецПроцедуры
