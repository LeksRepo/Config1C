
Функция СформироватьОтчет(ПериодОтчета, СписокПодразделений, ВидОтчета) Экспорт
	
	Перем Область;
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.ФиксацияСверху = 1;
	ТабДок.ФиксацияСлева = 1;
	
	Макет = Отчеты.КлючевыеЭкономическиеПоказатели.ПолучитьМакет("Макет");
	ОбластьШапкаСтатья = Макет.ПолучитьОбласть("Шапка|Статья");
	ОбластьШапкаКолонкаЗеленая = Макет.ПолучитьОбласть("Шапка|КолонкаЗеленая");
	ОбластьШапкаКолонкаКрасная = Макет.ПолучитьОбласть("Шапка|КолонкаКрасная");
	ОбластьШапкаКолонкаСиняя = Макет.ПолучитьОбласть("Шапка|КолонкаСиняя");
	ОбластьСтрокаСтатья = Макет.ПолучитьОбласть("Строка|Статья");
	ОбластьСтрокаКолонкаЗеленая = Макет.ПолучитьОбласть("Строка|КолонкаЗеленая");
	ОбластьСтрокаКолонкаКрасная = Макет.ПолучитьОбласть("Строка|КолонкаКрасная");
	ОбластьСтрокаКолонкаСиняя = Макет.ПолучитьОбласть("Строка|КолонкаСиняя");
	
	Таблица = ЗаполнитьТаблицу(ПериодОтчета, СписокПодразделений, ВидОтчета);
	
	//////////////////////////////////////
	// ШАПКА
	ПерваяКолонка = Истина;
	Для каждого Колонка Из Таблица.Колонки Цикл
		
		Если ПерваяКолонка Тогда
			ТабДок.Вывести(ОбластьШапкаСтатья);
			ПерваяКолонка = Ложь;
			Продолжить;
		КонецЕсли;
		
		Цвет = ОпределитьЦветКолонки(Колонка, ВидОтчета);
		Выполнить("Область = ОбластьШапкаКолонка" + Цвет);
		
		Область.Параметры.Статья = Колонка.Заголовок;
		ТабДок.Присоединить(Область);
		
	КонецЦикла;
	
	//////////////////////////////////////
	// СТРОКИ
	Для каждого Строка Из Таблица Цикл
		
		ОбластьСтрокаСтатья.Параметры.Период = Строка.Период;
		ТабДок.Вывести(ОбластьСтрокаСтатья);
		
		ПерваяКолонка = Истина;
		
		Для каждого Колонка Из Таблица.Колонки Цикл
			
			Если ПерваяКолонка Тогда
				ПерваяКолонка = Ложь;
				Продолжить;
			КонецЕсли;
			
			Цвет = ОпределитьЦветКолонки(Колонка, ВидОтчета);
			Выполнить("Область = ОбластьСтрокаКолонка" + Цвет);
			
			Область.Параметры.Сумма = Строка[Колонка.Имя];
			ТабДок.Присоединить(Область);
			
		КонецЦикла;
		
	КонецЦикла;
	
	//////////////////////////////////////
	// ИТОГИ
	ВсегоЗеленых = 0;
	ПерваяКолонка = Истина;
	ОбластьСтрокаСтатья.Параметры.Период = "Итог";
	ОбластьСтрокаСтатья.ТекущаяОбласть.Шрифт = Новый Шрифт(,,Истина);
	ТабДок.Вывести(ОбластьСтрокаСтатья);
	
	Для каждого Колонка Из Таблица.Колонки Цикл
		
		Если ПерваяКолонка Тогда
			ПерваяКолонка = Ложь;
			Продолжить;
		КонецЕсли;
		
		Цвет = ОпределитьЦветКолонки(Колонка, ВидОтчета);
		Выполнить("Область = ОбластьСтрокаКолонка" + Цвет);
		
		Область.ТекущаяОбласть.Шрифт = Новый Шрифт(,,Истина);
		Область.Параметры.Сумма = Таблица.Итог(Колонка.Имя);
		ТабДок.Присоединить(Область);
		
		Если Цвет = "Зеленая" Тогда // муахахахаххахахаха, надеюсь никто не найдёт и не расковыряет эту хню :D
			ВсегоЗеленых = ВсегоЗеленых + Область.Параметры.Сумма;
		КонецЕсли;
		
	КонецЦикла;
	
	//////////////////////////////////////
	// ПРОЦЕНТЫ
	ПерваяКолонка = Истина;
	ОбластьСтрокаСтатья.Параметры.Период = "% к зеленым";
	ОбластьСтрокаСтатья.ТекущаяОбласть.Шрифт = Новый Шрифт(,,Истина);
	ТабДок.Вывести(ОбластьСтрокаСтатья);
	
	Для каждого Колонка Из Таблица.Колонки Цикл
		
		Если ПерваяКолонка Тогда
			ПерваяКолонка = Ложь;
			Продолжить;
		КонецЕсли;
		
		Цвет = ОпределитьЦветКолонки(Колонка, ВидОтчета);
		Выполнить("Область = ОбластьСтрокаКолонка" + Цвет);
		
		Область.ТекущаяОбласть.Шрифт = Новый Шрифт(,,Истина);
		Область.ТекущаяОбласть.Формат = "ЧДЦ=2";
		Если ВсегоЗеленых <> 0 Тогда
			Сумма = Таблица.Итог(Колонка.Имя) / ВсегоЗеленых * 100;
		Иначе
			Сумма = 0;
		КонецЕсли;
		
		Сумма = ?(Сумма > 0, Сумма, - Сумма);
		
		Область.Параметры.Сумма = Сумма;
		ТабДок.Присоединить(Область);
		
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции

Функция ЗаполнитьТаблицу(ПериодОтчета, СписокПодразделений, ВидОтчета)
	
	ТЗ = Новый ТаблицаЗначений;
	
	///////////////////////////////////////////////////
	// создаём колонки таблицы
	
	ЗапросГруппы= Новый Запрос;
	ЗапросГруппы.Текст =
	"ВЫБРАТЬ
	|	Статьи.Ссылка,
	|	Статьи.Наименование
	|ИЗ
	|	Справочник.СтатьиДвиженияДенежныхСредств КАК Статьи
	|ГДЕ
	|	Статьи.ЭтоГруппа
	|	И НЕ Статьи.МеждуСчетами
	|	И Статьи.Предопределенный
	|
	|УПОРЯДОЧИТЬ ПО
	|	Статьи.НомерКолонки";
	
	Если ВидОтчета = "Касса" Тогда
		Счет = ПланыСчетов.Управленческий.ДенежныеСредства;
		СпрСтатьи = Справочники.СтатьиДвиженияДенежныхСредств;
	Иначе
		ЗапросГруппы.Текст = СтрЗаменить(ЗапросГруппы.Текст, "Справочник.СтатьиДвиженияДенежныхСредств", "Справочник.СтатьиДоходовРасходов");
		СпрСтатьи = Справочники.СтатьиДоходовРасходов;
		Счет = ПланыСчетов.Управленческий.ПрибыльУбытки;
	КонецЕсли;
	
	Результат = ЗапросГруппы.Выполнить();
	ВыборкаГруппы = Результат.Выбрать();
	ТЗ.Колонки.Добавить("Период",, "Период");
	ТЗ.Колонки.Добавить("Продажи",,"Продажи");
	Пока ВыборкаГруппы.Следующий() Цикл
		ТЗ.Колонки.Добавить(СпрСтатьи.ПолучитьИмяПредопределенного(ВыборкаГруппы.Ссылка),,ВыборкаГруппы.Наименование);
	КонецЦикла;
	ТЗ.Колонки.Добавить("Итог",,"Итог");
	
	///////////////////////////////////////////////////
	// запрос для данных отчета
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", ПериодОтчета.ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода", ПериодОтчета.ДатаОкончания);
	Запрос.УстановитьПараметр("СписокПодразделений", СписокПодразделений);
	Запрос.УстановитьПараметр("Счет", Счет);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УправленческийОбороты.Период КАК Период,
	|	ВЫРАЗИТЬ(УправленческийОбороты.Субконто1 КАК Справочник.СтатьиДвиженияДенежныхСредств).Родитель КАК Статья,
	|	ISNULL(УправленческийОбороты.СуммаОборот, 0) КАК Сумма
	|ИЗ
	|	РегистрБухгалтерии.Управленческий.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			День,
	|			Счет В ИЕРАРХИИ (&Счет),
	|			,
	|			Подразделение В (&СписокПодразделений)
	|				И ВЫРАЗИТЬ(Субконто1 КАК Справочник.СтатьиДвиженияДенежныхСредств).МеждуСчетами = ЛОЖЬ,
	|			,
	|			) КАК УправленческийОбороты
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период
	|ИТОГИ
	|	СУММА(Сумма)
	|ПО
	|	Период ПЕРИОДАМИ(ДЕНЬ, &НачалоПериода, &КонецПериода),
	|	Статья";
	
	Если ВидОтчета <> "Касса" Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Субконто1 КАК Справочник.СтатьиДвиженияДенежныхСредств)", "Субконто1 КАК Справочник.СтатьиДоходовРасходов)");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "УправленческийОбороты.СуммаОборот", "-УправленческийОбороты.СуммаОборот");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	// заполнение строк
	
	ВыборкаПериод = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ПЕРИОД" ,"ВСЕ");
	Пока ВыборкаПериод.Следующий() Цикл
		
		ИтогПоСтроке = 0;
		
		НоваяСтрока = ТЗ.Добавить();
		НоваяСтрока.Период =ВыборкаПериод.Период;
		ВыборкаСтатья = ВыборкаПериод.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "СТАТЬЯ" ,"ВСЕ");
		Пока ВыборкаСтатья.Следующий() Цикл
			ИмяПоля = СпрСтатьи.ПолучитьИмяПредопределенного(ВыборкаСтатья.Статья);
			НоваяСтрока[ИмяПоля] = ВыборкаСтатья.Сумма;
			
			Если (ВыборкаСтатья.Статья.ВидСтатьи = Перечисления.ВидыСтатейДДС.Выплата
				ИЛИ ВыборкаСтатья.Статья.ВидСтатьи = Перечисления.ВидыСтатейДР.Расход) И ТипЗнч(ВыборкаСтатья.Сумма) <> Тип("NULL") Тогда
				ИтогПоСтроке = ИтогПоСтроке + ВыборкаСтатья.Сумма;
			КонецЕсли;
			
		КонецЦикла;
		
		НоваяСтрока.Итог = ИтогПоСтроке;
		
	КонецЦикла;
	
	///////////////////////////////////////////////////////
	// заполнение колонки Продажи
	
	// договоры и доп. соглашения
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", ПериодОтчета.ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода", ПериодОтчета.ДатаОкончания);
	Запрос.УстановитьПараметр("СписокПодразделений", СписокПодразделений);
//		|	УправленческийОбороты.СуммаОборот КАК Сумма

	Запрос.Текст =
	"ВЫБРАТЬ
	|	УправленческийОбороты.Период КАК Период,
	|	УправленческийОбороты.СуммаОборот КАК Сумма
	|ИЗ
	|	РегистрБухгалтерии.Управленческий.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			День,
	|			Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПоказателиСотрудника),
	|			,
	|			Подразделение В (&СписокПодразделений)
	|				И Субконто1 = ЗНАЧЕНИЕ(Перечисление.ВидыПоказателейСотрудников.СтоимостьЗаключенныхДоговоров),
	|			,
	|			) КАК УправленческийОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(Спецификация.Дата, ДЕНЬ),
	|	Спецификация.СуммаДокумента
	|ИЗ
	|	Документ.Спецификация КАК Спецификация
	|ГДЕ
	|	Спецификация.Проведен
	|	И Спецификация.Изделие = ЗНАЧЕНИЕ(Справочник.Изделия.Детали)
	|	И Спецификация.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Спецификация.Подразделение В (&СписокПодразделений)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период
	|ИТОГИ
	|	СУММА(Сумма)
	|ПО
	|	Период ПЕРИОДАМИ(ДЕНЬ, &НачалоПериода, &КонецПериода)";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ПЕРИОД" ,"ВСЕ");
	
	Для каждого Строка Из ТЗ Цикл
		Выборка.Следующий();
		Строка.Продажи = Выборка.Сумма;
		
	КонецЦикла;
	
	Возврат ТЗ;
	
КонецФункции

Функция ОпределитьЦветКолонки(Колонка, ВидОтчета)
	
	Если ВидОтчета = "Касса" Тогда
		СпрСтатья = Справочники.СтатьиДвиженияДенежныхСредств;
		ВидСтатьи = Перечисления.ВидыСтатейДДС.Поступление;
	Иначе
		СпрСтатья = Справочники.СтатьиДоходовРасходов;
		ВидСтатьи = Перечисления.ВидыСтатейДР.Доход;
	КонецЕсли;
	
	// { Васильев Александр Леонидович [18.03.2014]
	// заменить бы без Попытки,
	// а то вылетает :(
	// } Васильев Александр Леонидович [18.03.2014]
	
	Попытка
		Статья = СпрСтатья[Колонка.Имя];
	Исключение
		Статья = СпрСтатья.ПустаяСсылка();
	КонецПопытки;
	
	Если НЕ ЗначениеЗаполнено(Статья) Тогда
		Цвет = "Синяя";
	Иначе
		Если Статья.ВидСтатьи = ВидСтатьи Тогда
			Цвет = "Зеленая";
		Иначе
			Цвет = "Красная";
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Цвет;
	
КонецФункции
