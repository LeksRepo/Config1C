
Функция СформироватьОтчет(ПериодОтчета, СписокПодразделений, ВидОтчета) Экспорт
	
	Перем Область;
	
	ТабДок = Новый ТабличныйДокумент;
	
	Макет = Отчеты.КлючевыеЭкономическиеПоказатели.ПолучитьМакет("Макет");
	ОбластьШапкаСтатья = Макет.ПолучитьОбласть("Шапка|Статья");
	ОбластьШапкаКолонкаЗеленая = Макет.ПолучитьОбласть("Шапка|КолонкаЗеленая");
	ОбластьШапкаКолонкаКрасная = Макет.ПолучитьОбласть("Шапка|КолонкаКрасная");
	ОбластьШапкаКолонкаСиняя = Макет.ПолучитьОбласть("Шапка|КолонкаСиняя");
	ОбластьСтрокаСтатья = Макет.ПолучитьОбласть("Строка|Статья");
	ОбластьСтрокаКолонкаЗеленая = Макет.ПолучитьОбласть("Строка|КолонкаЗеленая");
	ОбластьСтрокаКолонкаКрасная = Макет.ПолучитьОбласть("Строка|КолонкаКрасная");
	ОбластьСтрокаКолонкаСиняя = Макет.ПолучитьОбласть("Строка|КолонкаСиняя");
	
	Таблица = ЗаполнитьТаблицу(ПериодОтчета, СписокПодразделений, ВидОтчета);
	
	//////////////////////////////////////
	// ШАПКА
	ПерваяКолонка = Истина;
	Для каждого Колонка Из Таблица.Колонки Цикл
		
		Если ПерваяКолонка Тогда
			ТабДок.Вывести(ОбластьШапкаСтатья);
			ПерваяКолонка = Ложь;
			Продолжить;
		КонецЕсли;
		
		Цвет = ОпределитьЦветКолонки(Колонка);
		Выполнить("Область = ОбластьШапкаКолонка" + Цвет);
		
		Область.Параметры.Статья = Колонка.Заголовок;
		ТабДок.Присоединить(Область);
		
	КонецЦикла;
	
	//////////////////////////////////////
	// СТРОКИ
	Для каждого Строка Из Таблица Цикл
		
		ОбластьСтрокаСтатья.Параметры.Период = Строка.Период;
		ТабДок.Вывести(ОбластьСтрокаСтатья);
		
		ПерваяКолонка = Истина;
		
		Для каждого Колонка Из Таблица.Колонки Цикл
			
			Если ПерваяКолонка Тогда
				ПерваяКолонка = Ложь;
				Продолжить;
			КонецЕсли;
			
			Цвет = ОпределитьЦветКолонки(Колонка);
			Выполнить("Область = ОбластьСтрокаКолонка" + Цвет);
			
			Область.Параметры.Сумма = Строка[Колонка.Имя];
			ТабДок.Присоединить(Область);
			
		КонецЦикла;
		
	КонецЦикла;
	
	//////////////////////////////////////
	// ИТОГИ
	ПерваяКолонка = Истина;
	ОбластьСтрокаСтатья.Параметры.Период = "Итог";
	ОбластьСтрокаСтатья.ТекущаяОбласть.Шрифт = Новый Шрифт(,,Истина);
	ТабДок.Вывести(ОбластьСтрокаСтатья);
	
	Для каждого Колонка Из Таблица.Колонки Цикл
		
		Если ПерваяКолонка Тогда
			ПерваяКолонка = Ложь;
			Продолжить;
		КонецЕсли;
		
		Цвет = ОпределитьЦветКолонки(Колонка);
		Выполнить("Область = ОбластьСтрокаКолонка" + Цвет);
		
		Область.ТекущаяОбласть.Шрифт = Новый Шрифт(,,Истина);
		Область.Параметры.Сумма = Таблица.Итог(Колонка.Имя);
		ТабДок.Присоединить(Область);
		
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции

Функция ЗаполнитьТаблицу(ПериодОтчета, СписокПодразделений, ВидОтчета)
	
	ТЗ = Новый ТаблицаЗначений;
	СпрСтатьи = Справочники.СтатьиДвиженияДенежныхСредств; // в зависимости от вида отчета
	
	///////////////////////////////////////////////////
	// создаём колонки таблицы
	ЗапросГруппы= Новый Запрос;
	ЗапросГруппы.Текст =
	"ВЫБРАТЬ
	|	СтатьиДвиженияДенежныхСредств.Ссылка,
	|	СтатьиДвиженияДенежныхСредств.Наименование
	|ИЗ
	|	Справочник.СтатьиДвиженияДенежныхСредств КАК СтатьиДвиженияДенежныхСредств
	|ГДЕ
	|	СтатьиДвиженияДенежныхСредств.ЭтоГруппа
	|	И НЕ СтатьиДвиженияДенежныхСредств.МеждуСчетами
	|	И СтатьиДвиженияДенежныхСредств.Предопределенный
	|
	|УПОРЯДОЧИТЬ ПО
	|	СтатьиДвиженияДенежныхСредств.НомерКолонки";
	
	Результат = ЗапросГруппы.Выполнить();
	ВыборкаГруппы = Результат.Выбрать();
	ТЗ.Колонки.Добавить("Период",, "Период");
	ТЗ.Колонки.Добавить("ЗаключенныеДоговоры",,"Заключенные договоры");
	Пока ВыборкаГруппы.Следующий() Цикл
		ТЗ.Колонки.Добавить(СпрСтатьи.ПолучитьИмяПредопределенного(ВыборкаГруппы.Ссылка),,ВыборкаГруппы.Наименование);
	КонецЦикла;
	
	///////////////////////////////////////////////////
	// запрос для данных отчета
	Если ВидОтчета = "Касса" Тогда
		Счет = ПланыСчетов.Управленческий.ДенежныеСредства;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", ПериодОтчета.ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода", ПериодОтчета.ДатаОкончания);
	Запрос.УстановитьПараметр("СписокПодразделений", СписокПодразделений);
	Запрос.УстановитьПараметр("Счет", Счет);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УправленческийОбороты.Период КАК Период,
	|	ВЫРАЗИТЬ(УправленческийОбороты.Субконто1 КАК Справочник.СтатьиДвиженияДенежныхСредств).Родитель КАК Статья,
	|	УправленческийОбороты.СуммаОборот КАК Сумма
	|ИЗ
	|	РегистрБухгалтерии.Управленческий.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			День,
	|			Счет В ИЕРАРХИИ (&Счет),
	|			,
	|			Подразделение В (&СписокПодразделений)
	|				И ВЫРАЗИТЬ(Субконто1 КАК Справочник.СтатьиДвиженияДенежныхСредств).МеждуСчетами = ЛОЖЬ,
	|			,
	|			) КАК УправленческийОбороты
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период
	|ИТОГИ
	|	СУММА(Сумма)
	|ПО
	|	Период ПЕРИОДАМИ(ДЕНЬ, &НачалоПериода, &КонецПериода),
	|	Статья";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	// заполнение строк
	
	ВыборкаПериод = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ПЕРИОД" ,"ВСЕ");
	Пока ВыборкаПериод.Следующий() Цикл
		
		НоваяСтрока = ТЗ.Добавить();
		НоваяСтрока.Период =ВыборкаПериод.Период;
		ВыборкаСтатья = ВыборкаПериод.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "СТАТЬЯ" ,"ВСЕ");
		Пока ВыборкаСтатья.Следующий() Цикл
			ИмяПоля = СпрСтатьи.ПолучитьИмяПредопределенного(ВыборкаСтатья.Статья);
			НоваяСтрока[ИмяПоля] = ВыборкаСтатья.Сумма;
		КонецЦикла;
		
	КонецЦикла;
	
	// заполнить колонку заключенные договоры
	
	Возврат ТЗ;
	
КонецФункции

Функция ОпределитьЦветКолонки(Колонка)
	
	Попытка
		Статья = Справочники.СтатьиДвиженияДенежныхСредств[Колонка.Имя];
	Исключение
		Статья = Справочники.СтатьиДвиженияДенежныхСредств.ПустаяСсылка();
	КонецПопытки;
	
	Если НЕ ЗначениеЗаполнено(Статья) Тогда
		Цвет = "Синяя";
	Иначе
		Если Статья.ВидСтатьи = Перечисления.ВидыСтатейДДС.Поступление Тогда
			Цвет = "Зеленая";
		Иначе
			Цвет = "Красная";
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Цвет;
	
КонецФункции
