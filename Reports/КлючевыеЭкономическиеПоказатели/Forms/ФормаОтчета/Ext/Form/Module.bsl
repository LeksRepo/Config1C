
&НаКлиенте
Процедура Сформировать(Команда)
	
	СформироватьНаСервере();
	
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.ТабДокКасса);
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.ТабДокНачисления);
	
КонецПроцедуры

&НаСервере
Функция ЗаполнитьПоказатели()
	
	ПериодОтчета = Новый СтандартныйПериод(Отчет.НачалоПериода, Отчет.КонецПериода);
	
	ДоходЗаПериод = ПолучитьОборотПоСчету(ПланыСчетов.Управленческий.Доходы, ВидДвиженияБухгалтерии.Кредит);
	РасходЗаПериод = ПолучитьОборотПоСчету(ПланыСчетов.Управленческий.Расходы, ВидДвиженияБухгалтерии.Дебет);
	ПрибыльЗаПериод = ДоходЗаПериод - РасходЗаПериод;
	
	ДДСЗаПериод = ПолучитьОборотыПоДДС();
	ВыплатыЗаПериод = ДДСЗаПериод.ВыплатыЗаПериод;
	ПоступленияЗаПериод = ДДСЗаПериод.ПоступленияЗаПериод;
	
	СтоимостьОсновныхСредств = ПолучитьОстатокПоСчету(ПланыСчетов.Управленческий.ПервоначальнаяСтоимостьОС, ВидДвиженияБухгалтерии.Дебет);
	
	ОстатокДенежныхСредств = ПолучитьОстатокПоСчету(ПланыСчетов.Управленческий.ДенежныеСредства);
	ОстатокВКассах = ПолучитьОстатокПоСчету(ПланыСчетов.Управленческий.Касса);
	ОстатокВОперационныхКассах = ПолучитьОстатокПоСчету(ПланыСчетов.Управленческий.ОперационнаяКасса);
	ОстатокВПодотчете = ПолучитьОстатокПоСчету(ПланыСчетов.Управленческий.Подотчет);
	ОстатокНаРасчетныхСчетах = ПолучитьОстатокПоСчету(ПланыСчетов.Управленческий.РасчетныйСчет);
	
	ЗаймыВыданные = ПолучитьОстатокПоСчету(ПланыСчетов.Управленческий.ЗаймыВыданные, ВидДвиженияБухгалтерии.Дебет);
	ЗаймыПолученные = ПолучитьОстатокПоСчету(ПланыСчетов.Управленческий.ЗаймыПолученные, ВидДвиженияБухгалтерии.Кредит);
	
	СтоимостьЗаключенныхДоговоровЗаПериод = ПолучитьСтоимостьЗаключенныхДоговоров(ПериодОтчета);
	СтоимостьЗаключенныхДоговоровЗаДень = ПолучитьСтоимостьЗаключенныхДоговоров(Новый СтандартныйПериод(День, День));
	
	ОтгруженоЗаПериод = ПолучитьСуммуОтгруженныхИзделий(ПериодОтчета.ДатаНачала, ПериодОтчета.ДатаОкончания);
	ОтгруженоЗаДень = ПолучитьСуммуОтгруженныхИзделий(НачалоДня(День), КонецДня(День));
	
	ПродукцияУПокупателей = ПолучитьСуммуИзделийУКлиента();
	ПродукцияНаСкладе = ПолучитьСуммуИзделийНаСкладе();
	ТМЦНаСкладах = ПолучитьОстатокПоСчету(ПланыСчетов.Управленческий.МатериалыНаСкладе, ВидДвиженияБухгалтерии.Дебет);
	ТМЦВЦеху = ПолучитьОстатокПоСчету(ПланыСчетов.Управленческий.ОсновноеПроизводство, ВидДвиженияБухгалтерии.Дебет);
	
	МыДолжныПодразделениям = ПолучитьРазвернутыйОстатокПоСчету(ПланыСчетов.Управленческий.ВзаиморасчетыСПодразделениями, ВидДвиженияБухгалтерии.Кредит);
	НамДолжныПодразделения = ПолучитьРазвернутыйОстатокПоСчету(ПланыСчетов.Управленческий.ВзаиморасчетыСПодразделениями, ВидДвиженияБухгалтерии.Дебет);
	
	МыДолжныКлиентам = ПолучитьРазвернутыйОстатокПоСчету(ПланыСчетов.Управленческий.ВзаиморасчетыСПокупателями, ВидДвиженияБухгалтерии.Кредит);
	НамДолжныКлиенты = ПолучитьРазвернутыйОстатокПоСчету(ПланыСчетов.Управленческий.ВзаиморасчетыСПокупателями, ВидДвиженияБухгалтерии.Дебет);
	
	МыДолжныПоставщикам = ПолучитьРазвернутыйОстатокПоСчету(ПланыСчетов.Управленческий.ВзаиморасчетыСПоставщиками, ВидДвиженияБухгалтерии.Кредит);
	НамДолжныПоставщики = ПолучитьРазвернутыйОстатокПоСчету(ПланыСчетов.Управленческий.ВзаиморасчетыСПоставщиками, ВидДвиженияБухгалтерии.Дебет);
	
	МыДолжныСотрудникам = ПолучитьРазвернутыйОстатокПоСчету(ПланыСчетов.Управленческий.ВзаиморасчетыССотрудниками, ВидДвиженияБухгалтерии.Кредит);
	НамДолжныСотрудники = ПолучитьРазвернутыйОстатокПоСчету(ПланыСчетов.Управленческий.ВзаиморасчетыССотрудниками, ВидДвиженияБухгалтерии.Дебет);
	
	МыДолжныИтого = МыДолжныПодразделениям + МыДолжныКлиентам+ МыДолжныПоставщикам + МыДолжныСотрудникам;
	НамДолжныИтого = НамДолжныПодразделения+ НамДолжныКлиенты + НамДолжныПоставщики + НамДолжныСотрудники;
	
КонецФункции

&НаСервере
Процедура СформироватьНаСервере()
	
	ЗаполнитьПоказатели();
	
	ТабДокКасса = Отчеты.КлючевыеЭкономическиеПоказатели.СформироватьОтчет(Отчет.НачалоПериода, Отчет.КонецПериода, СписокПодразделений, "Касса");
	ТабДокНачисления = Отчеты.КлючевыеЭкономическиеПоказатели.СформироватьОтчет(Отчет.НачалоПериода, Отчет.КонецПериода, СписокПодразделений, "Начисления");
	
КонецПроцедуры

&НаСервере
Функция ПолучитьОборотПоСчету(Счет, ВидДвижения)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", Отчет.НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", Отчет.КонецПериода);
	Запрос.УстановитьПараметр("СписокПодразделений", СписокПодразделений);
	Запрос.УстановитьПараметр("Счет", Счет);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УправленческийОбороты.СуммаОборотДт,
	|	УправленческийОбороты.СуммаОборотКт
	|ИЗ
	|	РегистрБухгалтерии.Управленческий.Обороты(&НачалоПериода, &КонецПериода, , Счет В ИЕРАРХИИ (&Счет), , Подразделение В (&СписокПодразделений), , ) КАК УправленческийОбороты";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Если ВидДвижения = ВидДвиженияБухгалтерии.Дебет Тогда
		Оборот = Выборка.СуммаОборотДт;
	ИначеЕсли ВидДвижения = ВидДвиженияБухгалтерии.Кредит Тогда
		Оборот = Выборка.СуммаОборотКт;
	КонецЕсли;
	
	Возврат Оборот;
	
КонецФункции

&НаСервере
Функция ПолучитьОборотыПоДДС() 
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", Отчет.НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", Отчет.КонецПериода);
	Запрос.УстановитьПараметр("СписокПодразделений", СписокПодразделений);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УправленческийОбороты.СуммаОборотДт,
	|	УправленческийОбороты.СуммаОборотКт
	|ИЗ
	|	РегистрБухгалтерии.Управленческий.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			,
	|			Счет В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ДенежныеСредства)),
	|			,
	|			Подразделение В (&СписокПодразделений)
	|				И НЕ Субконто1.Внутрифирменная
	|				И НЕ Субконто1.МеждуСчетами,
	|			,
	|			) КАК УправленческийОбороты";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Движения = Новый Структура;
	Движения.Вставить("ВыплатыЗаПериод", Выборка.СуммаОборотКт);
	Движения.Вставить("ПоступленияЗаПериод", Выборка.СуммаОборотДт);
	
	Возврат Движения;
	
КонецФункции

&НаСервере
Функция ПолучитьОстатокПоСчету(Счет, ВидДвижения = Неопределено)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(День));
	Запрос.УстановитьПараметр("СписокПодразделений", СписокПодразделений);
	Запрос.УстановитьПараметр("Счет", Счет);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УправленческийОстатки.СуммаОстатокДт,
	|	УправленческийОстатки.СуммаОстатокКт,
	|	УправленческийОстатки.СуммаРазвернутыйОстатокДт,
	|	УправленческийОстатки.СуммаРазвернутыйОстатокКт,
	|	УправленческийОстатки.СуммаОстаток
	|ИЗ
	|	РегистрБухгалтерии.Управленческий.Остатки(ДОБАВИТЬКДАТЕ(&КонецПериода, СЕКУНДА, 1), Счет В ИЕРАРХИИ (&Счет), , Подразделение В (&СписокПодразделений)) КАК УправленческийОстатки";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Если ВидДвижения = ВидДвиженияБухгалтерии.Дебет Тогда
		Остаток = Выборка.СуммаРазвернутыйОстатокДт;
	ИначеЕсли ВидДвижения = ВидДвиженияБухгалтерии.Кредит Тогда
		Остаток = Выборка.СуммаРазвернутыйОстатокКт;
	Иначе
		Остаток = Выборка.СуммаОстаток;
	КонецЕсли;
	
	Возврат Остаток;
	
КонецФункции

&НаСервере
Функция ПолучитьСтоимостьЗаключенныхДоговоров(Период)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("КонецПериода", Период.ДатаОкончания);
	Запрос.УстановитьПараметр("НачалоПериода", Период.ДатаНачала);
	Запрос.УстановитьПараметр("СписокПодразделений", СписокПодразделений);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УправленческийОбороты.СуммаОборотДт
	|ИЗ
	|	РегистрБухгалтерии.Управленческий.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			,
	|			Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПоказателиСотрудника),
	|			,
	|			Подразделение В (&СписокПодразделений)
	|				И Субконто1 = ЗНАЧЕНИЕ(Перечисление.ВидыПоказателейСотрудников.СтоимостьЗаключенныхДоговоров),
	|			,
	|			) КАК УправленческийОбороты";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	Оборот = Выборка.СуммаОборотДт;
	
	Возврат Оборот;
	
КонецФункции

&НаСервере
Функция ПолучитьСуммуОтгруженныхИзделий(НачалоПериода, КонецПериода);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("СписокПодразделений", СписокПодразделений);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СУММА(УправленческийОбороты.Субконто1.СуммаДокумента) КАК Сумма
	|ИЗ
	|	РегистрБухгалтерии.Управленческий.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			,
	|			Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ГотоваяПродукция),
	|			,
	|			Подразделение В (&СписокПодразделений),
	|			КорСчет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ИзделияУКлиента)
	|				ИЛИ КорСчет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.РасходыПроизводства),
	|			) КАК УправленческийОбороты";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	Сумма = Выборка.Сумма;
	
	Возврат Сумма;
	
КонецФункции

&НаСервере
Функция ПолучитьСуммуИзделийУКлиента();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(День));
	Запрос.УстановитьПараметр("СписокПодразделений", СписокПодразделений);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СУММА(УправленческийОстатки.Субконто1.СуммаДокумента) КАК Сумма
	|ИЗ
	|	РегистрБухгалтерии.Управленческий.Остатки(ДОБАВИТЬКДАТЕ(&КонецПериода, СЕКУНДА, 1), Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ИзделияУКлиента), , Подразделение В (&СписокПодразделений)) КАК УправленческийОстатки";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	Сумма = Выборка.Сумма;
	
	Возврат Сумма;
	
КонецФункции

&НаСервере
Функция ПолучитьСуммуИзделийНаСкладе();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(День));
	Запрос.УстановитьПараметр("СписокПодразделений", СписокПодразделений);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СУММА(УправленческийОстатки.Субконто1.СуммаДокумента) КАК Сумма
	|ИЗ
	|	РегистрБухгалтерии.Управленческий.Остатки(ДОБАВИТЬКДАТЕ(&КонецПериода, СЕКУНДА, 1), Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ГотоваяПродукция), , Подразделение В (&СписокПодразделений)) КАК УправленческийОстатки";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	Сумма = Выборка.Сумма;
	
	Возврат Сумма;
	
КонецФункции

&НаСервере
Функция ПолучитьРазвернутыйОстатокПоСчету(Счет, ВидДвижения)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("КонецПериода", День);
	Запрос.УстановитьПараметр("СписокПодразделений", СписокПодразделений);
	Запрос.УстановитьПараметр("Счет", Счет);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УправленческийОстатки.Субконто1,
	|	УправленческийОстатки.Субконто2,
	|	УправленческийОстатки.Субконто3,
	|	УправленческийОстатки.СуммаРазвернутыйОстатокДт КАК СуммаРазвернутыйОстатокДт,
	|	УправленческийОстатки.СуммаРазвернутыйОстатокКт КАК СуммаРазвернутыйОстатокКт
	|ИЗ
	|	РегистрБухгалтерии.Управленческий.Остатки(&КонецПериода, Счет = &Счет, , Подразделение В (&СписокПодразделений)) КАК УправленческийОстатки
	|ИТОГИ
	|	СУММА(СуммаРазвернутыйОстатокДт),
	|	СУММА(СуммаРазвернутыйОстатокКт)
	|ПО
	|	ОБЩИЕ";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Если ВидДвижения = ВидДвиженияБухгалтерии.Дебет Тогда
		Остаток = Выборка.СуммаРазвернутыйОстатокДт;
	ИначеЕсли ВидДвижения = ВидДвиженияБухгалтерии.Кредит Тогда
		Остаток = Выборка.СуммаРазвернутыйОстатокКт;
	КонецЕсли;
	
	Возврат Остаток;
	
КонецФункции

&НаКлиенте
Процедура ТабДокКассаОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РасшифроватьЯчейку(Расшифровка);
	
КонецПроцедуры

&НаКлиенте
Процедура ТабДокНачисленияОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РасшифроватьЯчейку(Расшифровка);
	
КонецПроцедуры

&НаКлиенте
Функция РасшифроватьЯчейку(Расшифровка)
	
	ТабДок = СформироватьРасшифровку(Расшифровка);
	ТабДок.Показать("Расшифровка");
	
КонецФункции

&НаСервере
Функция СформироватьРасшифровку(Расшифровка)
	
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.ИмяПараметровПечати = "ПараметрыПечати_РасшифровкаКлючевыеПоказатели";
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Истина;
	ТабДок.ТолькоПросмотр = Истина;
	ТабДок.ОтображатьЗаголовки = Ложь;
	
	Макет = Отчеты.КлючевыеЭкономическиеПоказатели.ПолучитьМакет("МакетРасшифровки");
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	Если ТипЗнч(Расшифровка.Период) = Тип("Дата") Тогда
		НачалоПериода = НачалоДня(Расшифровка.Период);
		КонецПериода = КонецДня(Расшифровка.Период);
	Иначе
		НачалоПериода = Отчет.НачалоПериода;
		КонецПериода = Отчет.КонецПериода;
	КонецЕсли;
	
	ОбластьЗаголовок.Параметры.Статья = Расшифровка.Статья;
	ОбластьЗаголовок.Параметры.Период = ПредставлениеПериода(НачалоПериода, КонецПериода);
	
	ОбластьЗаголовок.Параметры.СписокПодразделений = ПолучитьСписокСтрокой(СписокПодразделений);
	
	ТабДок.Вывести(ОбластьЗаголовок);
	ТабДок.Вывести(ОбластьШапка);
	
	Если ТипЗнч(Расшифровка.Статья) = Тип("СправочникСсылка.СтатьиДвиженияДенежныхСредств") Тогда
		Счет = ПланыСчетов.Управленческий.ДенежныеСредства;
	ИначеЕсли ТипЗнч(Расшифровка.Статья) = Тип("СправочникСсылка.СтатьиДоходовРасходов") Тогда
		Счет = ПланыСчетов.Управленческий.ПрибыльУбытки;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	Запрос.УстановитьПараметр("Статья", Расшифровка.Статья);
	Запрос.УстановитьПараметр("СписокПодразделений", СписокПодразделений);
	Запрос.УстановитьПараметр("Счет", Счет);
	
	Если Расшифровка.Статья <> "Продажи" Тогда
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	УправленческийОбороты.СуммаОборот КАК Сумма,
		|	УправленческийОбороты.Регистратор КАК Документ,
		|	УправленческийОбороты.Регистратор.Комментарий КАК Комментарий,
		|	УправленческийОбороты.Субконто1 КАК Статья
		|ИЗ
		|	РегистрБухгалтерии.Управленческий.Обороты(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			Регистратор,
		|			Счет В ИЕРАРХИИ (&Счет),
		|			,
		|			Подразделение В (&СписокПодразделений)
		|				И Субконто1 В ИЕРАРХИИ (&Статья),
		|			,
		|			) КАК УправленческийОбороты
		|
		|УПОРЯДОЧИТЬ ПО
		|	УправленческийОбороты.Регистратор.Дата
		|ИТОГИ
		|	СУММА(Сумма)
		|ПО
		|	ОБЩИЕ";
		
	Иначе
		
		// запрос практически одинаковый
		// с запросом заполнения колонки
		// продажи, подумать как объединить
		// сложность в уровнях итогов
		// для заполнения таблицы есть
		// итог по дням. может следует
		// расшифровку тоже формировать
		// по дням?
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Доки.Сумма КАК Сумма,
		|	Доки.Документ,
		|	Доки.Статья
		|ИЗ
		|	(ВЫБРАТЬ
		|		УправленческийОбороты.СуммаОборот КАК Сумма,
		|		УправленческийОбороты.Регистратор КАК Документ,
		|		""Продажи"" КАК Статья
		|	ИЗ
		|		РегистрБухгалтерии.Управленческий.Обороты(
		|				&НачалоПериода,
		|				&КонецПериода,
		|				Регистратор,
		|				Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПоказателиСотрудника),
		|				,
		|				Подразделение В (&СписокПодразделений)
		|					И Субконто1 = ЗНАЧЕНИЕ(Перечисление.ВидыПоказателейСотрудников.СтоимостьЗаключенныхДоговоров),
		|				,
		|				) КАК УправленческийОбороты
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Спецификация.СуммаДокумента,
		|		Спецификация.Ссылка,
		|		""Продажи""
		|	ИЗ
		|		Документ.Спецификация КАК Спецификация
		|	ГДЕ
		|		Спецификация.Проведен
		|		И Спецификация.Изделие = ЗНАЧЕНИЕ(Справочник.Изделия.Детали)
		|		И Спецификация.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|		И Спецификация.Подразделение В(&СписокПодразделений)) КАК Доки
		|ИТОГИ
		|	СУММА(Сумма)
		|ПО
		|	ОБЩИЕ";
		
	КонецЕсли;
	
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаИтог = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаИтог.Следующий();
	ОбластьПодвал.Параметры.ИтогСумма = ВыборкаИтог.Сумма;
	
	ВыборкаДетальныеЗаписи = ВыборкаИтог.Выбрать();
	
	НомерСтроки = 1;
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ОбластьСтрока.Параметры.Заполнить(ВыборкаДетальныеЗаписи);
		ОбластьСтрока.Параметры.НомерСтроки = НомерСтроки;
		ТабДок.Вывести(ОбластьСтрока);
		НомерСтроки = 1 + НомерСтроки;
	КонецЦикла;
	
	ТабДок.Вывести(ОбластьПодвал);
	
	Возврат ТабДок;
	
КонецФункции

Функция ПолучитьСписокСтрокой(СписокЗначений)
	
	Результат = "";
	
	Для каждого Элем Из СписокЗначений Цикл
		
		Результат = Результат + Элем.Значение + ";";
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	// { Васильев Александр Леонидович [21.03.2014]
	// не уверен, нужна ли такая проверка...
	// } Васильев Александр Леонидович [21.03.2014]
	
	Если ТипЗнч(ИсточникВыбора) = Тип("УправляемаяФорма")
		И ИсточникВыбора.ИмяФормы = "Справочник.Подразделения.Форма.ФормаВыбораНескольких" Тогда
		
		Если НЕ ОбщегоНазначенияКлиентСервер.СпискиЗначенийИдентичны(ВыбранноеЗначение, СписокПодразделений) Тогда
			СписокПодразделений = ВыбранноеЗначение;
			ОбновитьСписокПодразделений();
			ТабДокНеАктуальны();
		КонецЕсли; 
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПодразделенийНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = Новый Структура("ОригинальныйСписок", СписокПодразделений);
	ОткрытьФорму("Справочник.Подразделения.Форма.ФормаВыбораНескольких", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьСписокПодразделений();
	ВыборПериодаКлиент.ВидПериодаПриИзменении(Неопределено, ВидПериода, Отчет.НачалоПериода, Отчет.КонецПериода, Период);
	
КонецПроцедуры

&НаКлиенте
Функция ОбновитьСписокПодразделений()
	
	Если СписокПодразделений.Количество() = 0 Тогда
		
		СписокПодразделений.Добавить();
		
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Функция ТабДокНеАктуальны()
	
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.ТабДокКасса, "НеАктуальность");
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.ТабДокНачисления, "НеАктуальность");
	
КонецФункции

&НаКлиенте
Процедура ПериодНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	ВыборПериодаКлиент.ПериодНачалоВыбораИзСписка(ЭтаФорма, Элемент, СтандартнаяОбработка, 
	ВидПериода, Период, Отчет.НачалоПериода, Отчет.КонецПериода); 
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	
	ВыборПериодаКлиент.ПериодПриИзменении(Элемент, Период, Отчет.НачалоПериода, Отчет.КонецПериода); 
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ВыборПериодаКлиент.ПериодОбработкаВыбора( 
	Элемент, ВыбранноеЗначение, СтандартнаяОбработка, 
	ВидПериода, Период, Отчет.НачалоПериода, Отчет.КонецПериода); 
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодАвтоПодбор(Элемент, Текст, ДанныеВыбора, Параметры, Ожидание, СтандартнаяОбработка)
	
	ВыборПериодаКлиент.ПериодАвтоПодбор( 
	Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка, 
	ВидПериода, Период, Отчет.НачалоПериода, Отчет.КонецПериода); 
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	ВыборПериодаКлиент.ПериодОкончаниеВводаТекста( 
	Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка, 
	ВидПериода, Период, Отчет.НачалоПериода, Отчет.КонецПериода); 
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВидПериода = Перечисления.ДоступныеПериодыОтчета.Месяц;
	
КонецПроцедуры
