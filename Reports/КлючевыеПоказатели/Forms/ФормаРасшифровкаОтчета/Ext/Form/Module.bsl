
&НаКлиенте
Процедура ТабДокОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Таб = РасшифроватьСтатью(Расшифровка);
	Таб.Показать("Статья: " + Расшифровка.Статья);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция РасшифроватьСтатью(Расшифровка)
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.ИмяПараметровПечати = "ПараметрыПечати_РасшифровкаСтатья";
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Истина;
	ТабДок.ТолькоПросмотр = Истина;
	ТабДок.ОтображатьЗаголовки = Ложь;
	
	Макет = Отчеты.КлючевыеПоказатели.ПолучитьМакет("МакетРасшифровкиРегистратор");
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	ОбластьЗаголовок.Параметры.Статья = Расшифровка.Статья;
	ОбластьЗаголовок.Параметры.Период = Расшифровка.Период;
	
	ОбластьЗаголовок.Параметры.СписокПодразделений = Расшифровка.СписокПодразделений;
	
	ТабДок.Вывести(ОбластьЗаголовок);
	ТабДок.Вывести(ОбластьШапка);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", Расшифровка.Период.ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода", Расшифровка.Период.ДатаОкончания);
	Запрос.УстановитьПараметр("Статья", Расшифровка.Статья);
	Запрос.УстановитьПараметр("СписокПодразделений", Расшифровка.СписокПодразделений);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	-УправленческийОбороты.СуммаОборот КАК Сумма,
	|	УправленческийОбороты.Регистратор КАК Документ,
	|	УправленческийОбороты.Регистратор.Комментарий КАК Комментарий,
	|	УправленческийОбороты.Субконто1 КАК Статья
	|ИЗ
	|	РегистрБухгалтерии.Управленческий.Обороты(
	|			&НачалоПериода,
	|			КОНЕЦПЕРИОДА(&КонецПериода, ДЕНЬ),
	|			Регистратор,
	|			Счет В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрибыльУбытки)),
	|			,
	|			Подразделение В (&СписокПодразделений)
	|				И Субконто1 В ИЕРАРХИИ (&Статья),
	|			,
	|			) КАК УправленческийОбороты
	|
	|УПОРЯДОЧИТЬ ПО
	|	УправленческийОбороты.Регистратор.Дата
	|ИТОГИ
	|	СУММА(Сумма)
	|ПО
	|	ОБЩИЕ";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаИтог = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаИтог.Следующий();
	ОбластьПодвал.Параметры.ИтогСумма = ВыборкаИтог.Сумма;
	
	ВыборкаДетальныеЗаписи = ВыборкаИтог.Выбрать();
	
	НомерСтроки = 1;
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ОбластьСтрока.Параметры.Заполнить(ВыборкаДетальныеЗаписи);
		ОбластьСтрока.Параметры.НомерСтроки = НомерСтроки;
		ТабДок.Вывести(ОбластьСтрока);
		НомерСтроки = 1 + НомерСтроки;
	КонецЦикла;
	
	ТабДок.Вывести(ОбластьПодвал);
	
	Возврат ТабДок;
	
КонецФункции

&НаСервере
Функция СформироватьОтчет(Период, Раздел, СписокПодразделений) Экспорт
	
	ТабДок.Очистить();
	
	Заголовок = "Расшифровка: " + Раздел;
	
	Макет = Отчеты.КлючевыеПоказатели.ПолучитьМакет("МакетРасшифровкиСтатья");
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьВидСтатьи = Макет.ПолучитьОбласть("ВидСтатьи");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", Период.ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода", Период.ДатаОкончания);
	Запрос.УстановитьПараметр("СписокПодразделений", СписокПодразделений);
	Запрос.УстановитьПараметр("Раздел", Раздел);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(УправленческийОбороты.Субконто1 КАК Справочник.СтатьиДоходовРасходов).ВидСтатьи КАК ВидСтатьи,
	|	УправленческийОбороты.Субконто1 КАК Статья,
	|	-УправленческийОбороты.СуммаОборот КАК Сумма
	|ИЗ
	|	РегистрБухгалтерии.Управленческий.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			,
	|			Счет В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрибыльУбытки)),
	|			,
	|			Подразделение В (&СписокПодразделений)
	|				И Субконто1.РазделАналитики = &Раздел,
	|			,
	|			) КАК УправленческийОбороты
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВидСтатьи
	|ИТОГИ
	|	СУММА(Сумма)
	|ПО
	|	ОБЩИЕ,
	|	ВидСтатьи";
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаОбщийИтог = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаОбщийИтог.Следующий();
	
	ВыборкаВидСтатьи = ВыборкаОбщийИтог.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ОбластьЗаголовок.Параметры.Раздел = Раздел;
	ОбластьЗаголовок.Параметры.ПредставлениеПериода = ПредставлениеПериода(Период.ДатаНачала, Период.ДатаОкончания);
	ОбластьЗаголовок.Параметры.СписокПодразделений = СписокПодразделений;
	
	ТабДок.Вывести(ОбластьЗаголовок);
	ТабДок.Вывести(ОбластьШапка);
	
	Пока ВыборкаВидСтатьи.Следующий() Цикл
		
		ОбластьВидСтатьи.Параметры.Заполнить(ВыборкаВидСтатьи);
		ТабДок.Вывести(ОбластьВидСтатьи);
		
		Выборка = ВыборкаВидСтатьи.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока Выборка.Следующий() Цикл
			ОбластьСтрока.Параметры.Расшифровка = Новый Структура("Статья,Период,СписокПодразделений", Выборка.Статья, Период, СписокПодразделений);
			ОбластьСтрока.Параметры.Заполнить(Выборка);
			
			ТабДок.Вывести(ОбластьСтрока);
		КонецЦикла;
		
	КонецЦикла;
	
	ОбластьПодвал.Параметры.Заполнить(ВыборкаОбщийИтог);
	ТабДок.Вывести(ОбластьПодвал);
	
КонецФункции
