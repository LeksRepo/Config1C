
Функция СформироватьОтчетЭкономика(НачалоПериода, КонецПериода, СписокПодразделений) Экспорт
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.ФиксацияСверху = 2;
	ТабДок.ФиксацияСлева = 1;
	
	Макет = Отчеты.КлючевыеПоказатели.ПолучитьМакет("Макет");
	ОбластьШапкаПериод = Макет.ПолучитьОбласть("Шапка|Период");
	ОбластьШапкаКолонкаЗеленая = Макет.ПолучитьОбласть("Шапка|КолонкаЗеленая");
	ОбластьШапкаКолонкаКрасная = Макет.ПолучитьОбласть("Шапка|КолонкаКрасная");
	ОбластьШапкаКолонкаСиняя = Макет.ПолучитьОбласть("Шапка|КолонкаСиняя");
	ОбластьСтрокаПериод = Макет.ПолучитьОбласть("Строка|Период");
	ОбластьСтрокаКолонкаЗеленая = Макет.ПолучитьОбласть("Строка|КолонкаЗеленая");
	ОбластьСтрокаКолонкаКрасная = Макет.ПолучитьОбласть("Строка|КолонкаКрасная");
	ОбластьСтрокаКолонкаСиняя = Макет.ПолучитьОбласть("Строка|КолонкаСиняя");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	Запрос.УстановитьПараметр("СписокПодразделений", СписокПодразделений);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РазделыАналитики.Порядок КАК НомерКолонки,
	|	РазделыАналитики.Ссылка КАК Раздел,
	|	-СУММА(ЕСТЬNULL(УправленческийОбороты.СуммаОборотДт, 0)) КАК Расход,
	|	СУММА(ЕСТЬNULL(УправленческийОбороты.СуммаОборотКт, 0)) КАК Доход
	|ИЗ
	|	Справочник.РазделыАналитики КАК РазделыАналитики
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Обороты(&НачалоПериода, КОНЕЦПЕРИОДА(&КонецПериода, ДЕНЬ), , Счет В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрибыльУбытки)), , Подразделение В (&СписокПодразделений), , ) КАК УправленческийОбороты
	|		ПО РазделыАналитики.Ссылка = УправленческийОбороты.Субконто1.РазделАналитики
	|
	|СГРУППИРОВАТЬ ПО
	|	РазделыАналитики.Порядок,
	|	РазделыАналитики.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерКолонки,
	|	РазделыАналитики.Ссылка.Наименование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УправленческийОбороты.Период КАК Период,
	|	РазделыАналитики.Ссылка КАК Раздел,
	|	-ЕСТЬNULL(УправленческийОбороты.СуммаОборотДт, 0) КАК Расход,
	|	ЕСТЬNULL(УправленческийОбороты.СуммаОборотКт, 0) КАК Доход
	|ИЗ
	|	Справочник.РазделыАналитики КАК РазделыАналитики
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Обороты(&НачалоПериода, КОНЕЦПЕРИОДА(&КонецПериода, ДЕНЬ), День, Счет В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрибыльУбытки)), , Подразделение В (&СписокПодразделений), , ) КАК УправленческийОбороты
	|		ПО РазделыАналитики.Ссылка = УправленческийОбороты.Субконто1.РазделАналитики
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период,
	|	УправленческийОбороты.Субконто1.НомерКолонки,
	|	УправленческийОбороты.Субконто1.Наименование
	|ИТОГИ
	|	МАКСИМУМ(Раздел),
	|	СУММА(Расход),
	|	СУММА(Доход)
	|ПО
	|	Период ПЕРИОДАМИ(ДЕНЬ, &НачалоПериода, &КонецПериода),
	|	РазделыАналитики.Ссылка.Наименование";
	
	ТабДок.Вывести(ОбластьШапкаПериод);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	// ШАПКА
	
	Выборка = РезультатЗапроса[0].Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ОбластьШапкаКолонкаЗеленая.Параметры.Статья = Выборка.Раздел;
		ОбластьШапкаКолонкаЗеленая.Параметры.Расшифровка = Новый Структура("Раздел, ВидРасшифровки", Выборка.Раздел, "Шапка");
		ТабДок.Присоединить(ОбластьШапкаКолонкаЗеленая);
		
	КонецЦикла;
	
	// ИТОГ ДОХОДЫ
	
	ОбластьСтрокаПериод.Параметры.Период = "Доход";
	ТабДок.Вывести(ОбластьСтрокаПериод);
	Выборка.Сбросить();
	
	Пока Выборка.Следующий() Цикл
		
		ОбластьСтрокаКолонкаЗеленая.Параметры.Сумма = Выборка.Доход;
		
		ТабДок.Присоединить(ОбластьСтрокаКолонкаЗеленая);
		
	КонецЦикла;
	
	/////////////////////////////////////////////////////////////////////////////////
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	ВыборкаПериод = РезультатЗапроса[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ПЕРИОД" ,"ВСЕ");
	
	Пока ВыборкаПериод.Следующий() Цикл
		
		Если НЕ ЗначениеЗаполнено(ВыборкаПериод.Период) Тогда
			Продолжить;
		КонецЕсли;
		
		ОбластьСтрокаПериод.Параметры.Период = ВыборкаПериод.Период;
		
		ТабДок.Вывести(ОбластьСтрокаПериод);
		
		// тут же нужно общий доход вывести
		
		ВыборкаРаздел = ВыборкаПериод.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Наименование" ,"ВСЕ");
		Пока ВыборкаРаздел.Следующий() Цикл
			
			ОбластьСтрокаКолонкаКрасная.Параметры.Сумма = ВыборкаРаздел.Расход;
			ОбластьСтрокаКолонкаКрасная.Параметры.Расшифровка = Новый Структура("Раздел, ВидРасшифровки, Период", ВыборкаРаздел.Раздел, "День", ВыборкаПериод.Период);
			ТабДок.Присоединить(ОбластьСтрокаКолонкаКрасная);
			
		КонецЦикла;
		
	КонецЦикла;
	
	// ИТОГ РАСХОДЫ
	
	ОбластьСтрокаПериод.Параметры.Период = "Итого расходов";
	ТабДок.Вывести(ОбластьСтрокаПериод);
	Выборка.Сбросить();
	
	Пока Выборка.Следующий() Цикл
		
		ОбластьСтрокаКолонкаКрасная.Параметры.Сумма = Выборка.Расход;
		ТабДок.Присоединить(ОбластьСтрокаКолонкаКрасная);
		
	КонецЦикла;
	
	// ПРИБЫЛЬ
	
	ОбластьСтрокаПериод.Параметры.Период = "Прибыль";
	ТабДок.Вывести(ОбластьСтрокаПериод);
	Выборка.Сбросить();
	
	Пока Выборка.Следующий() Цикл
		
		ОбластьСтрокаКолонкаСиняя.Параметры.Сумма = Выборка.Доход + Выборка.Расход;
		ТабДок.Присоединить(ОбластьСтрокаКолонкаСиняя);
		
	КонецЦикла;
	
	
	// ПРОЦЕНТ
	
	ОбластьСтрокаПериод.Параметры.Период = "%";
	ТабДок.Вывести(ОбластьСтрокаПериод);
	Выборка.Сбросить();
	
	Пока Выборка.Следующий() Цикл
		
		Прибыль = Выборка.Доход + Выборка.Расход;
		Если Выборка.Доход <> 0 Тогда
			Процент = Окр(Прибыль / Выборка.Доход * 100, 2);
		Иначе
			Процент = 0;
		КонецЕсли;
		
		ОбластьСтрокаКолонкаСиняя.Параметры.Сумма = Процент;
		ТабДок.Присоединить(ОбластьСтрокаКолонкаСиняя);
		
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции

Функция СформироватьОтчет(НачалоПериода, КонецПериода, СписокПодразделений, ВидОтчета) Экспорт
	
	Перем Область;
	Перем ТЗСтатьи;
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.ФиксацияСверху = 1;
	ТабДок.ФиксацияСлева = 1;
	
	Макет = Отчеты.КлючевыеПоказатели.ПолучитьМакет("Макет");
	ОбластьШапкаПериод = Макет.ПолучитьОбласть("Шапка|Период");
	ОбластьШапкаКолонкаЗеленая = Макет.ПолучитьОбласть("Шапка|КолонкаЗеленая");
	ОбластьШапкаКолонкаКрасная = Макет.ПолучитьОбласть("Шапка|КолонкаКрасная");
	ОбластьШапкаКолонкаСиняя = Макет.ПолучитьОбласть("Шапка|КолонкаСиняя");
	ОбластьСтрокаПериод = Макет.ПолучитьОбласть("Строка|Период");
	ОбластьСтрокаКолонкаЗеленая = Макет.ПолучитьОбласть("Строка|КолонкаЗеленая");
	ОбластьСтрокаКолонкаКрасная = Макет.ПолучитьОбласть("Строка|КолонкаКрасная");
	ОбластьСтрокаКолонкаСиняя = Макет.ПолучитьОбласть("Строка|КолонкаСиняя");
	
	Таблица = ЗаполнитьТаблицу(НачалоПериода, КонецПериода, СписокПодразделений, ВидОтчета, ТЗСтатьи);
	ДатыДопУменьшение = СформироватьМассивДат(СписокПодразделений, НачалоПериода, КонецПериода);
	
	//////////////////////////////////////
	// ШАПКА
	ПерваяКолонка = Истина;
	Для каждого Колонка Из Таблица.Колонки Цикл
		
		Если ПерваяКолонка Тогда
			ТабДок.Вывести(ОбластьШапкаПериод);
			ПерваяКолонка = Ложь;
			Продолжить;
		КонецЕсли;
		
		Цвет = ОпределитьЦветКолонки(Колонка, ТЗСтатьи);
		Выполнить("Область = ОбластьШапкаКолонка" + Цвет);
		
		СтрокаСтатья = ТЗСтатьи.Найти(Колонка.Имя, "ИмяПредопределенного");
		Если СтрокаСтатья <> Неопределено Тогда
			Статья = СтрокаСтатья.Ссылка;
			Ссылка = СтрокаСтатья.Ссылка;
		Иначе
			Статья = Колонка.Имя;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Ссылка) Тогда
			Если ТипЗнч(Ссылка.ВидСтатьи) = Тип("ПеречислениеСсылка.ВидыСтатейДДС") Тогда
				ВидРасшифровки = "ШапкаДДС";
			ИначеЕсли ТипЗнч(Ссылка.ВидСтатьи) = Тип("ПеречислениеСсылка.ВидыСтатейДР") Тогда
				ВидРасшифровки = "ШапкаДР";
			КонецЕсли;
		КонецЕсли;
		
		Область.Параметры.Расшифровка = Новый Структура("Период, Статья, ВидРасшифровки", "Месяц", Статья, ВидРасшифровки);
		Область.Параметры.Статья = Колонка.Заголовок;
		ТабДок.Присоединить(Область);
		
		Ссылка = Неопределено;
		ВидРасшифровки = Неопределено;
		
	КонецЦикла;
	
	//////////////////////////////////////
	// СТРОКИ
	Для каждого Строка Из Таблица Цикл
		
		ОбластьСтрокаПериод.Параметры.Период = Строка.Период;
		ТабДок.Вывести(ОбластьСтрокаПериод);
		
		ПерваяКолонка = Истина;
		
		Для каждого Колонка Из Таблица.Колонки Цикл
			
			Если ПерваяКолонка Тогда
				ПерваяКолонка = Ложь;
				Продолжить;
			КонецЕсли;
			
			Цвет = ОпределитьЦветКолонки(Колонка, ТЗСтатьи);
			Выполнить("Область = ОбластьСтрокаКолонка" + Цвет);
			
			Если Строка.Период = "Итого" ИЛИ Найти(Строка.Период, "%") > 0 Тогда
				Область.ТекущаяОбласть.Шрифт = Новый Шрифт(,, Истина);
				ВидРасшифровки = "Итого";
			КонецЕсли;
			
			Если Найти(Строка.Период, "%") > 0 Тогда
				Область.ТекущаяОбласть.Формат = "ЧДЦ = 2";
				ВидРасшифровки = "ПроцентКЗеленым";
			КонецЕсли;
			
			СтрокаСтатья = ТЗСтатьи.Найти(Колонка.Имя, "ИмяПредопределенного");
			Если СтрокаСтатья <> Неопределено Тогда
				Статья = СтрокаСтатья.Ссылка;
				Если ТипЗнч(СтрокаСтатья.ВидСтатьи) = Тип("ПеречислениеСсылка.ВидыСтатейДДС") Тогда
					ВидРасшифровки = "ДДС";
				ИначеЕсли ТипЗнч(СтрокаСтатья.ВидСтатьи) = Тип("ПеречислениеСсылка.ВидыСтатейДР") Тогда
					ВидРасшифровки = "ДР";
				КонецЕсли;
			Иначе
				Статья = Колонка.Имя;
				ВидРасшифровки = "ДР";
			КонецЕсли;
			
			Если Колонка.Имя = "Продажи" И ДатыДопУменьшение.Найти(Строка.Период) <> Неопределено Тогда
				Область.ТекущаяОбласть.ЦветТекста = ЦветаСтиля.ЦветОтрицательногоЧисла;
			Иначе
				Область.ТекущаяОбласть.ЦветТекста = Новый Цвет(0, 0, 0);
			КонецЕсли;
			
			Область.Параметры.Сумма = Строка[Колонка.Имя];
			Область.Параметры.Расшифровка = Новый Структура("Период, Статья, ВидРасшифровки", Строка.Период, Статья, ВидРасшифровки);
			ТабДок.Присоединить(Область);
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции

Функция ЗаполнитьТаблицу(НачалоПериода, КонецПериода, СписокПодразделений, ВидОтчета, ТЗСтатьи)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТЗСтатьи = Новый ТаблицаЗначений;
	ТЗСтатьи.Колонки.Добавить("Ссылка");
	ТЗСтатьи.Колонки.Добавить("ВидСтатьи");
	ТЗСтатьи.Колонки.Добавить("ИмяПредопределенного");
	ТЗСтатьи.Колонки.Добавить("Цвет");
	
	ТЗ = Новый ТаблицаЗначений;
	
	///////////////////////////////////////////////////
	// создаём колонки таблицы
	
	ЗапросГруппы= Новый Запрос;
	ЗапросГруппы.Текст =
	"ВЫБРАТЬ
	|	Статьи.Ссылка,
	|	Статьи.ВидСтатьи,
	|	Статьи.Наименование,
	|	ВЫБОР
	|		КОГДА Статьи.ВидСтатьи = ЗНАЧЕНИЕ(Перечисление.ВидыСтатейДДС.Выплата)
	|				ИЛИ Статьи.ВидСтатьи = ЗНАЧЕНИЕ(Перечисление.ВидыСтатейДР.Расход)
	|			ТОГДА ""Красная""
	|		ИНАЧЕ ""Зеленая""
	|	КОНЕЦ КАК Цвет
	|ИЗ
	|	Справочник.СтатьиДвиженияДенежныхСредств КАК Статьи
	|ГДЕ
	|	Статьи.ЭтоГруппа
	|	И НЕ Статьи.МеждуСчетами
	|	И Статьи.Предопределенный
	|
	|УПОРЯДОЧИТЬ ПО
	|	Статьи.НомерКолонки";
	
	Если ВидОтчета = "Касса" Тогда
		Счет = ПланыСчетов.Управленческий.ДенежныеСредства;
		СпрСтатьи = Справочники.СтатьиДвиженияДенежныхСредств;
	Иначе
		ЗапросГруппы.Текст = СтрЗаменить(ЗапросГруппы.Текст, "Справочник.СтатьиДвиженияДенежныхСредств", "Справочник.СтатьиДоходовРасходов");
		СпрСтатьи = Справочники.СтатьиДоходовРасходов;
		Счет = ПланыСчетов.Управленческий.ПрибыльУбытки;
	КонецЕсли;
	
	Результат = ЗапросГруппы.Выполнить();
	ВыборкаГруппы = Результат.Выбрать();
	ТЗ.Колонки.Добавить("Период",, "Период");
	ТЗ.Колонки.Добавить("Продажи",,"Продажи");
	Пока ВыборкаГруппы.Следующий() Цикл
		
		ИмяПредопределенного = СпрСтатьи.ПолучитьИмяПредопределенного(ВыборкаГруппы.Ссылка);
		
		НоваяСтрока = ТЗСтатьи.Добавить();
		НоваяСтрока.Ссылка = ВыборкаГруппы.Ссылка;
		НоваяСтрока.ВидСтатьи = ВыборкаГруппы.ВидСтатьи;
		НоваяСтрока.ИмяПредопределенного = ИмяПредопределенного;
		НоваяСтрока.Цвет = ВыборкаГруппы.Цвет;
		
		ТЗ.Колонки.Добавить(ИмяПредопределенного,,ВыборкаГруппы.Наименование);
		
	КонецЦикла;
	
	ТЗ.Колонки.Добавить("Итог",,"Итог");
	
	///////////////////////////////////////////////////
	// запрос для данных отчета
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	Запрос.УстановитьПараметр("СписокПодразделений", СписокПодразделений);
	Запрос.УстановитьПараметр("Счет", Счет);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УправленческийОбороты.Период КАК Период,
	|	ВЫРАЗИТЬ(УправленческийОбороты.Субконто1 КАК Справочник.СтатьиДвиженияДенежныхСредств).Родитель КАК Статья,
	|	ЕСТЬNULL(УправленческийОбороты.СуммаОборот, 0) КАК Сумма
	|ИЗ
	|	РегистрБухгалтерии.Управленческий.Обороты(
	|			&НачалоПериода,
	|			КОНЕЦПЕРИОДА(&КонецПериода, ДЕНЬ),
	|			День,
	|			Счет В ИЕРАРХИИ (&Счет),
	|			,
	|			Подразделение В (&СписокПодразделений)
	|				И ВЫРАЗИТЬ(Субконто1 КАК Справочник.СтатьиДвиженияДенежныхСредств).МеждуСчетами = ЛОЖЬ,
	|			,
	|			) КАК УправленческийОбороты
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период
	|ИТОГИ
	|	СУММА(Сумма)
	|ПО
	|	Период ПЕРИОДАМИ(ДЕНЬ, &НачалоПериода, &КонецПериода),
	|	Статья";
	
	Если ВидОтчета <> "Касса" Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Субконто1 КАК Справочник.СтатьиДвиженияДенежныхСредств)", "Субконто1 КАК Справочник.СтатьиДоходовРасходов)");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "УправленческийОбороты.СуммаОборот", "-УправленческийОбороты.СуммаОборот");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	// заполнение строк
	
	ВыборкаПериод = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ПЕРИОД" ,"ВСЕ");
	Пока ВыборкаПериод.Следующий() Цикл
		
		ИтогПоСтроке = 0;
		
		НоваяСтрока = ТЗ.Добавить();
		НоваяСтрока.Период =ВыборкаПериод.Период;
		ВыборкаСтатья = ВыборкаПериод.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "СТАТЬЯ" ,"ВСЕ");
		Пока ВыборкаСтатья.Следующий() Цикл
			ИмяПоля = СпрСтатьи.ПолучитьИмяПредопределенного(ВыборкаСтатья.Статья);
			НоваяСтрока[ИмяПоля] = ВыборкаСтатья.Сумма;
			
			Если (ВыборкаСтатья.Статья.ВидСтатьи = Перечисления.ВидыСтатейДДС.Выплата
				ИЛИ ВыборкаСтатья.Статья.ВидСтатьи = Перечисления.ВидыСтатейДР.Расход) И ТипЗнч(ВыборкаСтатья.Сумма) <> Тип("NULL") Тогда
				ИтогПоСтроке = ИтогПоСтроке + ВыборкаСтатья.Сумма;
			КонецЕсли;
			
		КонецЦикла;
		
		НоваяСтрока.Итог = ИтогПоСтроке;
		
	КонецЦикла;
	
	///////////////////////////////////////////////////////
	// заполнение колонки Продажи
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	Запрос.УстановитьПараметр("СписокПодразделений", СписокПодразделений);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УправленческийОбороты.Период КАК Период,
	|	УправленческийОбороты.СуммаОборот КАК Сумма
	|ИЗ
	|	РегистрБухгалтерии.Управленческий.Обороты(
	|			&НачалоПериода,
	|			КОНЕЦПЕРИОДА(&КонецПериода, ДЕНЬ),
	|			День,
	|			Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПоказателиСотрудника),
	|			,
	|			Подразделение В (&СписокПодразделений)
	|				И Субконто1 = ЗНАЧЕНИЕ(Перечисление.ВидыПоказателейСотрудников.СтоимостьЗаключенныхДоговоров),
	|			,
	|			) КАК УправленческийОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(Спецификация.Дата, ДЕНЬ),
	|	Спецификация.СуммаДокумента
	|ИЗ
	|	Документ.Спецификация КАК Спецификация
	|ГДЕ
	|	Спецификация.Проведен
	|	И Спецификация.Изделие = ЗНАЧЕНИЕ(Справочник.Изделия.Детали)
	|	И Спецификация.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Спецификация.Подразделение В(&СписокПодразделений)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период
	|ИТОГИ
	|	СУММА(Сумма)
	|ПО
	|	Период ПЕРИОДАМИ(ДЕНЬ, &НачалоПериода, &КонецПериода)";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ПЕРИОД" ,"ВСЕ");
	
	Для каждого Строка Из ТЗ Цикл
		Выборка.Следующий();
		Строка.Продажи = Выборка.Сумма;
	КонецЦикла;
	
	//////////////////////////////////////
	// ИТОГИ
	ВсегоЗеленых = 0;
	СтрокаИтоги = ТЗ.Добавить();
	Для каждого Колонка ИЗ ТЗ.Колонки Цикл
		
		Сумма = ТЗ.Итог(Колонка.Имя);
		СтрокаИтоги[Колонка.Имя] = Сумма;
		
		Строка = ТЗСтатьи.Найти(Колонка.Имя, "ИмяПредопределенного");
		
		Если Строка <> Неопределено И (Строка.ВидСтатьи = Перечисления.ВидыСтатейДДС.Поступление ИЛИ Строка.ВидСтатьи = Перечисления.ВидыСтатейДР.Доход ) Тогда
			ВсегоЗеленых = ВсегоЗеленых + Сумма;
		КонецЕсли;
		
	КонецЦикла;
	
	СтрокаИтоги.Период = "Итого";
	
	//////////////////////////////////////
	// ПРОЦЕНТЫ
	СтрокаПроцент = ТЗ.Добавить();
	Для каждого Колонка Из ТЗ.Колонки Цикл
		
		Если ВсегоЗеленых <> 0 Тогда
			Сумма = ТЗ.Итог(Колонка.Имя) / 2 / ВсегоЗеленых * 100;
		Иначе
			Сумма = 0;
		КонецЕсли;
		
		Сумма = ?(Сумма > 0, Сумма, - Сумма);
		СтрокаПроцент[Колонка.Имя] = Сумма;
	КонецЦикла;
	СтрокаПроцент.Период = "% к зеленым";
	
	Возврат ТЗ;
	
КонецФункции

Функция ОпределитьЦветКолонки(Колонка, ТЗСтатьи)
	
	Строка = ТЗСтатьи.Найти(Колонка.Имя, "ИмяПредопределенного");
	Если Строка = Неопределено Тогда
		Цвет = "Синяя";
	Иначе
		Цвет = Строка.Цвет;
	КонецЕсли;
	
	Возврат Цвет;
	
КонецФункции

Функция СформироватьМассивДат(СписокПодразделений, НачалоПериода, КонецПериода)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	Запрос.УстановитьПараметр("СписокПодразделений", СписокПодразделений);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДополнительноеСоглашение.Ссылка,
	|	ДополнительноеСоглашение.Договор,
	|	НАЧАЛОПЕРИОДА(ДополнительноеСоглашение.Дата, ДЕНЬ) КАК Дата
	|ИЗ
	|	Документ.ДополнительноеСоглашение КАК ДополнительноеСоглашение
	|ГДЕ
	|	ДополнительноеСоглашение.Договор.Подразделение В(&СписокПодразделений)
	|	И ДополнительноеСоглашение.Дата МЕЖДУ &НачалоПериода И КОНЕЦПЕРИОДА(&КонецПериода, ДЕНЬ)
	|	И ДополнительноеСоглашение.Проведен
	|	И ДополнительноеСоглашение.СуммаДокумента < 0";
	
	РезультатЗапроса = Запрос.Выполнить();
	Таблица = РезультатЗапроса.Выгрузить();
	Массив = Таблица.ВыгрузитьКолонку("Дата");
	
	Возврат Массив;
	
КонецФункции
