Функция СформироватьОтчет(ПараметрыОтчета) Экспорт
	
	Макет = Отчеты.АктСверки.ПолучитьМакет("АктСверки");
	
	МЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	МШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	МНачОстатки = Макет.ПолучитьОбласть("НачОстатки");
	МОбороты = Макет.ПолучитьОбласть("Обороты");
	МОборотыИтог = Макет.ПолучитьОбласть("ОборотыИтог");
	МКонОстатки = Макет.ПолучитьОбласть("КонОстатки");
	МПодвал = Макет.ПолучитьОбласть("Подвал");
		
	ПустаяСтрока = Макет.ПолучитьОбласть("ПустаяСтрока");
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Истина;
	ТабДок.ТолькоПросмотр = Истина;
	
	ДатаНачала = ПараметрыОтчета.Период.ДатаНачала;
	ДатаОкончания = ПараметрыОтчета.Период.ДатаОкончания;
	Контрагент = ПараметрыОтчета.Контрагент;
	Организация = ПараметрыОтчета.Организация;
	Подразделение = ПараметрыОтчета.Подразделение; 
	
	ОписаниеПериода = "за период: " + ПредставлениеПериода(НачалоДня(ДатаНачала), КонецДня(ДатаОкончания), "ФП = Истина");	
	ТекстЗаголовка = "взаимных расчетов " + ОписаниеПериода  + Символы.ПС + "между " + Организация + Символы.ПС + "и " + Контрагент;
	
	МЗаголовок.Параметры.ТекстЗаголовка = ТекстЗаголовка;
	МЗаголовок.Параметры.НазваниеОрганизации = Организация;
	МЗаголовок.Параметры.НаименованиеКонтрагента = Контрагент;
	
	ТабДок.Вывести(МЗаголовок);
	ТабДок.Вывести(МШапкаТаблицы);
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("НачалоПериода", ДатаНачала);
	Запрос.Параметры.Вставить("КонецПериода", ДатаОкончания);
	Запрос.Параметры.Вставить("Контрагент", Контрагент);
	Запрос.Параметры.Вставить("Подразделение", Подразделение);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УправленческийОстатки.СуммаОстатокДт КАК ОстатокДт,
	|	УправленческийОстатки.СуммаОстатокКт КАК ОстатокКт
	|ИЗ
	|	РегистрБухгалтерии.Управленческий.Остатки(
	|			&НачалоПериода,
	|			Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ВзаиморасчетыСПокупателями)
	|				ИЛИ Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ВзаиморасчетыСПоставщиками),
	|			,
	|			Подразделение = &Подразделение
	|				И Субконто1 = &Контрагент) КАК УправленческийОстатки";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	МНачОстатки.Параметры.СуммаНачальныйОстатокДт = Выборка.ОстатокДт;
	МНачОстатки.Параметры.СуммаНачальныйОстатокКт = Выборка.ОстатокКт;
	ТабДок.Вывести(МНачОстатки);	
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("НачалоПериода", ДатаНачала);
	Запрос.Параметры.Вставить("КонецПериода", ДатаОкончания);
	Запрос.Параметры.Вставить("Контрагент", Контрагент);
	Запрос.Параметры.Вставить("Подразделение", Подразделение);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УправленческийОбороты.СуммаОборотДт КАК СуммаОборотДт,
	|	УправленческийОбороты.СуммаОборотКт КАК СуммаОборотКт,
	|	УправленческийОбороты.Регистратор КАК Регистратор,
	|	УправленческийОбороты.Период КАК Период
	|ИЗ
	|	РегистрБухгалтерии.Управленческий.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			Регистратор,
	|			Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ВзаиморасчетыСПокупателями)
	|				ИЛИ Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ВзаиморасчетыСПоставщиками),
	|			,
	|			Подразделение = &Подразделение
	|				И Субконто1 = &Контрагент,
	|			,
	|			) КАК УправленческийОбороты
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период
	|ИТОГИ
	|	СУММА(СуммаОборотДт),
	|	СУММА(СуммаОборотКт)
	|ПО
	|	ОБЩИЕ";
	
	Результат = Запрос.Выполнить();
	
	Если НЕ Результат.Пустой() Тогда
		
		  Выборка = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		  Выборка.Следующий();
		  
		  МОборотыИтог.Параметры.СуммаОборотДт = Выборка.СуммаОборотДт;
		  МОборотыИтог.Параметры.СуммаОборотКт = Выборка.СуммаОборотКт;
		  
		  ВыборкаРегистратор = Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		  
		  Пока ВыборкаРегистратор.Следующий() Цикл
			  
			   МОбороты.Параметры.СуммаОборотДт = ВыборкаРегистратор.СуммаОборотДт;
			   МОбороты.Параметры.СуммаОборотКт = ВыборкаРегистратор.СуммаОборотКт;
			   МОбороты.Параметры.ДатаДокумента = ВыборкаРегистратор.Период;
			   МОбороты.Параметры.Регистратор = ВыборкаРегистратор.Регистратор;
			   ТабДок.Вывести(МОбороты);
			
		  КонецЦикла;
		   
		   ТабДок.Вывести(МОборотыИтог); 
		
	КонецЕсли;
	   
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("НачалоПериода", ДатаНачала);
	Запрос.Параметры.Вставить("КонецПериода", ДатаОкончания);
	Запрос.Параметры.Вставить("Контрагент", Контрагент);
	Запрос.Параметры.Вставить("Подразделение", Подразделение);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УправленческийОстатки.СуммаОстатокДт КАК ОстатокДт,
	|	УправленческийОстатки.СуммаОстатокКт КАК ОстатокКт
	|ИЗ
	|	РегистрБухгалтерии.Управленческий.Остатки(
	|			&КонецПериода,
	|			Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ВзаиморасчетыСПокупателями)
	|				ИЛИ Счет = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ВзаиморасчетыСПоставщиками),
	|			,
	|			Подразделение = &Подразделение
	|				И Субконто1 = &Контрагент) КАК УправленческийОстатки";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	МКонОстатки.Параметры.СуммаКонечныйОстатокДт = Выборка.ОстатокДт;
	МКонОстатки.Параметры.СуммаКонечныйОстатокКт = Выборка.ОстатокКт;
	ТабДок.Вывести(МКонОстатки);
		
	МПодвал.Параметры.НазваниеОрганизации = Организация;
	МПодвал.Параметры.НаименованиеКонтрагента = Контрагент;
	
	ФИОПредставителя = ПараметрыСеанса.ТекущийПользователь.ФизическоеЛицо;
	ФИОПредставителя = ФИОПредставителя.Фамилия + " " + Лев(ФИОПредставителя.Имя,1) + ". " + Лев(ФИОПредставителя.Отчество,1) + "."; 
	
	МПодвал.Параметры.ФИОПредставителя = ФИОПредставителя;
	
	ТабДок.Вывести(МПодвал);

	Возврат ТабДок;
	
КонецФункции