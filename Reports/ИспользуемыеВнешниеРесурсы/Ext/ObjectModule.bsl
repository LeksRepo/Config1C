#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Для общей формы "Форма отчета" подсистемы "Варианты отчетов".
Функция ВерсияСтандартныхФункцийОтчетов() Экспорт
	Возврат "1";
КонецФункции

// Для общей формы "Форма отчета" подсистемы "Варианты отчетов".
Функция ПолучитьНастройкиОтчета() Экспорт
	МодульФункцииОтчетовКлиентСервер = ОбщегоНазначения.ОбщийМодуль("ФункцииОтчетовКлиентСервер");
	НастройкиОтчета = МодульФункцииОтчетовКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию();
	НастройкиОтчета.ВыводитьСуммуВыделенныхЯчеек = Ложь;
	Возврат НастройкиОтчета;
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытийОбъекта

// Вызывается при выполнении отчета с помощью метода СкомпоноватьРезультат().
//
// Параметры:
//  ДокументРезультат - ТабличныйДокумент - документ, в который выводится результат,
//  ДанныеРасшифровки - Произвольный - переменная, в которую необходимо поместить
//    данные расшифровки,
//  СтандартнаяОбработка - Булево - в данный параметр передается признак выполнения
//    стандартной (системной) обработки события.
//
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДокументРезультат.Очистить();
	
	НачатьТранзакцию();
	
	Константы.ИспользуютсяПрофилиБезопасности.Установить(Истина);
	Константы.АвтоматическиНастраиватьРазрешенияВПрофиляхБезопасности.Установить(Истина);
	
	Обработки.НастройкаРазрешенийНаИспользованиеВнешнихРесурсов.ОчиститьПредоставленныеРазрешения();
	
	ЗапросыРазрешений = РаботаВБезопасномРежимеСлужебный.ЗапросыНаОбновлениеРазрешенийКонфигурации();
	
	ОперацииАдминистрирования = Обработки.НастройкаРазрешенийНаИспользованиеВнешнихРесурсов.ОперацииАдминистрированияВЗапросах(ЗапросыРазрешений);
	Дельта = Обработки.НастройкаРазрешенийНаИспользованиеВнешнихРесурсов.ДельтаИзмененийРазрешенийНаИспользованиеВнешнихРесурсов(ЗапросыРазрешений);
	
	ОтменитьТранзакцию();
	
	Отчеты.ИспользуемыеВнешниеРесурсы.ЗаполнитьПредставлениеРезультатаПримененияЗапросовНаИспользованиеВнешнихРесурсов(
		ДокументРезультат, ОперацииАдминистрирования, Дельта, Истина);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
