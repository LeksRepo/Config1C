Функция СформироватьОтчет(ПараметрыОтчета) Экспорт
	
	ТабДокТаблица = Новый ТабличныйДокумент;
	ТабДокМонтажи = Новый ТабличныйДокумент;
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабДок.ФиксацияСверху = 4;
	
	Макет = Отчеты.ЗамерыИВстречи.ПолучитьМакет("Макет");
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьШапкаТаблица = Макет.ПолучитьОбласть("ШапкаТаблица|Таблица");
	ОбластьСтрока = Макет.ПолучитьОбласть("СтрокаОтчета|Таблица");
	ОбластьСтрокаСотрудник = Макет.ПолучитьОбласть("СтрокаСотрудник|Таблица");
	
	ОбластьШапка.Параметры.Подразделение = ПараметрыОтчета.Подразделение;
	ОбластьШапка.Параметры.ПериодОтчета = ПредставлениеПериода(ПараметрыОтчета.ПериодОтчета.ДатаНачала, ПараметрыОтчета.ПериодОтчета.ДатаОкончания);
	ОбластьШапка.Параметры.Основание = ПараметрыОтчета.Основание;
	ТабДок.Вывести(ОбластьШапка);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Подразделение", ПараметрыОтчета.Подразделение);
	Запрос.УстановитьПараметр("Ответственный", ПараметрыОтчета.Ответственный);
	Запрос.УстановитьПараметр("ДатаНачала", ПараметрыОтчета.ПериодОтчета.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", КонецДня(ПараметрыОтчета.ПериодОтчета.ДатаОкончания));
	//RonEXI edit
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ФизическиеЛица.Ссылка КАК ФизЛицо
	|ПОМЕСТИТЬ ВТ_Сотрудники
	|ИЗ
	|	Справочник.ФизическиеЛица КАК ФизическиеЛица
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &Подразделение = ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ФизическиеЛица.Подразделение = &Подразделение
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА &Ответственный = ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ФизическиеЛица.Ссылка = &Ответственный
	|		КОНЕЦ
	|	И ФизическиеЛица.Активность
	|	И ФизическиеЛица.ЗамерыИВстречи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВстречаСКлиентом.Ссылка,
	|	ВстречаСКлиентом.Дата,
	|	ВстречаСКлиентом.Ответственный,
	|	ВстречаСКлиентом.Основание.ИмяЗаказчика + "" / "" + ВстречаСКлиентом.Основание.АдресЗамера КАК ИмяАдрес,
	|	""В"" КАК ВидСсылки
	|ПОМЕСТИТЬ ВТ_ВстречиЗамеры
	|ИЗ
	|	Документ.ВстречаСКлиентом КАК ВстречаСКлиентом
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &Подразделение = ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ВстречаСКлиентом.Подразделение = &Подразделение
	|		КОНЕЦ
	|	И ВстречаСКлиентом.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И НЕ ВстречаСКлиентом.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Замер.Ссылка,
	|	Замер.Дата,
	|	Замер.Замерщик,
	|	Замер.ИмяЗаказчика + "" / "" + Замер.АдресЗамера,
	|	""З""
	|ИЗ
	|	Документ.Замер КАК Замер
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &Подразделение = ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ Замер.Подразделение = &Подразделение
	|		КОНЕЦ
	|	И Замер.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И НЕ Замер.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Монтаж.Ссылка,
	|	Монтаж.ДатаМонтажа,
	|	Монтаж.Спецификация.ДокументОснование.Замерщик,
	|	Монтаж.Спецификация.ДокументОснование.АдресЗамера,
	|	""М""
	|ИЗ
	|	Документ.Монтаж КАК Монтаж
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &Подразделение = ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ Монтаж.Спецификация.ДокументОснование.Подразделение = &Подразделение
	|		КОНЕЦ
	|	И Монтаж.ДатаМонтажа МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И НЕ Монтаж.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Сотрудники.ФизЛицо КАК Ответственный,
	|	ВТ_ВстречиЗамеры.Ссылка КАК Ссылка,
	|	ВТ_ВстречиЗамеры.Дата КАК Время,
	|	ВТ_ВстречиЗамеры.ИмяАдрес КАК ИмяАдрес,
	|	ВТ_ВстречиЗамеры.ВидСсылки КАК ВидСсылки
	|ИЗ
	|	ВТ_Сотрудники КАК ВТ_Сотрудники
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВстречиЗамеры КАК ВТ_ВстречиЗамеры
	|		ПО ВТ_Сотрудники.ФизЛицо = ВТ_ВстречиЗамеры.Ответственный
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТ_Сотрудники.ФизЛицо.Фамилия,
	|	Время
	|ИТОГИ
	|	МАКСИМУМ(Ссылка),
	|	МАКСИМУМ(ИмяАдрес),
	|	МАКСИМУМ(ВидСсылки)
	|ПО
	|	Ответственный,
	|	Время ПЕРИОДАМИ(ЧАС, &ДатаНачала, &ДатаОкончания)";
	
	Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока Выборка.Следующий() Цикл
		
		ОбластьСтрокаСотрудник.Параметры.Заполнить(Выборка);
		ТабДокТаблица.Вывести(ОбластьСтрокаСотрудник);
		
		НачальнаяДата = Дата(1, 1, 1);
		
		ВыборкаПоСотруднику = Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Время", "ВСЕ");
		Пока ВыборкаПоСотруднику.Следующий() Цикл
			
			Если ЗначениеЗаполнено(ВыборкаПоСотруднику.Время) Тогда
				Если НачалоДня(ВыборкаПоСотруднику.Время) <> НачальнаяДата Тогда
					НачальнаяДата = НачалоДня(ВыборкаПоСотруднику.Время);
					ОбластьШапкаТаблица.Параметры.Дата = ВыборкаПоСотруднику.Время;
					ТабДокТаблица.Вывести(ОбластьШапкаТаблица);
				КонецЕсли;
				
				ЧасВремени = Час(ВыборкаПоСотруднику.Время);
				ЭтоМонтаж = ТипЗнч(ВыборкаПоСотруднику.Ссылка) = Тип("ДокументСсылка.Монтаж");
				
				Если ЧасВремени >= 9 И ЧасВремени <= 19 ИЛИ ЭтоМонтаж Тогда
					
					ОбластьСтрока.Параметры.Расшифровка = Новый Структура("Время, Ссылка, Основание, Подразделение, Ответственный", ВыборкаПоСотруднику.Время, ВыборкаПоСотруднику.Ссылка, ПараметрыОтчета.Основание, ПараметрыОтчета.Подразделение, ВыборкаПоСотруднику.Ответственный);
					
					Если ЭтоМонтаж Тогда
						ВыборкаПоДокументам = ВыборкаПоСотруднику.Выбрать();
						Пока ВыборкаПоДокументам.Следующий() Цикл
							ОбластьСтрока.Параметры.Заполнить(ВыборкаПоДокументам);
							ОбластьСтрока.Параметры.Время = Неопределено;
							ТабДокМонтажи.Вывести(ОбластьСтрока); 
						КонецЦикла;
					Иначе
						ОбластьСтрока.Параметры.Заполнить(ВыборкаПоСотруднику);
						ТабДокТаблица.Вывести(ОбластьСтрока);
					КонецЕсли;
					
				ИначеЕсли ЧасВремени = 23 Тогда
					
					ТабДокТаблица.Вывести(ТабДокМонтажи);
					ТабДокМонтажи.Очистить();
					
				КонецЕсли;
				
			КонецЕсли; // ЗначениеЗаполнено(ВыборкаПоСотруднику.Время)
			
		КонецЦикла; // Пока ВыборкаПоСотруднику.Следующий() Цикл
		
		ТабДок.Присоединить(ТабДокТаблица);
		ТабДокТаблица.Очистить();
		
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции

