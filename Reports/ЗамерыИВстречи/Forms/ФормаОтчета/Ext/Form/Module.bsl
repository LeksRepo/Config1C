
&НаКлиенте
Процедура Сформировать(Команда)
	СформироватьНаСервере();
КонецПроцедуры

&НаСервере
Процедура СформироватьНаСервере()
	
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("Подразделение", Подразделение);
	ПараметрыОтчета.Вставить("ПериодОтчета", ПериодОтчета);
	ПараметрыОтчета.Вставить("Ответственные", СписокСотрудников);
	ПараметрыОтчета.Вставить("Основание", Основание);
	
	ТабДок = Отчеты.ЗамерыИВстречи.СформироватьОтчет(ПараметрыОтчета);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Подразделение") Тогда
		Подразделение = Параметры.Подразделение;
	Иначе
		Подразделение = ПараметрыСеанса.ТекущееПодразделение;
	КонецЕсли;
	
	Если Параметры.Свойство("Ответственный") Тогда
		Ответственный = Параметры.Ответственный;
		СписокСотрудников.Добавить(Ответственный);
	КонецЕсли;
	
	Если Параметры.Свойство("Основание") Тогда
		Основание = Параметры.Основание;
	КонецЕсли;
	
	Если Параметры.Свойство("ФормаСпецификации") Тогда
		Владелец = Параметры.ФормаСпецификации;
	КонецЕсли;
	
	Если Параметры.Свойство("АдресЗамера") Тогда
		АдресЗамера = Параметры.АдресЗамера;
	КонецЕсли;
	
	ПериодОтчета.ДатаНачала = ТекущаяДата() - 86400 * 2;
	ПериодОтчета.ДатаОкончания = ТекущаяДата() + 86400 * 5;
	
КонецПроцедуры

&НаКлиенте
Процедура ТабДокОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	Если ТипЗнч(Расшифровка) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	Если Расшифровка.Свойство("Ссылка")
		И ЗначениеЗаполнено(Расшифровка.Ссылка) Тогда
		ОткрытьЗначение(Расшифровка.Ссылка);
		
		// { Васильев Александр Леонидович [21.09.2017]
		// Что-то ниже сложное намудрили. Не создаётся ведь новый документ, зачем параметры?
		// Удалить после 2017.11.30
		// } Васильев Александр Леонидович [21.09.2017]
		
		//Если ТипЗнч(Расшифровка.Ссылка) = Тип("ДокументСсылка.Замер") Тогда
		//	ОткрытьФорму("Документ.Замер.ФормаОбъекта", Новый Структура("Ключ", Расшифровка.Ссылка), ЭтаФорма);
		//ИначеЕсли ТипЗнч(Расшифровка.Ссылка) = Тип("ДокументСсылка.ВстречаСКлиентом") Тогда
		//	ОткрытьФорму("Документ.ВстречаСКлиентом.ФормаОбъекта", Новый Структура("Ключ", Расшифровка.Ссылка), ЭтаФорма);
		//КонецЕсли;
		
	ИначеЕсли Владелец = "ФормаСпецификации" Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Время",Расшифровка.Время);
		ПараметрыФормы.Вставить("Замерщик",Расшифровка.Ответственный);
		ПараметрыФормы.Вставить("АдресЗамера", ?(ЗначениеЗаполнено(АдресЗамера), АдресЗамера, "Введите адрес"));
		ПараметрыФормы.Вставить("Подразделение", Подразделение);
		ОткрытьФорму("Документ.Замер.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
		
	Иначе
		
		ВводЗамера = Истина;
		
		Если НЕ ПользователиКлиентСервер.ЭтоСеансВнешнегоПользователя() Тогда
			
			СписокКнопок = Новый СписокЗначений;
			ВидЗамера = ?(ЗначениеЗаполнено(Расшифровка.Основание), "Перезамер", "Замер");
			СписокКнопок.Добавить(КодВозвратаДиалога.Да, ВидЗамера);
			СписокКнопок.Добавить(КодВозвратаДиалога.Нет, "Встреча");
			СписокКнопок.Добавить(КодВозвратаДиалога.ОК, "Звонок");
			СписокКнопок.Добавить(КодВозвратаДиалога.Отмена);
			
			Текст = Новый ФорматированнаяСтрока("Основание: " + Расшифровка.Основание, Новый Шрифт(,,Истина));
			Ответ = Вопрос(Текст, СписокКнопок);
			
			Если Ответ = КодВозвратаДиалога.Нет ИЛИ Ответ = КодВозвратаДиалога.ОК Тогда
				
				ВводЗамера = Ложь;
				ЭтоЗвонок = Ответ = КодВозвратаДиалога.ОК;
				
				ПараметрыФормы = Новый Структура("Время, ДокументОснование, Подразделение, Ответственный, ЭтоЗвонок", Расшифровка.Время, Расшифровка.Основание, Расшифровка.Подразделение, Расшифровка.Ответственный, ЭтоЗвонок);
				ОткрытьФорму("Документ.ВстречаСКлиентом.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если ВводЗамера Тогда
			ПараметрыФормы = Новый Структура("Время, ДокументОснование, Замерщик, АдресЗамера", Расшифровка.Время, Расшифровка.Основание, Расшифровка.Ответственный, "Введите адрес");
			ОткрытьФорму("Документ.Замер.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	СформироватьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.ВстречаСКлиентом")
		ИЛИ ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.Замер") Тогда
		
		СформироватьНаСервере();
		Если Владелец = "ФормаСпецификации" Тогда
			ОповеститьОВыборе(ВыбранноеЗначение);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СписокЗначений") Тогда
		
		СписокСотрудников.Очистить();
		
		Для каждого Знч Из ВыбранноеЗначение Цикл
			СписокСотрудников.Добавить(Знч.Значение);
		КонецЦикла;
		
		СформироватьНаСервере();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьСотрудников(Команда)
	
	Если НЕ ЗначениеЗаполнено(Подразделение) Тогда
		Сообщить("Укажите подразделение");
		Возврат;
	КонецЕсли;
	
	ПараметрыПодбора = Новый Структура("ОригинальныйСписок, Подразделение, Активность", СписокСотрудников, Подразделение, Истина);
	ОткрытьФорму("Справочник.ФизическиеЛица.Форма.ФормаВыбораНескольких", ПараметрыПодбора, ЭтаФорма);
	
КонецПроцедуры
