
Функция СформироватьОтчет(ПараметрыОтчета) Экспорт
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.АвтоМасштаб = Истина;
	
	Макет = Отчеты.ДиагностикаБизнеса.ПолучитьМакет("Макет");
	
	Тело = Макет.ПолучитьОбласть("Тело");
	СтрокаПодразделение = Макет.ПолучитьОбласть("СтрокаПодразделение");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаМонтажа", ПараметрыОтчета.Дата);	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	докМонтаж.ДатаМонтажа КАК ДатаМонтажа,
	|	докДоговор.Спецификация,
	|	докАктВыполненияДоговора.Дата,
	|	Заметки.ТекстСодержания КАК Текст,
	|	докДоговор.Номер КАК НомерДоговора,
	|	докДоговор.Спецификация.Номер КАК НомерСпецификации,
	|	докДоговор.Спецификация.Изделие КАК ВидИзделия,
	|	докМонтаж.Монтажник,
	|	докМонтаж.Экспедитор,
	|	докДоговор.Автор.ФизическоеЛицо КАК Дизайнер,
	|	ВЫРАЗИТЬ(докДоговор.Спецификация.ДокументОснование КАК Документ.Замер).Замерщик КАК Замерщик,
	|	докДоговор.Спецификация.АдресМонтажа КАК Адрес,
	|	докДоговор.Спецификация.Производство КАК Подразделение,
	|	докДоговор.Ссылка КАК Договор
	|ИЗ
	|	Документ.Договор КАК докДоговор
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.АктВыполненияДоговора КАК докАктВыполненияДоговора
	|		ПО (докАктВыполненияДоговора.Договор = докДоговор.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Монтаж КАК докМонтаж
	|		ПО докДоговор.Спецификация = докМонтаж.Спецификация
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Заметки КАК Заметки
	|		ПО (докДоговор.Спецификация = (ВЫРАЗИТЬ(Заметки.Предмет КАК Документ.Спецификация)))
	|ГДЕ
	|	докДоговор.Спецификация.ПакетУслуг = ЗНАЧЕНИЕ(Перечисление.ПакетыУслуг.ДоставкаДоКлиентаИМонтаж)
	|	И докМонтаж.ДатаМонтажа <= &ДатаМонтажа
	|	И докАктВыполненияДоговора.Дата ЕСТЬ NULL 
	|ИТОГИ ПО
	|	Подразделение";
	
	ВыборкаПоПодразделениям = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаПоПодразделениям.Следующий() Цикл
		
		СтрокаПодразделение.Параметры.Заполнить(ВыборкаПоПодразделениям);				
		ТабДок.Вывести(СтрокаПодразделение);	
		ТабДок.НачатьГруппуСтрок();	
		
		Выборка = ВыборкаПоПодразделениям.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Тело.Параметры.Заполнить(Выборка);
			
			Тело.Параметры.Расшифровка = Новый Структура("Ссылка, Текст", Выборка.Спецификация, Выборка.Текст);
			Тело.Параметры.РасшифровкаСпецификация = Новый Структура("Спецификация", Выборка.Спецификация);
			Тело.Параметры.РасшифровкаДоговор = Новый Структура("Договор", Выборка.Договор);
			
			ТабДок.Вывести(Тело);
			
		КонецЦикла;
		
		ТабДок.ЗакончитьГруппуСтрок();
		
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции
