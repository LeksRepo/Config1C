
Функция СформироватьОтчет(ПараметрыОтчета) Экспорт
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДокСлужебок = Новый ТабличныйДокумент;

	ТабДок.АвтоМасштаб = Истина;
	
	Макет = Отчеты.ДиагностикаБизнеса.ПолучитьМакет("Макет");
	
	Тело = Макет.ПолучитьОбласть("Тело|КолонкиТело");
	СтрокаПодразделение = Макет.ПолучитьОбласть("СтрокаПодразделение|КолонкиТело");
	СтрокаДоговор = Макет.ПолучитьОбласть("ШапкаДоговор|КолонкиДоговор");
	ШапкаСлужебок = Макет.ПолучитьОбласть("ШапкаСлужебок|КолонкиСлужебок");
	СтрокаСлужебок = Макет.ПолучитьОбласть("СтрокаСлужебок|КолонкиСлужебок"); 
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаМонтажа", ПараметрыОтчета.Дата);	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	докМонтаж.ДатаМонтажа КАК ДатаМонтажа,
	|	докДоговор.Спецификация,
	|	докАктВыполненияДоговора.Дата,
	|	Заметки.ТекстСодержания КАК Текст,
	|	докДоговор.Номер КАК НомерДоговора,
	|	докДоговор.Спецификация.Номер КАК НомерСпецификации,
	|	докДоговор.Спецификация.Изделие КАК ВидИзделия,
	|	докМонтаж.Монтажник,
	|	докМонтаж.Экспедитор,
	|	докДоговор.Автор.ФизическоеЛицо КАК Дизайнер,
	|	ВЫРАЗИТЬ(докДоговор.Спецификация.ДокументОснование КАК Документ.Замер).Замерщик КАК Замерщик,
	|	докДоговор.Спецификация.АдресМонтажа КАК Адрес,
	|	докДоговор.Спецификация.Производство КАК Подразделение,
	|	докДоговор.Ссылка КАК Договор,
	|	СлужебнаяЗаписка.Ссылка КАК СЗ
	|ИЗ
	|	Документ.Договор КАК докДоговор
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.АктВыполненияДоговора КАК докАктВыполненияДоговора
	|		ПО (докАктВыполненияДоговора.Договор = докДоговор.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Монтаж КАК докМонтаж
	|		ПО докДоговор.Спецификация = докМонтаж.Спецификация
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Заметки КАК Заметки
	|		ПО (докДоговор.Спецификация = (ВЫРАЗИТЬ(Заметки.Предмет КАК Документ.Спецификация)))
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СлужебнаяЗаписка КАК СлужебнаяЗаписка
	|		ПО (докДоговор.Ссылка = СлужебнаяЗаписка.Документ
	|				ИЛИ докДоговор.Спецификация.Ссылка = СлужебнаяЗаписка.Документ)
	|ГДЕ
	|	докДоговор.Спецификация.ПакетУслуг = ЗНАЧЕНИЕ(Перечисление.ПакетыУслуг.ДоставкаДоКлиентаИМонтаж)
	|	И докМонтаж.ДатаМонтажа <= &ДатаМонтажа
	|	И докАктВыполненияДоговора.Дата ЕСТЬ NULL 
	|ИТОГИ ПО
	|	Подразделение,
	|	Договор";
	
	ВыборкаПоПодразделениям = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаПоПодразделениям.Следующий() Цикл
		
		СтрокаПодразделение.Параметры.Заполнить(ВыборкаПоПодразделениям);				
		ТабДок.Вывести(СтрокаПодразделение);	
		ТабДок.НачатьГруппуСтрок();	
		
		ВыборкаПоДоговорам = ВыборкаПоПодразделениям.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаПоДоговорам.Следующий() Цикл
			
			Выборка = ВыборкаПоДоговорам.Выбрать();
			
			Выборка.Следующий();
			
			Тело.Параметры.Заполнить(Выборка);
			СтрокаДоговор.Параметры.Заполнить(Выборка);
			
			СтрокаДоговор.Параметры.РасшифровкаСпецификация = Новый Структура("Спецификация", Выборка.Спецификация);
			СтрокаДоговор.Параметры.РасшифровкаДоговор = Новый Структура("Договор", Выборка.Договор);
			ШапкаСлужебок.Параметры.Расшифровка = Новый Структура("Ссылка, Текст", Выборка.Спецификация, Выборка.Текст);
			
			ТабДок.Вывести(СтрокаДоговор);
			ТабДок.Вывести(Тело);
			
			Выборка.Сбросить();
			
			ТабДокСлужебок.Очистить();
			ТабДокСлужебок.Вывести(ШапкаСлужебок);
			
			Пока Выборка.Следующий() Цикл
				
				СтрокаСлужебок.Параметры.Заполнить(Выборка);
				СтрокаСлужебок.Параметры.РасшифровкСЗ = Новый Структура("Служебка", Выборка.СЗ);
				ТабДокСлужебок.Вывести(СтрокаСлужебок);
				
			КонецЦикла;
			
			ТабДок.Присоединить(ТабДокСлужебок);
			
		КонецЦикла;
		
		ТабДок.ЗакончитьГруппуСтрок();
		
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции
