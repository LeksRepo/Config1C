
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Договор = Параметры.Договор;
	ТабДок = СформироватьОтчет(Договор);
КонецПроцедуры

&НаСервере
Функция СформироватьОтчет(Договор)
	
	Макет = Отчеты.ОтчетДополнительнаяМаржа.ПолучитьМакет("Расшифровка");
	
	СтрДоговор = Макет.ПолучитьОбласть("Договор");
	ТабШапка = Макет.ПолучитьОбласть("ТабШапка");
	ТабСтрока = Макет.ПолучитьОбласть("ТабСтрока");
	ТабИтог = Макет.ПолучитьОбласть("ТабИтог");
		
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Истина;
	ТабДок.ТолькоПросмотр = Истина;
	
	СтрДоговор.Параметры.Договор = Договор;
	СтрДоговор.Параметры.ДоговорРасшифровка = Договор;
	
	ТабДок.Вывести(СтрДоговор);
	ТабДок.Вывести(ТабШапка);
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Договор", Договор);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДокДоговор.Ссылка КАК ДоговорСсылка,
	|	ДокДоговор.Спецификация КАК СпецификацияСсылка,
	|	ДокДоговор.СуммаДокумента КАК СуммаПродажи
	|ПОМЕСТИТЬ втДоговоры
	|ИЗ
	|	Документ.АктВыполненияДоговора КАК ДокАкт
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Договор КАК ДокДоговор
	|		ПО ДокАкт.Договор = ДокДоговор.Ссылка
	|			И (ДокАкт.Проведен)
	|ГДЕ
	|	ДокДоговор.Ссылка = &Договор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДополнительноеСоглашение.Договор КАК ДоговорСсылка,
	|	ДополнительноеСоглашение.Ссылка КАК ДопСоглашениеСсылка,
	|	ДополнительноеСоглашение.СуммаДокумента КАК СуммаПродажи
	|ПОМЕСТИТЬ втДопСоглашения
	|ИЗ
	|	Документ.ДополнительноеСоглашение КАК ДополнительноеСоглашение
	|ГДЕ
	|	ДополнительноеСоглашение.Проведен
	|	И ДополнительноеСоглашение.Договор В
	|			(ВЫБРАТЬ
	|				т.ДоговорСсылка
	|			ИЗ
	|				втДоговоры КАК т)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Спецификация.Ссылка КАК СпецификацияСсылка,
	|	ЕСТЬNULL(втДоговоры.ДоговорСсылка, Спецификация.ДокументОснование.Договор) КАК ДоговорСсылка,
	|	Спецификация.СуммаДокумента КАК СуммаСпецификации
	|ПОМЕСТИТЬ втСпецификации
	|ИЗ
	|	Документ.Спецификация КАК Спецификация
	|		ЛЕВОЕ СОЕДИНЕНИЕ втДоговоры КАК втДоговоры
	|		ПО (втДоговоры.СпецификацияСсылка = Спецификация.Ссылка)
	|ГДЕ
	|	Спецификация.Проведен
	|	И (втДоговоры.ДоговорСсылка ЕСТЬ НЕ NULL 
	|			ИЛИ Спецификация.ДокументОснование В
	|				(ВЫБРАТЬ
	|					т.ДопСоглашениеСсылка
	|				ИЗ
	|					втДопСоглашения КАК т))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДоговоры.ДоговорСсылка КАК Договор,
	|	втДоговоры.СпецификацияСсылка КАК Спецификация,
	|	втДоговоры.СуммаПродажи,
	|	NULL КАК ДопСоглашение,
	|	0 КАК СуммаСпецификации
	|ИЗ
	|	втДоговоры КАК втДоговоры
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втДопСоглашения.ДоговорСсылка,
	|	NULL,
	|	втДопСоглашения.СуммаПродажи,
	|	втДопСоглашения.ДопСоглашениеСсылка,
	|	0
	|ИЗ
	|	втДопСоглашения КАК втДопСоглашения
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втСпецификации.ДоговорСсылка,
	|	втСпецификации.СпецификацияСсылка,
	|	0,
	|	NULL,
	|	втСпецификации.СуммаСпецификации
	|ИЗ
	|	втСпецификации КАК втСпецификации";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	СуммаПродажи = 0;
	СуммаСпецификации = 0;
	
	Пока Выборка.Следующий() Цикл
		
		Если ЗначениеЗаполнено(Выборка.Спецификация) Тогда
			ТабСтрока.Параметры.Документ = Выборка.Спецификация;
			ТабСтрока.Параметры.ДокРасшифровка = Выборка.Спецификация;
		Иначе
			ТабСтрока.Параметры.Документ = Выборка.ДопСоглашение;
			ТабСтрока.Параметры.ДокРасшифровка = Выборка.ДопСоглашение;
		КонецЕсли;
		
		ТабСтрока.Параметры.СуммаПродажи = Выборка.СуммаПродажи;
		ТабСтрока.Параметры.СуммаСпецификации = Выборка.СуммаСпецификации;
		
		СуммаПродажи = СуммаПродажи + Выборка.СуммаПродажи;
		СуммаСпецификации = СуммаСпецификации + Выборка.СуммаСпецификации; 
		
		ТабДок.Вывести(ТабСтрока);
		
	КонецЦикла;
		
	ТабИтог.Параметры.СуммаПродажи = СуммаПродажи;
	ТабИтог.Параметры.СуммаСпецификации = СуммаСпецификации;
	ТабИтог.Параметры.Маржа = СуммаПродажи - СуммаСпецификации;
	ТабДок.Вывести(ТабИтог);
	
	Возврат ТабДок;
	
КонецФункции

&НаКлиенте
Процедура ТабДокОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	ОткрытьЗначение(Расшифровка);
КонецПроцедуры


