
&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	Ключ = ПолучитьРасшифровкуНаСервере(Расшифровка);
	
	Если ЗначениеЗаполнено(Ключ) Тогда
		
		СтандартнаяОбработка = Ложь;
		фнПараметры = Новый Структура;
		фнПараметры.Вставить("Ключ", Ключ);
		ОткрытьФорму("РегистрСведений.НоменклатураПодразделений.ФормаЗаписи", фнПараметры);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьРасшифровкуНаСервере(фнРасшифровка)
	
	Перем Номенклатура, Подразделение, Ключ;
	
	Данные = ПолучитьИзВременногоХранилища(ДанныеРасшифровки);
	ЭлементыРасшифровки = Данные.Элементы.Получить(фнРасшифровка);
	Поля = ЭлементыРасшифровки.ПолучитьПоля();
	
	Если Поля.Количество() = 1 Тогда
		Поле = Поля[0].Поле;
	КонецЕсли;
	
	Если Поле = "Номенклатура" Тогда
		
		МассивРодители = ЭлементыРасшифровки.ПолучитьРодителей();
		Если МассивРодители.Количество() = 1 Тогда
			ПолеРодителя = Данные.Элементы.Получить(МассивРодители[0].Идентификатор).ПолучитьПоля();
			Номенклатура = ПолеРодителя[0].Значение;
		КонецЕсли;
		
		НастройкаПодразделение = Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы[0];
		
		Если НастройкаПодразделение <> Неопределено Тогда
			Подразделение = НастройкаПодразделение.Значение;
		КонецЕсли;
		
		Если ТипЗнч(Номенклатура) = Тип("СправочникСсылка.Номенклатура")
			И ЗначениеЗаполнено(Номенклатура)
			И НЕ Номенклатура.ЭтоГруппа
			И ТипЗнч(Подразделение) = Тип("СправочникСсылка.Подразделения")
			И ЗначениеЗаполнено(Подразделение) Тогда
			
			ЗначениеКлюча = Новый Структура;
			ЗначениеКлюча.Вставить("Номенклатура", Номенклатура);
			ЗначениеКлюча.Вставить("Подразделение", Подразделение);
			Ключ = РегистрыСведений.НоменклатураПодразделений.СоздатьКлючЗаписи(ЗначениеКлюча);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Ключ;
	
КонецФункции
