Функция СформироватьОтчет(ПараметрыОтчета) Экспорт 
	
	ДатаНачала = ПараметрыОтчета.Период.ДатаНачала;
	ДатаОкончания = ПараметрыОтчета.Период.ДатаОкончания;
	Сотрудник = ПараметрыОтчета.Сотрудник;
	БезЗавершенных = ПараметрыОтчета.БезЗавершенных;
	
	ВидОтчета = ПараметрыОтчета.ВидОтчета;
	МассивТипов = Новый Массив;
	Если ВидОтчета = "СлужебнаяЗаписка" Тогда
		МассивТипов.Добавить(Тип("ДокументСсылка.СлужебнаяЗаписка"));
	ИначеЕсли ВидОтчета = "Поручение" Тогда
		МассивТипов.Добавить(Тип("ДокументСсылка.Поручение"));
	Иначе
		МассивТипов.Добавить(Тип("ДокументСсылка.Поручение"));
		МассивТипов.Добавить(Тип("ДокументСсылка.СлужебнаяЗаписка"));
	КонецЕсли;
	ДопустимыеТипы = Новый ОписаниеТипов(МассивТипов);
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.Автомасштаб = Истина;
	
	Макет = Отчеты.ПорученияДетально.ПолучитьМакет("Макет");
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	
	ОбластьЗаголовок.Параметры.Период = ПараметрыОтчета.Период;
	ОбластьЗаголовок.Параметры.Сотрудник = Сотрудник;
	
	ТабДок.Вывести(ОбластьЗаголовок);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Поручение.СрокВыполнения КАК СрокВыполнения,
	|	Поручение.Задача КАК Текст,
	|	Поручение.Пояснение КАК Ответ,
	|	Поручение.Оценка КАК Оценка,
	|	Поручение.ДатаВыполнения КАК ДатаВыполнения,
	|	Поручение.Замечания КАК Примечание,
	|	Поручение.Документы.(
	|		Документ
	|	),
	|	Поручение.Поручения.(
	|		Поручение
	|	),
	|	Поручение.Ссылка,
	|	NULL КАК ДатаОтвета,
	|	Поручение.Автор,
	|	ВЫБОР
	|		КОГДА Поручение.Оценка <= """"
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Отметка
	|ИЗ
	|	Документ.Поручение КАК Поручение
	|ГДЕ
	|	Поручение.Сотрудник = &Сотрудник
	|	И Поручение.СрокВыполнения МЕЖДУ &ДатаНачала И &ДатаОкончания
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	NULL,
	|	СлужебнаяЗаписка.Проблема,
	|	СлужебнаяЗаписка.Ответ,
	|	NULL,
	|	NULL,
	|	NULL,
	|	СлужебнаяЗаписка.Документы.(
	|		Документ
	|	),
	|	СлужебнаяЗаписка.Поручения.(
	|		Поручение
	|	),
	|	СлужебнаяЗаписка.Ссылка,
	|	СлужебнаяЗаписка.ДатаОтвета,
	|	СлужебнаяЗаписка.Автор,
	|	СлужебнаяЗаписка.Завершить
	|ИЗ
	|	Документ.СлужебнаяЗаписка КАК СлужебнаяЗаписка
	|ГДЕ
	|	СлужебнаяЗаписка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И СлужебнаяЗаписка.Адресат = &Сотрудник
	|
	|УПОРЯДОЧИТЬ ПО
	|	СрокВыполнения";
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	Выборка = Запрос.Выполнить().Выбрать();
	МассивСтрок = Новый Массив;
	
	Пока Выборка.Следующий() Цикл
		
		Если БезЗавершенных И Выборка.Отметка Тогда
			Продолжить;
		КонецЕсли;
		
		Если НЕ ДопустимыеТипы.СодержитТип(ТипЗнч(Выборка.Ссылка)) Тогда
			Продолжить;
		КонецЕсли;
		
		МассивСтрок.Очистить();
		ТЗДок = Выборка.Документы.Выгрузить();
		ТабДокументов = "";
		Если ТЗДок.Количество() > 0 Тогда
			Для каждого Элемент Из ТЗДок Цикл
				ТабДокументов = ТабДокументов + Строка(Элемент.Документ) + Символы.ПС;
			КонецЦикла;
		КонецЕсли;
		ТЗПоручений = Выборка.Поручения.Выгрузить();
		ТабПоручений = "";
		Если ТЗПоручений.Количество() > 0 Тогда
			Для каждого Элемент Из ТЗПоручений Цикл
				ТабПоручений = ТабПоручений + Строка(Элемент.Поручение) + Символы.ПС;
			КонецЦикла;
		КонецЕсли;
		
		Если ТипЗнч(Выборка.Ссылка) = Тип("ДокументСсылка.СлужебнаяЗаписка") Тогда
			ОбластьТело = Макет.ПолучитьОбласть("ТелоСЗ");
		Иначе
			ОбластьТело = Макет.ПолучитьОбласть("Тело");
		КонецЕсли;
		
		ОбластьТело.Параметры.Заполнить(Выборка);
		ОбластьТело.Параметры.Документы = ТабДокументов;
		ОбластьТело.Параметры.Поручения = ТабПоручений;
		
		МассивСтрок.Добавить(ОбластьЗаголовок);
		МассивСтрок.Добавить(ОбластьТело);
		Если НЕ ТабДок.ПроверитьВывод(МассивСтрок) Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
			ТабДок.Вывести(ОбластьЗаголовок);
		КонецЕсли;
		ТабДок.Вывести(ОбластьТело);
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции 

