Функция СформироватьОтчет(ПараметрыОтчета) Экспорт 
	
	ДатаНачала = ПараметрыОтчета.Период.ДатаНачала;
	ДатаОкончания = ПараметрыОтчета.Период.ДатаОкончания;
	Адресат = ПараметрыОтчета.Адресат;
	ВидСЗ = ПараметрыОтчета.ВидСЗ;	
	УсловиеНаВид = "";
		
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.Автомасштаб = Истина;
	
	Макет = Отчеты.ПорученияДетально.ПолучитьМакет("Макет");
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьТело = Макет.ПолучитьОбласть("ТелоСЗ");
	
	ОбластьЗаголовок.Параметры.Период = ПараметрыОтчета.Период;
	ОбластьЗаголовок.Параметры.Адресат = Адресат;
	
	ТабДок.Вывести(ОбластьЗаголовок);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	Запрос.УстановитьПараметр("Адресат", Адресат);
	
	Если ВидСЗ = "Текущие" Тогда
		
		УсловиеНаВид = "И НЕ СлужебнаяЗаписка.Проведен";
		
	ИначеЕсли ВидСЗ = "Архив" Тогда
		
		УсловиеНаВид = "И СлужебнаяЗаписка.Проведен"; 
		
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СлужебнаяЗаписка.Проблема,
	|	СлужебнаяЗаписка.Ссылка,
	|	СлужебнаяЗаписка.Автор,
	|	СлужебнаяЗаписка.Документ КАК Основание,
	|	СлужебнаяЗаписка.ДатаКонтроля
	|ИЗ
	|	Документ.СлужебнаяЗаписка КАК СлужебнаяЗаписка
	|ГДЕ
	|	СлужебнаяЗаписка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И СлужебнаяЗаписка.Адресат = &Адресат " + УсловиеНаВид + "
	|
	|УПОРЯДОЧИТЬ ПО
	|	СлужебнаяЗаписка.ДатаКонтроля";
	
	Выборка = Запрос.Выполнить().Выбрать();
	МассивСтрок = Новый Массив;

	Пока Выборка.Следующий() Цикл
				
		МассивСтрок.Очистить();
			
		ОбластьТело.Параметры.Заполнить(Выборка);
		
		МассивСтрок.Добавить(ОбластьЗаголовок);
		МассивСтрок.Добавить(ОбластьТело);
		Если НЕ ТабДок.ПроверитьВывод(МассивСтрок) Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
			ТабДок.Вывести(ОбластьЗаголовок);
		КонецЕсли;
		ТабДок.Вывести(ОбластьТело);
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции 

