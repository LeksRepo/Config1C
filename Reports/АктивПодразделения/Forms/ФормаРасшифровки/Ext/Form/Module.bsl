
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Счет=Параметры.Счет;
	Подразделение=Параметры.Подразделение;
	НачалоПериода=Параметры.НачалоПериода;
	КонецПериода=Параметры.КонецПериода;
	
КонецПроцедуры

&НаСервере
Функция СформироватьТабличныйДокумент()
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.ОриентацияСтраницы=ОриентацияСтраницы.Ландшафт;
	ТабДок.Защита = Истина;
	
	Макет = Отчеты.АктивПодразделения.ПолучитьМакет("МакетАктивРасшифровка");
	
	ОблШапка = Макет.ПолучитьОбласть("Шапка");
	ОблШапка.АвтоМасштаб = Истина;
	ОблСтрока = Макет.ПолучитьОбласть("Строка");
	ОблСтрока2 = Макет.ПолучитьОбласть("Строка");
	ОблПодвал = Макет.ПолучитьОбласть("Подвал");
	
	ВидСубконто = "";
	
	Для каждого Вид Из Счет.ВидыСубконто Цикл
		ВидСубконто=ВидСубконто+Вид.ВидСубконто+Символы.ПС;
	КонецЦикла; 
	
	ОблШапка.Параметры.ПредставлениеПериода = Формат(НачалоПериода, "ДЛФ=DD")+" - "+Формат(КонецПериода, "ДЛФ=DD");
	ОблШапка.Параметры.Подразделение = Подразделение;
	ОблШапка.Параметры.Счет = Счет;
	ОблШапка.Параметры.Субконто = ВидСубконто;
	ТабДок.Вывести(ОблШапка);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Счет", Счет);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УправленческийОстаткиИОбороты.СуммаОборотДт КАК Приход,
	|	УправленческийОстаткиИОбороты.СуммаОборотКт КАК Расход,
	|	ВЫБОР
	|		КОГДА &Счет В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ДенежныеСредства))
	|			ТОГДА УправленческийОстаткиИОбороты.Субконто2
	|		ИНАЧЕ УправленческийОстаткиИОбороты.Субконто1
	|	КОНЕЦ КАК Субконто,
	|	ВЫБОР
	|		КОГДА &Счет В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ДенежныеСредства))
	|			ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК ТипСубконто,
	|	УправленческийОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокДт КАК ОстатокДебетНач,
	|	УправленческийОстаткиИОбороты.СуммаНачальныйРазвернутыйОстатокКт КАК ОстатокКредитНач,
	|	УправленческийОстаткиИОбороты.СуммаКонечныйРазвернутыйОстатокДт КАК ОстатокДебетКон,
	|	УправленческийОстаткиИОбороты.СуммаКонечныйРазвернутыйОстатокКт КАК ОстатокКредитКон
	|ИЗ
	|	РегистрБухгалтерии.Управленческий.ОстаткиИОбороты(&НачалоПериода, &КонецПериода, , , Счет В ИЕРАРХИИ (&Счет), , Подразделение В (&Подразделение)) КАК УправленческийОстаткиИОбороты
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВЫБОР
	|		КОГДА &Счет В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ДенежныеСредства))
	|			ТОГДА УправленческийОстаткиИОбороты.Субконто2.Наименование
	|		ИНАЧЕ УправленческийОстаткиИОбороты.Субконто1.Наименование
	|	КОНЕЦ
	|ИТОГИ
	|	СУММА(Приход),
	|	СУММА(Расход),
	|	МАКСИМУМ(ТипСубконто),
	|	СУММА(ОстатокДебетНач),
	|	СУММА(ОстатокКредитНач),
	|	СУММА(ОстатокДебетКон),
	|	СУММА(ОстатокКредитКон)
	|ПО
	|	ОБЩИЕ,
	|	Субконто";
	
	Результат = Запрос.Выполнить();
	
	Если НЕ Результат.Пустой() Тогда
	
		Выборка = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Выборка.Следующий();
		
		ОблПодвал.Параметры.Заполнить(Выборка);
		
		ТабДок.НачатьАвтоГруппировкуСтрок();
		
		Выборка = Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.ТипЗаписи() = ТипЗаписиЗапроса.ИтогПоГруппировке Тогда
				
			   Если ЗначениеЗаполнено(Выборка.Субконто) Тогда
	    		   СубКон = Выборка.Субконто;
	    	   Иначе
	    		   СубКон = "<...>";
	    	   КонецЕсли;
	    	   
	    	   Если НЕ (Выборка.Приход = 0 И Выборка.Расход = 0) Тогда
	    		   ОблСтрока.Параметры.РасПарам = Выборка.Субконто;
	    	   Иначе
	    		   ОблСтрока.Параметры.РасПарам = Неопределено;
	    	   КонецЕсли; 
	   
	    	   ОблСтрока.Параметры.Субконто = СубКон;
			   
			   ОблСтрока.Параметры.Заполнить(Выборка);
						   
			   ТипСубконто = Выборка.ТипСубконто;	
			   ТабДок.Вывести(ОблСтрока,0);
			  
			КонецЕсли;    

		 КонецЦикла;
		  
		 ТабДок.ЗакончитьАвтогруппировкуСтрок(); 
		 		 
		 ТабДок.Вывести(ОблПодвал);
		 
	КонецЕсли;

	Возврат ТабДок;	
	
КонецФункции

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ТабличныйДокумент = СформироватьТабличныйДокумент();
КонецПроцедуры

&НаКлиенте
Процедура ТабличныйДокументОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
		
	СтандартнаяОбработка = Ложь;
	
	ПараметрыРасшифровки = Новый Структура();
	ПараметрыРасшифровки.Вставить("Субконто",Расшифровка);
	ПараметрыРасшифровки.Вставить("ТипСубконто",ТипСубконто);
	ПараметрыРасшифровки.Вставить("Счет",Счет);
	ПараметрыРасшифровки.Вставить("Подразделение",Подразделение);
	ПараметрыРасшифровки.Вставить("НачалоПериода",НачалоПериода);
	ПараметрыРасшифровки.Вставить("КонецПериода",КонецПериода);
	
	
	ОткрытьФорму("Отчет.АктивПодразделения.Форма.ФормаРасшифровкиДокумент", ПараметрыРасшифровки, ЭтаФорма);
	
КонецПроцедуры


