Перем СуммаНач,СуммаКон,СуммаДт,СуммаКт;

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Счет=Параметры.Счет;
	Подразделение=Параметры.Подразделение;
	НачалоПериода=Параметры.НачалоПериода;
	КонецПериода=Параметры.КонецПериода;
	
КонецПроцедуры

&НаСервере
Функция СформироватьТабличныйДокумент(ВернутьСтруктуру = Ложь)
	
	СуммаНач=0;
	СуммаКон=0;
	СуммаДт=0;
	СуммаКт=0;
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.ОриентацияСтраницы=ОриентацияСтраницы.Ландшафт;
	ТабДок.Защита=Истина;
	
	Макет = Отчеты.АктивПодразделения.ПолучитьМакет("МакетАктивРасшифровка");
	
	ОблШапка = Макет.ПолучитьОбласть("Шапка");
	ОблСтрока = Макет.ПолучитьОбласть("Строка");
	ОблСтрока2 = Макет.ПолучитьОбласть("Строка2");
	ОблПодвал = Макет.ПолучитьОбласть("Подвал");
	ОблПодвал.АвтоМасштаб = Истина;
	
	ВидСубконто="";
	
	Для каждого Вид Из Счет.ВидыСубконто Цикл
		ВидСубконто=ВидСубконто+Вид.ВидСубконто+Символы.ПС;
	КонецЦикла; 
	
	ОблШапка.Параметры.ПредставлениеПериода = Формат(НачалоПериода, "ДЛФ=DD")+" - "+Формат(КонецПериода, "ДЛФ=DD");
	ОблШапка.Параметры.Подразделение = Подразделение;
	ОблШапка.Параметры.Счет = Счет;
	ОблШапка.Параметры.Субконто = ВидСубконто;
	ТабДок.Вывести(ОблШапка);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Счет", Счет);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УправленческийОстаткиИОбороты.СуммаНачальныйОстаток КАК СуммаНачальныйОстаток,
	|	УправленческийОстаткиИОбороты.СуммаКонечныйОстаток КАК СуммаКонечныйОстаток,
	|	УправленческийОстаткиИОбороты.СуммаОборотДт КАК СуммаОборотДт,
	|	УправленческийОстаткиИОбороты.СуммаОборотКт КАК СуммаОборотКт,
	|	УправленческийОстаткиИОбороты.Субконто1 КАК Субконто1,
	|	УправленческийОстаткиИОбороты.Субконто2 КАК Субконто2
	|ИЗ
	|	РегистрБухгалтерии.Управленческий.ОстаткиИОбороты(&НачалоПериода, &КонецПериода, , , Счет В ИЕРАРХИИ (&Счет), , Подразделение В (&Подразделение)) КАК УправленческийОстаткиИОбороты
	|
	|УПОРЯДОЧИТЬ ПО
	|	Субконто1
	|ИТОГИ
	|	СУММА(СуммаНачальныйОстаток),
	|	СУММА(СуммаКонечныйОстаток),
	|	СУММА(СуммаОборотДт),
	|	СУММА(СуммаОборотКт)
	|ПО
	|	Субконто1 ТОЛЬКО ИЕРАРХИЯ";
	Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ТабДок.НачатьАвтоГруппировкуСтрок();
	
	Пока Выборка.Следующий() Цикл

		   СуммаНач = СуммаНач + Выборка.СуммаНачальныйОстаток;
		   СуммаКон = СуммаКон + Выборка.СуммаКонечныйОстаток;
		   СуммаДт = СуммаДт + Выборка.СуммаОборотДт;
		   СуммаКт = СуммаКт + Выборка.СуммаОборотКт;
	
		   ОблСтрока.Параметры.Субконто = Выборка.Субконто1;
		   ОблСтрока.Параметры.НачальныйОстаток = Выборка.СуммаНачальныйОстаток;
		   ОблСтрока.Параметры.Приход = Выборка.СуммаОборотДт;
		   ОблСтрока.Параметры.Расход = Выборка.СуммаОборотКт;
		   ОблСтрока.Параметры.КонечныйОстаток = Выборка.СуммаКонечныйОстаток;

		   ТабДок.Вывести(ОблСтрока,0);
		   
		   ВыборкаСубконто2=Выборка.Выбрать();
		   
		   Пока ВыборкаСубконто2.Следующий() Цикл
			   
		   	   Если ЗначениеЗаполнено(ВыборкаСубконто2.Субконто2) Тогда
			   	   ОблСтрока2.Параметры.Субконто = ВыборкаСубконто2.Субконто2;
			   Иначе
			       ОблСтрока2.Параметры.Субконто = ВыборкаСубконто2.Субконто1;
			   КонецЕсли; 
		   
			   ОблСтрока2.Параметры.НачальныйОстаток = ВыборкаСубконто2.СуммаНачальныйОстаток;
			   ОблСтрока2.Параметры.Приход = ВыборкаСубконто2.СуммаОборотДт;
			   ОблСтрока2.Параметры.Расход = ВыборкаСубконто2.СуммаОборотКт;
			   ОблСтрока2.Параметры.КонечныйОстаток = ВыборкаСубконто2.СуммаКонечныйОстаток;
			   
			   ТабДок.Вывести(ОблСтрока2,1);

		   КонецЦикла;

	 КонецЦикла;
	   
	 ТабДок.ЗакончитьАвтогруппировкуСтрок();  
	 ТабДок.ПоказатьУровеньГруппировокСтрок(0);
	 
	 ОблПодвал.Параметры.НачальныйОстаток = Формат(СуммаНач, "ЧДЦ=2");
	 ОблПодвал.Параметры.Приход = Формат(СуммаДт, "ЧДЦ=2");
	 ОблПодвал.Параметры.Расход = Формат(СуммаКт, "ЧДЦ=2");
	 ОблПодвал.Параметры.КонечныйОстаток = Формат(СуммаКон, "ЧДЦ=2");
	 ТабДок.Вывести(ОблПодвал);
	 	
	Возврат ТабДок;	
	
КонецФункции

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ТабличныйДокумент = СформироватьТабличныйДокумент();
КонецПроцедуры

&НаКлиенте
Процедура Свернуть(Команда)
	ТабличныйДокумент.ПоказатьУровеньГруппировокСтрок(0);
КонецПроцедуры

&НаКлиенте
Процедура Развернуть(Команда)
	ТабличныйДокумент.ПоказатьУровеньГруппировокСтрок(10);
КонецПроцедуры


