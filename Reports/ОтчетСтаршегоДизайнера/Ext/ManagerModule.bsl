
Функция СформироватьОтчет(ПараметрыОтчета) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.АвтоМасштаб = Истина;
		
	Макет = Отчеты.ОтчетСтаршегоДизайнера.ПолучитьМакет("Макет");
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьШапкаЗамеры = Макет.ПолучитьОбласть("ШапкаЗамеры");
	ОбластьСтрокаЗамеры = Макет.ПолучитьОбласть("СтрокаЗамеры");
	ОбластьШапкаМонтаж = Макет.ПолучитьОбласть("ШапкаМонтаж");
	ОбластьСтрокаМонтаж = Макет.ПолучитьОбласть("СтрокаМонтаж");
	ОбластьЗамерщик = Макет.ПолучитьОбласть("Замерщик");

	ОбластьПустаяСтрока = Макет.ПолучитьОбласть("ПустаяСтрока");
	
	ОбластьШапка.Параметры.Заполнить(ПараметрыОтчета);
	ТабДок.Вывести(ОбластьШапка);

	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Подразделение", ПараметрыОтчета.Подразделение);
	Запрос.УстановитьПараметр("СтаршийДизайнер", ПараметрыОтчета.СтаршийДизайнер);
	
	# Область Запрос
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	докЗамер.АдресЗамера КАК Адрес,
	|	докЗамер.Замерщик.Фамилия + "" "" + ПОДСТРОКА(докЗамер.Замерщик.Имя, 1, 1) + "". "" + ПОДСТРОКА(докЗамер.Замерщик.Отчество, 1, 1) + ""."" КАК Замерщик,
	|	докЗамер.Офис КАК Офис,
	|	докЗамер.Автор.ФизическоеЛицо.Фамилия + "" "" + ПОДСТРОКА(докЗамер.Автор.ФизическоеЛицо.Имя, 1, 1) + "". "" + ПОДСТРОКА(докЗамер.Автор.ФизическоеЛицо.Отчество, 1, 1) + ""."" КАК Автор,
	|	докЗамер.Ссылка КАК Ссылка,
	|	докЗамер.Дата КАК ДатаЗамера,
	|	Встречи.Дата КАК ДатаВстречи,
	|	Встречи.Ответственный.Фамилия + "" "" + ПОДСТРОКА(Встречи.Ответственный.Имя, 1, 1) + "". "" + ПОДСТРОКА(Встречи.Ответственный.Отчество, 1, 1) + ""."" КАК Дизайнер,
	|	NULL КАК Статус,
	|	NULL КАК ДатаМонтажа,
	|	""Замер"" КАК Вид,
	|	NULL КАК Изделие
	|ИЗ
	|	Документ.Замер КАК докЗамер
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АктивностьЗамеров КАК АктивностьЗамеров
	|		ПО (АктивностьЗамеров.Замер = докЗамер.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			Замер.Ссылка КАК Ссылка,
	|			МАКСИМУМ(ВстречаСКлиентом.Ответственный) КАК Ответственный,
	|			МАКСИМУМ(ВстречаСКлиентом.Дата) КАК Дата
	|		ИЗ
	|			Документ.Замер КАК Замер
	|				ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВстречаСКлиентом КАК ВстречаСКлиентом
	|				ПО (ВстречаСКлиентом.Основание = Замер.Ссылка)
	|		
	|		СГРУППИРОВАТЬ ПО
	|			Замер.Ссылка) КАК Встречи
	|		ПО докЗамер.Ссылка = Встречи.Ссылка
	|ГДЕ
	|	АктивностьЗамеров.Статус
	|	И докЗамер.Подразделение = &Подразделение
	|	И ВЫБОР
	|			КОГДА &СтаршийДизайнер <> ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|				ТОГДА докЗамер.Замерщик = &СтаршийДизайнер
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	докСпецификация.АдресМонтажа,
	|	ВЫРАЗИТЬ(докСпецификация.ДокументОснование КАК Документ.Замер).Замерщик.Фамилия + "" "" + ПОДСТРОКА(ВЫРАЗИТЬ(докСпецификация.ДокументОснование КАК Документ.Замер).Замерщик.Имя, 1, 1) + "". "" + ПОДСТРОКА(ВЫРАЗИТЬ(докСпецификация.ДокументОснование КАК Документ.Замер).Замерщик.Отчество, 1, 1) + ""."",
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	СтатусСпецификацииСрезПоследних.Статус,
	|	докСпецификация.ДатаМонтажа,
	|	""Монтаж"",
	|	докСпецификация.Изделие
	|ИЗ
	|	Документ.Спецификация КАК докСпецификация
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусСпецификации.СрезПоследних КАК СтатусСпецификацииСрезПоследних
	|		ПО (СтатусСпецификацииСрезПоследних.Спецификация = докСпецификация.Ссылка)
	|ГДЕ
	|	докСпецификация.Подразделение = &Подразделение
	|	И ВЫБОР
	|			КОГДА &СтаршийДизайнер <> ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|				ТОГДА ВЫРАЗИТЬ(докСпецификация.ДокументОснование КАК Документ.Замер).Замерщик = &СтаршийДизайнер
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И докСпецификация.Проведен
	|	И СтатусСпецификацииСрезПоследних.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыСпецификации.Установлен)
	|	И докСпецификация.ПакетУслуг = ЗНАЧЕНИЕ(Перечисление.ПакетыУслуг.ДоставкаДоКлиентаИМонтаж)
	|	И докСпецификация.ДатаМонтажа <> ДАТАВРЕМЯ(1, 1, 1)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Вид,
	|	ДатаЗамера,
	|	ДатаМонтажа
	|ИТОГИ ПО
	|	Замерщик,
	|	Вид";
	
	#КонецОбласти
	
	Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам); 	
	Пока Выборка.Следующий() Цикл	
		
		ТабДок.Вывести(ОбластьПустаяСтрока);		
		ОбластьЗамерщик.Параметры.Заполнить(Выборка);
		ТабДок.Вывести(ОбластьЗамерщик);
				
		ВыборкаПоСотрудникам = Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаПоСотрудникам.Следующий() Цикл
						
			
			ЭтоЗамеры = ВыборкаПоСотрудникам.Вид = "Замер";
			
			ТабДок.Вывести(ОбластьПустаяСтрока);
			
			ТабДок.Вывести(?(ЭтоЗамеры, ОбластьШапкаЗамеры, ОбластьШапкаМонтаж));
			
			ВыборкаПоДокумента = ВыборкаПоСотрудникам.Выбрать();		
			
			Пока ВыборкаПоДокумента.Следующий() Цикл
				
				Если ЭтоЗамеры Тогда 
					
					ОбластьСтрокаЗамеры.Параметры.Заполнить(ВыборкаПоДокумента);
					
					Если НачалоДня(ВыборкаПоДокумента.ДатаЗамера) = НачалоДня(ТекущаяДата()) Тогда
						ОбластьСтрокаЗамеры.Области.СтрокаЗамеры.Шрифт = Новый Шрифт(,,Истина);
					Иначе
						ОбластьСтрокаЗамеры.Области.СтрокаЗамеры.Шрифт = Новый Шрифт(,,Ложь);
					КонецЕсли;
					
					ТабДок.Вывести(ОбластьСтрокаЗамеры);	
					
				Иначе
					
					ОбластьСтрокаМонтаж.Параметры.Заполнить(ВыборкаПоДокумента);
					
					Если НачалоДня(ВыборкаПоДокумента.ДатаМонтажа) = НачалоДня(ТекущаяДата()) Тогда
						ОбластьСтрокаМонтаж.Области.СтрокаМонтаж.Шрифт = Новый Шрифт(,,Истина);
					Иначе
						ОбластьСтрокаМонтаж.Области.СтрокаМонтаж.Шрифт = Новый Шрифт(,,Ложь);
					КонецЕсли;
					
					ТабДок.Вывести(ОбластьСтрокаМонтаж);
					
				КонецЕсли;
				
			КонецЦикла;
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции
