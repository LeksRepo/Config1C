
Функция СформироватьОтчет(ПараметрыОтчета) Экспорт
	
	Макет = Отчеты.ГрафикЗамеров.ПолучитьМакет("ГрафикЗамера");
	ДатаНачала = ПараметрыОтчета.Период.ДатаНачала;
	ДатаОкончания = ПараметрыОтчета.Период.ДатаОкончания;
	Замерщик = ПараметрыОтчета.Замерщик;
	Подразделение = ПараметрыОтчета.Подразделение;
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("НачалоПериода", ДатаНачала);
	Запрос.Параметры.Вставить("КонецПериода", ДатаОкончания);
	Запрос.Параметры.Вставить("Замерщик", Замерщик);
	Запрос.Параметры.Вставить("Подразделение", Подразделение);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДокументЗамер.АдресЗамера КАК Адрес,
	|	ДокументЗамер.ИмяЗаказчика КАК Имя,
	|	ДокументЗамер.Телефон,
	|	ДокументЗамер.Дата КАК Дата,
	|	ЧАС(ДокументЗамер.Дата) КАК ВремяЗамера,
	|	ЧАС(ДокументЗамер.Дата) + 1 КАК ВремяЗамераПлюсОдин,
	|	ДокументЗамер.Офис КАК Офис,
	|	ДокументЗамер.Замерщик КАК Замерщик,
	|	ДокументЗамер.Комментарий КАК Комментарий,
	|	ДокументЗамер.Автор КАК Дизайнер
	|ИЗ
	|	Документ.Замер КАК ДокументЗамер
	|ГДЕ
	|	ДокументЗамер.Проведен
	|	И ДокументЗамер.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ДокументЗамер.Замерщик = &Замерщик
	|	И ВЫБОР
	|			КОГДА &Подразделение = ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ДокументЗамер.Подразделение = &Подразделение
	|		КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата";
	
	Если НЕ ЗначениеЗаполнено(Замерщик) Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И ДокументЗамер.Замерщик = &Замерщик", "");
		
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Шапка = Макет.ПолучитьОбласть("Шапка");
	Шапка.Параметры.ПериодСтр = ПредставлениеПериода(ДатаНачала, ДатаОкончания);
	Строка = Макет.ПолучитьОбласть("Строка");
	ПустаяСтрока = Макет.ПолучитьОбласть("ПустаяСтрока");
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.АвтоМасштаб = Истина;
	ТабДок.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Ложь;
	ТабДок.ТолькоПросмотр = Ложь;
	
	ТабДок.Вывести(Шапка);
	
	НомерСтроки = 5;
	НачалоТекДата = 5;
	ТекДата = '0001.01.01';
	
	Пока Выборка.Следующий() Цикл
		
		Если НЕ ЗначениеЗаполнено(ТекДата) Тогда
			
			ТекДата = НачалоДня(Выборка.Дата) ;
			
		КонецЕсли;
		
		НомерСтроки = 1 + НомерСтроки;
		
		Если НачалоДня(ТекДата) <> НачалоДня(Выборка.Дата) Тогда
			
			ТабДок.Вывести(ПустаяСтрока);
			ТабДок.Вывести(ПустаяСтрока);
			
			НомерСтроки = 2 + НомерСтроки;
			Область = ТабДок.Область(НачалоТекДата, 1, НомерСтроки - 2, 1);
			
			Область.Объединить();
			
			ТекДата = НачалоДня(Выборка.Дата);
			НачалоТекДата = НомерСтроки - 1;
			
		КонецЕсли;
		
		Строка.Параметры.Заполнить(Выборка);
		Адрес = ЛексСервер.ПолучитьСтруктуруИзАдреса(Выборка.Адрес);
		Строка.Параметры.Подъезд = Адрес.Подъезд;
		Строка.Параметры.Этаж = Адрес.Этаж;
		Строка.Параметры.Код = Адрес.КодПодъезда;
		Строка.Параметры.Дата = Формат((Выборка.Дата), "ДФ = дд.ММ")+" "+Формат((Выборка.Дата), "ДФ = дддд");
		
		ТабДок.Вывести(Строка);
		
	КонецЦикла;
	
	ТабДок.Вывести(ПустаяСтрока);
	ТабДок.Вывести(ПустаяСтрока);
	НомерСтроки = 2 + НомерСтроки;
	Область = ТабДок.Область(НачалоТекДата, 1, НомерСтроки - 1, 1);
	
	Область.Объединить();
	
	Возврат ТабДок;
	
КонецФункции

