////////////////////////////////////////////////////////////////////////////////
// Подсистема "Работа с файлами".
//
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Вызывается при ошибке захвате файла
// Параметры
//  ДанныеФайла - Структура - структура с данными файла
//  СтандартнаяОбработка - Булево - уникальный идентификатор формы
//
Процедура ПриОшибкеЗахватаФайла(ДанныеФайла, СтандартнаяОбработка) Экспорт

КонецПроцедуры

//Используется при обновлении файлов в каталоге пользователя.
//Параметры:
// 	РабочийКаталог - Рабочий каталог пользователя, где хранить файлы базы.
// 	ИмяДБФ - Имя DBF файла в рабочем каталоге в который попадает информация о имени файла и его контрольной суммы.
// 	ТаблицаЗначений - Таблица в которой хранятся данные об имени, двоичных данных, размере и др. файлов, которые необходимо синхронизировать.
//
// 	Процедура сохраняет файлы к каталоге. Для шаблонов HTML файлов создаются новые файлы с префиксом "Рабочий_"
//   
Процедура СинхронизацияФайлов(РабочийКаталог, ИмяДБФ, ТаблицаЗначений) Экспорт
	
	ФайлДБФ = Новый XBase(РабочийКаталог + ИмяДБФ);
	Количество = ТаблицаЗначений.Количество();
	Индикатор = 1;
	
	Для каждого Элемент Из ТаблицаЗначений Цикл
		Если Элемент.Обновить Тогда
			Путь = РабочийКаталог + Элемент.Имя;
			
			ФайлДБФ.Первая();
			Пока Не ФайлДБФ.ВКонце() Цикл
				Если СокрЛП(ФайлДБФ.NAME) = Элемент.Имя Тогда
					ФайлДБФ.Удалить();
					Прервать;
				КонецЕсли;
				ФайлДБФ.Следующая();
			КонецЦикла;
			
			Если Элемент.Размер > 0 Тогда
				ФайлВКаталоге = Новый Файл(Путь);
				Если ФайлВКаталоге.Существует() Тогда
					ФайлВКаталоге.УстановитьТолькоЧтение(Ложь);
				КонецЕсли;
				Элемент.Данные.Записать(Путь);
				Если ЗначениеЗаполнено(Найти(Элемент.Имя, "HTML")) Тогда
					
					ТекстHTML = Новый ТекстовыйДокумент;
					ТекстHTML.Прочитать(Путь);
					Текст = ТекстHTML.ПолучитьТекст();
					
					ИмяКаталога = СтрЗаменить(РабочийКаталог, "\", "\\");

					Текст = СтрЗаменить(Текст, "%ЛОКАЛЬНЫЙ_ПУТЬ_К_ФАЙЛУ%", ИмяКаталога);
					ТекстHTML.УстановитьТекст(Текст);
					ПутьНовыйHTML = РабочийКаталог + "Рабочий_" + Элемент.Имя;
					ТекстHTML.Записать(ПутьНовыйHTML);
					
				КонецЕсли;
				ФайлДБФ.Добавить();
				ФайлДБФ.NAME = Элемент.Имя;
				ФайлДБФ.SUM = Элемент.ХэшБазы;
			КонецЕсли;
			
			ОбработкаПрерыванияПользователя();
			
			Индикатор = Индикатор + 1;
			ИндикаторПроцент = Индикатор/Количество * 100;
			
			НадписьПодробнее = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Сохраняется файл ""%1"" (%2 КБ) ...'"), Элемент.Имя, Элемент.Размер);
			ТекстСостояния = НСтр("ru = 'Синхронизация нескольких файлов.'");
			
			Состояние(ТекстСостояния, ИндикаторПроцент, НадписьПодробнее, БиблиотекаКартинок.Синхронизация);
			ФайлДБФ.Записать();
		КонецЕсли;
	КонецЦикла;
	ФайлДБФ.ЗакрытьФайл();
		
КонецПроцедуры