////////////////////////////////////////////////////////////////////////////////
// Подсистема "Обновление версии ИБ"
// Клиентские процедуры и функции для интерактивного обновления информационной базы.
//
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Проверить, есть ли необходимость выполнять обновление информационной базы, и
// если необходимо - выполняется обновление.
// Если обновление не удалось выполнить, то процедура предлагает завершить работу системы.
//
Процедура ВыполнитьОбновлениеИнформационнойБазы() Экспорт
	
	ОбновитьИнформационнуюБазу();
	
КонецПроцедуры

// Если есть непоказанные описания изменения и у пользователя не отключен
// показ - открыть форму ОписаниеИзмененийСистемы.
//
Процедура ПоказатьОписаниеИзмененийСистемы() 
	
	ПараметрыРаботыКлиента = СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиентаПриЗапуске();
	Если ПараметрыРаботыКлиента.ПоказатьОписаниеИзмененийСистемы Или ВывестиОписаниеИзмененийДляАдминистратора = Истина Тогда
		ОткрытьФорму("ОбщаяФорма.ОписаниеИзмененийСистемы");
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЙ ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Только для внутреннего использования.
Процедура ПриНачалеРаботыСистемы() Экспорт
	
	ПараметрыРаботыКлиента = СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиентаПриЗапуске();
	Если НЕ ПараметрыРаботыКлиента.ДоступноИспользованиеРазделенныхДанных Тогда
		Возврат;
	КонецЕсли;
	
	ПоказатьОписаниеИзмененийСистемы();
	
КонецПроцедуры

// Только для внутреннего использования.
Процедура ОбновитьИнформационнуюБазу(ПриЗапускеКлиентскогоПриложения = Ложь) Экспорт
	
	ПараметрыРаботыКлиента = СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиентаПриЗапуске();
	Если НЕ ПараметрыРаботыКлиента.ДоступноИспользованиеРазделенныхДанных Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ПараметрыРаботыКлиента.НеобходимоОбновлениеИнформационнойБазы Тогда
		Возврат;
	КонецЕсли;
	
	Состояние(НСтр("ru = 'Пожалуйста, подождите, выполняется обновление информационной базы...'"));
	ДокументОписаниеОбновлений = Неопределено;
	
	Попытка
		ВыполненныеОбработчикиОбновления = ОбновлениеИнформационнойБазыВызовСервера.ВыполнитьОбновлениеИнформационнойБазы(, ПриЗапускеКлиентскогоПриложения);
	Исключение
		ТекстСообщенияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр(
			"ru = 'При обновлении информационной базы возникла ошибка:
			|
			|%1
			|
			|Подробности см. в Журнале регистрации.'"), 
			КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ВызватьИсключение ТекстСообщенияОбОшибке;
	КонецПопытки;
	
	Состояние(НСтр("ru = 'Обновление информационной базы выполнено успешно.'"));
	
	Если ВыполненныеОбработчикиОбновления <> Неопределено Тогда
		ОбновитьИнтерфейс();
	КонецЕсли;
	
	ВывестиОписаниеИзмененийДляАдминистратора = ВыполненныеОбработчикиОбновления <> "";
	
КонецПроцедуры
