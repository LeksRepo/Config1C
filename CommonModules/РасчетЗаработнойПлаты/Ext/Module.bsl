Процедура РасчетЗаработнойПлаты (Регистратор) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ОсновныеНачисленияДанныеГрафика.ЗначениеПериодДействия КАК ДнейНорма,
	               |	ОсновныеНачисленияДанныеГрафика.ЗначениеФактическийПериодДействия КАК ДнейФакт,
	               |	ОсновныеНачисленияДанныеГрафика.Сторно,
	               |	ОсновныеНачисленияДанныеГрафика.ВидРасчета,
	               |	ОсновныеНачисленияДанныеГрафика.НомерСтроки,
	               |	ОсновныеНачисленияДанныеГрафика.Размер,
	               |	ОсновныеНачисленияДанныеГрафика.ВидРасчета.СпособРасчета КАК СпособРасчета
	               |ИЗ
	               |	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(
	               |			Регистратор = &Регистратор
	               |				И ПериодРегистрации = &ПериодРегистрации) КАК ОсновныеНачисленияДанныеГрафика";
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	Запрос.УстановитьПараметр("ПериодРегистрации", НачалоМесяца(Регистратор.ПериодРегистрации));
	
	
	НаборЗаписейРегистраРасчета = РегистрыРасчета.ОсновныеНачисления.СоздатьНаборЗаписей();
	НаборЗаписейРегистраРасчета.Отбор.Регистратор.Установить(Регистратор);
	НаборЗаписейРегистраРасчета.Прочитать(); 		
	
	НаборЗаписейРегистраНакопления = РегистрыНакопления.ЗаработнаяПлата.СоздатьНаборЗаписей();
	НаборЗаписейРегистраНакопления.Отбор.Регистратор.Установить(Регистратор);
	НаборЗаписейРегистраНакопления.Прочитать();
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаписьРегистраРасчета = НаборЗаписейРегистраРасчета[Выборка.НомерСтроки - 1];
		ЗаписьРегистраНакопления = НаборЗаписейРегистраНакопления[Выборка.НомерСтроки - 1];
		ВидРасчета = Выборка.ВидРасчета;
		ДнейНорма = Выборка.ДнейНорма;
		ДнейФакт = Выборка.ДнейФакт;
		Параметр = Выборка.Размер;
		Сторно = Выборка.Сторно;
		СпособРасчета = Выборка.СпособРасчета;
		
		Если СпособРасчета = Перечисления.СпособРасчета.ОкладПоДням Тогда
			ЗаписьРегистраРасчета.Результат = ДнейФакт / ДнейНорма * Параметр;
			ЗаписьРегистраНакопления.Сумма = ЗаписьРегистраРасчета.Результат;
			ЗаписьРегистраНакопления.Комментарий = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("(Отработанно дней/Дней по графику)*Оклад= %1/%2*%3", ДнейФакт, ДнейНорма, Параметр);
		ИначеЕсли СпособРасчета = Перечисления.СпособРасчета.ФиксированнаяСумма Тогда			
			ЗаписьРегистраРасчета.Результат = Параметр;
			ЗаписьРегистраНакопления.Сумма = ЗаписьРегистраРасчета.Результат;
		КонецЕсли; 
		
		Если Сторно Тогда
			ЗаписьРегистраРасчета.Результат = -ЗаписьРегистраРасчета.Результат;
			//ЗаписьРегистраРасчета.ОтработаноДней = -ЗаписьРегистраРасчета.ОтработаноДней;
		КонецЕсли; 
	КонецЦикла; 
	
	НаборЗаписейРегистраРасчета.Записать(, Истина);
	НаборЗаписейРегистраНакопления.Записать();
	
КонецПроцедуры
