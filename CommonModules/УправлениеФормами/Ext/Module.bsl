 
Функция ЭтоНовый(НашаФорма) Экспорт
	
	Возврат НашаФорма.Объект.Ссылка.Пустая();
	
КонецФункции

Процедура УправлениеВидимостью(НашаФорма, ИменаРеквизитов, Видимость) Экспорт
	
	ИменаРеквизитов = СтрЗаменить(ИменаРеквизитов,",",Символы.ПС);
	
	Для сч=1 по СтрЧислоСтрок(ИменаРеквизитов) Цикл
		ИмяРеквизита = СокрЛП(СтрПолучитьСтроку(ИменаРеквизитов,сч));
		Если НашаФорма.Элементы[ИмяРеквизита].Видимость <> Видимость Тогда
			НашаФорма.Элементы[ИмяРеквизита].Видимость = Видимость;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура УправлениеАвтоОтметкаНезаполненного(НашаФорма, ИменаРеквизитов, АвтоОтметкаНезаполненного) Экспорт
	
	ИменаРеквизитов = СтрЗаменить(ИменаРеквизитов,",",Символы.ПС);
	
	Для сч=1 по СтрЧислоСтрок(ИменаРеквизитов) Цикл
		ИмяРеквизита = СокрЛП(СтрПолучитьСтроку(ИменаРеквизитов,сч));
		Если НашаФорма.Элементы[ИмяРеквизита].АвтоОтметкаНезаполненного <> АвтоОтметкаНезаполненного Тогда
			НашаФорма.Элементы[ИмяРеквизита].АвтоОтметкаНезаполненного = АвтоОтметкаНезаполненного;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// <ПриИзмененииРеквизитовВТЧКлиент>
// ПРОЦЕДУРА "ПриИзменении" НА КЛИЕНТЕ.
// ВЫЗОВ ЭТОЙ ПРОЦЕДУРЫ ДОЛЖЕН ОСУЩЕСТВЛЯТЬСЯ ИЗ ФОРМ ДОКУМЕНТОВ.
// СтруктураДействий - Структура. Возможно передавать следующие поля:
//  "ЗаполнитьЕдиницуХранения".
//  "ЗаполнитьКоэффициент".
//  "ЗаполнитьКоличество".
//  "ПересчитатьСумму".
Процедура ПриИзмененииРеквизитовВТЧКлиент(ТекущаяСтрока, СтруктураДействий) Экспорт
	
	Перем ЗначениеИзСтруктуры;
	
	СтруктураТЧ = Новый Структура;
	СтруктураТЧ.Вставить("СтруктураПолейТЧ", ПолучитьСтруктуруПолейТЧ(СтруктураДействий));
	СтруктураТЧ.Вставить("ТекущаяСтрока" , ПолучитьДанныеТекущейСтроки(ТекущаяСтрока, СтруктураТЧ.СтруктураПолейТЧ));
	СтруктураТЧ.Вставить("НеобходимВызовСервера",ПолучитьПараметрыОбработкиТЧ(СтруктураДействий));
	Если СтруктураТЧ.НеобходимВызовСервера Тогда
		УправлениеФормамиСервер.ПриИзмененииРеквизитовВТЧСервер(СтруктураТЧ, СтруктураДействий);
	Иначе
		УправлениеФормами.ОбработатьСтрокуТЧСервер(СтруктураТЧ.ТекущаяСтрока, СтруктураДействий);
	КонецЕсли;
	ЗаполнитьЗначенияСвойств(ТекущаяСтрока, СтруктураТЧ.ТекущаяСтрока);
	
КонецПроцедуры

Функция ПолучитьСтруктуруПолейТЧ(СтруктураДействий)

	Перем СтруктураПараметровДействия;

	СтруктураПолейТЧ = Новый Структура;

	Если СтруктураДействий.Свойство("ЗаполнитьЕдиницуХранения", СтруктураПараметровДействия) Тогда
		СтруктураПолейТЧ.Вставить("Номенклатура");
		СтруктураПолейТЧ.Вставить("ЕдиницаХранения");
	КонецЕсли;

	Если СтруктураДействий.Свойство("ЗаполнитьКоэффициент", СтруктураПараметровДействия) Тогда
		СтруктураПолейТЧ.Вставить("ЕдиницаХранения");
		СтруктураПолейТЧ.Вставить("Коэффициент");
	КонецЕсли;

	Если СтруктураДействий.Свойство("ЗаполнитьКоличество", СтруктураПараметровДействия) Тогда
		СтруктураПолейТЧ.Вставить("Количество");
	КонецЕсли;

	Если СтруктураДействий.Свойство("ПересчитатьСумму", СтруктураПараметровДействия) Тогда
		СтруктураПолейТЧ.Вставить("Сумма");
		СтруктураПолейТЧ.Вставить("Цена");
		СтруктураПолейТЧ.Вставить("Количество");
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ПересчитатьЦену", СтруктураПараметровДействия) Тогда
		СтруктураПолейТЧ.Вставить("Сумма");
		СтруктураПолейТЧ.Вставить("Цена");
		СтруктураПолейТЧ.Вставить("Количество");
	КонецЕсли;

Возврат СтруктураПолейТЧ;

КонецФункции

Функция ПолучитьДанныеТекущейСтроки(ТекущаяСтрока, СтруктураПолейТЧ)

	ДанныеТекущейСтроки = Новый Структура;
	ДобавитьВСтруктуру(ДанныеТекущейСтроки, СтруктураПолейТЧ);
	ЗаполнитьЗначенияСвойств(ДанныеТекущейСтроки, ТекущаяСтрока);

	Возврат ДанныеТекущейСтроки;

КонецФункции

Процедура ДобавитьВСтруктуру(Приемник, Источник)

	Для Каждого КлючИЗначение Из Источник Цикл
		Приемник.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла;

КонецПроцедуры

Функция ПолучитьПараметрыОбработкиТЧ(СтруктураДействий)
	
	Перем СтруктураПараметровДействия;

	НеобходимВызовСервера     = Ложь;
	Если СтруктураДействий.Свойство("ЗаполнитьЕдиницуХранения", СтруктураПараметровДействия) Тогда
		НеобходимВызовСервера     = Истина;
	Конецесли;
	
	Если СтруктураДействий.Свойство("ЗаполнитьКоэффициент", СтруктураПараметровДействия) Тогда
		НеобходимВызовСервера     = Истина;
	Конецесли;
	
	Возврат НеобходимВызовСервера;
	
КонецФункции
		
Процедура ОбработатьСтрокуТЧСервер(ТекущаяСтрока, СтруктураДействий) Экспорт

 	ЗаполнитьКоличествоКлиент(ТекущаяСтрока, СтруктураДействий);
  	ПересчитатьСуммуКлиент(ТекущаяСтрока, СтруктураДействий);
  	ПересчитатьЦенуКлиент(ТекущаяСтрока, СтруктураДействий);

КонецПроцедуры

Процедура ЗаполнитьКоличествоКлиент(ТекущаяСтрока, СтруктураДействий)

	Перем ПараметрДействия;

	Если СтруктураДействий.Свойство("ЗаполнитьКоличество", ПараметрДействия) Тогда
    	ТекущаяСтрока.Количество = 1;
	КонецЕсли;

КонецПроцедуры

Процедура ПересчитатьСуммуКлиент(ТекущаяСтрока, СтруктураДействий)

	Перем ПараметрДействия;

	Если СтруктураДействий.Свойство("ПересчитатьСумму", ПараметрДействия) Тогда
    	ТекущаяСтрока.Сумма = ТекущаяСтрока.Цена*ТекущаяСтрока.Количество;
	КонецЕсли;

КонецПроцедуры

Процедура ПересчитатьЦенуКлиент(ТекущаяСтрока, СтруктураДействий)

	Перем ПараметрДействия;

	Если СтруктураДействий.Свойство("ПересчитатьЦену", ПараметрДействия) Тогда
		Если ТекущаяСтрока.Количество<>0 Тогда
    		ТекущаяСтрока.Цена = ТекущаяСтрока.Сумма/ТекущаяСтрока.Количество;
		Иначе
    		ТекущаяСтрока.Цена = 0;
		КонецЕсли; 
	КонецЕсли;

КонецПроцедуры