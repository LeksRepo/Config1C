
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	УстановитьУсловноеОформление();
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	КлючВарианта = Параметры.КлючТекущихНастроек;
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	ОтчетИнформация = ВариантыОтчетов.СформироватьИнформациюОбОтчетеПоПолномуИмени(Параметры.КлючОбъекта);
	Если ТипЗнч(ОтчетИнформация.ТекстОшибки) = Тип("Строка") Тогда
		ВызватьИсключение ОтчетИнформация.ТекстОшибки;
	КонецЕсли;
	ОтчетИнформация.Удалить("ОтчетМетаданные");
	ОтчетИнформация.Удалить("ТекстОшибки");
	ОтчетИнформация.Вставить("ОтчетПолноеИмя", Параметры.КлючОбъекта);
	ОтчетИнформация = Новый ФиксированнаяСтруктура(ОтчетИнформация);
	
	ПолныеПраваНаВарианты = ВариантыОтчетов.ПолныеПраваНаВарианты();
	
	Если НЕ ПолныеПраваНаВарианты Тогда
		Элементы.ПоказыватьЛичныеВариантыОтчетовДругихАвторов.Видимость = Ложь;
		Элементы.ПоказыватьЛичныеВариантыОтчетовДругихАвторовКМ.Видимость = Ложь;
		ПоказыватьЛичныеВариантыОтчетовДругихАвторов = Ложь;
	КонецЕсли;
	
	ЗаполнитьСписокВариантов();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗагрузкойДанныхИзНастроекНаСервере(Настройки)
	Показывать = Настройки.Получить("ПоказыватьЛичныеВариантыОтчетовДругихАвторов");
	Если Показывать <> ПоказыватьЛичныеВариантыОтчетовДругихАвторов Тогда
		ПоказыватьЛичныеВариантыОтчетовДругихАвторов = Показывать;
		Элементы.ПоказыватьЛичныеВариантыОтчетовДругихАвторов.Пометка = Показывать;
		Элементы.ПоказыватьЛичныеВариантыОтчетовДругихАвторовКМ.Пометка = Показывать;
		ЗаполнитьСписокВариантов();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = ВариантыОтчетовКлиентСервер.ИмяСобытияИзменениеВарианта() Тогда
		ЗаполнитьСписокВариантов();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборАвторПриИзменении(Элемент)
	ОтборВключен = ЗначениеЗаполнено(ОтборАвтор);
	
	Группы = ДеревоВариантовОтчета.ПолучитьЭлементы();
	Для Каждого Группа Из Группы Цикл
		ВложенныеВарианты = Группа.ПолучитьЭлементы();
		Для Каждого Вариант Из ВложенныеВарианты Цикл
			Вариант.СкрытОтбором = ОтборВключен И Вариант.Автор <> ОтборАвтор;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревовариантовотчета

&НаКлиенте
Процедура ДеревоВариантовОтчетаПриАктивизацииСтроки(Элемент)
	Вариант = Элементы.ДеревоВариантовОтчета.ТекущиеДанные;
	Если Вариант = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Вариант.КлючВарианта) Тогда
		ВариантОписание = "";
	Иначе
		ВариантОписание = Вариант.Описание;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДеревоВариантовОтчетаПередНачаломИзменения(Элемент, Отказ)
	Отказ = Истина;
	ОткрытьВариантДляИзменения();
КонецПроцедуры

&НаКлиенте
Процедура ДеревоВариантовОтчетаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДеревоВариантовОтчетаПередУдалением(Элемент, Отказ)
	Отказ = Истина;
	
	Вариант = Элементы.ДеревоВариантовОтчета.ТекущиеДанные;
	Если Вариант = Неопределено ИЛИ НЕ ЗначениеЗаполнено(Вариант.КлючВарианта) Тогда
		Возврат;
	КонецЕсли;
	
	Если Вариант.ИндексКартинки = 4 Тогда
		ТекстВопроса = НСтр("ru = 'Снять с ""%1"" пометку на удаление?'");
	Иначе
		ТекстВопроса = НСтр("ru = 'Пометить ""%1"" на удаление?'");
	КонецЕсли;
	ТекстВопроса = СтрЗаменить(ТекстВопроса, "%1", Вариант.Наименование);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Вариант", Вариант);
	Обработчик = Новый ОписаниеОповещения("ДеревоВариантовОтчетаПередУдалениемЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60, КодВозвратаДиалога.Да);
КонецПроцедуры

&НаКлиенте
Процедура ДеревоВариантовОтчетаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ВыбратьИЗакрыть();
КонецПроцедуры

&НаКлиенте
Процедура ДеревоВариантовОтчетаВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ВыбратьИЗакрыть();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоказыватьЛичныеВариантыОтчетовДругихАвторов(Команда)
	ПоказыватьЛичныеВариантыОтчетовДругихАвторов = НЕ ПоказыватьЛичныеВариантыОтчетовДругихАвторов;
	Элементы.ПоказыватьЛичныеВариантыОтчетовДругихАвторов.Пометка = ПоказыватьЛичныеВариантыОтчетовДругихАвторов;
	Элементы.ПоказыватьЛичныеВариантыОтчетовДругихАвторовКМ.Пометка = ПоказыватьЛичныеВариантыОтчетовДругихАвторов;
	
	ЗаполнитьСписокВариантов();
	
	Для Каждого ГруппаДерева Из ДеревоВариантовОтчета.ПолучитьЭлементы() Цикл
		Если ГруппаДерева.СкрытОтбором = Ложь Тогда
			Элементы.ДеревоВариантовОтчета.Развернуть(ГруппаДерева.ПолучитьИдентификатор(), Истина);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	ЗаполнитьСписокВариантов();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	Инструкция = ВариантыОтчетов.ИнструкцияУсловногоОформления();
	Инструкция.Поля = "ДеревоВариантовОтчета, ДеревоВариантовОтчетаПредставление, ДеревоВариантовОтчетаАвтор";
	Инструкция.Отборы.Вставить("ДеревоВариантовОтчета.СкрытОтбором", Истина);
	Инструкция.Оформление.Вставить("Видимость", Ложь);
	Инструкция.Оформление.Вставить("Отображать", Ложь);
	ВариантыОтчетов.ДобавитьЭлементУсловногоОформления(ЭтотОбъект, Инструкция);
	
	Инструкция = ВариантыОтчетов.ИнструкцияУсловногоОформления();
	Инструкция.Поля = "ДеревоВариантовОтчетаПредставление, ДеревоВариантовОтчетаАвтор";
	Инструкция.Отборы.Вставить("ДеревоВариантовОтчета.АвторТекущийПользователь", Истина);
	Инструкция.Оформление.Вставить("ЦветТекста", ЦветаСтиля.МоиВариантыОтчетовЦвет);
	ВариантыОтчетов.ДобавитьЭлементУсловногоОформления(ЭтотОбъект, Инструкция);
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьИЗакрыть()
	Вариант = Элементы.ДеревоВариантовОтчета.ТекущиеДанные;
	Если Вариант = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Вариант.КлючВарианта) Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("КлючВарианта", Вариант.КлючВарианта);
	Если Вариант.ИндексКартинки = 4 Тогда
		ТекстВопроса = НСтр("ru = 'Выбранный вариант отчета помечен на удаление.
		|Выбрать этот варианта отчета?'");
		Обработчик = Новый ОписаниеОповещения("ВыбратьИЗакрытьЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60);
	Иначе
		ВыбратьИЗакрытьЗавершение(КодВозвратаДиалога.Да, ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьИЗакрытьЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	Если Ответ = КодВозвратаДиалога.Да Тогда
		Закрыть(Новый ВыборНастроек(ДополнительныеПараметры.КлючВарианта));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВариантДляИзменения()
	Вариант = Элементы.ДеревоВариантовОтчета.ТекущиеДанные;
	Если Вариант = Неопределено ИЛИ НЕ ЗначениеЗаполнено(Вариант.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	Если НЕ ПравоИзмененияВарианта(Вариант, ПолныеПраваНаВарианты) Тогда
		ТекстПредупреждения = НСтр("ru = 'Недостаточно прав доступа для изменения варианта ""%1"".'");
		ТекстПредупреждения = СтрЗаменить(ТекстПредупреждения, "%1", Вариант.Наименование);
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	ПоказатьЗначение(, Вариант.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура ДеревоВариантовОтчетаПередУдалениемЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	Если Ответ = КодВозвратаДиалога.Да Тогда
		УдалитьВариантНаСервере(ДополнительныеПараметры.Вариант.Ссылка, ДополнительныеПараметры.Вариант.ИндексКартинки);
	КонецЕсли;
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПравоИзмененияВарианта(Вариант, ПолныеПраваНаВарианты)
	Возврат ПолныеПраваНаВарианты ИЛИ Вариант.АвторТекущийПользователь;
КонецФункции

&НаСервере
Процедура ЗаполнитьСписокВариантов()
	
	ТекущийКлючВарианта = КлючВарианта;
	Если ЗначениеЗаполнено(Элементы.ДеревоВариантовОтчета.ТекущаяСтрока) Тогда
		ТекущаяСтрокаДерева = ДеревоВариантовОтчета.НайтиПоИдентификатору(Элементы.ДеревоВариантовОтчета.ТекущаяСтрока);
		Если ЗначениеЗаполнено(ТекущаяСтрокаДерева.КлючВарианта) Тогда
			ТекущийКлючВарианта = ТекущаяСтрокаДерева.КлючВарианта;
		КонецЕсли;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВариантыОтчетов.Ссылка,
	|	ВариантыОтчетов.Наименование,
	|	ВариантыОтчетов.КлючВарианта,
	|	ВЫРАЗИТЬ(ВариантыОтчетов.Описание КАК СТРОКА(200)) КАК Описание,
	|	ВЫБОР
	|		КОГДА ВариантыОтчетов.ТипОтчета = &ТипОтчетаВнутренний
	|				И ВариантыОтчетов.Пользовательский = ЛОЖЬ
	|				И ВариантыОтчетов.ВидимостьПоУмолчаниюПереопределена = ЛОЖЬ
	|			ТОГДА ВариантыОтчетов.ПредопределенныйВариант.ВидимостьПоУмолчанию
	|		ИНАЧЕ ВариантыОтчетов.ВидимостьПоУмолчанию
	|	КОНЕЦ КАК ВидимостьПоУмолчанию,
	|	ВариантыОтчетов.Автор,
	|	ВариантыОтчетов.ТолькоДляАвтора,
	|	ВариантыОтчетов.Пользовательский,
	|	ВариантыОтчетов.ПометкаУдаления,
	|	ВариантыОтчетов.ПредопределенныйВариант
	|ПОМЕСТИТЬ втВсеВарианты
	|ИЗ
	|	Справочник.ВариантыОтчетов КАК ВариантыОтчетов
	|ГДЕ
	|	ВариантыОтчетов.Отчет = &Отчет
	|	И (ВариантыОтчетов.Пользовательский = ИСТИНА
	|			ИЛИ ВариантыОтчетов.ПометкаУдаления = ЛОЖЬ)
	|	И (ВариантыОтчетов.ТолькоДляАвтора = ЛОЖЬ
	|			ИЛИ ВариантыОтчетов.Автор = &ТекущийПользователь)
	|	И НЕ ВариантыОтчетов.ПредопределенныйВариант В (&ОтключенныеВариантыПрограммы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеВарианты.Ссылка КАК Ссылка,
	|	РазмещениеРазделенного.РазделИлиГруппа КАК Подсистема,
	|	РазмещениеРазделенного.Использование КАК Использование
	|ПОМЕСТИТЬ втРазделенные
	|ИЗ
	|	втВсеВарианты КАК ВсеВарианты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВариантыОтчетов.Размещение КАК РазмещениеРазделенного
	|		ПО ВсеВарианты.Ссылка = РазмещениеРазделенного.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеВарианты.Ссылка КАК Ссылка,
	|	РазмещениеОбщего.Подсистема КАК Подсистема
	|ПОМЕСТИТЬ втОбщие
	|ИЗ
	|	втВсеВарианты КАК ВсеВарианты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПредопределенныеВариантыОтчетов.Размещение КАК РазмещениеОбщего
	|		ПО ВсеВарианты.ПредопределенныйВариант = РазмещениеОбщего.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(табРазделенные.Ссылка, табОбщие.Ссылка) КАК Ссылка,
	|	ЕСТЬNULL(табРазделенные.Подсистема, табОбщие.Подсистема) КАК Подсистема
	|ПОМЕСТИТЬ втПодсистемы
	|ИЗ
	|	втРазделенные КАК табРазделенные
	|		ПОЛНОЕ СОЕДИНЕНИЕ втОбщие КАК табОбщие
	|		ПО табРазделенные.Ссылка = табОбщие.Ссылка
	|			И табРазделенные.Подсистема = табОбщие.Подсистема
	|ГДЕ
	|	(табРазделенные.Использование = ИСТИНА
	|			ИЛИ табРазделенные.Использование ЕСТЬ NULL )
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	втВсеВарианты.Ссылка,
	|	втВсеВарианты.Наименование,
	|	втВсеВарианты.КлючВарианта,
	|	втВсеВарианты.ВидимостьПоУмолчанию,
	|	втВсеВарианты.Автор,
	|	втВсеВарианты.ТолькоДляАвтора,
	|	втВсеВарианты.Пользовательский,
	|	втВсеВарианты.ПометкаУдаления,
	|	МИНИМУМ(ВЫБОР
	|			КОГДА втВсеВарианты.ПометкаУдаления = ИСТИНА
	|				ТОГДА 3
	|			КОГДА НастройкиПанели.Видимость = ИСТИНА
	|				ТОГДА 1
	|			КОГДА НастройкиПанели.Видимость = ЛОЖЬ
	|				ТОГДА 2
	|			КОГДА втВсеВарианты.ВидимостьПоУмолчанию = ИСТИНА
	|				ТОГДА 1
	|			КОГДА втВсеВарианты.Автор = &ТекущийПользователь
	|				ТОГДА 1
	|			ИНАЧЕ 2
	|		КОНЕЦ) КАК НомерГруппы,
	|	втВсеВарианты.Описание,
	|	ВЫБОР
	|		КОГДА втВсеВарианты.ПометкаУдаления
	|			ТОГДА 4
	|		КОГДА втВсеВарианты.Пользовательский
	|			ТОГДА 3
	|		ИНАЧЕ 5
	|	КОНЕЦ КАК ИндексКартинки,
	|	ВЫБОР
	|		КОГДА втВсеВарианты.Автор = &ТекущийПользователь
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК АвторТекущийПользователь
	|ИЗ
	|	втПодсистемы КАК втПодсистемы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиВариантовОтчетов КАК НастройкиПанели
	|		ПО втПодсистемы.Ссылка = НастройкиПанели.Вариант
	|			И втПодсистемы.Подсистема = НастройкиПанели.РазделИлиГруппа
	|			И (НастройкиПанели.Пользователь = &ТекущийПользователь)
	|		ПОЛНОЕ СОЕДИНЕНИЕ втВсеВарианты КАК втВсеВарианты
	|		ПО втПодсистемы.Ссылка = втВсеВарианты.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	втВсеВарианты.ТолькоДляАвтора,
	|	втВсеВарианты.ПометкаУдаления,
	|	втВсеВарианты.Пользовательский,
	|	втВсеВарианты.Автор,
	|	втВсеВарианты.КлючВарианта,
	|	втВсеВарианты.ВидимостьПоУмолчанию,
	|	втВсеВарианты.Наименование,
	|	втВсеВарианты.Ссылка,
	|	втВсеВарианты.Описание";
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Отчет", ОтчетИнформация.Отчет);
	Запрос.УстановитьПараметр("ТекущийПользователь", ТекущийПользователь);
	Запрос.УстановитьПараметр("ТипОтчетаВнутренний", Перечисления.ТипыОтчетов.Внутренний);
	Запрос.УстановитьПараметр("ОтключенныеВариантыПрограммы", ВариантыОтчетовПовтИсп.ОтключенныеВариантыПрограммы());
	
	Если ПоказыватьЛичныеВариантыОтчетовДругихАвторов Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И (ВариантыОтчетов.ТолькоДляАвтора = ЛОЖЬ
		|			ИЛИ ВариантыОтчетов.Автор = &ТекущийПользователь)", "");
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	ТаблицаВариантов = Запрос.Выполнить().Выгрузить();
	
	// Добавить предопределенные варианты внешнего отчета в таблицу вариантов (для сортировки при добавлении в дерево).
	Если ОтчетИнформация.ТипОтчета = Перечисления.ТипыОтчетов.Внешний Тогда
		
		Попытка
			ОтчетОбъект = ВнешниеОтчеты.Создать(ОтчетИнформация.ОтчетИмя);
		Исключение
			ВариантыОтчетов.ОшибкаПоВарианту(Неопределено,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не удалось получить список предопределенных вариантов
					|внешнего отчета ""%1"":'"),
					ОтчетИнформация.ОтчетИмя
				) + Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			Возврат;
		КонецПопытки;
		
		Если ОтчетОбъект.СхемаКомпоновкиДанных <> Неопределено Тогда
			Для Каждого ВариантНастроекКД Из ОтчетОбъект.СхемаКомпоновкиДанных.ВариантыНастроек Цикл
				Вариант = ТаблицаВариантов.Добавить();
				Вариант.НомерГруппы    = 1;
				Вариант.КлючВарианта   = ВариантНастроекКД.Имя;
				Вариант.Наименование   = ВариантНастроекКД.Представление;
				Вариант.ИндексКартинки = 5;
				Вариант.АвторТекущийПользователь = Ложь;
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
	ТаблицаВариантов.Сортировать("НомерГруппы ВОЗР, Наименование ВОЗР");
	ДеревоВариантовОтчета.ПолучитьЭлементы().Очистить();
	ГруппыДерева = Новый Соответствие;
	ГруппыДерева.Вставить(1, ДеревоВариантовОтчета.ПолучитьЭлементы());
	
	Для Каждого СтрокаТаблицы Из ТаблицаВариантов Цикл
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.КлючВарианта) Тогда
			Продолжить;
		КонецЕсли;
		НаборСтрокДерева = ГруппыДерева.Получить(СтрокаТаблицы.НомерГруппы);
		Если НаборСтрокДерева = Неопределено Тогда
			ГруппаДерева = ДеревоВариантовОтчета.ПолучитьЭлементы().Добавить();
			ГруппаДерева.НомерГруппы = СтрокаТаблицы.НомерГруппы;
			Если СтрокаТаблицы.НомерГруппы = 2 Тогда
				ГруппаДерева.Наименование = НСтр("ru = 'Скрытые в панелях отчетов'");
				ГруппаДерева.ИндексКартинки = 0;
				ГруппаДерева.КартинкаАвтора = -1;
			ИначеЕсли СтрокаТаблицы.НомерГруппы = 3 Тогда
				ГруппаДерева.Наименование = НСтр("ru = 'Помеченные на удаление'");
				ГруппаДерева.ИндексКартинки = 1;
				ГруппаДерева.КартинкаАвтора = -1;
			КонецЕсли;
			НаборСтрокДерева = ГруппаДерева.ПолучитьЭлементы();
			ГруппыДерева.Вставить(СтрокаТаблицы.НомерГруппы, НаборСтрокДерева);
		КонецЕсли;
		
		Вариант = НаборСтрокДерева.Добавить();
		ЗаполнитьЗначенияСвойств(Вариант, СтрокаТаблицы);
		Если Вариант.КлючВарианта = ТекущийКлючВарианта Тогда
			Элементы.ДеревоВариантовОтчета.ТекущаяСтрока = Вариант.ПолучитьИдентификатор();
		КонецЕсли;
		Вариант.КартинкаАвтора = ?(Вариант.ТолькоДляАвтора, -1, 0);
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УдалитьВариантНаСервере(Ссылка, ИндексКартинки)
	ВариантОбъект = Ссылка.ПолучитьОбъект();
	ПометкаУдаления = НЕ ВариантОбъект.ПометкаУдаления;
	Пользовательский = ВариантОбъект.Пользовательский;
	ВариантОбъект.УстановитьПометкуУдаления(ПометкаУдаления);
	ИндексКартинки = ?(ПометкаУдаления, 4, ?(Пользовательский, 3, 5));
КонецПроцедуры

#КонецОбласти
