////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	// Отбор не помеченных на удаление.
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список, "ПометкаУдаления", Ложь, ВидСравненияКомпоновкиДанных.Равно, , Истина,
		РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ);
	
	Если Параметры.Отбор.Свойство("ЭтоДополнительноеСведение") Тогда
		ЭтоДополнительноеСведение = Параметры.Отбор.ЭтоДополнительноеСведение;
	КонецЕсли;
	
	Если Параметры.ЗакрыватьПриВыборе = Ложь Тогда
		// Режим подбора.
		Если ЭтоДополнительноеСведение = Истина Тогда
			АвтоЗаголовок = Ложь;
			Заголовок = НСтр("ru = 'Подбор дополнительных сведений'");
			
		ИначеЕсли ЭтоДополнительноеСведение = Ложь Тогда
			АвтоЗаголовок = Ложь;
			Заголовок = НСтр("ru = 'Подбор дополнительных реквизитов'");
		КонецЕсли;
		Элементы.Список.МножественныйВыбор = Истина;
	Иначе
		// Режим выбора.
		Если ЭтоДополнительноеСведение = Истина Тогда
			АвтоЗаголовок = Ложь;
			Заголовок = НСтр("ru = 'Выбор дополнительного сведения'");
			
		ИначеЕсли ЭтоДополнительноеСведение = Ложь Тогда
			АвтоЗаголовок = Ложь;
			Заголовок = НСтр("ru = 'Выбор дополнительного реквизита'");
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьВыбранныеЗначения();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ Список

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	ДобавитьВыбранноеЗначение(Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)
	
	Если Элемент.ТекущиеДанные <> Неопределено
	   И НЕ Элемент.ТекущиеДанные.ЭтоГруппа Тогда
		
		Отказ = Истина;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Ключ", Элемент.ТекущаяСтрока);
		ПараметрыФормы.Вставить("ЭтоДополнительноеСведение", ЭтоДополнительноеСведение);
		
		ОткрытьФорму(
			"ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.ФормаОбъекта",
			ПараметрыФормы);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если Не Группа Тогда
	
		Отказ = Истина;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ЭтоДополнительноеСведение", ЭтоДополнительноеСведение);
		
		Если Копирование Тогда
			ПараметрыФормы.Вставить("ЗначениеКопирования", Элемент.ТекущаяСтрока);
		Иначе
			ЗначенияЗаполнения = Новый Структура;
			ЗначенияЗаполнения.Вставить("Родитель", Родитель);
			ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
		КонецЕсли;
		
		ОткрытьФорму(
			"ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.ФормаОбъекта",
			ПараметрыФормы);
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ЗаполнитьВыбранныеЗначения()
	
	Если Параметры.Свойство("ВыбранныеЗначения")
	   И ТипЗнч(Параметры.ВыбранныеЗначения) = Тип("Массив") Тогда
		
		СписокВыбранных.ЗагрузитьЗначения(Параметры.ВыбранныеЗначения);
	КонецЕсли;
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ЭлементЦветаОформления = ЭлементУсловногоОформления.Оформление.Элементы.Найти("Font");
	ЭлементЦветаОформления.Значение = Новый Шрифт(, , Истина);
	ЭлементЦветаОформления.Использование = Истина;
	
	ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Список.Ссылка");
	ЭлементОтбораДанных.ВидСравнения   = ВидСравненияКомпоновкиДанных.ВСписке;
	ЭлементОтбораДанных.ПравоеЗначение = СписокВыбранных;
	ЭлементОтбораДанных.Использование  = Истина;
	
	ЭлементОформляемогоПоля = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ЭлементОформляемогоПоля.Поле = Новый ПолеКомпоновкиДанных("Наименование");
	ЭлементОформляемогоПоля.Использование = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьВыбранноеЗначение(Значение)
	
	Если ТипЗнч(Значение) = Тип("Массив") Тогда
		Для каждого Ссылка Из Значение Цикл
			СписокВыбранных.Добавить(Ссылка);
		КонецЦикла;
	Иначе
		СписокВыбранных.Добавить(Значение);
	КонецЕсли;
	
КонецПроцедуры
