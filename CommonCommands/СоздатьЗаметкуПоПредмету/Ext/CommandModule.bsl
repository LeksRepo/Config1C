
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ПредметЗаметки = ПараметрКоманды;
	
	Если Не ЗначениеЗаполнено(ПредметЗаметки) Тогда
		Возврат;
	КонецЕсли;
	
	Заметка = ПолучитьЗаметкуПоПредмету(ПредметЗаметки);
	
	Если ЗначениеЗаполнено(Заметка) Тогда
		ПараметрыФормы = Новый Структура("Ключ", Заметка);
	Иначе
		ПараметрыФормы = Новый Структура("Предмет", ПараметрКоманды);
		ПараметрыФормы.Вставить("Напоминать", Ложь);
	КонецЕсли;
	
	ОткрытьФорму("Справочник.Заметки.ФормаОбъекта", ПараметрыФормы, ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьЗаметкуПоПредмету(Предмет)
	
	Перем Результат;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Предмет", Предмет);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Заметки.Ссылка
	|ИЗ
	|	Справочник.Заметки КАК Заметки
	|ГДЕ
	|	Заметки.Предмет = &Предмет";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Результат = Выборка.Ссылка;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции
