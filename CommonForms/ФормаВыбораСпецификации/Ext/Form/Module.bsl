
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Статус = Параметры.Статус;
	Подразделение = Параметры.Подразделение;
	ПереданнаяТаблица = Параметры.ТЗ.Выгрузить();
	НайденнаяСтрока = Неопределено;
	ОбщаяСуммаНаряда = 0;
	
	Элементы.ТЗМатериалЗаказчикаНеПредоставлен.Видимость = Ложь;
	
	ЗаполнитьТаблицуСпецификаций(Подразделение, Статус, ПереданнаяТаблица);
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	ОповеститьОВыборе(ПолучитьАдресХранилища());
	
КонецПроцедуры

&НаСервере
Функция ПолучитьАдресХранилища()
	
	Отбор = Новый Структура("Пометка", Истина);
	ВремТЗ = ТЗ.Выгрузить(Отбор, "Спецификация, Срочный, СуммаНаряда, ДатаИзготовления, ОсобыеУслуги, ЦветЛДСПОсновной");
	Возврат ПоместитьВоВременноеХранилище(ВремТЗ);
	
КонецФункции

&НаКлиенте
Процедура Отмена(Команда)
	
	ЭтаФорма.Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыделитьВсе(Команда)
	
	Для каждого Элемент Из ТЗ Цикл
		Элемент.Пометка = Истина;
	КонецЦикла;
	ОбщаяСуммаНаряда = ТЗ.Итог("СуммаНаряда");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьВсе(Команда)
	
	Для каждого Элемент Из ТЗ Цикл
		Элемент.Пометка = Ложь;
	КонецЦикла;
	ОбщаяСуммаНаряда = 0;
	
КонецПроцедуры

&НаКлиенте
Процедура ТЗПометкаПриИзменении(Элемент)
	
	Данные = Элементы.ТЗ.ТекущиеДанные;
	Если Данные.Пометка Тогда
		ОбщаяСуммаНаряда = ОбщаяСуммаНаряда + Данные.СуммаНаряда;
	Иначе
		ОбщаяСуммаНаряда = ОбщаяСуммаНаряда - Данные.СуммаНаряда;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусПриИзменении(Элемент)
	
	ТЗ.Очистить();
	ЗаполнитьТаблицуСпецификаций(Подразделение, Статус);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуСпецификаций(Подразделение, Статус, ПереданнаяТаблица = Неопределено)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.УстановитьПараметр("Статус", Статус);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СтатусСпецификацииСрезПоследних.Спецификация,
	|	СтатусСпецификацииСрезПоследних.Спецификация.Номер КАК Номер,
	|	СтатусСпецификацииСрезПоследних.Спецификация.Срочный КАК Срочный,
	|	СтатусСпецификацииСрезПоследних.Спецификация.ДатаИзготовления КАК ДатаИзготовления,
	|	СтатусСпецификацииСрезПоследних.Спецификация.СуммаНарядаСпецификации КАК СуммаНаряда,
	|	СтатусСпецификацииСрезПоследних.Статус,
	|	СтатусСпецификацииСрезПоследних.Спецификация.ДатаОтгрузки КАК ДатаОтгрузки,
	|	СтатусСпецификацииСрезПоследних.Спецификация.Изделие КАК Изделие,
	|	СтатусСпецификацииСрезПоследних.Спецификация.ОсобыеУслуги КАК ОсобыеУслуги,
	|	НаличиеЗаметокПоПредмету.ЕстьЗаметка,
	|	СтатусСпецификацииСрезПоследних.Спецификация.ЦветЛДСПОсновной КАК ЦветЛДСПОсновной,
	|	СтатусСпецификацииСрезПоследних.Спецификация.ТребуетсяПриходМатериаловЗаказчика КАК ТребуетсяПриходМатериаловЗаказчика
	|ИЗ
	|	РегистрСведений.СтатусСпецификации.СрезПоследних(, ) КАК СтатусСпецификацииСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НаличиеЗаметокПоПредмету КАК НаличиеЗаметокПоПредмету
	|		ПО СтатусСпецификацииСрезПоследних.Спецификация = НаличиеЗаметокПоПредмету.Предмет
	|ГДЕ
	|	СтатусСпецификацииСрезПоследних.Спецификация.Проведен
	|	И СтатусСпецификацииСрезПоследних.Спецификация.Подразделение = &Подразделение
	|	И ВЫБОР
	|			КОГДА &Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСпецификации.ПустаяСсылка)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ СтатусСпецификацииСрезПоследних.Статус В (&Статус)
	|		КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номер";
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	ТаблицаРезультат = РезультатЗапроса.Выгрузить();
	ТаблицаДокументов = ТаблицаРезультат.Скопировать(Новый Структура("ТребуетсяПриходМатериаловЗаказчика",Истина),"Спецификация");
	
	ЕстьСпецификацииБезПрихода = Ложь;
	Если  ЗначениеЗаполнено(ТаблицаРезультат) Тогда
	
		СпецификацииБезПрихода = ПроверитьПоступлениеМатериаловЗаказчика(ТаблицаДокументов);
		ЕстьСпецификацииБезПрихода = ЗначениеЗаполнено(СпецификацииБезПрихода);
	
	КонецЕсли;
		  
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ТЗ.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
		Если ЕстьСпецификацииБезПрихода Тогда
		
			   НоваяСтрока.МатериалЗаказчикаНеПредоставлен = СпецификацииБезПрихода.Найти(Выборка.Спецификация)<>Неопределено;
		
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ПереданнаяТаблица) Тогда
			
			НайденнаяСтрока = ПереданнаяТаблица.Найти(Выборка.Спецификация, "Спецификация");
			
			Если ЗначениеЗаполнено(НайденнаяСтрока) Тогда
				
				НоваяСтрока.Пометка = Истина;
				ОбщаяСуммаНаряда = ОбщаяСуммаНаряда + НоваяСтрока.СуммаНаряда;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЕстьСпецификацииБезПрихода Тогда
		Элементы.ТЗМатериалЗаказчикаНеПредоставлен.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПроверитьПоступлениеМатериаловЗаказчика(СписокСпецификаций)
	
	СпецификацииБезПрихода = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокСпецификаций", СписокСпецификаций);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ВТ.Спецификация КАК Документ.Спецификация) КАК Спецификация
		|ПОМЕСТИТЬ ВТ
		|ИЗ
		|	&СписокСпецификаций КАК ВТ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПриходМатериаловЗаказчика.Ссылка КАК ПриходМатериаловСсылка,
		|	ПриходМатериаловЗаказчика.Спецификация
		|ПОМЕСТИТЬ ПриходМатериалов
		|ИЗ
		|	Документ.ПриходМатериаловЗаказчика КАК ПриходМатериаловЗаказчика
		|ГДЕ
		|	ПриходМатериаловЗаказчика.Проведен
		|	И ПриходМатериаловЗаказчика.Спецификация В
		|			(ВЫБРАТЬ
		|				ВТ.Спецификация
		|			ИЗ
		|				ВТ КАК ВТ)
		|	И ПриходМатериаловЗаказчика.Проведен
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ.Спецификация КАК Спецификация,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ПриходМатериалов.ПриходМатериаловСсылка) КАК ЕстьПриход
		|ИЗ
		|	ВТ КАК ВТ
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПриходМатериалов КАК ПриходМатериалов
		|		ПО ВТ.Спецификация = ПриходМатериалов.Спецификация
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ.Спецификация
		|
		|ИМЕЮЩИЕ
		|	КОЛИЧЕСТВО(ПриходМатериалов.ПриходМатериаловСсылка) = 0";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		    СпецификацииБезПрихода = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Спецификация");
	КонецЕсли;
		
	Возврат СпецификацииБезПрихода;
	

КонецФункции // ПроверитьПоступлениеМатериаловЗаказчика()

&НаКлиенте
Процедура ТЗВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ТЗ.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные.Пометка = НЕ ТекущиеДанные.Пометка;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьЗаметку(Команда)
	
	Данные = Элементы.ТЗ.ТекущиеДанные;
	
	Если Данные.ЕстьЗаметка Тогда
		
		Заметка = ПолучитьЗаметку(Данные.Спецификация);
		
		Если ЗначениеЗаполнено(Заметка) Тогда
			ОткрытьЗначение(Заметка);
		КонецЕсли;
			
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьЗаметку(Спецификация)
	
	Заметка = Неопределено;
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Предмет",Спецификация);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Заметки.Ссылка
	|ИЗ
	|	Справочник.Заметки КАК Заметки
	|ГДЕ
	|	Заметки.Предмет = &Предмет";
	
	Результат = Запрос.Выполнить();
	
	Если НЕ Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Заметка = Выборка.Ссылка;
		
	КонецЕсли;
	
	Возврат Заметка;
	
КонецФункции	
