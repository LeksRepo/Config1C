&НаКлиенте
Перем ПараметрыОбработчика;

&НаКлиенте
Перем ПеременныеКлиента;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ПанельОтчетовКлючВарианта = " - ";
	
	// Локальные переменные
	ОтчетОбъект = РеквизитФормыВЗначение("Отчет");
	ОтчетМетаданные = ОтчетОбъект.Метаданные();
	
	РежимРасшифровки = (Параметры.Свойство("Расшифровка") И Параметры.Расшифровка <> Неопределено);
	РежимВариантаОтчета = (Параметры.Свойство("КлючВарианта") И Параметры.КлючВарианта <> Неопределено);
	КлючОбъекта = ОтчетМетаданные.ПолноеИмя();
	НаименованиеОтчета = СокрЛП(ОтчетМетаданные.Представление());
	ПравоВывода = ПравоДоступа("Вывод", Метаданные);
	
	// Реквизиты
	ВерсияСтандартныхФункцийОтчетов = "";
	Попытка
		ВерсияСтандартныхФункцийОтчетов = ОтчетОбъект.ВерсияСтандартныхФункцийОтчетов();
	Исключение
		// Обработка не требуется. Отчет не имеет программного интерфейса для тесной интеграции с формой отчета.
	КонецПопытки;
	
	// Параметры по умолчанию
	Если ВерсияСтандартныхФункцийОтчетов = "1" Тогда
		НастройкиОтчета = ОтчетОбъект.ПолучитьНастройкиОтчета();
	Иначе
		НастройкиОтчета = ФункцииОтчетовКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию();
	КонецЕсли;
	
	Если НастройкиОтчета.Свойство("ВыводитьСуммуВыделенныхЯчеек") И Не НастройкиОтчета.ВыводитьСуммуВыделенныхЯчеек Тогда
		Элементы.ГруппаСумма.Видимость = Ложь;
		Элементы.ОтчетТабличныйДокумент.УстановитьДействие("ПриАктивизацииОбласти", "");
	КонецЕсли;
	Элементы.ПустаяДекорация1.Видимость = Не Элементы.ГруппаСумма.Видимость;
	
	// Параметры печати печати и сохранения положения окна.
	УстановитьКлючиФормы();
	
	// Параметры формы
	ФормаПараметры = Новый Структура(
		"КлючНазначенияИспользования, КлючПользовательскихНастроек,
		|Расшифровка, СформироватьПриОткрытии, ТолькоПросмотр,
		|ФиксированныеНастройки, ОтчетСсылка, Раздел, Подсистема, ПодсистемаПредставление");
	ЗаполнитьЗначенияСвойств(ФормаПараметры, Параметры);
	Если НЕ ЗначениеЗаполнено(ФормаПараметры.ОтчетСсылка) Тогда
		ОтчетИнформация = ВариантыОтчетов.СформироватьИнформациюОбОтчетеПоПолномуИмени(КлючОбъекта);
		Если НЕ ЗначениеЗаполнено(ОтчетИнформация.ТекстОшибки) Тогда
			ФормаПараметры.ОтчетСсылка = ОтчетИнформация.Отчет;
		Иначе
			ФормаПараметры.ОтчетСсылка = КлючОбъекта;
		КонецЕсли;
	КонецЕсли;
	
	ФормаПараметры = Новый ФиксированнаяСтруктура(ФормаПараметры);
	
	ВариантСсылка = ВариантыОтчетов.ПолучитьСсылку(ФормаПараметры.ОтчетСсылка, КлючТекущегоВарианта);
	
	// Скрытие команд вариантов
	Если Не Параметры.Свойство("ВидимостьКомандВариантовОтчетов", ВидимостьКомандВариантовОтчетов) Тогда
		ВидимостьКомандВариантовОтчетов = ВариантыОтчетовПовтИсп.ПравоДобавления();
	КонецЕсли;
	
	// Регистрация элементов, команд и реквизитов формы, которые не удаляются при перезаполнении быстрых настроек
	НаборРеквизитов = ПолучитьРеквизиты();
	Для Каждого Реквизит Из НаборРеквизитов Цикл
		ПолноеИмяРеквизита = Реквизит.Имя + ?(ПустаяСтрока(Реквизит.Путь), "", "." + Реквизит.Путь);
		ПостоянныеРеквизиты.Добавить(ПолноеИмяРеквизита);
	КонецЦикла;
	
	Для Каждого Команда Из Команды Цикл
		ПостоянныеКоманды.Добавить(Команда.Имя);
	КонецЦикла;
	
	// Скрытие кнопки выбора варианта из расшифровки.
	Если РежимРасшифровки Тогда
		Элементы.ФормаЗагрузитьВариант.Видимость = Ложь;
		Если ЗначениеЗаполнено(ОтчетНаименованиеТекущегоВарианта) Тогда
			РежимРасшифровкиПредставлениеВладельца = ОтчетНаименованиеТекущегоВарианта;
		КонецЕсли;
	КонецЕсли;
	
	// Приведение зависимых элементов формы к кондиции
	ВидимостьДоступностьКорректность();
	
	Элементы.ГруппаДругиеВариантыОтчета.Заголовок = НаименованиеОтчета + " (" + НСтр("ru = 'варианты отчета'") + "):";
	
	// Механизмы расширения
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.РассылкаОтчетов") Тогда
		МодульРассылкаОтчетов = ОбщегоНазначения.ОбщийМодуль("РассылкаОтчетов");
		МодульРассылкаОтчетов.ФормаОтчетаДобавитьКоманды(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	КонецЕсли;
	ФормаОтчетаПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
	// Если в подменю одна команда, то выпадающий список не отображается.
	Если Элементы.Отправить.ПодчиненныеЭлементы.Количество() = 1 Тогда
		Элементы.Отправить.Вид = ВидГруппыФормы.ГруппаКнопок;
		Элементы.ОтправитьПоЭлектроннойПочте.Заголовок = Элементы.Отправить.Заголовок + "...";
	КонецЕсли;
	Если Элементы.ОтправитьВсеДействия.ПодчиненныеЭлементы.Количество() = 1 Тогда
		Элементы.ОтправитьВсеДействия.Вид = ВидГруппыФормы.ГруппаКнопок;
		Элементы.ОтправитьПоЭлектроннойПочтеВсеДействия.Заголовок = Элементы.ОтправитьВсеДействия.Заголовок + "...";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПеременныеКлиента = Новый Структура;
	Если НеобходимоОбновитьНастройки Тогда
		ПриОбновленииСоставаПользовательскихНастроекНаСервере(Истина);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	Результат = Ложь;
	
	СуффиксФормыИсточника = ИсточникВыбора.ИмяФормы;
	ПозицияТочки = СтрДлина(СуффиксФормыИсточника);
	Пока Сред(СуффиксФормыИсточника, ПозицияТочки, 1) <> "." Цикл
		ПозицияТочки = ПозицияТочки - 1;
	КонецЦикла;
	СуффиксФормыИсточника = ВРег(Сред(СуффиксФормыИсточника, ПозицияТочки + 1));
	
	Если СуффиксФормыИсточника = ВРег("ФормаНастроекОтчета")
		Или СуффиксФормыИсточника = ВРег("ФормаНастроек") Тогда
		ИзмененияИзФормыВариантаОтчета = Ложь;
	ИначеЕсли СуффиксФормыИсточника = ВРег("ФормаВариантаОтчета")
		Или СуффиксФормыИсточника = ВРег("ФормаВарианта") Тогда
		ИзмененияИзФормыВариантаОтчета = Истина;
	Иначе
		ИзмененияИзФормыВариантаОтчета = Неопределено;
	КонецЕсли;
	
	Если ИзмененияИзФормыВариантаОтчета <> Неопределено Тогда
		БыстрыеНастройкиЗаполнить(ВыбранноеЗначение);
		Результат = Истина;
	КонецЕсли;
	
	// Механизмы расширения
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.РассылкаОтчетов") Тогда
		МодульРассылкаОтчетовКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("РассылкаОтчетовКлиент");
		МодульРассылкаОтчетовКлиент.ФормаОтчетаОбработкаВыбора(ЭтотОбъект, ВыбранноеЗначение, ИсточникВыбора, Результат);
	КонецЕсли;
	ФормаОтчетаКлиентПереопределяемый.ОбработкаВыбора(ЭтотОбъект, ВыбранноеЗначение, ИсточникВыбора, Результат);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	ОповещениеОбработано = Ложь;
	Если ИмяСобытия = ВариантыОтчетовКлиентСервер.ИмяСобытияИзменениеВарианта() Тогда
		ПанельОтчетовКлючВарианта = " - ";
		ВидимостьДоступностьКорректность();
		ОповещениеОбработано = Истина;
	КонецЕсли;
	
	ФормаОтчетаКлиентПереопределяемый.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник, ОповещениеОбработано);
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеВариантаНаСервере(НовыеНастройкиКД)
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	НеобходимоОбновитьНастройки = Истина;
	
	// Вызов переопределяемого модуля
	ФормаОтчетаПереопределяемый.ПередЗагрузкойВариантаНаСервере(ЭтотОбъект, НовыеНастройкиКД);
	
	// Загрузка фиксированных настроек для режима расшифровки.
	Если РежимРасшифровки Тогда
		Если Параметры <> Неопределено И Параметры.Свойство("Расшифровка") Тогда
			Отчет.КомпоновщикНастроек.ЗагрузитьФиксированныеНастройки(Параметры.Расшифровка.ПрименяемыеНастройки);
			Отчет.КомпоновщикНастроек.ФиксированныеНастройки.ДополнительныеСвойства.Вставить("РежимРасшифровки", Истина);
		КонецЕсли;
	КонецЕсли;
	
	// Параметры печати печати и сохранения положения окна.
	УстановитьКлючиФормы();
	
	// Заполнение панели быстрых настроек
	РежимВариантаОтчета = Истина;
	ВариантСсылка = ВариантыОтчетов.ПолучитьСсылку(ФормаПараметры.ОтчетСсылка, КлючТекущегоВарианта);
	
	// Вызов переопределяемого модуля
	ФормаОтчетаПереопределяемый.ПриЗагрузкеВариантаНаСервере(ЭтотОбъект, НовыеНастройкиКД);
КонецПроцедуры

&НаСервере
Процедура ПриСохраненииВариантаНаСервере(Настройки)
	
	// Заголовок
	ВидимостьДоступностьКорректность("ВариантОтчета");
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеПользовательскихНастроекНаСервере(НовыеПользовательскиеНастройкиКД)
	
	НеобходимоОбновитьНастройки = Истина;
	
	// Вызов переопределяемого модуля
	ФормаОтчетаПереопределяемый.ПриЗагрузкеПользовательскихНастроекНаСервере(ЭтотОбъект, НовыеПользовательскиеНастройкиКД);
	
КонецПроцедуры

&НаСервере
Процедура ПриОбновленииСоставаПользовательскихНастроекНаСервере(СтандартнаяОбработка)
	НеобходимоОбновитьНастройки = Ложь;
	
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("Событие", Новый Структура);
	ПараметрыЗаполнения.Событие.Вставить("Имя", "ПриОбновленииСоставаПользовательскихНастроекНаСервере");
	ПараметрыЗаполнения.Событие.Вставить("СтандартнаяОбработка", СтандартнаяОбработка);
	БыстрыеНастройкиЗаполнить(ПараметрыЗаполнения);
	Если ПараметрыЗаполнения.Событие.СтандартнаяОбработка <> СтандартнаяОбработка Тогда
		СтандартнаяОбработка = ПараметрыЗаполнения.Событие.СтандартнаяОбработка;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриСохраненииПользовательскихНастроекНаСервере(Настройки)
	ВариантыОтчетов.ПриСохраненииПользовательскихНастроекНаСервере(ЭтотОбъект, Настройки);
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	Если ФоновоеЗаданиеИдентификатор <> Неопределено Тогда
		ФоновоеЗаданиеОтменить(ФоновоеЗаданиеИдентификатор);
		ФоновоеЗаданиеИдентификатор = Неопределено;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

////////////////////////////////////////////////////////////////////////////////
// Табличный документ

&НаКлиенте
Процедура ОтчетТабличныйДокументОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	ФормаОтчетаКлиентПереопределяемый.ОбработкаРасшифровки(ЭтотОбъект, Элемент, Расшифровка, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ОтчетТабличныйДокументОбработкаДополнительнойРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	ФормаОтчетаКлиентПереопределяемый.ОбработкаДополнительнойРасшифровки(ЭтотОбъект, Элемент, Расшифровка, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ОтчетТабличныйДокументПриАктивизацииОбласти(Элемент)
	Если НастройкиОтчета.ВыводитьСуммуВыделенныхЯчеек Тогда
		ИнтервалОжидания = ?(ПолучитьСкоростьКлиентскогоСоединения() = СкоростьКлиентскогоСоединения.Низкая, 1, 0.2);
		ПодключитьОбработчикОжидания("Подключаемый_РассчитатьСуммуЯчеек", ИнтервалОжидания, Истина);
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Подключаемые

&НаКлиенте
Процедура Подключаемый_ФлажокИспользование_ПриИзменении(Элемент)
	ИдентификаторЭлемента = Прав(Элемент.Имя, 32);
	
	ПользовательскаяНастройкаКД = НайтиПользовательскуюНастройкуЭлемента(ИдентификаторЭлемента);
	ПользовательскаяНастройкаКД.Использование = ЭтотОбъект[Элемент.Имя];
	
	ВсеСвязиВедущего = БыстрыйПоискОтключаемыхСвязей.Получить(ИдентификаторЭлемента);
	Если ВсеСвязиВедущего <> Неопределено Тогда
		Если ТипЗнч(ПользовательскаяНастройкаКД) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
			Значение = ПользовательскаяНастройкаКД.Значение;
		Иначе
			Значение = ПользовательскаяНастройкаКД.ПравоеЗначение;
		КонецЕсли;
		ОбновитьСвязиВедущегоСПодчиненными(ИдентификаторЭлемента, Значение, ПользовательскаяНастройкаКД.Использование, ВсеСвязиВедущего, Ложь);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПолеВвода_ПриИзменении(Элемент)
	ЗначениеИмя = Элемент.Имя;
	ИдентификаторЭлемента  = Прав(ЗначениеИмя, 32);
	ШаблонГенерацииПрефикс = Лев(ЗначениеИмя, Найти(ЗначениеИмя, "_Значение_")-1);
	
	ИспользованиеИмя = ШаблонГенерацииПрефикс + "_Использование_" + ИдентификаторЭлемента;
	Значение = ЭтотОбъект[ЗначениеИмя];
	
	Если ТипЗнч(Значение) = Тип("ОписаниеТипов") Тогда
		МассивТипов = Значение.Типы();
		Если МассивТипов.Количество() > 0 Тогда
			Значение = МассивТипов[0];
		КонецЕсли;
	КонецЕсли;
	
	ПользовательскаяНастройкаКД = НайтиПользовательскуюНастройкуЭлемента(ИдентификаторЭлемента);
	Если ТипЗнч(ПользовательскаяНастройкаКД) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
		ПользовательскаяНастройкаКД.Значение = Значение;
	Иначе
		ПользовательскаяНастройкаКД.ПравоеЗначение = Значение;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Значение) И Не ПользовательскаяНастройкаКД.Использование Тогда
		ПользовательскаяНастройкаКД.Использование = Истина;
		Если Элементы.Найти(ИспользованиеИмя) <> Неопределено Тогда
			ЭтотОбъект[ИспользованиеИмя] = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если ПользовательскаяНастройкаКД.Использование Тогда
		// Включение связей когда был включен флажок "Использование".
		// Когда связи включены - очистка значений при изменении значения.
		ВсеСвязиВедущего = БыстрыйПоискОтключаемыхСвязей.Получить(ИдентификаторЭлемента);
		Если ВсеСвязиВедущего <> Неопределено Тогда
			ОбновитьСвязиВедущегоСПодчиненными(ИдентификаторЭлемента, Значение, ПользовательскаяНастройкаКД.Использование, ВсеСвязиВедущего, Истина);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ТаблицаПорядка_Выбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ИмяКолонки = Поле.Имя;
	Если Лев(ИмяКолонки, 39) = "ПорядокКомпоновкиДанных_КолонкаПорядок_" Тогда // Изменение порядка.
		
		СтандартнаяОбработка = Ложь;
		
		ИзменитьНаправлениеСортировкиТаблицы(ИмяКолонки, Неопределено);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ТаблицаПорядка_КолонкаИспользование_ПриИзменении(Элемент)
	
	КолонкаИспользованиеИмя = Элемент.Имя;
	
	ТаблицаИмя = СтрЗаменить(КолонкаИспользованиеИмя, "_КолонкаИспользование_", "_Таблица_");
	СтрокаТаблицы = Элементы[ТаблицаИмя].ТекущиеДанные;
	Если СтрокаТаблицы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПользовательскаяНастройкаКД = НайтиПользовательскуюНастройкуЭлемента(КолонкаИспользованиеИмя);
	
	// Отражение изменений флажка элемента таблицы в соответствующей пользовательской настройке.
	Если ТипЗнч(ПользовательскаяНастройкаКД) = Тип("СтруктураНастроекКомпоновкиДанных") Тогда
		ИндексСтрокиТаблицы = ЭтотОбъект[ТаблицаИмя].Индекс(СтрокаТаблицы);
		ПолеГруппировки = ПользовательскаяНастройкаКД.Структура.Получить(ИндексСтрокиТаблицы);
		ПолеГруппировки.Использование = СтрокаТаблицы.Использование;
		ПолеГруппировки.Состояние     = СостояниеЭлементаНастройкиКомпоновкиДанных[?(СтрокаТаблицы.Использование, "Включен", "Отключен")];
	Иначе //ВыбранныеПоляКомпоновкиДанных, ПорядокКомпоновкиДанных
		СтрокаПользовательскойНастройки = ФункцииОтчетовКлиентСервер.НайтиВложеннуюПользовательскуюНастройку(ПользовательскаяНастройкаКД, СтрокаТаблицы.Идентификатор);
		Если СтрокаПользовательскойНастройки <> Неопределено Тогда
			СтрокаПользовательскойНастройки.Использование = СтрокаТаблицы.Использование;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Подключаемые - Стандартный период

&НаКлиенте
Процедура Подключаемый_СтандартныйПериод_Вид_ПриИзменении(Элемент)
	
	ВидПериодаИмя = Элемент.Имя;
	ИдентификаторЭлемента  = Прав(ВидПериодаИмя, 32);
	ШаблонГенерацииПрефикс = Лев(ВидПериодаИмя, Найти(ВидПериодаИмя, "_Вид_")-1);
	
	СтраницыИмя            = ШаблонГенерацииПрефикс + "_Страницы_"      + ИдентификаторЭлемента;
	ИспользованиеИмя       = ШаблонГенерацииПрефикс + "_Использование_" + ИдентификаторЭлемента;
	ПериодЗначениеИмя      = ШаблонГенерацииПрефикс + "_Значение_"      + ИдентификаторЭлемента;
	ПериодПредставлениеИмя = ШаблонГенерацииПрефикс + "_Представление_" + ИдентификаторЭлемента;
	
	Значение = ЭтотОбъект[ПериодЗначениеИмя];
	
	ВидПериода = ЭтотОбъект[ВидПериодаИмя];
	
	Если ВидПериода = ПредопределенноеЗначение("Перечисление.ДоступныеПериодыОтчета.ПроизвольныйПериод") Тогда
		
		// Переключение страницы.
		СтраницаПроизвольныйИмя = ШаблонГенерацииПрефикс + "_СтраницаПроизвольный_" + ИдентификаторЭлемента;
		Элементы[СтраницыИмя].ТекущаяСтраница = Элементы[СтраницаПроизвольныйИмя];
		
		Значение.Вариант = ВариантСтандартногоПериода.ПроизвольныйПериод;
		
	Иначе
		
		// Переключение страницы.
		СтраницаСтандартныйИмя = ШаблонГенерацииПрефикс + "_СтраницаСтандартный_" + ИдентификаторЭлемента;
		Элементы[СтраницыИмя].ТекущаяСтраница = Элементы[СтраницаСтандартныйИмя];
		
		// Приведение значения периода в соответствие с выбранным видом.
		НачалоПериода = НачалоДня(Значение.ДатаНачала);
		Если НЕ ЗначениеЗаполнено(НачалоПериода) Тогда
			НачалоПериода = НачалоДня(ОбщегоНазначенияКлиент.ДатаСеанса());
		КонецЕсли;
		НачалоПериода = ФункцииОтчетовКлиентСервер.НачалоПериодаОтчета(ВидПериода, НачалоПериода);
		КонецПериода  = ФункцииОтчетовКлиентСервер.КонецПериодаОтчета(ВидПериода, НачалоПериода);
		
		Значение.ДатаНачала    = НачалоПериода;
		Значение.ДатаОкончания = КонецПериода;
		
	КонецЕсли;
	
	Представление = ФункцииОтчетовКлиентСервер.ПредставлениеСтандартногоПериода(Значение, ВидПериода);
	ЭтотОбъект[ПериодПредставлениеИмя] = Представление;
	ЭтотОбъект[ПериодЗначениеИмя]      = Значение;
	
	ПользовательскаяНастройкаКД = НайтиПользовательскуюНастройкуЭлемента(ИдентификаторЭлемента);
	Если ШаблонГенерацииПрефикс = "Параметр" Тогда
		ПользовательскаяНастройкаКД.Значение = Значение;
	Иначе
		ПользовательскаяНастройкаКД.ПравоеЗначение = Значение;
	КонецЕсли;
	
	Если Элементы.Найти(ИспользованиеИмя) <> Неопределено Тогда
		ЭтотОбъект[ИспользованиеИмя] = Истина;
		ПользовательскаяНастройкаКД.Использование = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СтандартныйПериод_Значение_НачалоВыбора(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	// Формирование сведений по элементу.
	ПериодПредставлениеИмя = Элемент.Имя;
	ИдентификаторЭлемента  = Прав(ПериодПредставлениеИмя, 32);
	ШаблонГенерацииПрефикс = Лев(ПериодПредставлениеИмя, Найти(ПериодПредставлениеИмя, "_Представление_")-1);
	
	ПериодЗначениеИмя = ШаблонГенерацииПрефикс + "_Значение_"      + ИдентификаторЭлемента;
	ВидПериодаИмя     = ШаблонГенерацииПрефикс + "_Вид_"           + ИдентификаторЭлемента;
	ИспользованиеИмя  = ШаблонГенерацииПрефикс + "_Использование_" + ИдентификаторЭлемента;
	
	Значение   = ЭтотОбъект[ПериодЗначениеИмя];
	ВидПериода = ЭтотОбъект[ВидПериодаИмя];
	
	НачалоПериода = Значение.ДатаНачала;
	Если НачалоПериода = '00010101' тогда
		НачалоПериода = ФункцииОтчетовКлиентСервер.НачалоПериодаОтчета(ВидПериода, ОбщегоНазначенияКлиент.ДатаСеанса());
	КонецЕсли;
	
	// Параметры для чтения из обработчиков:
	ПараметрыВыбора = Новый Структура;
	// Для выбора значения:
	ПараметрыВыбора.Вставить("Значение",               Значение);
	ПараметрыВыбора.Вставить("Элемент",                Элемент);
	// Для загрузки значения:
	ПараметрыВыбора.Вставить("ИдентификаторЭлемента",  ИдентификаторЭлемента);
	ПараметрыВыбора.Вставить("ПериодПредставлениеИмя", ПериодПредставлениеИмя);
	ПараметрыВыбора.Вставить("ПериодЗначениеИмя",      ПериодЗначениеИмя);
	ПараметрыВыбора.Вставить("ИспользованиеИмя",       ИспользованиеИмя);
	ПараметрыВыбора.Вставить("ВидПериода",             ВидПериода);
	ПараметрыВыбора.Вставить("ЭтоПараметр",            (ШаблонГенерацииПрефикс = "Параметр"));
	
	ВыборПериодаИзВыпадающегоСписка(-1, ПараметрыВыбора);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СтандартныйПериод_НачалоПериода_ПриИзменении(Элемент)
	
	// Формирование сведений по элементу.
	ПериодНачалоИмя = Элемент.Имя;
	ИдентификаторЭлемента  = Прав(ПериодНачалоИмя, 32);
	ШаблонГенерацииПрефикс = Лев(ПериодНачалоИмя, Найти(ПериодНачалоИмя, "_Начало_")-1);
	ПериодЗначениеИмя = ШаблонГенерацииПрефикс + "_Значение_" + ИдентификаторЭлемента;
	ИспользованиеИмя  = ШаблонГенерацииПрефикс + "_Использование_" + ИдентификаторЭлемента;
	
	Значение = ЭтотОбъект[ПериодЗначениеИмя];
	
	Если Значение.ДатаНачала <> '00010101' Тогда
		Значение.ДатаНачала = НачалоДня(Значение.ДатаНачала);
	КонецЕсли;
	
	// Запись значения в пользовательские настройки компоновки данных.
	ПользовательскаяНастройкаКД = НайтиПользовательскуюНастройкуЭлемента(ИдентификаторЭлемента);
	Если ШаблонГенерацииПрефикс = "Параметр" Тогда
		ПользовательскаяНастройкаКД.Значение = Значение;
	Иначе
		ПользовательскаяНастройкаКД.ПравоеЗначение = Значение;
	КонецЕсли;
	
	Если Значение.ДатаНачала <> '00010101' И Элементы.Найти(ИспользованиеИмя) <> Неопределено Тогда
		ЭтотОбъект[ИспользованиеИмя] = Истина;
		ПользовательскаяНастройкаКД.Использование = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СтандартныйПериод_КонецПериода_ПриИзменении(Элемент)
	
	// Формирование сведений по элементу.
	ПериодОкончаниеИмя = Элемент.Имя;
	ИдентификаторЭлемента  = Прав(ПериодОкончаниеИмя, 32);
	ШаблонГенерацииПрефикс = Лев(ПериодОкончаниеИмя, Найти(ПериодОкончаниеИмя, "_Окончание_")-1);
	
	ПериодЗначениеИмя = ШаблонГенерацииПрефикс + "_Значение_" + ИдентификаторЭлемента;
	ИспользованиеИмя  = ШаблонГенерацииПрефикс + "_Использование_" + ИдентификаторЭлемента;
	
	Значение = ЭтотОбъект[ПериодЗначениеИмя];
	
	Если Значение.ДатаОкончания <> '00010101' Тогда
		Значение.ДатаОкончания = КонецДня(Значение.ДатаОкончания);
	КонецЕсли;
	
	// Запись значения в пользовательские настройки компоновки данных.
	ПользовательскаяНастройкаКД = НайтиПользовательскуюНастройкуЭлемента(ИдентификаторЭлемента);
	Если ШаблонГенерацииПрефикс = "Параметр" Тогда
		ПользовательскаяНастройкаКД.Значение = Значение;
	Иначе
		ПользовательскаяНастройкаКД.ПравоеЗначение = Значение;
	КонецЕсли;
	
	Если Значение.ДатаОкончания <> '00010101' И Элементы.Найти(ИспользованиеИмя) <> Неопределено Тогда
		ЭтотОбъект[ИспользованиеИмя] = Истина;
		ПользовательскаяНастройкаКД.Использование = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СтандартныйПериод_Значение_Очистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Подключаемые - Список значений с кнопкой "Подбор"

&НаКлиенте
Процедура Подключаемый_СписокСПодбором_ПриИзменении(Элемент)
	
	ПользовательскаяНастройкаКД = НайтиПользовательскуюНастройкуЭлемента(Элемент.Имя);
	Если ТипЗнч(ПользовательскаяНастройкаКД) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
		ПользовательскаяНастройкаКД.Значение = ЭтотОбъект[Элемент.Имя];
	Иначе
		ПользовательскаяНастройкаКД.ПравоеЗначение = ЭтотОбъект[Элемент.Имя];
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СписокСПодбором_ОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	// Добавление выбранных элементов с контролем уникальности.
	СписокЗначений = ЭтотОбъект[Элемент.Имя];
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Массив") Тогда
		Для Каждого ЭлементМассива Из ВыбранноеЗначение Цикл
			Если СписокЗначений.НайтиПоЗначению(ЭлементМассива) = Неопределено Тогда
				СписокЗначений.Добавить(ЭлементМассива);
			КонецЕсли;
		КонецЦикла;
	Иначе
		Если СписокЗначений.НайтиПоЗначению(ВыбранноеЗначение) = Неопределено Тогда
			СписокЗначений.Добавить(ВыбранноеЗначение);
		КонецЕсли;
	КонецЕсли;
	
	// Отражение изменений в пользовательской настройке.
	ПользовательскаяНастройкаКД = НайтиПользовательскуюНастройкуЭлемента(Элемент.Имя);
	Если ТипЗнч(ПользовательскаяНастройкаКД) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
		ПользовательскаяНастройкаКД.Значение = СписокЗначений;
	Иначе
		ПользовательскаяНастройкаКД.ПравоеЗначение = СписокЗначений;
	КонецЕсли;
	
	// Включение флажка Использование.
	ИспользованиеИмя = СтрЗаменить(Элемент.Имя, "_СписокЗначений_", "_Использование_");
	Если Элементы.Найти(ИспользованиеИмя) <> Неопределено Тогда
		ЭтотОбъект[ИспользованиеИмя] = Истина;
	КонецЕсли;
	ПользовательскаяНастройкаКД.Использование = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СписокСТипомТип_ПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	СписокЗначений = ЭтотОбъект[Элемент.Имя];
	ТекущееЗначение = Элемент.ТекущиеДанные.Значение;
	Если ТекущееЗначение = Новый ОписаниеТипов("Неопределено") Тогда
		Отказ = Истина; // Запрет пустых значений для типа "Тип".
	Иначе
		Для Каждого ЭлементСписка Из СписокЗначений Цикл
			Если ЭлементСписка.Значение = ТекущееЗначение И ЭлементСписка.ПолучитьИдентификатор() <> Элемент.ТекущаяСтрока Тогда
				Отказ = Истина; // Запрет дублей.
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если Отказ Тогда
		Элемент.ЗакончитьРедактированиеСтроки(Истина); // Перезапуск события "ПередОкончаниемРедактирования" с ОтменаРедактирования = Истина.
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СписокСТипомТип_ПриИзменении(Элемент)
	
	ПользовательскаяНастройкаКД = НайтиПользовательскуюНастройкуЭлемента(Элемент.Имя);
	СписокЗначений = ЭтотОбъект[Элемент.Имя];
	СписокЗначенийДляСКД = Новый СписокЗначений;
	ТипОписаниеТипов = Тип("ОписаниеТипов");
	ОписаниеТиповНеопределено = Новый ОписаниеТипов("Неопределено");
	
	Для Каждого ЭлементСписка Из СписокЗначений Цикл
		ЗначениеЭлемента = ЭлементСписка.Значение;
		Если ТипЗнч(ЗначениеЭлемента) = ТипОписаниеТипов Тогда
			Если ЗначениеЭлемента = ОписаниеТиповНеопределено Тогда
				Продолжить; // Запрет пустых значений для типа "Тип".
			КонецЕсли;
			ЗначениеЭлемента = ЗначениеЭлемента.Типы()[0];
		КонецЕсли;
		СписокЗначенийДляСКД.Добавить(ЗначениеЭлемента);
	КонецЦикла;
	
	Если ТипЗнч(ПользовательскаяНастройкаКД) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
		ПользовательскаяНастройкаКД.Значение = СписокЗначенийДляСКД;
	Иначе
		ПользовательскаяНастройкаКД.ПравоеЗначение = СписокЗначенийДляСКД;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СкрытьПоказатьНастройкиИДругиеОтчеты(Команда)
	СостояниеДоИзменения = Новый Структура("Видимость, ДополнительныйРежимОтображения, Картинка, Текст");
	ЗаполнитьЗначенияСвойств(СостояниеДоИзменения, Элементы.ОтчетТабличныйДокумент.ОтображениеСостояния);
	
	Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ВидимостьПравойПанели", Не Элементы.ПраваяПанель.Видимость);
	ВидимостьДоступностьКорректность("СкрытьПоказатьНастройкиИДругиеОтчеты");
	
	ЗаполнитьЗначенияСвойств(Элементы.ОтчетТабличныйДокумент.ОтображениеСостояния, СостояниеДоИзменения);
КонецПроцедуры

&НаКлиенте
Процедура СкрытьПоказатьБыстрыеНастройки(Команда)
	СостояниеДоИзменения = Новый Структура("Видимость, ДополнительныйРежимОтображения, Картинка, Текст");
	ЗаполнитьЗначенияСвойств(СостояниеДоИзменения, Элементы.ОтчетТабличныйДокумент.ОтображениеСостояния);
	
	Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ВидимостьБыстрыхНастроек", Не Элементы.ПанельБыстрыхНастроек.Видимость);
	Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ВидимостьПанелиОтчетов", Ложь);
	ВидимостьДоступностьКорректность("СкрытьПоказатьБыстрыеНастройки");
	
	ЗаполнитьЗначенияСвойств(Элементы.ОтчетТабличныйДокумент.ОтображениеСостояния, СостояниеДоИзменения);
КонецПроцедуры

&НаКлиенте
Процедура СкрытьПоказатьОтчеты(Команда)
	СостояниеДоИзменения = Новый Структура("Видимость, ДополнительныйРежимОтображения, Картинка, Текст");
	ЗаполнитьЗначенияСвойств(СостояниеДоИзменения, Элементы.ОтчетТабличныйДокумент.ОтображениеСостояния);
	
	Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ВидимостьБыстрыхНастроек", Ложь);
	Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ВидимостьПанелиОтчетов", Не Элементы.ПанельОтчетов.Видимость);
	ВидимостьДоступностьКорректность("СкрытьПоказатьПанельОтчетов");
	
	ЗаполнитьЗначенияСвойств(Элементы.ОтчетТабличныйДокумент.ОтображениеСостояния, СостояниеДоИзменения);
КонецПроцедуры

&НаКлиенте
Процедура ВариантНажатие(Элемент)
	Найденные = ВариантыПанели.НайтиСтроки(Новый Структура("НадписьИмя", Элемент.Имя));
	Если Найденные.Количество() <> 1 Тогда
		Возврат;
	КонецЕсли;
	Вариант = Найденные[0];
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("КлючВарианта",            Вариант.КлючВарианта);
	ПараметрыОткрытия.Вставить("Раздел",                  ФормаПараметры.Раздел);
	ПараметрыОткрытия.Вставить("Подсистема",              ФормаПараметры.Подсистема);
	ПараметрыОткрытия.Вставить("ПодсистемаПредставление", ФормаПараметры.ПодсистемаПредставление);
	
	// Открытие
	Если Вариант.Дополнительный Тогда
		
		ПараметрыОткрытия.Вставить("Вариант",      Вариант.Ссылка);
		ПараметрыОткрытия.Вставить("Отчет",        Вариант.Отчет);
		ВариантыОтчетовКлиент.ОткрытьВариантДополнительногоОтчета(ПараметрыОткрытия);
		
	ИначеЕсли Не ЗначениеЗаполнено(Вариант.ИмяОтчета) Тогда
		
		ТекстПредупреждения = СтрЗаменить(НСтр("ru = 'Не заполнено имя отчета для варианта ""%1"".'"), "%1", Вариант.Наименование);
		ПоказатьПредупреждение(, ТекстПредупреждения);
		
	Иначе
		
		Уникальность = "Отчет." + Вариант.ИмяОтчета;
		Если ЗначениеЗаполнено(Вариант.КлючВарианта) Тогда
			Уникальность = Уникальность + "/КлючВарианта." + Вариант.КлючВарианта;
		КонецЕсли;
		
		ПараметрыОткрытия.Вставить("КлючПараметровПечати", Уникальность);
		ПараметрыОткрытия.Вставить("КлючСохраненияПоложенияОкна", Уникальность);
		
		ОткрытьФорму("Отчет." + Вариант.ИмяОтчета + ".Форма", ПараметрыОткрытия, Неопределено, Уникальность);
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВсеНастройки(Команда)
	
	ПараметрыФормы = Новый Структура;
	Для Каждого КлючИЗначение Из ФормаПараметры Цикл
		ПараметрыФормы.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла;
	ПараметрыФормы.Вставить("Вариант",                               Отчет.КомпоновщикНастроек.Настройки);
	ПараметрыФормы.Вставить("КлючВарианта",                          Строка(КлючТекущегоВарианта));
	ПараметрыФормы.Вставить("ПользовательскиеНастройки",             Отчет.КомпоновщикНастроек.ПользовательскиеНастройки);
	ПараметрыФормы.Вставить("ПредставлениеВарианта",                 Строка(ОтчетНаименованиеТекущегоВарианта));
	ПараметрыФормы.Вставить("ПредставлениеПользовательскихНастроек", "");
	
	ОткрытьФорму(КлючОбъекта + ".ФормаНастроек", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВариантОтчета(Команда)
	
	ПараметрыФормы = Новый Структура;
	Для Каждого КлючИЗначение Из ФормаПараметры Цикл
		ПараметрыФормы.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла;
	ПараметрыФормы.Вставить("Вариант",                               Отчет.КомпоновщикНастроек.Настройки);
	ПараметрыФормы.Вставить("КлючВарианта",                          Строка(КлючТекущегоВарианта));
	ПараметрыФормы.Вставить("ПользовательскиеНастройки",             Отчет.КомпоновщикНастроек.ПользовательскиеНастройки);
	ПараметрыФормы.Вставить("ПредставлениеВарианта",                 Строка(ОтчетНаименованиеТекущегоВарианта));
	ПараметрыФормы.Вставить("ПредставлениеПользовательскихНастроек", "");
	
	ОткрытьФорму(КлючОбъекта + ".ФормаВарианта", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СброситьНастройки(Команда)
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("Событие", Новый Структура);
	ПараметрыЗаполнения.Событие.Вставить("Имя", "СброситьНастройки");
	ПараметрыЗаполнения.Вставить("ПользовательскиеНастройкиМодифицированы", Истина);
	ПараметрыЗаполнения.Вставить("СброситьНастройки", Истина);
	
	БыстрыеНастройкиЗаполнить(ПараметрыЗаполнения);
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьПоЭлектроннойПочте(Команда)
	ОтображениеСостояния = Элементы.ОтчетТабличныйДокумент.ОтображениеСостояния;
	Если ОтображениеСостояния.Видимость = Истина
		И ОтображениеСостояния.ДополнительныйРежимОтображения = ДополнительныйРежимОтображения.Неактуальность Тогда
		ТекстВопроса = НСтр("ru = 'Отчет не сформирован. Сформировать?'");
		Обработчик = Новый ОписаниеОповещения("ОтправитьПоЭлектроннойПочтеЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60, КодВозвратаДиалога.Да);
	Иначе
		ПоказатьДиалогОтправкиПоЭлектроннойПочте();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОтчетСкомпоноватьРезультат(Команда)
	
	Если СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиента().ИнформационнаяБазаФайловая
		Или ТипЗнч(ФормаПараметры.ОтчетСсылка) = Тип("Строка") Тогда
		
		СформироватьНепосредственно();
		
	Иначе
		
		Если ФоновоеЗаданиеЗапустить() Тогда
			ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчика);
			ПодключитьОбработчикОжидания("ФоновоеЗаданиеПроверитьНаКлиенте", 1, Истина);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВычислитьСумму(Команда)
	СуммаВыделенныхЯчеек = ВычислитьСуммуСервер(ОтчетТабличныйДокумент, ВыделенныеОбласти());
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Подключаемые

&НаКлиенте
Процедура Подключаемый_Команда(Команда)
	// Механизмы расширения
	Результат = Ложь;
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.РассылкаОтчетов") Тогда
		МодульРассылкаОтчетовКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("РассылкаОтчетовКлиент");
		МодульРассылкаОтчетовКлиент.ФормаОтчетаОбработчикКоманды(ЭтотОбъект, Команда, Результат);
	КонецЕсли;
	ФормаОтчетаКлиентПереопределяемый.ОбработчикКоманды(ЭтотОбъект, Команда, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СписокСПодбором_Подбор(Команда)
	
	КнопкаПодборИмя    = Команда.Имя;
	
	ИдентификаторЭлемента  = Прав(КнопкаПодборИмя, 32);
	ШаблонГенерацииПрефикс = Лев(КнопкаПодборИмя, Найти(КнопкаПодборИмя, "_Подбор_")-1);
	
	ТаблицаИмя         = ШаблонГенерацииПрефикс + "_СписокЗначений_"   + ИдентификаторЭлемента;
	КолонкаЗначениеИмя = ШаблонГенерацииПрефикс + "_Колонка_Значение_" + ИдентификаторЭлемента;
	КоманднаяПанельИмя = ШаблонГенерацииПрефикс + "_КоманднаяПанель_"  + ИдентификаторЭлемента;
	
	ТаблицаЗначение = ЭтотОбъект[ТаблицаИмя];
	КолонкаЗначениеЭлемент = Элементы[КолонкаЗначениеИмя];
	
	ПараметрыЭлемента = Новый Структура;
	ПараметрыЭлемента.Вставить("ИдентификаторЭлемента",  ИдентификаторЭлемента);
	ПараметрыЭлемента.Вставить("ВыбранныйТип",           Неопределено);
	ПараметрыЭлемента.Вставить("КолонкаЗначениеЭлемент", КолонкаЗначениеЭлемент);
	ПараметрыЭлемента.Вставить("ТаблицаЭлемент",         Элементы[ТаблицаИмя]);
	ПараметрыЭлемента.Вставить("ВыборТолькоГрупп",       КолонкаЗначениеЭлемент.ВыборГруппИЭлементов = ГруппыИЭлементы.Группы);
	
	Если ЗначениеЗаполнено(КолонкаЗначениеЭлемент.СвязьПоТипу.ПутьКДанным) Тогда
		ЗначениеВедущего = ЭтотОбъект[КолонкаЗначениеЭлемент.СвязьПоТипу.ПутьКДанным];
		Если ЗначениеЗаполнено(ЗначениеВедущего) Тогда
			ТипВедущего = ТипЗнч(ЗначениеВедущего);
			Если ТаблицаЗначение.ТипЗначения.СодержитТип(ТипВедущего) Тогда
				// Тип определен ведущим.
				ПараметрыЭлемента.ВыбранныйТип = ТипВедущего;
				ПоказатьДиалогПодбораДляСписка(ПараметрыЭлемента);
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// Выбор типа из списка.
	СписокВыбора = Новый СписокЗначений;
	
	ПростыеТипы = Новый Соответствие;
	ПростыеТипы.Вставить(Тип("Строка"), Истина);
	ПростыеТипы.Вставить(Тип("Дата"),   Истина);
	ПростыеТипы.Вставить(Тип("Число"),  Истина);
	
	МассивТипов = ТаблицаЗначение.ТипЗначения.Типы();
	Для Каждого Тип Из МассивТипов Цикл
		// Исключение типов, для которых нет групп.
		Если ПараметрыЭлемента.ВыборТолькоГрупп Тогда
			ИмяОбъектаМетаданных = БыстрыйПоискИменОбъектовМетаданных.Получить(Тип);
			ВидОбъектаМетаданных = ВРег(Лев(ИмяОбъектаМетаданных, Найти(ИмяОбъектаМетаданных, ".")-1));
			Если ВидОбъектаМетаданных <> "СПРАВОЧНИК" И ВидОбъектаМетаданных <> "ПЛАНВИДОВХАРАКТЕРИСТИК" И ВидОбъектаМетаданных <> "ПЛАНСЧЕТОВ" Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		// Исключение простых типов.
		Если ПростыеТипы[Тип] = Истина Тогда
			Продолжить;
		КонецЕсли;
		// Добавление типа в список выбора.
		СписокВыбора.Добавить(Тип, Строка(Тип));
	КонецЦикла;
	
	Если СписокВыбора.Количество() = 0 Тогда
		ПараметрыЭлемента.ТаблицаЭлемент.ДобавитьСтроку();
		Возврат;
	ИначеЕсли СписокВыбора.Количество() = 1 Тогда
		// Один тип - выбор не требуется.
		ПараметрыЭлемента.ВыбранныйТип = СписокВыбора[0].Значение;
		ПоказатьДиалогПодбораДляСписка(ПараметрыЭлемента);
	Иначе
		// Более одного типа.
		Обработчик = Новый ОписаниеОповещения("ПоказатьДиалогПодбораДляСпискаПослеВыбораТипа", ЭтотОбъект, ПараметрыЭлемента);
		ПоказатьВыборИзМеню(Обработчик, СписокВыбора, Элементы[КоманднаяПанельИмя]);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СписокСПодбором_Добавить(Команда)
	
	КнопкаПодборИмя = Команда.Имя;
	
	ИдентификаторЭлемента  = Прав(КнопкаПодборИмя, 32);
	ШаблонГенерацииПрефикс = Лев(КнопкаПодборИмя, Найти(КнопкаПодборИмя, "_Подбор_") - 1);
	
	ТаблицаИмя = ШаблонГенерацииПрефикс + "_СписокЗначений_" + ИдентификаторЭлемента;
	
	Элементы[ТаблицаИмя].ДобавитьСтроку();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ТаблицаПорядка_ПереместитьВверх(Команда)
	Направление = -1;
	ТаблицаИмя = СтрЗаменить(Команда.Имя, "_Команда_ПереместитьВверх_", "_Таблица_");
	ПереместитьСтрокуТаблицы(Направление, ТаблицаИмя);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ТаблицаПорядка_ПереместитьВниз(Команда)
	Направление = 1;
	ТаблицаИмя = СтрЗаменить(Команда.Имя, "_Команда_ПереместитьВниз_", "_Таблица_");
	ПереместитьСтрокуТаблицы(Направление, ТаблицаИмя);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ТаблицаПорядка_СортироватьПоВозрастанию(Команда)
	
	// Установка направления сортировки.
	ИзменитьНаправлениеСортировкиТаблицы(Команда.Имя, НаправлениеСортировкиКомпоновкиДанных.Возр);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ТаблицаПорядка_СортироватьПоУбыванию(Команда)
	
	// Установка направления сортировки.
	ИзменитьНаправлениеСортировкиТаблицы(Команда.Имя, НаправлениеСортировкиКомпоновкиДанных.Убыв);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// Клиент

&НаКлиенте
Процедура ВыборПериодаИзВыпадающегоСписка(Результат, ПараметрыВыбора) Экспорт
	Если Результат = Неопределено Тогда
		Возврат; // Отмена выбора.
	КонецЕсли;
	
	Если Результат = -1 Тогда // Начало выбора.
		// Чтение параметров для формирования списка из сохраненного значения периода.
		ПараметрыВыбора.Вставить("НачалоПериода", ПараметрыВыбора.Значение.ДатаНачала);
		ПараметрыВыбора.Вставить("Вариант",       ПараметрыВыбора.Значение.Вариант);
		ИндексНачальногоЗначения = Неопределено;
	ИначеЕсли ТипЗнч(Результат.Значение) = Тип("Структура") Тогда
		// Чтение параметров для формирования списка из выбранного значения.
		ПараметрыВыбора.Вставить("НачалоПериода", Результат.Значение.НачалоПериода);
		ПараметрыВыбора.Вставить("Вариант",       Результат.Значение.Вариант);
		ИндексНачальногоЗначения = Результат.Значение.ИндексНачальногоЗначения;
	Иначе
		// Загрузка результата выбора.
		ЗагрузитьРезультатВыбораПериодаИзВыпадающегоСписка(Результат, ПараметрыВыбора);
		Возврат;
	КонецЕсли;
	
	// Формирование списка выбора.
	Если ПараметрыВыбора.Вариант = Неопределено Или ПараметрыВыбора.Вариант = ВариантСтандартногоПериода.ПроизвольныйПериод Тогда
		
		СписокПериодов = ФункцииОтчетовКлиентСервер.СписокФиксированныхПериодов(ПараметрыВыбора.НачалоПериода, ПараметрыВыбора.ВидПериода);
		
		Если ИндексНачальногоЗначения = Неопределено Тогда
			ИндексНачальногоЗначения = СписокПериодов.НайтиПоЗначению(ПараметрыВыбора.НачалоПериода);
		КонецЕсли;
		
		ОписаниеНавигационногоЭлемента = Новый Структура;
		ОписаниеНавигационногоЭлемента.Вставить("НачалоПериода",            СписокПериодов[0].Значение);
		ОписаниеНавигационногоЭлемента.Вставить("Вариант",                  Неопределено);
		ОписаниеНавигационногоЭлемента.Вставить("ИндексНачальногоЗначения", 0);
		СписокПериодов[0].Значение = ОписаниеНавигационногоЭлемента;
		
		ОписаниеНавигационногоЭлемента = Новый Структура;
		ОписаниеНавигационногоЭлемента.Вставить("НачалоПериода",            СписокПериодов[8].Значение);
		ОписаниеНавигационногоЭлемента.Вставить("Вариант",                  Неопределено);
		ОписаниеНавигационногоЭлемента.Вставить("ИндексНачальногоЗначения", 8);
		СписокПериодов[8].Значение = ОписаниеНавигационногоЭлемента;
		
		Если Не ПараметрыВыбора.Свойство("ВариантСтандартногоПериодаПоВиду") Тогда
			ПараметрыВыбора.Вставить("ВариантСтандартногоПериодаПоВиду", ВариантСтандартногоПериодаПоВиду(ПараметрыВыбора.ВидПериода));
		КонецЕсли;
		
		ОписаниеНавигационногоЭлемента = Новый Структура;
		ОписаниеНавигационногоЭлемента.Вставить("НачалоПериода",            ПараметрыВыбора.НачалоПериода);
		ОписаниеНавигационногоЭлемента.Вставить("Вариант",                  ПараметрыВыбора.ВариантСтандартногоПериодаПоВиду);
		ОписаниеНавигационногоЭлемента.Вставить("ИндексНачальногоЗначения", Неопределено);
		СписокПериодов.Добавить(ОписаниеНавигационногоЭлемента, НСтр("ru = 'Относительный...'"));
		
	Иначе
		
		СписокПериодов = ФункцииОтчетовКлиентСервер.СписокВычисляемыхПериодов(ПараметрыВыбора.ВидПериода);
		
		Если ИндексНачальногоЗначения = Неопределено Тогда
			ИндексНачальногоЗначения = СписокПериодов.НайтиПоЗначению(ПараметрыВыбора.Вариант);
		КонецЕсли;
		
		ОписаниеНавигационногоЭлемента = Новый Структура;
		ОписаниеНавигационногоЭлемента.Вставить("НачалоПериода",            ПараметрыВыбора.НачалоПериода);
		ОписаниеНавигационногоЭлемента.Вставить("Вариант",                  Неопределено);
		ОписаниеНавигационногоЭлемента.Вставить("ИндексНачальногоЗначения", Неопределено);
		СписокПериодов.Добавить(ОписаниеНавигационногоЭлемента, НСтр("ru = 'Фиксированный...'"));
		
	КонецЕсли;
	
	Если ИндексНачальногоЗначения = Неопределено Тогда
		ИндексНачальногоЗначения = СписокПериодов.Количество() - 1;
	КонецЕсли;
	
	ПеременныеКлиента.Вставить("ВыборПериодаИзВыпадающегоСписка", Новый Структура);
	ПеременныеКлиента.ВыборПериодаИзВыпадающегоСписка.Вставить("ПараметрыВыбора", ПараметрыВыбора);
	ПеременныеКлиента.ВыборПериодаИзВыпадающегоСписка.Вставить("СписокПериодов", СписокПериодов);
	ПеременныеКлиента.ВыборПериодаИзВыпадающегоСписка.Вставить("ИндексНачальногоЗначения", ИндексНачальногоЗначения);
	Если Результат = -1 Тогда
		НачатьВыборПериодаИзВыпадающегоСписка();
	Иначе
		ПодключитьОбработчикОжидания("НачатьВыборПериодаИзВыпадающегоСписка", 0.1, Истина);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НачатьВыборПериодаИзВыпадающегоСписка()
	Если ПеременныеКлиента.Свойство("ВыборПериодаИзВыпадающегоСписка") Тогда
		Контекст = ПеременныеКлиента.ВыборПериодаИзВыпадающегоСписка;
		ПеременныеКлиента.Удалить("ВыборПериодаИзВыпадающегоСписка");
		Обработчик = Новый ОписаниеОповещения("ВыборПериодаИзВыпадающегоСписка", ЭтотОбъект, Контекст.ПараметрыВыбора);
		ПоказатьВыборИзСписка(Обработчик, Контекст.СписокПериодов, Контекст.ПараметрыВыбора.Элемент, Контекст.ИндексНачальногоЗначения);
	КонецЕслИ;
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьРезультатВыбораПериодаИзВыпадающегоСписка(Результат, ПараметрыВыбора)
	Значение = ПараметрыВыбора.Значение;
	
	// Запись результата выбора в данные формы и пользовательские настройки КД.
	Если ТипЗнч(Результат.Значение) = Тип("ВариантСтандартногоПериода") Тогда
		ЭтотОбъект[ПараметрыВыбора.ПериодПредставлениеИмя] = ?(ПустаяСтрока(Результат.Представление), Строка(Результат.Значение), Результат.Представление);
		Значение.Вариант = Результат.Значение;
	Иначе
		НачалоПериода = ФункцииОтчетовКлиентСервер.НачалоПериодаОтчета(ПараметрыВыбора.ВидПериода, Результат.Значение);
		КонецПериода  = ФункцииОтчетовКлиентСервер.КонецПериодаОтчета(ПараметрыВыбора.ВидПериода, Результат.Значение);
		
		ЭтотОбъект[ПараметрыВыбора.ПериодПредставлениеИмя] = Результат.Представление;
		
		Значение.Вариант = ВариантСтандартногоПериода.ПроизвольныйПериод;
		Значение.ДатаНачала    = НачалоПериода;
		Значение.ДатаОкончания = КонецПериода;
	КонецЕсли;
	
	ЭтотОбъект[ПараметрыВыбора.ПериодЗначениеИмя] = Значение;
	
	ПользовательскаяНастройкаКД = НайтиПользовательскуюНастройкуЭлемента(ПараметрыВыбора.ИдентификаторЭлемента);
	Если ПараметрыВыбора.ЭтоПараметр Тогда
		ПользовательскаяНастройкаКД.Значение = Значение;
	Иначе
		ПользовательскаяНастройкаКД.ПравоеЗначение = Значение;
	КонецЕсли;
	
	Если Элементы.Найти(ПараметрыВыбора.ИспользованиеИмя) <> Неопределено Тогда
		ЭтотОбъект[ПараметрыВыбора.ИспользованиеИмя] = Истина;
		ПользовательскаяНастройкаКД.Использование = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ВариантСтандартногоПериодаПоВиду(ВидПериода)
	Если ВидПериода = ПредопределенноеЗначение("Перечисление.ДоступныеПериодыОтчета.День") Тогда
		Возврат ВариантСтандартногоПериода.Сегодня;
	ИначеЕсли ВидПериода = ПредопределенноеЗначение("Перечисление.ДоступныеПериодыОтчета.Неделя") Тогда
		Возврат ВариантСтандартногоПериода.ЭтаНеделя;
	ИначеЕсли ВидПериода = ПредопределенноеЗначение("Перечисление.ДоступныеПериодыОтчета.Декада") Тогда
		Возврат ВариантСтандартногоПериода.ЭтаДекада;
	ИначеЕсли ВидПериода = ПредопределенноеЗначение("Перечисление.ДоступныеПериодыОтчета.Месяц") Тогда
		Возврат ВариантСтандартногоПериода.ЭтотМесяц;
	ИначеЕсли ВидПериода = ПредопределенноеЗначение("Перечисление.ДоступныеПериодыОтчета.Квартал") Тогда
		Возврат ВариантСтандартногоПериода.ЭтотКвартал;
	ИначеЕсли ВидПериода = ПредопределенноеЗначение("Перечисление.ДоступныеПериодыОтчета.Полугодие") Тогда
		Возврат ВариантСтандартногоПериода.ЭтоПолугодие;
	ИначеЕсли ВидПериода = ПредопределенноеЗначение("Перечисление.ДоступныеПериодыОтчета.Год") Тогда
		Возврат ВариантСтандартногоПериода.ЭтотГод;
	КонецЕсли;
КонецФункции

&НаКлиенте
Процедура ПереместитьСтрокуТаблицы(Направление, ТаблицаИмя)
	
	СтрокаТаблицы = Элементы[ТаблицаИмя].ТекущиеДанные;
	Если СтрокаТаблицы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Таблица = ЭтотОбъект[ТаблицаИмя];
	ИндексСтрокиТаблицы = Таблица.Индекс(СтрокаТаблицы);
	Если ИндексСтрокиТаблицы + Направление < 0 ИЛИ ИндексСтрокиТаблицы + Направление >= Таблица.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	ПользовательскаяНастройкаКД = НайтиПользовательскуюНастройкуЭлемента(ТаблицаИмя);
	СтрокаПользовательскойНастройки = ФункцииОтчетовКлиентСервер.НайтиВложеннуюПользовательскуюНастройку(ПользовательскаяНастройкаКД, СтрокаТаблицы.Идентификатор);
	
	ПользовательскаяНастройкаКД.Элементы.Сдвинуть(СтрокаПользовательскойНастройки, Направление);
	Таблица.Сдвинуть(ИндексСтрокиТаблицы, Направление);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьНаправлениеСортировкиТаблицы(ИмяЭлемента, Направление)
	
	// Формирование сведений о таблице.
	ИдентификаторЭлемента  = Прав(ИмяЭлемента, 32);
	ШаблонГенерацииПрефикс = Лев(ИмяЭлемента, Найти(ИмяЭлемента, "_")-1);
	ТаблицаИмя = ШаблонГенерацииПрефикс + "_Таблица_" + ИдентификаторЭлемента;
	
	// Получение данных.
	СтрокаТаблицы = Элементы[ТаблицаИмя].ТекущиеДанные;
	Если СтрокаТаблицы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПользовательскаяНастройкаКД = НайтиПользовательскуюНастройкуЭлемента(ИдентификаторЭлемента);
	СтрокаПользовательскойНастройки = ФункцииОтчетовКлиентСервер.НайтиВложеннуюПользовательскуюНастройку(ПользовательскаяНастройкаКД, СтрокаТаблицы.Идентификатор);
	Если СтрокаПользовательскойНастройки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// Заполнение необязательных параметров.
	Если Направление = Неопределено Тогда
		Если СтрокаПользовательскойНастройки.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр Тогда
			Направление = НаправлениеСортировкиКомпоновкиДанных.Убыв;
		Иначе
			Направление = НаправлениеСортировкиКомпоновкиДанных.Возр;
		КонецЕсли;
	КонецЕсли;
	
	// Изменение направления сортировки.
	СтрокаПользовательскойНастройки.ТипУпорядочивания = Направление;
	СтрокаТаблицы.Направление = Направление;
	
КонецПроцедуры

&НаКлиенте
Функция НайтиПользовательскуюНастройкуЭлемента(ИмяИлиИдентификаторЭлемента)
	// Для пользовательских настроек хранятся идентификаторы компоновки данных,
	//  поскольку они не могут храниться по ссылке (происходит копирование значения).
	Если СтрДлина(ИмяИлиИдентификаторЭлемента) = 32 Тогда
		ИдентификаторЭлемента = ИмяИлиИдентификаторЭлемента;
	Иначе
		ИдентификаторЭлемента = Прав(ИмяИлиИдентификаторЭлемента, 32);
	КонецЕсли;
	ИдентификаторКД = БыстрыйПоискПользовательскихНастроек.Получить(ИдентификаторЭлемента);
	Возврат Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.ПолучитьОбъектПоИдентификатору(ИдентификаторКД);
КонецФункции

&НаКлиенте
Функция ВыполнитьКонтекстныйВызовСервера(КлючОперации, ПараметрыОперации) Экспорт // Исключение из стандартов разработки
	// Программный интерфейс для контекстного вызова сервера из клиентского общего модуля.
	
	Возврат КонтекстныйВызовСервера(КлючОперации, ПараметрыОперации);
	
КонецФункции

&НаКлиенте
Процедура ФоновоеЗаданиеПроверитьНаКлиенте()
	Если ФоновоеЗаданиеВыполнено() Тогда
		ПоказатьОповещениеПользователя(НСтр("ru = 'Отчет сформирован'"), , Заголовок);
	Иначе
		ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчика);
		ПодключитьОбработчикОжидания("ФоновоеЗаданиеПроверитьНаКлиенте", ПараметрыОбработчика.ТекущийИнтервал, Истина);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_РассчитатьСуммуЯчеек()
	Сумма = РассчитатьСуммуВыделенныхЯчеек(ОтчетТабличныйДокумент, Неопределено);
	Если ТипЗнч(Сумма) = Тип("Число") Тогда
		СуммаВыделенныхЯчеек = Формат(Сумма, "ЧН=0");
	Иначе
		СуммаВыделенныхЯчеек = "-";
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция ВыделенныеОбласти()
	Результат = Новый Массив;
	Для Каждого ВыделеннаяОбласть Из ОтчетТабличныйДокумент.ВыделенныеОбласти Цикл
		Если ТипЗнч(ВыделеннаяОбласть) <> Тип("ОбластьЯчеекТабличногоДокумента") Тогда
			Продолжить;
		КонецЕсли;
		Структура = Новый Структура("Верх, Низ, Лево, Право, ТипОбласти");
		ЗаполнитьЗначенияСвойств(Структура, ВыделеннаяОбласть);
		Результат.Добавить(Структура);
	КонецЦикла;
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура ОтправитьПоЭлектроннойПочтеЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	Если Ответ = КодВозвратаДиалога.Да Тогда
		СформироватьНепосредственно();
		ПоказатьДиалогОтправкиПоЭлектроннойПочте();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьДиалогОтправкиПоЭлектроннойПочте()
	ТабличныеДокументы = Новый СписокЗначений;
	ТабличныеДокументы.Добавить(ЭтотОбъект.ОтчетТабличныйДокумент, ЭтотОбъект.ОтчетНаименованиеТекущегоВарианта);
	
	ФормаЗаголовок = СтрЗаменить(НСтр("ru = 'Отправка отчета ""%1"" по почте'"), "%1", ЭтотОбъект.ОтчетНаименованиеТекущегоВарианта);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТабличныеДокументы", ТабличныеДокументы);
	ПараметрыФормы.Вставить("Тема",               ЭтотОбъект.ОтчетНаименованиеТекущегоВарианта);
	ПараметрыФормы.Вставить("Заголовок",          ФормаЗаголовок);
	
	ОткрытьФорму("ОбщаяФорма.ОтправкаТабличныхДокументовПоПочте", ПараметрыФормы, , );
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьДиалогПодбораДляСпискаПослеВыбораТипа(ВыбранныйЭлемент, ПараметрыЭлемента) Экспорт
	Если ВыбранныйЭлемент <> Неопределено Тогда
		ПараметрыЭлемента.ВыбранныйТип = ВыбранныйЭлемент.Значение;
		ПоказатьДиалогПодбораДляСписка(ПараметрыЭлемента);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьДиалогПодбораДляСписка(ПараметрыЭлемента)
	КолонкаЗначениеЭлемент = ПараметрыЭлемента.КолонкаЗначениеЭлемент;
	
	// Полное имя формы выбора.
	// Свойство "ФормаВыбора" недоступно на клиенте даже для чтения,
	//   поэтому для хранения предустановленных имен форм выбора используется коллекция БыстрыйПоискИменОбъектовМетаданных.
	ПутьКФорме = БыстрыйПоискИменОбъектовМетаданных.Получить(ПараметрыЭлемента.ИдентификаторЭлемента);
	Если Не ЗначениеЗаполнено(ПутьКФорме) Тогда
		ИмяОбъектаМетаданных = БыстрыйПоискИменОбъектовМетаданных.Получить(ПараметрыЭлемента.ВыбранныйТип);
		Если ПараметрыЭлемента.ВыборТолькоГрупп Тогда
			ВидОбъектаМетаданных = ВРег(Лев(ИмяОбъектаМетаданных, Найти(ИмяОбъектаМетаданных, ".")-1));
			Если ВидОбъектаМетаданных = "СПРАВОЧНИК" Или ВидОбъектаМетаданных = "ПЛАНВИДОВХАРАКТЕРИСТИК" Тогда
				ПутьКФорме = ИмяОбъектаМетаданных + ".ФормаВыбораГруппы";
			Иначе
				ПутьКФорме = ИмяОбъектаМетаданных + ".ФормаВыбора";
			КонецЕсли;
		Иначе
			ПутьКФорме = ИмяОбъектаМетаданных + ".ФормаВыбора";
		КонецЕсли;
	КонецЕсли;
	
	ВыборГруппИЭлементов = ФункцииОтчетовКлиентСервер.ПривестиЗначениеКТипуИспользованиеГруппИЭлементов(КолонкаЗначениеЭлемент.ВыборГруппИЭлементов);
	
	ПараметрыФормыВыбора = Новый Структура;
	// Стандартные параметры формы.
	ПараметрыФормыВыбора.Вставить("ЗакрыватьПриВыборе",            Ложь);
	ПараметрыФормыВыбора.Вставить("ЗакрыватьПриЗакрытииВладельца", Истина);
	ПараметрыФормыВыбора.Вставить("Отбор",                         Новый Структура);
	// Стандартные параметры формы выбора (см. Расширение управляемой формы для динамического списка).
	ПараметрыФормыВыбора.Вставить("ВыборГруппИЭлементов",          ВыборГруппИЭлементов);
	ПараметрыФормыВыбора.Вставить("МножественныйВыбор",            Истина);
	ПараметрыФормыВыбора.Вставить("РежимВыбора",                   Истина);
	// Предполагаемые реквизиты.
	ПараметрыФормыВыбора.Вставить("РежимОткрытияОкна",             РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	ПараметрыФормыВыбора.Вставить("РазрешитьНачалоПеретаскивания", Ложь);
	
	// Добавление фиксированных параметров выбора.
	Для Каждого ПараметрВыбора Из КолонкаЗначениеЭлемент.ПараметрыВыбора Цикл
		Если ПустаяСтрока(ПараметрВыбора.Имя) Тогда
			Продолжить;
		КонецЕсли;
		Если ЗначениеЗаполнено(ПараметрВыбора.Имя) Тогда
			Если ВРег(Лев(ПараметрВыбора.Имя, 6)) = ВРег("Отбор.") Тогда
				ПараметрыФормыВыбора.Отбор.Вставить(Сред(ПараметрВыбора.Имя, 7), ПараметрВыбора.Значение);
			Иначе
				ПараметрыФормыВыбора.Вставить(ПараметрВыбора.Имя, ПараметрВыбора.Значение);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	// Добавление динамических параметров выбора (от ведущих).
	Для Каждого СвязьПараметраВыбора Из КолонкаЗначениеЭлемент.СвязиПараметровВыбора Цикл
		Если ПустаяСтрока(СвязьПараметраВыбора.Имя) Тогда
			Продолжить;
		КонецЕсли;
		ВедущийЗначение = ЭтотОбъект[СвязьПараметраВыбора.ПутьКДанным];
		Если ВРег(Лев(СвязьПараметраВыбора.Имя, 6)) = ВРег("Отбор.") Тогда
			ПараметрыФормыВыбора.Отбор.Вставить(Сред(СвязьПараметраВыбора.Имя, 7), ВедущийЗначение);
		Иначе
			ПараметрыФормыВыбора.Вставить(СвязьПараметраВыбора.Имя, ВедущийЗначение);
		КонецЕсли;
	КонецЦикла;
	
	ОткрытьФорму(ПутьКФорме, ПараметрыФормыВыбора, ПараметрыЭлемента.ТаблицаЭлемент);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Клиент или сервер

&НаКлиентеНаСервереБезКонтекста
Функция ПривестиИдентификаторКИмени(Идентификатор)
	Возврат СтрЗаменить(Строка(Идентификатор), "-", "");
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция РассчитатьСуммуВыделенныхЯчеек(ТабличныйДокумент, ВыделенныеОбласти)
	
	#Если Клиент Тогда
		ВыделенныеОбласти = ТабличныйДокумент.ВыделенныеОбласти;
	#КонецЕсли
	
	#Если Клиент И Не ТолстыйКлиентОбычноеПриложение Тогда
		КоличествоВыделенныхОбластей = ВыделенныеОбласти.Количество();
		Если КоличествоВыделенныхОбластей = 0 Тогда
			Возврат 0;
		ИначеЕсли КоличествоВыделенныхОбластей >= 100 Тогда
			Возврат Неопределено; // Нужен вызов сервера.
		КонецЕсли;
		КоличествоВыделенныхЯчеек = 0;
	#КонецЕсли
	
	Сумма = 0;
	ПроверенныеЯчейки = Новый Соответствие;
	
	Для Каждого ВыделеннаяОбласть Из ВыделенныеОбласти Цикл
		#Если Клиент Тогда
			Если ТипЗнч(ВыделеннаяОбласть) <> Тип("ОбластьЯчеекТабличногоДокумента") Тогда
				Продолжить;
			КонецЕсли;
		#КонецЕсли
		
		ВыделеннаяОбластьВерх = ВыделеннаяОбласть.Верх;
		ВыделеннаяОбластьНиз = ВыделеннаяОбласть.Низ;
		ВыделеннаяОбластьЛево = ВыделеннаяОбласть.Лево;
		ВыделеннаяОбластьПраво = ВыделеннаяОбласть.Право;
		
		Если ВыделеннаяОбластьВерх = 0 Тогда
			ВыделеннаяОбластьВерх = 1;
		КонецЕсли;
		
		Если ВыделеннаяОбластьНиз = 0 Тогда
			ВыделеннаяОбластьНиз = ТабличныйДокумент.ВысотаТаблицы;
		КонецЕсли;
		
		Если ВыделеннаяОбластьЛево = 0 Тогда
			ВыделеннаяОбластьЛево = 1;
		КонецЕсли;
		
		Если ВыделеннаяОбластьПраво = 0 Тогда
			ВыделеннаяОбластьПраво = ТабличныйДокумент.ШиринаТаблицы;
		КонецЕсли;
		
		Если ВыделеннаяОбласть.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Колонки Тогда
			ВыделеннаяОбластьВерх = ВыделеннаяОбласть.Низ;
			ВыделеннаяОбластьНиз = ТабличныйДокумент.ВысотаТаблицы;
		КонецЕсли;
		
		ВыделеннаяОбластьВысота = ВыделеннаяОбластьНиз   - ВыделеннаяОбластьВерх;
		ВыделеннаяОбластьШирина = ВыделеннаяОбластьПраво - ВыделеннаяОбластьЛево;
		
		#Если Клиент И Не ТолстыйКлиентОбычноеПриложение Тогда
			КоличествоВыделенныхЯчеек = КоличествоВыделенныхЯчеек + ВыделеннаяОбластьШирина * ВыделеннаяОбластьВысота;
			Если КоличествоВыделенныхЯчеек >= 1000 Тогда
				Возврат Неопределено; // Нужен вызов сервера.
			КонецЕсли;
		#КонецЕсли
		
		Для НомерКолонки = ВыделеннаяОбластьЛево По ВыделеннаяОбластьПраво Цикл
			Для НомерСтроки = ВыделеннаяОбластьВерх По ВыделеннаяОбластьНиз Цикл
				Ячейка = ТабличныйДокумент.Область(НомерСтроки, НомерКолонки, НомерСтроки, НомерКолонки);
				Если ПроверенныеЯчейки.Получить(Ячейка.Имя) = Неопределено Тогда
					ПроверенныеЯчейки.Вставить(Ячейка.Имя, Истина);
				Иначе
					Продолжить;
				КонецЕсли;
				
				Если Ячейка.Видимость = Истина Тогда
					Если Ячейка.ТипОбласти <> ТипОбластиЯчеекТабличногоДокумента.Колонки
						И Ячейка.СодержитЗначение И ТипЗнч(Ячейка.Значение) = Тип("Число") Тогда
						Сумма = Сумма + Ячейка.Значение;
					ИначеЕсли ЗначениеЗаполнено(Ячейка.Текст) Тогда
						Сумма = Сумма + СтрокаВЧисло(Ячейка.Текст);
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	Возврат Сумма;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СтрокаВЧисло(ИсходнаяСтрока)
	// Превращает строку в число без вызова исключений. Стандартная функция преобразования
	//   Число() строго контролирует отсутствие каких-либо символов кроме числовых.
	
	Результат = 0;
	ЗнаковПослеЗапятой = -1;
	ЗнакОтрицательный = Ложь;
	Для НомерСимвола = 1 По СтрДлина(ИсходнаяСтрока) Цикл
		КодСимвола = КодСимвола(ИсходнаяСтрока, НомерСимвола);
		Если КодСимвола = 32 Или КодСимвола = 160 Тогда // Пробел или неразрывный пробел.
			// Пропуск (действие не требуется).
		ИначеЕсли КодСимвола = 45 Тогда // Минус.
			Если Результат <> 0 Тогда
				Возврат 0;
			КонецЕсли;
			ЗнакОтрицательный = Истина;
		ИначеЕсли КодСимвола = 44 Или КодСимвола = 46 Тогда // Запятая или точка.
			Если ЗнаковПослеЗапятой <> -1 Тогда
				Возврат 0; // Разделитель уже был, следовательно это не число.
			КонецЕсли;
			ЗнаковПослеЗапятой = 0; // Запуск отсчета знаков после запятой.
		ИначеЕсли КодСимвола > 47 И КодСимвола < 58 Тогда // Число.
			Если ЗнаковПослеЗапятой <> -1 Тогда
				ЗнаковПослеЗапятой = ЗнаковПослеЗапятой + 1;
			КонецЕсли;
			Число = КодСимвола - 48;
			Результат = Результат * 10 + Число;
		Иначе
			Возврат 0;
		КонецЕсли;
	КонецЦикла;
	
	Если ЗнаковПослеЗапятой > 0 Тогда
		Результат = Результат / Pow(10, ЗнаковПослеЗапятой);
	КонецЕсли;
	Если ЗнакОтрицательный Тогда
		Результат = -Результат;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Вызов сервера

&НаСервере
Процедура ВидимостьДоступностьКорректность(Изменения = "")
	// Быстрые настройки
	Если Изменения = ""
		Или Изменения = "СкрытьПоказатьБыстрыеНастройки"
		Или Изменения = "СкрытьПоказатьПанельОтчетов"
		Или Изменения = "СкрытьПоказатьНастройкиИДругиеОтчеты" Тогда
		
		// Вычисление параметров отображения.
		ПользовательскиеНастройкиКД = Отчет.КомпоновщикНастроек.ПользовательскиеНастройки;
		ПоказыватьКомандыВариантовОтчетов = РежимВариантаОтчета И ВидимостьКомандВариантовОтчетов;
		ЕстьМедленныеНастройки = РежимВариантаОтчета И ПользовательскиеНастройкиКД.Элементы.Количество() > 0;
		
		// Применение параметров отображения.
		Элементы.ВсеНастройки.Видимость = ЕстьМедленныеНастройки;
		Элементы.КомандыВариантовОтчета.Видимость = ПоказыватьКомандыВариантовОтчетов;
		Элементы.КомандыВариантовПользовательскихНастроек.Видимость = ПоказыватьКомандыВариантовОтчетов И ЕстьМедленныеНастройки;
		
		Если ПоказыватьКомандыВариантовОтчетов Тогда
			ЗаполнитьПанельОтчета();
		КонецЕсли;
		
		ВидимостьПравойПанели = Неопределено;
		ПользовательскиеНастройкиКД.ДополнительныеСвойства.Свойство("ВидимостьПравойПанели", ВидимостьПравойПанели);
		Если ТипЗнч(ВидимостьПравойПанели) <> Тип("Булево") Тогда
			ВидимостьПравойПанели = Истина;
		КонецЕсли;
		
		Элементы.СтраницаБыстрыеНастройки.Видимость = ЕстьБыстрыеНастройки;
		Элементы.СтраницаДругиеОтчеты.Видимость = ЕстьДругиеОтчеты;
		Если ЕстьБыстрыеНастройки И ЕстьДругиеОтчеты Тогда
			Элементы.СкрытьПоказатьНастройкиИДругиеОтчеты.Видимость = Истина;
			Элементы.СкрытьПоказатьНастройкиИДругиеОтчеты.Заголовок = НСтр("ru = 'Настройки и другие отчеты'");
			Элементы.ПраваяПанель.ОтображениеСтраниц = ОтображениеСтраницФормы.ЗакладкиСверху;
		ИначеЕсли ЕстьБыстрыеНастройки Тогда
			Элементы.СкрытьПоказатьНастройкиИДругиеОтчеты.Видимость = Истина;
			Элементы.СкрытьПоказатьНастройкиИДругиеОтчеты.Заголовок = НСтр("ru = 'Настройки'");
			Элементы.ПраваяПанель.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
		ИначеЕсли ЕстьДругиеОтчеты Тогда
			Элементы.СкрытьПоказатьНастройкиИДругиеОтчеты.Видимость = Истина;
			Элементы.СкрытьПоказатьНастройкиИДругиеОтчеты.Заголовок = НСтр("ru = 'Другие отчеты'");
			Элементы.ПраваяПанель.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
		Иначе
			Элементы.СкрытьПоказатьНастройкиИДругиеОтчеты.Видимость = Ложь;
			ВидимостьПравойПанели = Ложь;
		КонецЕсли;
		
		Если Элементы.ПраваяПанель.Видимость <> ВидимостьПравойПанели Тогда
			Элементы.ПраваяПанель.Видимость = ВидимостьПравойПанели;
		КонецЕсли;
		Элементы.СкрытьПоказатьНастройкиИДругиеОтчеты.Пометка = ВидимостьПравойПанели;
		
	КонецЕсли;
	
	// Заголовок
	Если Изменения = "" ИЛИ Изменения = "ВариантОтчета" Тогда
		ОтчетНаименованиеТекущегоВарианта = СокрЛП(ОтчетНаименованиеТекущегоВарианта);
		Если ЗначениеЗаполнено(ОтчетНаименованиеТекущегоВарианта) Тогда
			Заголовок = ОтчетНаименованиеТекущегоВарианта;
		Иначе
			Заголовок = НаименованиеОтчета;
		Конецесли;
		Если РежимРасшифровки Тогда
			Заголовок = РежимРасшифровкиПредставлениеВладельца + " (" + НРег(Заголовок) + ")";
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура БыстрыеНастройкиЗаполнить(Знач ПараметрыЗаполнения)
	
	// Вызов переопределяемого модуля.
	ФормаОтчетаПереопределяемый.ПередЗаполнениемПанелиБыстрыхНастроек(ЭтотОбъект, ПараметрыЗаполнения);
	
	// Запись новых настроек варианта и пользовательских настроек в компоновщик.
	БыстрыеНастройкиЗагрузитьНастройкиВКомпоновщик(ПараметрыЗаполнения);
	
	// Удаление элементов старых настроек.
	БыстрыеНастройкиУдалитьСтарыеЭлементыИКоманды(ПараметрыЗаполнения);
	
	// Добавление элементов актуальных настроек и загрузка значений.
	БыстрыеНастройкиСоздатьЭлементыУправленияИЗагрузитьЗначения(ПараметрыЗаполнения);
	
	// Заголовок и свойства элементов.
	ВидимостьДоступностьКорректность();
	
	// Вызов переопределяемого модуля.
	ФормаОтчетаПереопределяемый.ПослеЗаполненияПанелиБыстрыхНастроек(ЭтотОбъект, ПараметрыЗаполнения);
	
КонецПроцедуры

&НаСервере
Функция КонтекстныйВызовСервера(КлючОперации, ПараметрыОперации)
	РезультатВызова = Новый Структура;
	ФормаОтчетаПереопределяемый.КонтекстныйВызовСервера(ЭтотОбъект, КлючОперации, ПараметрыОперации, РезультатВызова);
	Возврат РезультатВызова;
КонецФункции

&НаСервере
Процедура СформироватьНепосредственно()
	
	ДопСвойства = Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства;
	
	// Формирование отчета.
	ДопСвойства.Вставить("КлючВарианта", КлючТекущегоВарианта);
	
	ПараметрыПечатиДоФормирования = НастройкиОтчета.ПараметрыПечатиПоУмолчанию;
	ЗаполнитьЗначенияСвойств(ПараметрыПечатиДоФормирования, ОтчетТабличныйДокумент);
	
	ИнформацияОбОшибке = Неопределено;
	Попытка
		СкомпоноватьРезультат(РежимКомпоновкиРезультата.Авто);
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
	КонецПопытки;
	ДопСвойства.Удалить("КлючВарианта");
	
	ЗаполнитьЗначенияСвойств(ОтчетТабличныйДокумент, ПараметрыПечатиДоФормирования);
	
	Если ИнформацияОбОшибке <> Неопределено Тогда
		ПоказатьОшибкиФормирования(ИнформацияОбОшибке);
		Возврат;
	КонецЕсли;
	
	// Перезаполнение настроек.
	РезультатФормирования = Новый Структура;
	РезультатФормирования.Вставить("ВариантМодифицирован", Ложь);
	РезультатФормирования.Вставить("ПользовательскиеНастройкиМодифицированы", Ложь);
	Если ВариантыОтчетовКлиентСервер.СвойствоСтруктуры(ДопСвойства, "ВариантМодифицирован") = Истина Тогда
		РезультатФормирования.ВариантМодифицирован = Истина;
	КонецЕсли;
	Если РезультатФормирования.ВариантМодифицирован
		Или ВариантыОтчетовКлиентСервер.СвойствоСтруктуры(ДопСвойства, "ПользовательскиеНастройкиМодифицированы") = Истина Тогда
		РезультатФормирования.ПользовательскиеНастройкиМодифицированы = Истина;
	КонецЕсли;
	ДопСвойства.Удалить("ВариантМодифицирован");
	ДопСвойства.Удалить("ПользовательскиеНастройкиМодифицированы");
	
	Если РезультатФормирования.ВариантМодифицирован
		Или РезультатФормирования.ПользовательскиеНастройкиМодифицированы Тогда
		РезультатФормирования.Вставить("Событие", Новый Структура);
		РезультатФормирования.Событие.Вставить("Имя", "ПослеФормирования");
		РезультатФормирования.Событие.Вставить("Непосредственно", Истина);
		БыстрыеНастройкиЗаполнить(РезультатФормирования);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ФоновоеЗаданиеЗапустить()
	Если Не ПроверитьЗаполнение() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ДопСвойства = Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства;
	ДопСвойства.Вставить("КлючВарианта", КлючТекущегоВарианта);
	
	// Запуск фонового задания
	ПараметрыФормированияОтчета = Новый Структура;
	ПараметрыФормированияОтчета.Вставить("ОтчетСсылка", ФормаПараметры.ОтчетСсылка);
	ПараметрыФормированияОтчета.Вставить("Настройки",                 Отчет.КомпоновщикНастроек.Настройки);
	ПараметрыФормированияОтчета.Вставить("ФиксированныеНастройки",    Отчет.КомпоновщикНастроек.ФиксированныеНастройки);
	ПараметрыФормированияОтчета.Вставить("ПользовательскиеНастройки", Отчет.КомпоновщикНастроек.ПользовательскиеНастройки);
	
	Попытка
		РезультатФоновогоЗадания = ДлительныеОперации.ЗапуститьВыполнениеВФоне(
			УникальныйИдентификатор,
			"ВариантыОтчетов.СформироватьОтчет",
			ПараметрыФормированияОтчета,
			НСтр("ru = 'Варианты отчетов: Формирование отчета'"));
		ДопСвойства.Удалить("КлючВарианта");
	Исключение
		ДопСвойства.Удалить("КлючВарианта");
		ПоказатьОшибкиФормирования(ИнформацияОбОшибке());
		ВызватьИсключение;
	КонецПопытки;
	
	ФоновоеЗаданиеИдентификатор  = РезультатФоновогоЗадания.ИдентификаторЗадания;
	ФоновоеЗаданиеАдресХранилища = РезультатФоновогоЗадания.АдресХранилища;
	
	Если РезультатФоновогоЗадания.ЗаданиеВыполнено Тогда
		ФоновоеЗаданиеЗагрузитьРезультат();
		ЗаданиеЗапущено = Ложь;
	Иначе
		ОтображениеСостояния = Элементы.ОтчетТабличныйДокумент.ОтображениеСостояния;
		ОтображениеСостояния.Видимость                      = Истина;
		ОтображениеСостояния.ДополнительныйРежимОтображения = ДополнительныйРежимОтображения.Неактуальность;
		ОтображениеСостояния.Картинка                       = БиблиотекаКартинок.ДлительнаяОперация48;
		ОтображениеСостояния.Текст                          = НСтр("ru = 'Отчет формируется...'");
		
		ЗаданиеЗапущено = Истина;
	КонецЕсли;
	
	Возврат ЗаданиеЗапущено;
	
КонецФункции

&НаСервере
Функция ФоновоеЗаданиеВыполнено()
	Попытка
		ЗаданиеВыполнено = ДлительныеОперации.ЗаданиеВыполнено(ФоновоеЗаданиеИдентификатор);
	Исключение
		ПоказатьОшибкиФормирования(ИнформацияОбОшибке());
		ВызватьИсключение;
	КонецПопытки;
	
	Если ЗаданиеВыполнено Тогда
		ФоновоеЗаданиеЗагрузитьРезультат();
	КонецЕсли;
	
	Возврат ЗаданиеВыполнено;
КонецФункции

&НаСервереБезКонтекста
Процедура ФоновоеЗаданиеОтменить(ФоновоеЗаданиеИдентификатор)
	ДлительныеОперации.ОтменитьВыполнениеЗадания(ФоновоеЗаданиеИдентификатор);
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВычислитьСуммуСервер(Знач ОтчетТабличныйДокумент, Знач ВыделенныеОбласти)
	Сумма = РассчитатьСуммуВыделенныхЯчеек(ОтчетТабличныйДокумент, ВыделенныеОбласти);
	Возврат Формат(Сумма, "ЧН=0");
КонецФункции

&НаСервере
Процедура ОбновитьСвязиВедущегоСПодчиненными(ИдентификаторЭлемента, Значение, Использование, ВсеСвязиВедущего, ЗначениеИзменено)
	
	// Если ведущий используется, тогда связь регистрируется в элементе подчиненного в соответствии с выбранным типом.
	Если ВсеСвязиВедущего.Свойство("ПараметрыВыбораПодчиненныхПоТипуВедущего") Тогда
		ЗначениеТип = ТипЗнч(Значение);
		Для Каждого СтрокаСвязиОтбора Из ВсеСвязиВедущего.ПараметрыВыбораПодчиненныхПоТипуВедущего Цикл
			СвязьВключена = Использование И ЗначениеТип = СтрокаСвязиОтбора.ВедущийТип;
			ПодчиненныйЭлемент = Элементы[СтрокаСвязиОтбора.ПодчиненныйИмяЭлемента];
			УстановитьСвязьПараметровВыбора(ПодчиненныйЭлемент, СтрокаСвязиОтбора, СвязьВключена);
		КонецЦикла;
	КонецЕсли;
	
	Если ВсеСвязиВедущего.Свойство("ПараметрыВыбораПодчиненных") Тогда
		Для Каждого СтрокаСвязиОтбора Из ВсеСвязиВедущего.ПараметрыВыбораПодчиненных Цикл
			ПодчиненныйЭлемент = Элементы[СтрокаСвязиОтбора.ПодчиненныйИмяЭлемента];
			УстановитьСвязьПараметровВыбора(ПодчиненныйЭлемент, СтрокаСвязиОтбора, Использование);
			Если ЗначениеИзменено = Истина И СтрокаСвязиОтбора.Действие = РежимИзмененияСвязанногоЗначения.Очищать Тогда
				Если ТипЗнч(ЭтотОбъект[СтрокаСвязиОтбора.ПодчиненныйИмяРеквизита]) = Тип("СписокЗначений") Тогда
					ЭтотОбъект[СтрокаСвязиОтбора.ПодчиненныйИмяРеквизита].Очистить();
				Иначе
					ЭтотОбъект[СтрокаСвязиОтбора.ПодчиненныйИмяРеквизита] = Неопределено;
				КонецЕсли;
				Если Не ПустаяСтрока(СтрокаСвязиОтбора.ПодчиненныйИмяФлажка) Тогда
					ЭтотОбъект[СтрокаСвязиОтбора.ПодчиненныйИмяФлажка] = Ложь;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Сервер

&НаСервере
Процедура БыстрыеНастройкиЗагрузитьНастройкиВКомпоновщик(ПараметрыЗаполнения)
	Если Не ПараметрыЗаполнения.Свойство("ВариантМодифицирован") Тогда
		ПараметрыЗаполнения.Вставить("ВариантМодифицирован", Ложь);
	КонецЕсли;
	Если Не ПараметрыЗаполнения.Свойство("ПользовательскиеНастройкиМодифицированы") Тогда
		ПараметрыЗаполнения.Вставить("ПользовательскиеНастройкиМодифицированы", Ложь);
	КонецЕсли;
	
	Если ПараметрыЗаполнения.Свойство("КомпоновщикНастроекКД") Тогда
		Отчет.КомпоновщикНастроек.ЗагрузитьНастройки(ПараметрыЗаполнения.КомпоновщикНастроекКД.Настройки);
		Отчет.КомпоновщикНастроек.ЗагрузитьПользовательскиеНастройки(ПараметрыЗаполнения.КомпоновщикНастроекКД.ПользовательскиеНастройки);
	КонецЕсли;
	
	Если ПараметрыЗаполнения.Свойство("НастройкиКД") Тогда
		Отчет.КомпоновщикНастроек.ЗагрузитьНастройки(ПараметрыЗаполнения.НастройкиКД);
	КонецЕсли;
	
	Если ПараметрыЗаполнения.Свойство("ПользовательскиеНастройкиКД") Тогда
		Отчет.КомпоновщикНастроек.ЗагрузитьПользовательскиеНастройки(ПараметрыЗаполнения.ПользовательскиеНастройкиКД);
	КонецЕсли;
	
	Если ПараметрыЗаполнения.Свойство("СброситьНастройки") И ПараметрыЗаполнения.СброситьНастройки = Истина Тогда
		ПустойКомпоновщик = Новый КомпоновщикНастроекКомпоновкиДанных;
		Отчет.КомпоновщикНастроек.ЗагрузитьПользовательскиеНастройки(ПустойКомпоновщик.ПользовательскиеНастройки);
	КонецЕсли;
	
	Если ПараметрыЗаполнения.ВариантМодифицирован Тогда
		ВариантМодифицирован = Истина;
	КонецЕсли;
	
	Если ПараметрыЗаполнения.ПользовательскиеНастройкиМодифицированы Тогда
		ПользовательскиеНастройкиМодифицированы = Истина;
	КонецЕсли;
	
	Если ПараметрыЗаполнения.ВариантМодифицирован
		Или ПараметрыЗаполнения.ПользовательскиеНастройкиМодифицированы Тогда
		Если ПараметрыЗаполнения.Свойство("Событие") И ПараметрыЗаполнения.Событие.Имя = "ПослеФормирования" Тогда
			Возврат;
		КонецЕсли;
		ОтображениеСостояния = Элементы.ОтчетТабличныйДокумент.ОтображениеСостояния;
		ОтображениеСостояния.Видимость = Истина;
		ОтображениеСостояния.Текст     = НСтр("ru = 'Отчет не сформирован. Нажмите ""Сформировать"" для получения отчета.'");
		ОтображениеСостояния.ДополнительныйРежимОтображения = ДополнительныйРежимОтображения.Неактуальность;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура БыстрыеНастройкиУдалитьСтарыеЭлементыИКоманды(ПараметрыЗаполнения)
	
	// Удаление элементов.
	УдаляемыеЭлементы = Новый Массив;
	ДобавитьПодчиненныеЭлементы(УдаляемыеЭлементы, Элементы.Периоды.ПодчиненныеЭлементы);
	ДобавитьПодчиненныеЭлементы(УдаляемыеЭлементы, Элементы.БыстрыеНастройки.ПодчиненныеЭлементы);
	Для Каждого Элемент Из УдаляемыеЭлементы Цикл
		Элементы.Удалить(Элемент);
	КонецЦикла;
	
	// Удаление команд
	УдаляемыеКоманды = Новый Массив;
	Для Каждого Команда Из Команды Цикл
		Если ПостоянныеКоманды.НайтиПоЗначению(Команда.Имя) = Неопределено Тогда
			УдаляемыеКоманды.Добавить(Команда);
		КонецЕсли;
	КонецЦикла;
	Для Каждого Команда Из УдаляемыеКоманды Цикл
		Команды.Удалить(Команда);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьПодчиненныеЭлементы(Куда, Откуда)
	Для Каждого ПодчиненныйЭлемент Из Откуда Цикл
		Если ТипЗнч(ПодчиненныйЭлемент) = Тип("ГруппаФормы")
			Или ТипЗнч(ПодчиненныйЭлемент) = Тип("ТаблицаФормы") Тогда
			ДобавитьПодчиненныеЭлементы(Куда, ПодчиненныйЭлемент.ПодчиненныеЭлементы);
		КонецЕсли;
		Куда.Добавить(ПодчиненныйЭлемент);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура БыстрыеНастройкиСоздатьЭлементыУправленияИЗагрузитьЗначения(ПараметрыЗаполнения)
	// Кэши для быстрого поиска с клиента.
	СоответствиеПользовательскихНастроек = Новый Соответствие;
	СоответствиеИменОбъектовМетаданных   = Новый Соответствие;
	СоответствиеОтключаемыхСвязей        = Новый Соответствие;
	
	ЕстьБыстрыеНастройки = Ложь;
	
	// Удаление реквизитов
	ПараметрыЗаполнения.Вставить("Реквизиты", Новый Структура);
	ПараметрыЗаполнения.Реквизиты.Вставить("Добавляемые",  Новый Массив);
	ПараметрыЗаполнения.Реквизиты.Вставить("Удаляемые",    Новый Массив);
	ПараметрыЗаполнения.Реквизиты.Вставить("Существующие", Новый Соответствие);
	ВсеРеквизиты = ПолучитьРеквизиты();
	Для Каждого Реквизит Из ВсеРеквизиты Цикл
		ПолноеИмяРеквизита = Реквизит.Имя + ?(ПустаяСтрока(Реквизит.Путь), "", "." + Реквизит.Путь);
		Если ПостоянныеРеквизиты.НайтиПоЗначению(ПолноеИмяРеквизита) = Неопределено Тогда
			ПараметрыЗаполнения.Реквизиты.Существующие.Вставить(ПолноеИмяРеквизита, Реквизит.ТипЗначения);
		КонецЕсли;
	КонецЦикла;
	
	// Локальные переменные для установки значений и свойств после создания реквизитов.
	ДобавленныеПоляВвода          = Новый Структура;
	ДобавленныеСпискиЗначений     = Новый Массив;
	ДобавленныеТаблицыСФлажками   = Новый Массив;
	ДобавленныеСтандартныеПериоды = Новый Массив;
	
	// Структура связей.
	Связи = СоздатьСтруктуруТаблицСвязей();
	
	ПоискИдентификаторовБыстрыхНастроекПоПолюКомпоновкиДанных = Новый Соответствие;
	ИменаОсновныхРеквизитовФормы     = Новый Соответствие;
	ИменаЭлементовДляУстановкиСвязей = Новый Соответствие;
	ИменаФлажковИспользование        = Новый Соответствие;
	НастройкиСВидомСравненияРавно    = Новый Соответствие;
	
	КомпоновщикНастроекКД       = Отчет.КомпоновщикНастроек;
	ПользовательскиеНастройкиКД = КомпоновщикНастроекКД.ПользовательскиеНастройки;
	НастройкиКД                 = КомпоновщикНастроекКД.ПолучитьНастройки();
	
	Режимы = РежимОтображенияЭлементаНастройкиКомпоновкиДанных;
	
	// Перебор коллекций пользовательских настроек.
	//   Отдельный цикл верхнего уровня нужен для поддержки сценария,
	//   когда коллекция отборов выведена в пользовательские настройки
	//   и можно добавлять новые отборы не меняя настроек варианта.
	НаборыПользовательскихНастроек = Новый Массив;
	НаборыПользовательскихНастроек.Добавить(ПользовательскиеНастройкиКД.Элементы);
	Пока Истина Цикл
		Если НаборыПользовательскихНастроек.Количество() = 0 Тогда
			Прервать;
		КонецЕсли;
		НаборПользовательскихНастроек = НаборыПользовательскихНастроек[0];
		НаборыПользовательскихНастроек.Удалить(0);
		ЭтоНаборОтбора = (ТипЗнч(НаборПользовательскихНастроек) = Тип("КоллекцияЭлементовОтбораКомпоновкиДанных"));
		
		// Перебор пользовательских настроек.
		Для Каждого ПользовательскаяНастройка Из НаборПользовательскихНастроек Цикл
			
			Если ПользовательскаяНастройка.РежимОтображения = Режимы.Недоступный
				Или ПользовательскаяНастройка.РежимОтображения = Режимы.Обычный Тогда
				Продолжить;
			КонецЕсли;
			
			ИдентификаторСКД = ПользовательскаяНастройка.ИдентификаторПользовательскойНастройки;
			Если Не ЗначениеЗаполнено(ИдентификаторСКД) Тогда
				Продолжить;
			КонецЕсли;
			ИдентификаторЭлемента = ПривестиИдентификаторКИмени(ИдентификаторСКД);
			
			СоответствиеПользовательскихНастроек.Вставить(ИдентификаторЭлемента, ПользовательскиеНастройкиКД.ПолучитьИдентификаторПоОбъекту(ПользовательскаяНастройка));
			
			Если ЭтоНаборОтбора Тогда
				ОбщаяНастройка = ПользовательскаяНастройка;
			Иначе
				ОбщаяНастройка = ФункцииОтчетовКлиентСервер.ПолучитьОбъектПоПользовательскомуИдентификатору(НастройкиКД, ИдентификаторСКД);
				Если ОбщаяНастройка = Неопределено Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			ТипОбщейНастройки = ТипЗнч(ОбщаяНастройка);
			Если ТипОбщейНастройки = Тип("ОтборКомпоновкиДанных") Тогда
				НаборыПользовательскихНастроек.Добавить(ПользовательскаяНастройка.Элементы);
				Продолжить;
			КонецЕсли;
			Если Не ЭтоНаборОтбора
				И ПользовательскаяНастройка.РежимОтображения = Режимы.Авто
				И ОбщаяНастройка.РежимОтображения <> Режимы.БыстрыйДоступ Тогда
				Продолжить;
			КонецЕсли;
			
			ДоступнаяНастройка = ФункцииОтчетовКлиентСервер.НайтиДоступнуюНастройку(НастройкиКД, ОбщаяНастройка);
			
			СтруктураПредставлений = Новый Структура("Представление, ПредставлениеПользовательскойНастройки", "", "");
			ЗаполнитьЗначенияСвойств(СтруктураПредставлений, ОбщаяНастройка);
			Если ЗначениеЗаполнено(СтруктураПредставлений.ПредставлениеПользовательскойНастройки) Тогда
				ЭлементЗаголовок = СтруктураПредставлений.ПредставлениеПользовательскойНастройки;
			ИначеЕсли ЗначениеЗаполнено(СтруктураПредставлений.Представление) Тогда
				ЭлементЗаголовок = СтруктураПредставлений.Представление;
			ИначеЕсли ДоступнаяНастройка <> Неопределено И ЗначениеЗаполнено(ДоступнаяНастройка.Заголовок) Тогда
				ЭлементЗаголовок = ДоступнаяНастройка.Заголовок;
			Иначе
				ЭлементЗаголовок = ПредставлениеПользовательскойНастройки(ПользовательскаяНастройка, ОбщаяНастройка, ТипОбщейНастройки);
			КонецЕсли;
			
			СменитьТипНаГруппуЭлементовОтбора = ЗначениеЗаполнено(СтруктураПредставлений.Представление);
			
			Если ТипОбщейНастройки = Тип("ГруппаЭлементовОтбораКомпоновкиДанных")
				ИЛИ ТипОбщейНастройки = Тип("НастройкиВложенногоОбъектаКомпоновкиДанных")
				ИЛИ СменитьТипНаГруппуЭлементовОтбора Тогда
				
				ШаблонГенерации = Новый Структура;
				ШаблонГенерации.Вставить("Тип",     "ТолькоФлажокИспользование");
				ШаблонГенерации.Вставить("Префикс", "ГруппаОтбора");
				
			ИначеЕсли ТипОбщейНастройки = Тип("ГруппировкаКомпоновкиДанных")
				ИЛИ ТипОбщейНастройки = Тип("ТаблицаКомпоновкиДанных")
				ИЛИ ТипОбщейНастройки = Тип("ГруппировкаТаблицыКомпоновкиДанных")
				ИЛИ ТипОбщейНастройки = Тип("ДиаграммаКомпоновкиДанных")
				ИЛИ ТипОбщейНастройки = Тип("ГруппировкаДиаграммыКомпоновкиДанных") Тогда
				
				ШаблонГенерации = Новый Структура;
				ШаблонГенерации.Вставить("Тип",     "ТолькоФлажокИспользование");
				ШаблонГенерации.Вставить("Префикс", "ЭлементСтруктуры");
				
			ИначеЕсли ТипОбщейНастройки = Тип("ЭлементУсловногоОформленияКомпоновкиДанных") Тогда
				
				ШаблонГенерации = Новый Структура;
				ШаблонГенерации.Вставить("Тип",     "ТолькоФлажокИспользование");
				ШаблонГенерации.Вставить("Префикс", "УсловноеОформление");
				
			ИначеЕсли ТипОбщейНастройки = Тип("ВыбранныеПоляКомпоновкиДанных") Тогда
				
				ШаблонГенерации = Новый Структура;
				ШаблонГенерации.Вставить("Тип",      "ТаблицаПорядкаСФлажками");
				ШаблонГенерации.Вставить("Префикс",  "ВыбранныеПоляКомпоновкиДанных");
				ШаблонГенерации.Вставить("Иерархия", Истина);
				
			ИначеЕсли ТипОбщейНастройки = Тип("ПорядокКомпоновкиДанных") Тогда
				
				ШаблонГенерации = Новый Структура;
				ШаблонГенерации.Вставить("Тип",      "ТаблицаПорядкаСФлажками");
				ШаблонГенерации.Вставить("Префикс",  "ПорядокКомпоновкиДанных");
				ШаблонГенерации.Вставить("Иерархия", Ложь);
				
			ИначеЕсли ТипОбщейНастройки = Тип("СтруктураНастроекКомпоновкиДанных") Тогда
				
				ШаблонГенерации = Новый Структура;
				ШаблонГенерации.Вставить("Тип",      "ТаблицаПорядкаСФлажками");
				ШаблонГенерации.Вставить("Префикс",  "СтруктураНастроекКомпоновкиДанных");
				ШаблонГенерации.Вставить("Иерархия", Ложь);
				
			ИначеЕсли ТипОбщейНастройки = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
				
				Если ДоступнаяНастройка = Неопределено Тогда
					Продолжить; // Имя параметра изменилось или параметр был удален.
				КонецЕсли;
				
				ШаблонГенерации = Новый Структура;
				ШаблонГенерации.Вставить("Префикс", "Параметр");
				
				АнализТиповИСвязей(ИдентификаторЭлемента, ДоступнаяНастройка, ШаблонГенерации, СоответствиеИменОбъектовМетаданных, Связи); // Определение типа значения настройки.
				
				ШаблонГенерации.Вставить("ФлажокИспользование", ДоступнаяНастройка.Использование <> ИспользованиеПараметраКомпоновкиДанных.Всегда);
				Если ТипЗнч(ПользовательскаяНастройка.Значение) = Тип("СтандартныйПериод") Тогда
					ШаблонГенерации.Вставить("Тип", "СтандартныйПериод");
				ИначеЕсли ДоступнаяНастройка.ДоступенСписокЗначений Тогда
					ШаблонГенерации.Вставить("Тип", "СписокСПодбором");
					НастройкиСВидомСравненияРавно.Вставить(ИдентификаторЭлемента, Истина);
				ИначеЕсли Не ШаблонГенерации.ФлажокИспользование И ШаблонГенерации.СодержитТипБулево И ШаблонГенерации.КоличествоТипов = 1 Тогда
					ШаблонГенерации.Вставить("Тип", "ТолькоФлажокЗначения"); // Замена поля "Да/Нет" на флажок.
				Иначе
					ШаблонГенерации.Вставить("Тип", "ПолеВвода");
					НастройкиСВидомСравненияРавно.Вставить(ИдентификаторЭлемента, Истина);
				КонецЕсли;
				
				Если ТипЗнч(ДоступнаяНастройка.ДоступныеЗначения) = Тип("СписокЗначений") И ДоступнаяНастройка.ДоступныеЗначения.Количество() > 0 Тогда
					ШаблонГенерации.Вставить("ВыборИзСписка", Истина);
				Иначе
					ШаблонГенерации.Вставить("ВыборИзСписка", Ложь);
				КонецЕсли;
				
				Если Не ШаблонГенерации.ФлажокИспользование Тогда
					ПользовательскаяНастройка.Использование = Истина;
				КонецЕсли;
				
				ПолеКД = Новый ПолеКомпоновкиДанных("ПараметрыДанных." + Строка(ОбщаяНастройка.Параметр));
				ПоискИдентификаторовБыстрыхНастроекПоПолюКомпоновкиДанных.Вставить(ПолеКД, ИдентификаторЭлемента);
				
			ИначеЕсли ТипОбщейНастройки = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
				
				Если ДоступнаяНастройка = Неопределено Тогда
					Продолжить; // Имя поля изменилось или поле было удалено.
				КонецЕсли;
				
				ШаблонГенерации = Новый Структура;
				ШаблонГенерации.Вставить("Префикс", "Отбор");
				ШаблонГенерации.Вставить("ФлажокИспользование", Истина);
				
				АнализТиповИСвязей(ИдентификаторЭлемента, ДоступнаяНастройка, ШаблонГенерации, СоответствиеИменОбъектовМетаданных, Связи); // Определение типа значения настройки.
				
				Если ТипЗнч(ПользовательскаяНастройка.ПравоеЗначение) = Тип("СтандартныйПериод") Тогда
					ШаблонГенерации.Вставить("Тип", "СтандартныйПериод");
				ИначеЕсли ПользовательскаяНастройка.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено
					Или ПользовательскаяНастройка.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено Тогда
					ШаблонГенерации.Вставить("Тип", "ТолькоФлажокИспользование");
					ЭлементЗаголовок = ЭлементЗаголовок + ": " + НРег(Строка(ПользовательскаяНастройка.ВидСравнения));
				ИначеЕсли ПользовательскаяНастройка.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке
					Или ПользовательскаяНастройка.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии
					Или ПользовательскаяНастройка.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСписке
					Или ПользовательскаяНастройка.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСпискеПоИерархии Тогда
					ШаблонГенерации.Вставить("Тип", "СписокСПодбором");
					Если ПользовательскаяНастройка.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСписке
						Или ПользовательскаяНастройка.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСпискеПоИерархии Тогда
						ЭлементЗаголовок = ЭлементЗаголовок + " (" + НСтр("ru = 'за исключением выбранных'") + ")";
					КонецЕсли;
					ШаблонГенерации.Вставить("ВыборГруппИЭлементов", ГруппыИЭлементы.ГруппыИЭлементы);
				Иначе
					ШаблонГенерации.Вставить("Тип", "ПолеВвода");
					Если ПользовательскаяНастройка.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно Тогда
						НастройкиСВидомСравненияРавно.Вставить(ИдентификаторЭлемента, Истина);
					КонецЕсли;
					Если ПользовательскаяНастройка.ВидСравнения <> ВидСравненияКомпоновкиДанных.Равно
						И ПользовательскаяНастройка.ВидСравнения <> ВидСравненияКомпоновкиДанных.Содержит Тогда
						ЭлементЗаголовок = ЭлементЗаголовок + " (" + НРег(Строка(ПользовательскаяНастройка.ВидСравнения)) + ")";
					КонецЕсли;
					Если ПользовательскаяНастройка.ВидСравнения = ВидСравненияКомпоновкиДанных.ВИерархии
						Или ПользовательскаяНастройка.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВИерархии Тогда
						ШаблонГенерации.Вставить("ВыборГруппИЭлементов", ГруппыИЭлементы.Группы);
					КонецЕсли;
				КонецЕсли;
				
				ПоискИдентификаторовБыстрыхНастроекПоПолюКомпоновкиДанных.Вставить(ОбщаяНастройка.ЛевоеЗначение, ИдентификаторЭлемента);
				
				Если ТипЗнч(ДоступнаяНастройка.ДоступныеЗначения) = Тип("СписокЗначений") И ДоступнаяНастройка.ДоступныеЗначения.Количество() > 0 Тогда
					ШаблонГенерации.Вставить("ВыборИзСписка", Истина);
				Иначе
					ШаблонГенерации.Вставить("ВыборИзСписка", Ложь);
				КонецЕсли;
				
			Иначе
				
				Продолжить;
				
			КонецЕсли;
			
			ЕстьБыстрыеНастройки = Истина;
			
			////////////////////////////////////////////////////////////////////////////////
			// Генерация
			//
			Если ШаблонГенерации.Тип = "ТолькоФлажокИспользование" Или ШаблонГенерации.Тип = "ТолькоФлажокЗначения" Тогда
				
				Если ШаблонГенерации.Тип = "ТолькоФлажокИспользование" Тогда
					ФлажокИмя = ШаблонГенерации.Префикс + "_Использование_" + ИдентификаторЭлемента;
					ДействиеПриИзменении = "Подключаемый_ФлажокИспользование_ПриИзменении";
				Иначе
					ФлажокИмя = ШаблонГенерации.Префикс + "_Значение_" + ИдентификаторЭлемента;
					ДействиеПриИзменении = "Подключаемый_ПолеВвода_ПриИзменении";
				КонецЕсли;
				
				// Реквизиты.
				БыстрыеНастройкиДобавитьРеквизит(ПараметрыЗаполнения, ФлажокИмя, "Булево");
				
				// Флажок.
				ИспользованиеОтбора = Элементы.Добавить(ФлажокИмя, Тип("ПолеФормы"), Элементы.БыстрыеНастройки);
				ИспользованиеОтбора.Вид                = ВидПоляФормы.ПолеФлажка;
				ИспользованиеОтбора.Заголовок          = ЭлементЗаголовок;
				ИспользованиеОтбора.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
				ИспользованиеОтбора.УстановитьДействие("ПриИзменении", ДействиеПриИзменении);
				
				// Значения.
				Если ШаблонГенерации.Тип = "ТолькоФлажокИспользование" Тогда
					ДобавленныеПоляВвода.Вставить(ФлажокИмя, ПользовательскаяНастройка.Использование);
				Иначе
					ДобавленныеПоляВвода.Вставить(ФлажокИмя, ПользовательскаяНастройка.Значение);
				КонецЕсли;
				
			ИначеЕсли ШаблонГенерации.Тип = "СтандартныйПериод" Тогда
				
				ГруппаИмя = ШаблонГенерации.Префикс + "_Группа_" + ИдентификаторЭлемента;
				ИспользованиеИмя       = ШаблонГенерации.Префикс + "_Использование_" + ИдентификаторЭлемента;
				ВидПериодаИмя          = ШаблонГенерации.Префикс + "_Вид_"           + ИдентификаторЭлемента;
				ПериодЗначениеИмя      = ШаблонГенерации.Префикс + "_Значение_"      + ИдентификаторЭлемента;
				ПериодПредставлениеИмя = ШаблонГенерации.Префикс + "_Представление_" + ИдентификаторЭлемента;
				ПериодНачалоИмя        = ШаблонГенерации.Префикс + "_Начало_"        + ИдентификаторЭлемента;
				ПериодОкончаниеИмя     = ШаблонГенерации.Префикс + "_Окончание_"     + ИдентификаторЭлемента;
				СтраницыИмя             = ШаблонГенерации.Префикс + "_Страницы_"             + ИдентификаторЭлемента;
				СтраницаСтандартныйИмя  = ШаблонГенерации.Префикс + "_СтраницаСтандартный_"  + ИдентификаторЭлемента;
				СтраницаПроизвольныйИмя = ШаблонГенерации.Префикс + "_СтраницаПроизвольный_" + ИдентификаторЭлемента;
				
				// Реквизиты.
				БыстрыеНастройкиДобавитьРеквизит(ПараметрыЗаполнения, ВидПериодаИмя,          "ПеречислениеСсылка.ДоступныеПериодыОтчета");
				БыстрыеНастройкиДобавитьРеквизит(ПараметрыЗаполнения, ПериодПредставлениеИмя, "Строка");
				БыстрыеНастройкиДобавитьРеквизит(ПараметрыЗаполнения, ПериодЗначениеИмя,      "СтандартныйПериод");
				
				// Основная Группа
				ГруппаСтрок = ?(ШаблонГенерации.Префикс = "Параметр", Элементы.Периоды, Элементы.БыстрыеНастройки);
				Группа = Элементы.Добавить(ГруппаИмя, Тип("ГруппаФормы"), ГруппаСтрок);
				Группа.Вид                 = ВидГруппыФормы.ОбычнаяГруппа;
				Группа.Группировка         = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
				Группа.Отображение         = ОтображениеОбычнойГруппы.Нет;
				Группа.ОтображатьЗаголовок = Ложь;
				Группа.Заголовок           = ЭлементЗаголовок;
				
				Если ШаблонГенерации.ФлажокИспользование Тогда
					
					БыстрыеНастройкиДобавитьРеквизит(ПараметрыЗаполнения, ИспользованиеИмя, "Булево");
					
					// Флажок
					ПолеФлажка = Элементы.Добавить(ИспользованиеИмя, Тип("ПолеФормы"), Группа);
					ПолеФлажка.Вид                = ВидПоляФормы.ПолеФлажка;
					ПолеФлажка.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
					ПолеФлажка.УстановитьДействие("ПриИзменении", "Подключаемый_ФлажокИспользование_ПриИзменении");
					ПолеФлажка.Заголовок = ЭлементЗаголовок + ":";
					
					// Значение флажка.
					ДобавленныеПоляВвода.Вставить(ИспользованиеИмя, ПользовательскаяНастройка.Использование);
					
				КонецЕсли;
				
				// Вид периода - Элемент.
				ВидПериодаЭлемент = Элементы.Добавить(ВидПериодаИмя, Тип("ПолеФормы"), Группа);
				ВидПериодаЭлемент.Вид                      = ВидПоляФормы.ПолеВвода;
				ВидПериодаЭлемент.Заголовок                = ЭлементЗаголовок;
				ВидПериодаЭлемент.РежимВыбораИзСписка      = Истина;
				ВидПериодаЭлемент.РастягиватьПоГоризонтали = Ложь;
				ВидПериодаЭлемент.Ширина                   = 12;
				ВидПериодаЭлемент.УстановитьДействие("ПриИзменении", "Подключаемый_СтандартныйПериод_Вид_ПриИзменении");
				
				Если ШаблонГенерации.ФлажокИспользование Тогда
					ВидПериодаЭлемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
				Иначе
					ВидПериодаЭлемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;
				КонецЕсли;
				
				// Вид периода - Список выбора.
				Если ШаблонГенерации.Префикс = "Параметр" Тогда
					МинимальнаяПериодичность = НастройкиОтчета.СоответствиеПериодичностиПараметров[ОбщаяНастройка.Параметр];
				Иначе
					МинимальнаяПериодичность = НастройкиОтчета.СоответствиеПериодичностиПараметров[ОбщаяНастройка.ЛевоеЗначение];
				КонецЕсли;
				Если МинимальнаяПериодичность = Неопределено Тогда
					МинимальнаяПериодичность = Перечисления.ДоступныеПериодыОтчета.День;
				КонецЕсли;
				
				ДоступныеПериоды = ФункцииОтчетовКлиентСервер.ПолучитьСписокДоступныхПериодов();
				Для Индекс = ДоступныеПериоды.Найти(МинимальнаяПериодичность) По ДоступныеПериоды.ВГраница() Цикл
					ВидПериодаЭлемент.СписокВыбора.Добавить(ДоступныеПериоды[Индекс]);
				КонецЦикла;
				
				// Страницы.
				ГруппаСтраниц = Элементы.Добавить(СтраницыИмя, Тип("ГруппаФормы"), Группа);
				ГруппаСтраниц.Вид                = ВидГруппыФормы.Страницы;
				ГруппаСтраниц.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
				
				// Страница СтандартныйПериод.
				СтраницаСтандартныйПериод = Элементы.Добавить(СтраницаСтандартныйИмя, Тип("ГруппаФормы"), ГруппаСтраниц);
				СтраницаСтандартныйПериод.Вид                 = ВидГруппыФормы.Страница;
				СтраницаСтандартныйПериод.Группировка         = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
				СтраницаСтандартныйПериод.ОтображатьЗаголовок = Ложь;
				
				// Страница Произвольный.
				СтраницаПроизвольныйПериод = Элементы.Добавить(СтраницаПроизвольныйИмя, Тип("ГруппаФормы"), ГруппаСтраниц);
				СтраницаПроизвольныйПериод.Вид                 = ВидГруппыФормы.Страница;
				СтраницаПроизвольныйПериод.Группировка         = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
				СтраницаПроизвольныйПериод.ОтображатьЗаголовок = Ложь;
				
				// Стандартный период.
				Период = Элементы.Добавить(ПериодПредставлениеИмя, Тип("ПолеФормы"), СтраницаСтандартныйПериод);
				Период.Вид                  = ВидПоляФормы.ПолеВвода;
				Период.Заголовок            = НСтр("ru = 'Период'");
				Период.ПоложениеЗаголовка   = ПоложениеЗаголовкаЭлементаФормы.Нет;
				Период.КнопкаВыбора         = Истина;
				Период.КнопкаОткрытия       = Ложь;
				Период.КнопкаОчистки        = Ложь;
				Период.КнопкаРегулирования  = Ложь;
				Период.РедактированиеТекста = Ложь;
				Период.УстановитьДействие("Очистка",      "Подключаемый_СтандартныйПериод_Значение_Очистка");
				Период.УстановитьДействие("НачалоВыбора", "Подключаемый_СтандартныйПериод_Значение_НачалоВыбора");
				
				// Начало произвольного периода
				ПериодНачало = Элементы.Добавить(ПериодНачалоИмя, Тип("ПолеФормы"), СтраницаПроизвольныйПериод);
				ПериодНачало.Вид                       = ВидПоляФормы.ПолеВвода;
				ПериодНачало.РастягиватьПоГоризонтали  = Истина;
				ПериодНачало.КнопкаВыбора              = Истина;
				ПериодНачало.КнопкаОткрытия            = Ложь;
				ПериодНачало.КнопкаОчистки             = Ложь;
				ПериодНачало.КнопкаРегулирования       = Ложь;
				ПериодНачало.РедактированиеТекста      = Истина;
				ПериодНачало.Заголовок                 = НСтр("ru = 'с'");
				ПериодНачало.УстановитьДействие("ПриИзменении", "Подключаемый_СтандартныйПериод_НачалоПериода_ПриИзменении");
				
				Если ШаблонГенерации.Префикс = "Параметр" Тогда
					ПериодНачало.АвтоОтметкаНезаполненного = ДоступнаяНастройка.ЗапрещатьНезаполненныеЗначения;
				КонецЕсли;
				
				// Окончание произвольного периода.
				ПериодОкончание = Элементы.Добавить(ПериодОкончаниеИмя, Тип("ПолеФормы"), СтраницаПроизвольныйПериод);
				ПериодОкончание.Вид = ВидПоляФормы.ПолеВвода;
				ЗаполнитьЗначенияСвойств(ПериодОкончание, ПериодНачало, "РастягиватьПоГоризонтали, РедактированиеТекста,
				|КнопкаВыбора, КнопкаОткрытия, КнопкаОчистки, КнопкаРегулирования, АвтоОтметкаНезаполненного");
				ПериодОкончание.Заголовок = НСтр("ru = 'по'");
				ПериодОкончание.УстановитьДействие("ПриИзменении", "Подключаемый_СтандартныйПериод_КонецПериода_ПриИзменении");
				
				// Значения.
				Если ШаблонГенерации.Префикс = "Параметр" Тогда
					Значение = ПользовательскаяНастройка.Значение;
				Иначе
					Значение = ПользовательскаяНастройка.ПравоеЗначение;
				КонецЕсли;
				НачалоПериода = Значение.ДатаНачала;
				КонецПериода  = Значение.ДатаОкончания;
				ВидПериода    = ФункцииОтчетовКлиентСервер.ПолучитьВидСтандартногоПериода(Значение, ВидПериодаЭлемент.СписокВыбора);
				Представление = ФункцииОтчетовКлиентСервер.ПредставлениеСтандартногоПериода(Значение, ВидПериода);
				
				ОписаниеРеквизита = Новый Структура;
				ОписаниеРеквизита.Вставить("ПериодЗначениеИмя",      ПериодЗначениеИмя);
				ОписаниеРеквизита.Вставить("ВидПериодаИмя",          ВидПериодаИмя);
				ОписаниеРеквизита.Вставить("ПериодНачалоИмя",        ПериодНачалоИмя);
				ОписаниеРеквизита.Вставить("ПериодОкончаниеИмя",     ПериодОкончаниеИмя);
				ОписаниеРеквизита.Вставить("ПериодПредставлениеИмя", ПериодПредставлениеИмя);
				ОписаниеРеквизита.Вставить("Значение",               Значение);
				ОписаниеРеквизита.Вставить("ВидПериода",             ВидПериода);
				ОписаниеРеквизита.Вставить("Представление",          Представление);
				ДобавленныеСтандартныеПериоды.Добавить(ОписаниеРеквизита);
				
				// Активация страницы.
				Если ВидПериода = Перечисления.ДоступныеПериодыОтчета.ПроизвольныйПериод Тогда
					ГруппаСтраниц.ТекущаяСтраница = СтраницаПроизвольныйПериод;
				Иначе
					ГруппаСтраниц.ТекущаяСтраница = СтраницаСтандартныйПериод;
				КонецЕсли;
				
			ИначеЕсли ШаблонГенерации.Тип = "ПолеВвода" Тогда
				
				ГруппаИмя        = ШаблонГенерации.Префикс + "_Группа_"        + ИдентификаторЭлемента;
				ИспользованиеИмя = ШаблонГенерации.Префикс + "_Использование_" + ИдентификаторЭлемента;
				ЗначениеИмя      = ШаблонГенерации.Префикс + "_Значение_"      + ИдентификаторЭлемента;
				
				ИменаОсновныхРеквизитовФормы.Вставить(ИдентификаторЭлемента, ЗначениеИмя);
				ИменаЭлементовДляУстановкиСвязей.Вставить(ИдентификаторЭлемента, ЗначениеИмя);
				ИменаФлажковИспользование.Вставить(ИдентификаторЭлемента, ?(ШаблонГенерации.ФлажокИспользование, ИспользованиеИмя, ""));
				
				// Группа с флажком.
				Если ШаблонГенерации.ФлажокИспользование Тогда
					
					// Добавление основной группы
					Группа = Элементы.Добавить(ГруппаИмя, Тип("ГруппаФормы"), Элементы.БыстрыеНастройки);
					Группа.Вид                 = ВидГруппыФормы.ОбычнаяГруппа;
					Группа.Группировка         = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
					Группа.Отображение         = ОтображениеОбычнойГруппы.Нет;
					Группа.Заголовок           = ЭлементЗаголовок;
					Группа.ОтображатьЗаголовок = Ложь;
					
					БыстрыеНастройкиДобавитьРеквизит(ПараметрыЗаполнения, ИспользованиеИмя, "Булево");
					
					// Флажок
					ПолеФлажка = Элементы.Добавить(ИспользованиеИмя, Тип("ПолеФормы"), Группа);
					ПолеФлажка.Вид                = ВидПоляФормы.ПолеФлажка;
					ПолеФлажка.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
					ПолеФлажка.УстановитьДействие("ПриИзменении", "Подключаемый_ФлажокИспользование_ПриИзменении");
					ПолеФлажка.Заголовок = ЭлементЗаголовок + ":";
					
					// Значение флажка.
					ДобавленныеПоляВвода.Вставить(ИспользованиеИмя, ПользовательскаяНастройка.Использование);
					
				Иначе
					
					Группа = Элементы.БыстрыеНастройки;
					
				КонецЕсли;
				
				// Реквизит
				БыстрыеНастройкиДобавитьРеквизит(ПараметрыЗаполнения, ЗначениеИмя, ШаблонГенерации.ОписаниеТипов);
				
				ПолеВвода = Элементы.Добавить(ЗначениеИмя, Тип("ПолеФормы"), Группа);
				ПолеВвода.Вид                 = ВидПоляФормы.ПолеВвода;
				ПолеВвода.Заголовок           = ЭлементЗаголовок;
				ПолеВвода.КнопкаОткрытия      = Ложь;
				ПолеВвода.КнопкаРегулирования = Ложь;
				ПолеВвода.УстановитьДействие("ПриИзменении", "Подключаемый_ПолеВвода_ПриИзменении");
				
				ЗаполнитьЗначенияСвойств(ПолеВвода, ДоступнаяНастройка, "БыстрыйВыбор, Маска, ФормаВыбора, ФорматРедактирования");
				
				Если ШаблонГенерации.ФлажокИспользование Тогда
					ПолеВвода.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
				Иначе
					ПолеВвода.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Авто;
				КонецЕсли;
				
				Если ШаблонГенерации.Свойство("ВыборГруппИЭлементов") Тогда
					ПолеВвода.ВыборГруппИЭлементов = ШаблонГенерации.ВыборГруппИЭлементов;
				Иначе
					ПолеВвода.ВыборГруппИЭлементов = ФункцииОтчетовКлиентСервер.ПривестиЗначениеКТипуГруппыИЭлементы(ДоступнаяНастройка.ВыборГруппИЭлементов);
				КонецЕсли;
				
				Если ШаблонГенерации.Префикс = "Параметр" Тогда
					ПолеВвода.АвтоОтметкаНезаполненного = ДоступнаяНастройка.ЗапрещатьНезаполненныеЗначения;
				КонецЕсли;
				
				// Поля ввода следующих типов не растягиваются по горизонтали и не имеют кнопки очистки:
				//     Дата, Булево, Число, Тип.
				ПолеВвода.КнопкаОчистки            = ШаблонГенерации.СодержитДругиеТипы;
				ПолеВвода.РастягиватьПоГоризонтали = ШаблонГенерации.СодержитДругиеТипы;
				
				Если ШаблонГенерации.ВыборИзСписка Тогда
					ПолеВвода.РежимВыбораИзСписка = Истина;
					ПолеВвода.РастягиватьПоГоризонтали = Истина;
					Для Каждого ЭлементСписка Из ДоступнаяНастройка.ДоступныеЗначения Цикл
						Если ЭлементСписка.Значение = Тип("Неопределено") Тогда
							Продолжить; // Запрет пустых значений для типа "Тип".
						КонецЕсли;
						ЭлементСпискаВыбора = ПолеВвода.СписокВыбора.Добавить();
						ЗаполнитьЗначенияСвойств(ЭлементСпискаВыбора, ЭлементСписка);
						Если ТипЗнч(ЭлементСпискаВыбора.Значение) = Тип("Тип") Тогда
							МассивТипов = Новый Массив;
							МассивТипов.Добавить(ЭлементСпискаВыбора.Значение);
							ЭлементСпискаВыбора.Значение = Новый ОписаниеТипов(МассивТипов);
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
				
				// Фиксированные параметры выбора.
				Если ШаблонГенерации.Свойство("ПараметрыВыбора") Тогда
					ПолеВвода.ПараметрыВыбора = Новый ФиксированныйМассив(ШаблонГенерации.ПараметрыВыбора);
				КонецЕсли;
				
				// Значение реквизита.
				Если ШаблонГенерации.Префикс = "Параметр" Тогда
					Значение = ПользовательскаяНастройка.Значение;
				Иначе
					Значение = ПользовательскаяНастройка.ПравоеЗначение;
				КонецЕсли;
				Если ТипЗнч(Значение) = Тип("СтандартнаяДатаНачала") Тогда
					Значение = Дата(Значение);
				ИначеЕсли ТипЗнч(Значение) = Тип("Тип") Тогда
					МассивТипов = Новый Массив;
					МассивТипов.Добавить(Значение);
					Значение = Новый ОписаниеТипов(МассивТипов);
				КонецЕсли;
				ДобавленныеПоляВвода.Вставить(ЗначениеИмя, Значение);
				
			ИначеЕсли ШаблонГенерации.Тип = "СписокСПодбором" Тогда
				
				ГруппаИмя          = ШаблонГенерации.Префикс + "_Группа_"           + ИдентификаторЭлемента;
				ГруппаЗаголовокИмя = ШаблонГенерации.Префикс + "_ГруппаЗаголовка_"  + ИдентификаторЭлемента;
				ИспользованиеИмя   = ШаблонГенерации.Префикс + "_Использование_"    + ИдентификаторЭлемента;
				ДекорацияИмя       = ШаблонГенерации.Префикс + "_Декорация_"        + ИдентификаторЭлемента;
				ТаблицаИмя         = ШаблонГенерации.Префикс + "_СписокЗначений_"   + ИдентификаторЭлемента;
				КолонкаЗначениеИмя = ШаблонГенерации.Префикс + "_Колонка_Значение_" + ИдентификаторЭлемента;
				КоманднаяПанельИмя = ШаблонГенерации.Префикс + "_КоманднаяПанель_"  + ИдентификаторЭлемента;
				КнопкаПодборИмя    = ШаблонГенерации.Префикс + "_Подбор_"           + ИдентификаторЭлемента;
				
				ИменаОсновныхРеквизитовФормы.Вставить(ИдентификаторЭлемента, ТаблицаИмя);
				ИменаЭлементовДляУстановкиСвязей.Вставить(ИдентификаторЭлемента, КолонкаЗначениеИмя);
				ИменаФлажковИспользование.Вставить(ИдентификаторЭлемента, ?(ШаблонГенерации.ФлажокИспользование, ИспользованиеИмя, ""));
				
				// Добавление основной группы
				Группа = Элементы.Добавить(ГруппаИмя, Тип("ГруппаФормы"), Элементы.БыстрыеНастройки);
				Группа.Вид                 = ВидГруппыФормы.ОбычнаяГруппа;
				Группа.Группировка         = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
				Группа.Отображение         = ОтображениеОбычнойГруппы.Нет;
				Группа.Заголовок           = ЭлементЗаголовок;
				Группа.ОтображатьЗаголовок = Ложь;
				
				// Группа-строка для заголовка и командной панели таблицы.
				ГруппаЗаголовокТаблицы = Элементы.Добавить(ГруппаЗаголовокИмя, Тип("ГруппаФормы"), Группа);
				ГруппаЗаголовокТаблицы.Вид                 = ВидГруппыФормы.ОбычнаяГруппа;
				ГруппаЗаголовокТаблицы.Группировка         = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
				ГруппаЗаголовокТаблицы.Отображение         = ОтображениеОбычнойГруппы.Нет;
				ГруппаЗаголовокТаблицы.ОтображатьЗаголовок = Ложь;
				
				// Флажок.
				Если ШаблонГенерации.ФлажокИспользование Тогда
					
					БыстрыеНастройкиДобавитьРеквизит(ПараметрыЗаполнения, ИспользованиеИмя, "Булево");
					
					ПолеФлажка = Элементы.Добавить(ИспользованиеИмя, Тип("ПолеФормы"), ГруппаЗаголовокТаблицы);
					ПолеФлажка.Вид                = ВидПоляФормы.ПолеФлажка;
					ПолеФлажка.Заголовок          = ЭлементЗаголовок + ":";
					ПолеФлажка.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
					ПолеФлажка.УстановитьДействие("ПриИзменении", "Подключаемый_ФлажокИспользование_ПриИзменении");
					
					// Значение флажка
					ДобавленныеПоляВвода.Вставить(ИспользованиеИмя, ПользовательскаяНастройка.Использование);
					
				КонецЕсли;
				
				// Заголовок / Пустая декорация.
				ПустаяДекорация = Элементы.Добавить(ДекорацияИмя, Тип("ДекорацияФормы"), ГруппаЗаголовокТаблицы);
				ПустаяДекорация.Вид                      = ВидДекорацииФормы.Надпись;
				ПустаяДекорация.Заголовок                = ?(ШаблонГенерации.ФлажокИспользование, " ", ЭлементЗаголовок + ":");
				ПустаяДекорация.РастягиватьПоГоризонтали = Истина;
				
				// Подбор.
				ТаблицаКоманднаяПанель = Элементы.Добавить(КоманднаяПанельИмя, Тип("ГруппаФормы"), ГруппаЗаголовокТаблицы);
				ТаблицаКоманднаяПанель.Вид                      = ВидГруппыФормы.КоманднаяПанель;
				ТаблицаКоманднаяПанель.ГоризонтальноеПоложение  = ГоризонтальноеПоложениеЭлемента.Право;
				ТаблицаКоманднаяПанель.РастягиватьПоГоризонтали = Ложь;
				ТаблицаКоманднаяПанель.Ширина                   = 10;
				
				Если ШаблонГенерации.СодержитСсылочныеТипы Тогда
					КомандаПодбор = Команды.Добавить(КнопкаПодборИмя);
					КомандаПодбор.Действие    = "Подключаемый_СписокСПодбором_Подбор";
					КомандаПодбор.Заголовок   = НСтр("ru = 'Подбор'");
					КомандаПодбор.Отображение = ОтображениеКнопки.Текст;
				Иначе
					КомандаПодбор = Команды.Добавить(КнопкаПодборИмя);
					КомандаПодбор.Действие    = "Подключаемый_СписокСПодбором_Добавить";
					КомандаПодбор.Заголовок   = НСтр("ru = 'Добавить'");
					КомандаПодбор.Отображение = ОтображениеКнопки.Текст;
					КомандаПодбор.Картинка    = БиблиотекаКартинок.СоздатьЭлементСписка;
				КонецЕсли;
				
				КнопкаПодбор = Элементы.Добавить(КнопкаПодборИмя, Тип("КнопкаФормы"), ТаблицаКоманднаяПанель);
				КнопкаПодбор.ИмяКоманды = КнопкаПодборИмя;
				
				// Таблица.
				БыстрыеНастройкиДобавитьРеквизит(ПараметрыЗаполнения, ТаблицаИмя, "СписокЗначений");
				
				ТаблицаФормы = Элементы.Добавить(ТаблицаИмя, Тип("ТаблицаФормы"), Группа);
				ТаблицаФормы.Отображение               = ОтображениеТаблицы.Список;
				ТаблицаФормы.ПоложениеЗаголовка        = ПоложениеЗаголовкаЭлементаФормы.Нет;
				ТаблицаФормы.ПоложениеКоманднойПанели  = ПоложениеКоманднойПанелиЭлементаФормы.Нет;
				ТаблицаФормы.ВертикальныеЛинии         = Ложь;
				ТаблицаФормы.ГоризонтальныеЛинии       = Ложь;
				ТаблицаФормы.Шапка                     = Ложь;
				ТаблицаФормы.Подвал                    = Ложь;
				ТаблицаФормы.ИзменятьПорядокСтрок      = Истина;
				ТаблицаФормы.РастягиватьПоГоризонтали  = Истина;
				ТаблицаФормы.РастягиватьПоВертикали    = Истина;
				ТаблицаФормы.Высота                    = 3;
				
				// Колонка "Значение".
				КолонкаЗначениеЭлемент = Элементы.Добавить(КолонкаЗначениеИмя, Тип("ПолеФормы"), ТаблицаФормы);
				КолонкаЗначениеЭлемент.Вид = ВидПоляФормы.ПолеВвода;
				
				ЗаполнитьЗначенияСвойств(КолонкаЗначениеЭлемент, ДоступнаяНастройка, "БыстрыйВыбор, Маска, ФормаВыбора, ФорматРедактирования");
				
				Если ШаблонГенерации.Свойство("ВыборГруппИЭлементов") Тогда
					КолонкаЗначениеЭлемент.ВыборГруппИЭлементов = ШаблонГенерации.ВыборГруппИЭлементов;
				Иначе
					КолонкаЗначениеЭлемент.ВыборГруппИЭлементов = ФункцииОтчетовКлиентСервер.ПривестиЗначениеКТипуГруппыИЭлементы(ДоступнаяНастройка.ВыборГруппИЭлементов);
				КонецЕсли;
				
				Если ШаблонГенерации.ВыборИзСписка Тогда
					КолонкаЗначениеЭлемент.РежимВыбораИзСписка = Истина;
					Для Каждого ЭлементСписка Из ДоступнаяНастройка.ДоступныеЗначения Цикл
						Если ЭлементСписка.Значение = Тип("Неопределено") Тогда
							Продолжить;
						КонецЕсли;
						ЭлементСпискаВыбора = КолонкаЗначениеЭлемент.СписокВыбора.Добавить();
						ЗаполнитьЗначенияСвойств(ЭлементСпискаВыбора, ЭлементСписка);
						Если ТипЗнч(ЭлементСпискаВыбора.Значение) = Тип("Тип") Тогда
							МассивТипов = Новый Массив;
							МассивТипов.Добавить(ЭлементСпискаВыбора.Значение);
							ЭлементСпискаВыбора.Значение = Новый ОписаниеТипов(МассивТипов);
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
				
				// Заполнение имен объектов метаданных в разрезах типов и идентификаторов элементов (для предустановленных).
				// Используется при клике по кнопке "Подбор" для получения имени формы подбора.
				Если ЗначениеЗаполнено(КолонкаЗначениеЭлемент.ФормаВыбора) Тогда
					СоответствиеИменОбъектовМетаданных.Вставить(ИдентификаторЭлемента, КолонкаЗначениеЭлемент.ФормаВыбора);
				КонецЕсли;
				
				// Фиксированные параметры выбора.
				Если ШаблонГенерации.Свойство("ПараметрыВыбора") Тогда
					КолонкаЗначениеЭлемент.ПараметрыВыбора = Новый ФиксированныйМассив(ШаблонГенерации.ПараметрыВыбора);
				КонецЕсли;
				
				// Некоторые обработчики событий можно подключить только после установки связи элементов с данными.
				СобытияТаблицы = Новый Структура;
				Если ШаблонГенерации.СодержитТипТип Тогда
					СобытияТаблицы.Вставить("ПередОкончаниемРедактирования", "Подключаемый_СписокСТипомТип_ПередОкончаниемРедактирования");
					СобытияТаблицы.Вставить("ПриИзменении",                  "Подключаемый_СписокСТипомТип_ПриИзменении");
				Иначе
					СобытияТаблицы.Вставить("ПриИзменении",    "Подключаемый_СписокСПодбором_ПриИзменении");
					СобытияТаблицы.Вставить("ОбработкаВыбора", "Подключаемый_СписокСПодбором_ОбработкаВыбора");
				КонецЕсли;
				
				// Значение списка.
				Если ШаблонГенерации.Префикс = "Параметр" Тогда
					СписокЗначений = ПользовательскаяНастройка.Значение;
				Иначе
					СписокЗначений = ПользовательскаяНастройка.ПравоеЗначение;
				КонецЕсли;
				
				ОписаниеРеквизита = Новый Структура;
				ОписаниеРеквизита.Вставить("ИмяТаблицы",         ТаблицаИмя);
				ОписаниеРеквизита.Вставить("СобытияТаблицы",     СобытияТаблицы);
				ОписаниеРеквизита.Вставить("ИмяКолонкиЗначение", КолонкаЗначениеИмя);
				ОписаниеРеквизита.Вставить("Значение",           СписокЗначений);
				ОписаниеРеквизита.Вставить("ОписаниеТипов",      ШаблонГенерации.ОписаниеТипов);
				ОписаниеРеквизита.Вставить("СодержитТипТип",     ШаблонГенерации.СодержитТипТип);
				ДобавленныеСпискиЗначений.Добавить(ОписаниеРеквизита);
				
			ИначеЕсли ШаблонГенерации.Тип = "ТаблицаПорядкаСФлажками" Тогда
				
				ГруппаИмя                  = ШаблонГенерации.Префикс + "_Группа_"                   + ИдентификаторЭлемента;
				ТаблицаИмя                 = ШаблонГенерации.Префикс + "_Таблица_"                  + ИдентификаторЭлемента;
				КолонкаИспользованиеИмя    = ШаблонГенерации.Префикс + "_КолонкаИспользование_"     + ИдентификаторЭлемента;
				КолонкаПредставлениеИмя    = ШаблонГенерации.Префикс + "_КолонкаПредставление_"     + ИдентификаторЭлемента;
				КолонкаПорядокИмя          = ШаблонГенерации.Префикс + "_КолонкаПорядок_"           + ИдентификаторЭлемента;
				КоманднаяПанельИмя         = ШаблонГенерации.Префикс + "_КоманднаяПанель_"          + ИдентификаторЭлемента;
				КомандаПереместитьВверхИмя = ШаблонГенерации.Префикс + "_Команда_ПереместитьВверх_" + ИдентификаторЭлемента;
				КомандаПереместитьВнизИмя  = ШаблонГенерации.Префикс + "_Команда_ПереместитьВниз_"  + ИдентификаторЭлемента;
				КомандаСортироватьПоВозрастаниюИмя = ШаблонГенерации.Префикс + "_Команда_СортироватьПоВозрастанию_" + ИдентификаторЭлемента;
				КомандаСортироватьПоУбываниюИмя    = ШаблонГенерации.Префикс + "_Команда_СортироватьПоУбыванию_"    + ИдентификаторЭлемента;
				
				// Реквизиты.
				БыстрыеНастройкиДобавитьРеквизит(ПараметрыЗаполнения, ТаблицаИмя, "ТаблицаЗначений");
				БыстрыеНастройкиДобавитьРеквизит(ПараметрыЗаполнения, ТаблицаИмя + ".Идентификатор", "Строка");
				БыстрыеНастройкиДобавитьРеквизит(ПараметрыЗаполнения, ТаблицаИмя + ".Использование", "Булево");
				БыстрыеНастройкиДобавитьРеквизит(ПараметрыЗаполнения, ТаблицаИмя + ".Представление", "Строка");
				
				Если ШаблонГенерации.Префикс = "ПорядокКомпоновкиДанных" Тогда
					БыстрыеНастройкиДобавитьРеквизит(ПараметрыЗаполнения, ТаблицаИмя + ".Направление", "НаправлениеСортировкиКомпоновкиДанных");
				ИначеЕсли ШаблонГенерации.Префикс = "ВыбранныеПоляКомпоновкиДанных" Тогда
					БыстрыеНастройкиДобавитьРеквизит(ПараметрыЗаполнения, ТаблицаИмя + ".ЭтоГруппа", "Булево");
				КонецЕсли;
				
				// Группы и элементы
				Группа = Элементы.Добавить(ГруппаИмя, Тип("ГруппаФормы"), Элементы.БыстрыеНастройки);
				Группа.Вид                      = ВидГруппыФормы.ОбычнаяГруппа;
				Группа.Группировка              = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
				Группа.Отображение              = ОтображениеОбычнойГруппы.СлабоеВыделение;
				Группа.ОтображатьЗаголовок      = Истина;
				Группа.РастягиватьПоГоризонтали = Истина;
				Группа.Заголовок                = ЭлементЗаголовок + ":";
				
				ТаблицаФормы = Элементы.Добавить(ТаблицаИмя, Тип("ТаблицаФормы"), Группа);
				ТаблицаФормы.Отображение               = ОтображениеТаблицы.Список;
				ТаблицаФормы.ПоложениеЗаголовка        = ПоложениеЗаголовкаЭлементаФормы.Нет;
				ТаблицаФормы.ПоложениеКоманднойПанели  = ПоложениеКоманднойПанелиЭлементаФормы.Нет;
				ТаблицаФормы.ВертикальныеЛинии         = Ложь;
				ТаблицаФормы.ГоризонтальныеЛинии       = Ложь;
				ТаблицаФормы.Шапка                     = Ложь;
				ТаблицаФормы.Подвал                    = Ложь;
				ТаблицаФормы.ИзменятьПорядокСтрок      = Истина;
				ТаблицаФормы.РастягиватьПоГоризонтали  = Истина;
				ТаблицаФормы.РастягиватьПоВертикали    = Истина;
				ТаблицаФормы.Высота                    = 3;
				ТаблицаФормы.ИзменятьСоставСтрок       = Ложь;
				ТаблицаФормы.ИзменятьПорядокСтрок      = Ложь;
				ТаблицаФормы.РежимВыделения            = РежимВыделенияТаблицы.Одиночный;
				
				КолонкаИспользование = Элементы.Добавить(КолонкаИспользованиеИмя, Тип("ПолеФормы"), ТаблицаФормы);
				КолонкаИспользование.Вид                 = ВидПоляФормы.ПолеФлажка;
				КолонкаИспользование.РежимРедактирования = РежимРедактированияКолонки.Непосредственно;
				КолонкаИспользование.УстановитьДействие("ПриИзменении", "Подключаемый_ТаблицаПорядка_КолонкаИспользование_ПриИзменении");
				
				КолонкаПредставление = Элементы.Добавить(КолонкаПредставлениеИмя, Тип("ПолеФормы"), ТаблицаФормы);
				КолонкаПредставление.Вид            = ВидПоляФормы.ПолеВвода;
				КолонкаПредставление.ТолькоПросмотр = Истина;
				
				// Командная панель
				ТаблицаКоманднаяПанель = Элементы.Добавить(КоманднаяПанельИмя, Тип("ГруппаФормы"), Группа);
				ТаблицаКоманднаяПанель.Вид                 = ВидГруппыФормы.ОбычнаяГруппа;
				ТаблицаКоманднаяПанель.Группировка         = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
				ТаблицаКоманднаяПанель.Отображение         = ОтображениеОбычнойГруппы.Нет;
				ТаблицаКоманднаяПанель.ОтображатьЗаголовок = Ложь;
				
				// Команды
				КомандаПереместитьВверх = Команды.Добавить(КомандаПереместитьВверхИмя);
				КомандаПереместитьВверх.Действие    = "Подключаемый_ТаблицаПорядка_ПереместитьВверх";
				КомандаПереместитьВверх.Картинка    = БиблиотекаКартинок.ПереместитьВверх;
				КомандаПереместитьВверх.Отображение = ОтображениеКнопки.Картинка;
				
				КомандаПереместитьВниз = Команды.Добавить(КомандаПереместитьВнизИмя);
				КомандаПереместитьВниз.Действие    = "Подключаемый_ТаблицаПорядка_ПереместитьВниз";
				КомандаПереместитьВниз.Картинка    = БиблиотекаКартинок.ПереместитьВниз;
				КомандаПереместитьВниз.Отображение = ОтображениеКнопки.Картинка;
				
				// Кнопки
				КнопкаПереместитьВверх = Элементы.Добавить(КомандаПереместитьВверхИмя, Тип("КнопкаФормы"), ТаблицаКоманднаяПанель);
				КнопкаПереместитьВверх.ИмяКоманды = КомандаПереместитьВверхИмя;
				КнопкаПереместитьВверх.Заголовок  = НСтр("ru = 'Переместить строку вверх'");
				
				КнопкаПереместитьВниз = Элементы.Добавить(КомандаПереместитьВнизИмя, Тип("КнопкаФормы"), ТаблицаКоманднаяПанель);
				КнопкаПереместитьВниз.ИмяКоманды = КомандаПереместитьВнизИмя;
				КнопкаПереместитьВниз.Заголовок  = НСтр("ru = 'Переместить строку вниз'");
				
				Если ШаблонГенерации.Префикс = "ПорядокКомпоновкиДанных" Тогда
					
					КомандаПереместитьВверх.Подсказка = НСтр("ru = 'Поднять приоритет сортировки по колонке'");
					КомандаПереместитьВниз.Подсказка  = НСтр("ru = 'Понизить приоритет сортировки по колонке'");
					
					КолонкаПорядок = Элементы.Добавить(КолонкаПорядокИмя, Тип("ПолеФормы"), ТаблицаФормы);
					КолонкаПорядок.Вид                      = ВидПоляФормы.ПолеВвода;
					КолонкаПорядок.ТолькоПросмотр           = Истина;
					КолонкаПорядок.РастягиватьПоГоризонтали = Ложь;
					КолонкаПорядок.Ширина                   = 11;
					КолонкаПорядок.ГиперссылкаЯчейки        = Истина;
					
					// Команды
					КомандаПереместитьВверх = Команды.Добавить(КомандаСортироватьПоВозрастаниюИмя);
					КомандаПереместитьВверх.Действие    = "Подключаемый_ТаблицаПорядка_СортироватьПоВозрастанию";
					КомандаПереместитьВверх.Картинка    = БиблиотекаКартинок.СортироватьСписокПоВозрастанию;
					КомандаПереместитьВверх.Отображение = ОтображениеКнопки.Картинка;
					
					КомандаПереместитьВниз = Команды.Добавить(КомандаСортироватьПоУбываниюИмя);
					КомандаПереместитьВниз.Действие    = "Подключаемый_ТаблицаПорядка_СортироватьПоУбыванию";
					КомандаПереместитьВниз.Картинка    = БиблиотекаКартинок.СортироватьСписокПоУбыванию;
					КомандаПереместитьВниз.Отображение = ОтображениеКнопки.Картинка;
					
					// Кнопки
					КнопкаПереместитьВверх = Элементы.Добавить(КомандаСортироватьПоВозрастаниюИмя, Тип("КнопкаФормы"), ТаблицаФормы.КонтекстноеМеню);
					КнопкаПереместитьВверх.ИмяКоманды = КомандаСортироватьПоВозрастаниюИмя;
					КнопкаПереместитьВверх.Заголовок  = НСтр("ru = 'Сортировать по возрастанию'");
					
					КнопкаПереместитьВниз = Элементы.Добавить(КомандаСортироватьПоУбываниюИмя, Тип("КнопкаФормы"), ТаблицаФормы.КонтекстноеМеню);
					КнопкаПереместитьВниз.ИмяКоманды = КомандаСортироватьПоУбываниюИмя;
					КнопкаПереместитьВниз.Заголовок  = НСтр("ru = 'Сортировать по убыванию'");
					
				ИначеЕсли ШаблонГенерации.Префикс = "ВыбранныеПоляКомпоновкиДанных" Тогда
					
					КомандаПереместитьВверх.Подсказка = НСтр("ru = 'Переместить поле выше или левее'");
					КомандаПереместитьВниз.Подсказка  = НСтр("ru = 'Переместить поле ниже или правее'");
					
					// Условное оформление для групп
					ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить(); 
					ЭлементУсловногоОформления.Использование = Истина;
					
					ОтборОформления = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
					ОтборОформления.Использование = Истина;
					ОтборОформления.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных(ТаблицаИмя + ".ЭтоГруппа");
					ОтборОформления.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
					ОтборОформления.ПравоеЗначение = Истина;
					
					ПолеОформление = ЭлементУсловногоОформления.Поля.Элементы.Добавить(); 
					ПолеОформление.Использование = Истина;
					ПолеОформление.Поле          = Новый ПолеКомпоновкиДанных(ТаблицаИмя);
					
					ШрифтОформления = ЭлементУсловногоОформления.Оформление.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Шрифт")); 
					ШрифтОформления.Значение      = Новый Шрифт(ШрифтОформления.Значение, , , Истина, , Истина, ); 
					ШрифтОформления.Использование = Истина;
					
				ИначеЕсли ШаблонГенерации.Префикс = "СтруктураНастроекКомпоновкиДанных" Тогда
					
					КомандаПереместитьВверх.Подсказка = НСтр("ru = 'Переместить поле вверх'");
					КомандаПереместитьВниз.Подсказка  = НСтр("ru = 'Переместить поле вниз'");
					
				КонецЕсли;
				
				// Значения.
				ОписаниеРеквизита = Новый Структура;
				ОписаниеРеквизита.Вставить("ИмяГруппы",                 ГруппаИмя);
				ОписаниеРеквизита.Вставить("ИмяТаблицы",                ТаблицаИмя);
				ОписаниеРеквизита.Вставить("ИмяКолонкиИспользование",   КолонкаИспользованиеИмя);
				ОписаниеРеквизита.Вставить("ИмяКолонкиПредставление",   КолонкаПредставлениеИмя);
				ОписаниеРеквизита.Вставить("ИмяКолонкиПорядок",         КолонкаПорядокИмя);
				ОписаниеРеквизита.Вставить("ПользовательскаяНастройка", ПользовательскаяНастройка);
				ДобавленныеТаблицыСФлажками.Добавить(ОписаниеРеквизита);
				
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	// Удаление старых и добавление новых реквизитов.
	Для Каждого КлючИЗначение Из ПараметрыЗаполнения.Реквизиты.Существующие Цикл
		ПараметрыЗаполнения.Реквизиты.Удаляемые.Добавить(КлючИЗначение.Ключ);
	КонецЦикла;
	ИзменитьРеквизиты(ПараметрыЗаполнения.Реквизиты.Добавляемые, ПараметрыЗаполнения.Реквизиты.Удаляемые);
	
	// Установка значений и связей полей ввода.
	Для Каждого КлючИЗначение Из ДобавленныеПоляВвода Цикл
		ИмяРеквизита = КлючИЗначение.Ключ;
		ЭтотОбъект[ИмяРеквизита] = КлючИЗначение.Значение;
		Элементы[ИмяРеквизита].ПутьКДанным = ИмяРеквизита;
	КонецЦикла;
	
	// Установка значений и связей списков значений.
	Для Каждого ОписаниеРеквизита Из ДобавленныеСтандартныеПериоды Цикл
		ЭтотОбъект[ОписаниеРеквизита.ПериодЗначениеИмя]      = ОписаниеРеквизита.Значение;
		ЭтотОбъект[ОписаниеРеквизита.ВидПериодаИмя]          = ОписаниеРеквизита.ВидПериода;
		ЭтотОбъект[ОписаниеРеквизита.ПериодПредставлениеИмя] = ОписаниеРеквизита.Представление;
		Элементы[ОписаниеРеквизита.ВидПериодаИмя].ПутьКДанным          = ОписаниеРеквизита.ВидПериодаИмя;
		Элементы[ОписаниеРеквизита.ПериодПредставлениеИмя].ПутьКДанным = ОписаниеРеквизита.ПериодПредставлениеИмя;
		Элементы[ОписаниеРеквизита.ПериодНачалоИмя].ПутьКДанным        = ОписаниеРеквизита.ПериодЗначениеИмя + ".ДатаНачала";
		Элементы[ОписаниеРеквизита.ПериодОкончаниеИмя].ПутьКДанным     = ОписаниеРеквизита.ПериодЗначениеИмя + ".ДатаОкончания";
	КонецЦикла;
	
	// Установка значений и связей списков значений.
	Для Каждого ОписаниеРеквизита Из ДобавленныеСпискиЗначений Цикл
		ИмяТаблицы = ОписаниеРеквизита.ИмяТаблицы;
		ТаблицаФормы = Элементы[ИмяТаблицы];
		Если ОписаниеРеквизита.СодержитТипТип И ТипЗнч(ОписаниеРеквизита.Значение) = Тип("СписокЗначений") Тогда
			СписокЗначений = Новый СписокЗначений;
			Для Каждого ЭлементСпискаСКД Из ОписаниеРеквизита.Значение Цикл
				ЭлементСписка = СписокЗначений.Добавить();
				ЗаполнитьЗначенияСвойств(ЭлементСписка, ЭлементСпискаСКД);
				Если ТипЗнч(ЭлементСписка.Значение) = Тип("Тип") Тогда
					МассивТипов = Новый Массив;
					МассивТипов.Добавить(ЭлементСписка.Значение);
					ЭлементСписка.Значение = Новый ОписаниеТипов(МассивТипов);
				КонецЕсли;
			КонецЦикла;
			ЭтотОбъект[ИмяТаблицы] = СписокЗначений;
		Иначе
			ЭтотОбъект[ИмяТаблицы] = ОписаниеРеквизита.Значение;
		КонецЕсли;
		ЭтотОбъект[ИмяТаблицы].ТипЗначения = ОписаниеРеквизита.ОписаниеТипов;
		ТаблицаФормы.ПутьКДанным = ИмяТаблицы;
		Элементы[ОписаниеРеквизита.ИмяКолонкиЗначение].ПутьКДанным = ИмяТаблицы + ".Значение";
		// Некоторые обработчики событий можно подключить только после установки связи элементов с данными.
		Для Каждого КлючИЗначение Из ОписаниеРеквизита.СобытияТаблицы Цикл
			ТаблицаФормы.УстановитьДействие(КлючИЗначение.Ключ, КлючИЗначение.Значение);
		КонецЦикла;
	КонецЦикла;
	
	// Установка значений и связей таблиц порядка с флажками.
	Для Каждого ОписаниеРеквизита Из ДобавленныеТаблицыСФлажками Цикл
		
		ИмяТаблицы = ОписаниеРеквизита.ИмяТаблицы;
		Таблица = ЭтотОбъект[ИмяТаблицы];
		Таблица.Очистить();
		ПользовательскаяНастройка = ОписаниеРеквизита.ПользовательскаяНастройка;
		ТипОбщейНастройки = ТипЗнч(ПользовательскаяНастройка);
		
		Если ТипОбщейНастройки = Тип("ВыбранныеПоляКомпоновкиДанных") Тогда
			
			ФункцииОтчетовКлиентСервер.ДобавитьЭлементыНастройки(ЭтотОбъект, Таблица, ПользовательскаяНастройка, ПользовательскаяНастройка, Истина);
			
		ИначеЕсли ТипОбщейНастройки = Тип("ПорядокКомпоновкиДанных") Тогда
		
			Для Каждого ЭлементПорядка Из ПользовательскаяНастройка.Элементы Цикл
				Если ТипЗнч(ЭлементПорядка) = Тип("ЭлементПорядкаКомпоновкиДанных") Тогда
					ДоступноеПоле = ФункцииОтчетовКлиентСервер.ПолучитьДоступноеПоле(НастройкиКД.ДоступныеПоляПорядка, ЭлементПорядка.Поле);
					Если ДоступноеПоле = Неопределено Тогда
						Продолжить;
					КонецЕсли;
					СтрокаПорядка = Таблица.Добавить();
					СтрокаПорядка.Использование = ЭлементПорядка.Использование;
					СтрокаПорядка.Идентификатор = ПользовательскаяНастройка.ПолучитьИдентификаторПоОбъекту(ЭлементПорядка);
					СтрокаПорядка.Представление = ДоступноеПоле.Заголовок;
					СтрокаПорядка.Направление   = ЭлементПорядка.ТипУпорядочивания;
				КонецЕсли;
			КонецЦикла;
			
		ИначеЕсли ТипОбщейНастройки = Тип("СтруктураНастроекКомпоновкиДанных") Тогда
			
			Для Каждого ЭлементСтруктуры Из ПользовательскаяНастройка.Структура Цикл
				Представление = "";
				Если ТипЗнч(ЭлементСтруктуры) = Тип("ГруппировкаКомпоновкиДанных") 
					ИЛИ ТипЗнч(ЭлементСтруктуры) = Тип("ГруппировкаТаблицыКомпоновкиДанных") 
					ИЛИ ТипЗнч(ЭлементСтруктуры) = Тип("ГруппировкаДиаграммыКомпоновкиДанных") Тогда
					
					Для Каждого ПолеГруппировки Из ЭлементСтруктуры.ПоляГруппировки.Элементы Цикл
						Если НЕ ПолеГруппировки.Использование Тогда
							Продолжить;
						КонецЕсли;
						ДоступноеПоле = ФункцииОтчетовКлиентСервер.ПолучитьДоступноеПоле(НастройкиКД.ДоступныеПоляГруппировок, ПолеГруппировки.Поле);
						Если ДоступноеПоле = Неопределено Тогда
							Представление = Строка(ПолеГруппировки.Поле);
						ИначеЕсли ДоступноеПоле.Родитель <> Неопределено Тогда
							Представление = Представление + СтрЗаменить(СтрЗаменить(ДоступноеПоле.Заголовок, ДоступноеПоле.Родитель.Заголовок, ""), ".", "")+ ", ";
						ИНаче
							Представление = Представление + ДоступноеПоле.Заголовок + ", ";
						КонецЕсли;
					КонецЦикла;
					
					Представление = Лев(Представление, СтрДлина(Представление) - 2);
				Иначе
					Представление = НСтр("ru = 'Таблица / диаграмма'");
				КонецЕсли;
				
				СтрокиГруппировки = Таблица.Добавить();
				СтрокиГруппировки.Идентификатор = ЭлементСтруктуры.ИдентификаторПользовательскойНастройки;
				СтрокиГруппировки.Использование = ЭлементСтруктуры.Использование;
				СтрокиГруппировки.Представление = Представление;
			КонецЦикла;
			
		КонецЕсли;
		
		Если Таблица.Количество() = 0 Тогда
			Элементы[ОписаниеРеквизита.ИмяГруппы].Видимость = Ложь;
		Иначе
			Элементы[ИмяТаблицы].ПутьКДанным = ИмяТаблицы;
			Элементы[ОписаниеРеквизита.ИмяКолонкиИспользование].ПутьКДанным = ИмяТаблицы + ".Использование";
			Элементы[ОписаниеРеквизита.ИмяКолонкиПредставление].ПутьКДанным = ИмяТаблицы + ".Представление";
			Если ТипОбщейНастройки = Тип("ПорядокКомпоновкиДанных") Тогда
				Элементы[ОписаниеРеквизита.ИмяКолонкиПорядок].ПутьКДанным = ИмяТаблицы + ".Направление";
				Элементы[ИмяТаблицы].УстановитьДействие("Выбор", "Подключаемый_ТаблицаПорядка_Выбор");
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	// Регистрация связи параметров выбора (динамическая связь, отключаемая флажком Использование).
	Найденные = Связи.ОбъектовМетаданных.НайтиСтроки(Новый Структура("ЕстьПодчиненные, ЕстьВедущие", Истина, Истина));
	Для Каждого СтрокаТаблицы Из Найденные Цикл
		ПодчиненныйИмяТипа = СтрЗаменить(СтрокаТаблицы.ПодчиненныйПолноеИмя, ".", "Ссылка.");
		ВедущийИмяТипа     = СтрЗаменить(СтрокаТаблицы.ВедущийПолноеИмя,     ".", "Ссылка.");
		Для Каждого ВедущийИдентификатор Из СтрокаТаблицы.ВедущийМассивИдентификаторов Цикл
			// Если ведущий - отбор, который имеет вид сравнения не равно, тогда связь игнорируется.
			Если НастройкиСВидомСравненияРавно.Получить(ВедущийИдентификатор) <> Истина Тогда
				Продолжить;
			КонецЕсли;
			
			ВсеСвязиВедущего = СоответствиеОтключаемыхСвязей.Получить(ВедущийИдентификатор);
			Если ВсеСвязиВедущего = Неопределено Тогда
				ВсеСвязиВедущего = Новый Структура;
			КонецЕсли;
			Если ВсеСвязиВедущего.Свойство("ПараметрыВыбораПодчиненныхПоТипуВедущего") Тогда
				ПараметрыВыбораПодчиненныхПоТипуВедущего = ВсеСвязиВедущего.ПараметрыВыбораПодчиненныхПоТипуВедущего;
			Иначе
				ПараметрыВыбораПодчиненныхПоТипуВедущего = Новый Массив;
			КонецЕсли;
			
			ВедущийИдентификаторКД = СоответствиеПользовательскихНастроек.Получить(ВедущийИдентификатор);
			ВедущийПользовательскаяНастройкаКД = ПользовательскиеНастройкиКД.ПолучитьОбъектПоИдентификатору(ВедущийИдентификаторКД);
			Для Каждого ПодчиненныйИдентификатор Из СтрокаТаблицы.ПодчиненныйМассивИдентификаторов Цикл
				
				// Если ведущий может отключаться (используется НЕ всегда),
				//   то связь регистрируется в таблице связей.
				//   Связь должна включаться/отключаться из события ПриИзменении флажка Использование
				//   Если связь типизирована по владельцу
				//         - владелец имеет несколько типов
				//         - связь задана для конкретного типа
				//       тогда связь должна включаться/отключаться также и из обработчика события ПриИзменении.
				ОписаниеСвязи = Новый Структура;
				ОписаниеСвязи.Вставить("ВедущийТип", СтрокаТаблицы.ВедущийТип);
				ОписаниеСвязи.Вставить("ВедущийИмяРеквизита", ИменаОсновныхРеквизитовФормы[ВедущийИдентификатор]);
				ОписаниеСвязи.Вставить("ПодчиненныйИдентификатор", ПодчиненныйИдентификатор);
				ОписаниеСвязи.Вставить("ПодчиненныйИмяРеквизита", ИменаОсновныхРеквизитовФормы[ПодчиненныйИдентификатор]);
				ОписаниеСвязи.Вставить("ПодчиненныйИмяЭлемента", ИменаЭлементовДляУстановкиСвязей[ПодчиненныйИдентификатор]);
				ОписаниеСвязи.Вставить("ПодчиненныйИмяПараметраФормы", "Отбор." + СтрокаТаблицы.ПодчиненныйРеквизит);
				ОписаниеСвязи.Вставить("Действие", РежимИзмененияСвязанногоЗначения.НеИзменять);
				
				ПараметрыВыбораПодчиненныхПоТипуВедущего.Добавить(ОписаниеСвязи);
				
				// Если ведущий используется, тогда связь регистрируется в элементе подчиненного в соответствии с выбранным типом.
				Если ВедущийПользовательскаяНастройкаКД.Использование
					И ТипЗнч(ЭтотОбъект[ОписаниеСвязи.ВедущийИмяРеквизита]) = ОписаниеСвязи.ВедущийТип Тогда
					ПодчиненныйЭлемент = Элементы.Найти(ОписаниеСвязи.ПодчиненныйИмяЭлемента);
					Если ПодчиненныйЭлемент <> Неопределено Тогда
						УстановитьСвязьПараметровВыбора(ПодчиненныйЭлемент, ОписаниеСвязи, Истина);
					КонецЕсли;
				КонецЕсли;
				
			КонецЦикла;
			
			ВсеСвязиВедущего.Вставить("ПараметрыВыбораПодчиненныхПоТипуВедущего", ПараметрыВыбораПодчиненныхПоТипуВедущего);
			СоответствиеОтключаемыхСвязей.Вставить(ВедущийИдентификатор, ВсеСвязиВедущего);
		КонецЦикла;
	КонецЦикла;
	
	ПоискЗначенийОбычныхНастроекПоПолюКомпоновкиДанных = Неопределено;
	
	// Связи по типу
	Для Каждого СтрокаТаблицы Из Связи.ПоТипу Цикл
		
		ВедущийИдентификатор = ПоискИдентификаторовБыстрыхНастроекПоПолюКомпоновкиДанных.Получить(СтрокаТаблицы.ВедущийПолеКД);
		ПодчиненныйИдентификатор = СтрокаТаблицы.ПодчиненныйИдентификатор;
		ПодчиненныйИмяЭлемента   = ИменаЭлементовДляУстановкиСвязей[ПодчиненныйИдентификатор];
		Если Не ЗначениеЗаполнено(ПодчиненныйИмяЭлемента) Тогда
			Продолжить; // Подчиненный имеет такой вид сравнения ("Заполнено" или "Не заполнено"), при котором его связи отключаются.
		КонецЕсли;
		ПодчиненныйЭлемент = Элементы[ПодчиненныйИмяЭлемента];
		
		Если ВедущийИдентификатор = Неопределено Тогда // Ведущий не выведен в быстрые настройки.
			
			// Инициализация соответствия для быстрого поиска значений обычных настроек.
			Если ПоискЗначенийОбычныхНастроекПоПолюКомпоновкиДанных = Неопределено Тогда
				ПоискЗначенийОбычныхНастроекПоПолюКомпоновкиДанных = Новый Соответствие;
				РекурсивныйАнализНастроекКД(Неопределено, ПоискЗначенийОбычныхНастроекПоПолюКомпоновкиДанных); // Заполнение соответствия.
			КонецЕсли;
			
			// Поиск значения ведущего.
			ВедущийЗначение = ПоискЗначенийОбычныхНастроекПоПолюКомпоновкиДанных.Получить(СтрокаТаблицы.ВедущийПолеКД);
			Если ВедущийЗначение = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			// Установка фиксированного ограничения типа.
			МассивТипов = Новый Массив;
			МассивТипов.Добавить(ТипЗнч(ВедущийЗначение));
			ПодчиненныйЭлемент.ОграничениеТипа = Новый ОписаниеТипов(МассивТипов);
			
		Иначе // Ведущий выведен в быстрые настройки.
			
			// Регистрация отключаемой связи ведущего с подчиненным (по типу).
			ВсеСвязиВедущего = СоответствиеОтключаемыхСвязей.Получить(ВедущийИдентификатор);
			Если ВсеСвязиВедущего = Неопределено Тогда
				ВсеСвязиВедущего = Новый Структура;
			КонецЕсли;
			Если ВсеСвязиВедущего.Свойство("СвязиСПодчиненнымиПоТипу") Тогда
				СвязиСПодчиненнымиПоТипу = ВсеСвязиВедущего.СвязиСПодчиненнымиПоТипу;
			Иначе
				СвязиСПодчиненнымиПоТипу = Новый Массив;
			КонецЕсли;
			
			ВедущийИдентификаторКД = СоответствиеПользовательскихНастроек.Получить(ВедущийИдентификатор);
			ВедущийПользовательскаяНастройкаКД = ПользовательскиеНастройкиКД.ПолучитьОбъектПоИдентификатору(ВедущийИдентификаторКД);
			Если ТипЗнч(ВедущийПользовательскаяНастройкаКД) = Тип("ЭлементОтбораКомпоновкиДанных")
				И ВедущийПользовательскаяНастройкаКД.ВидСравнения <> ВидСравненияКомпоновкиДанных.ВСписке
				И ВедущийПользовательскаяНастройкаКД.ВидСравнения <> ВидСравненияКомпоновкиДанных.Равно Тогда
				Продолжить; // Ведущий имеет такой вид сравнения ("Заполнено" или "Не заполнено"), при котором его связи отключаются.
			КонецЕсли;
			
			ОписаниеСвязи = Новый Структура;
			ОписаниеСвязи.Вставить("ВедущийИмяРеквизита",      ИменаОсновныхРеквизитовФормы[ВедущийИдентификатор]);
			ОписаниеСвязи.Вставить("ПодчиненныйИдентификатор", ПодчиненныйИдентификатор);
			ОписаниеСвязи.Вставить("ПодчиненныйИмяРеквизита",  ИменаОсновныхРеквизитовФормы[ПодчиненныйИдентификатор]);
			ОписаниеСвязи.Вставить("ПодчиненныйИмяЭлемента",   ПодчиненныйИмяЭлемента);
			ОписаниеСвязи.Вставить("НомерСубконто",            СтрокаТаблицы.НомерСубконто);
			
			СвязиСПодчиненнымиПоТипу.Добавить(ОписаниеСвязи);
			
			ВсеСвязиВедущего.Вставить("СвязиСПодчиненнымиПоТипу", СвязиСПодчиненнымиПоТипу);
			СоответствиеОтключаемыхСвязей.Вставить(ВедущийИдентификатор, ВсеСвязиВедущего);
			
			// Если ведущий используется, тогда связь регистрируется в элементе подчиненного в соответствии с выбранным типом.
			Если ВедущийПользовательскаяНастройкаКД.Использование Тогда
				ПодчиненныйЭлемент.СвязьПоТипу = Новый СвязьПоТипу(ОписаниеСвязи.ВедущийИмяРеквизита, ОписаниеСвязи.НомерСубконто);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	// Связи параметров выбора
	Для Каждого СтрокаТаблицы Из Связи.ПараметровВыбора Цикл
		
		ВедущийИдентификатор = ПоискИдентификаторовБыстрыхНастроекПоПолюКомпоновкиДанных.Получить(СтрокаТаблицы.ВедущийПолеКД);
		ПодчиненныйИдентификатор = СтрокаТаблицы.ПодчиненныйИдентификатор;
		ПодчиненныйИмяЭлемента   = ИменаЭлементовДляУстановкиСвязей[ПодчиненныйИдентификатор];
		Если Не ЗначениеЗаполнено(ПодчиненныйИмяЭлемента) Тогда
			Продолжить; // Подчиненный имеет такой вид сравнения ("Заполнено" или "Не заполнено"), при котором его связи отключаются.
		КонецЕсли;
		ПодчиненныйЭлемент = Элементы[ПодчиненныйИмяЭлемента];
		
		Если ВедущийИдентификатор = Неопределено Тогда // Ведущий не выведен в быстрые настройки.
			
			// Инициализация соответствия для быстрого поиска значений обычных настроек.
			Если ПоискЗначенийОбычныхНастроекПоПолюКомпоновкиДанных = Неопределено Тогда
				ПоискЗначенийОбычныхНастроекПоПолюКомпоновкиДанных = Новый Соответствие;
				РекурсивныйАнализНастроекКД(Неопределено, ПоискЗначенийОбычныхНастроекПоПолюКомпоновкиДанных);
			КонецЕсли;
			
			// Поиск значения ведущего.
			ВедущийЗначение = ПоискЗначенийОбычныхНастроекПоПолюКомпоновкиДанных.Получить(СтрокаТаблицы.ВедущийПолеКД);
			Если ВедущийЗначение = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			// Установка фиксированного значения параметра подчиненного.
			МассивТипов = Новый Массив;
			МассивТипов.Добавить(ТипЗнч(ВедущийЗначение));
			ПодчиненныйЭлемент.ОграничениеТипа = Новый ОписаниеТипов(МассивТипов);
			
		Иначе // Ведущий выведен в быстрые настройки.
			
			// Регистрация отключаемой связи ведущего с подчиненными (параметров выбора).
			ВсеСвязиВедущего = СоответствиеОтключаемыхСвязей.Получить(ВедущийИдентификатор);
			Если ВсеСвязиВедущего = Неопределено Тогда
				ВсеСвязиВедущего = Новый Структура;
			КонецЕсли;
			Если ВсеСвязиВедущего.Свойство("ПараметрыВыбораПодчиненных") Тогда
				ПараметрыВыбораПодчиненных = ВсеСвязиВедущего.ПараметрыВыбораПодчиненных;
			Иначе
				ПараметрыВыбораПодчиненных = Новый Массив;
			КонецЕсли;
			
			ВедущийИдентификаторКД = СоответствиеПользовательскихНастроек.Получить(ВедущийИдентификатор);
			ВедущийПользовательскаяНастройкаКД = ПользовательскиеНастройкиКД.ПолучитьОбъектПоИдентификатору(ВедущийИдентификаторКД);
			Если ТипЗнч(ВедущийПользовательскаяНастройкаКД) = Тип("ЭлементОтбораКомпоновкиДанных")
				И ВедущийПользовательскаяНастройкаКД.ВидСравнения <> ВидСравненияКомпоновкиДанных.ВСписке
				И ВедущийПользовательскаяНастройкаКД.ВидСравнения <> ВидСравненияКомпоновкиДанных.Равно Тогда
				Продолжить; // Ведущий имеет такой вид сравнения ("Заполнено" или "Не заполнено"), при котором его связи отключаются.
			КонецЕсли;
			
			ОписаниеСвязи = Новый Структура;
			ОписаниеСвязи.Вставить("ВедущийИмяРеквизита",          ИменаОсновныхРеквизитовФормы[ВедущийИдентификатор]);
			ОписаниеСвязи.Вставить("ПодчиненныйИдентификатор",     ПодчиненныйИдентификатор);
			ОписаниеСвязи.Вставить("ПодчиненныйИмяРеквизита",      ИменаОсновныхРеквизитовФормы[ПодчиненныйИдентификатор]);
			ОписаниеСвязи.Вставить("ПодчиненныйИмяФлажка",         ИменаФлажковИспользование[ПодчиненныйИдентификатор]);
			ОписаниеСвязи.Вставить("ПодчиненныйИмяЭлемента",       ПодчиненныйИмяЭлемента);
			ОписаниеСвязи.Вставить("ПодчиненныйИмяПараметраФормы", СтрокаТаблицы.ПодчиненныйИмяПараметра);
			ОписаниеСвязи.Вставить("Действие",                     СтрокаТаблицы.Действие);
			
			ПараметрыВыбораПодчиненных.Добавить(ОписаниеСвязи);
			
			ВсеСвязиВедущего.Вставить("ПараметрыВыбораПодчиненных", ПараметрыВыбораПодчиненных);
			СоответствиеОтключаемыхСвязей.Вставить(ВедущийИдентификатор, ВсеСвязиВедущего);
			
			// Если ведущий используется, тогда связь регистрируется в элементе подчиненного в соответствии с выбранным типом.
			Если ВедущийПользовательскаяНастройкаКД.Использование Тогда
				УстановитьСвязьПараметровВыбора(ПодчиненныйЭлемент, ОписаниеСвязи, Истина);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если Элементы.Периоды.ПодчиненныеЭлементы.Количество() > 0 Тогда
		Элементы.БыстрыеНастройки.Отображение = ОтображениеОбычнойГруппы.СлабоеВыделение;
	Иначе
		Элементы.БыстрыеНастройки.Отображение = ОтображениеОбычнойГруппы.Нет;
	КонецЕсли;
	
	// Сохранение соответствий для быстрого поиска в данные формы.
	БыстрыйПоискПользовательскихНастроек = Новый ФиксированноеСоответствие(СоответствиеПользовательскихНастроек);
	БыстрыйПоискИменОбъектовМетаданных   = Новый ФиксированноеСоответствие(СоответствиеИменОбъектовМетаданных);
	БыстрыйПоискОтключаемыхСвязей        = Новый ФиксированноеСоответствие(СоответствиеОтключаемыхСвязей);
	
КонецПроцедуры

&НаСервере
Процедура БыстрыеНастройкиДобавитьРеквизит(ПараметрыЗаполнения, ПолноеИмяРеквизита, ТипРеквизита)
	Если ТипЗнч(ТипРеквизита) = Тип("ОписаниеТипов") Тогда
		ТипыДобавляемого = ТипРеквизита;
	ИначеЕсли ТипЗнч(ТипРеквизита) = Тип("Строка") Тогда
		ТипыДобавляемого = Новый ОписаниеТипов(ТипРеквизита);
	ИначеЕсли ТипЗнч(ТипРеквизита) = Тип("Массив") Тогда
		ТипыДобавляемого = Новый ОписаниеТипов(ТипРеквизита);
	ИначеЕсли ТипЗнч(ТипРеквизита) = Тип("Тип") Тогда
		МассивТипов = Новый Массив;
		МассивТипов.Добавить(ТипРеквизита);
		ТипыДобавляемого = Новый ОписаниеТипов(МассивТипов);
	Иначе
		Возврат;
	КонецЕсли;
	
	ТипыСуществующего = ПараметрыЗаполнения.Реквизиты.Существующие.Получить(ПолноеИмяРеквизита);
	Если ОписанияТиповСовпадает(ТипыСуществующего, ТипыДобавляемого) Тогда
		ПараметрыЗаполнения.Реквизиты.Существующие.Удалить(ПолноеИмяРеквизита);
	Иначе
		ПозицияТочки = Найти(ПолноеИмяРеквизита, ".");
		Если ПозицияТочки = 0 Тогда
			ПутьКРеквизиту = "";
			КраткоеИмяРеквизита = ПолноеИмяРеквизита;
		Иначе
			ПутьКРеквизиту = Лев(ПолноеИмяРеквизита, ПозицияТочки - 1);
			КраткоеИмяРеквизита = Сред(ПолноеИмяРеквизита, ПозицияТочки + 1);
		КонецЕсли;
		
		ПараметрыЗаполнения.Реквизиты.Добавляемые.Добавить(Новый РеквизитФормы(КраткоеИмяРеквизита, ТипыДобавляемого, ПутьКРеквизиту));
		Если ТипыСуществующего <> Неопределено Тогда
			ПараметрыЗаполнения.Реквизиты.Удаляемые.Добавить(ПолноеИмяРеквизита);
			ПараметрыЗаполнения.Реквизиты.Существующие.Удалить(ПолноеИмяРеквизита);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ОписанияТиповСовпадает(ОписаниеТипов1, ОписаниеТипов2)
	Если ОписаниеТипов1 = Неопределено Или ОписаниеТипов2 = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат ОписаниеТипов1 = ОписаниеТипов2
		Или ОбщегоНазначения.ЗначениеВСтрокуXML(ОписаниеТипов1) = ОбщегоНазначения.ЗначениеВСтрокуXML(ОписаниеТипов2);
КонецФункции

&НаСервере
Процедура УстановитьКлючиФормы()
	Если ПравоВывода Тогда
		Уникальность = КлючОбъекта;
		Если ЗначениеЗаполнено(КлючТекущегоВарианта) Тогда
			Уникальность = Уникальность + "/КлючВарианта." + КлючТекущегоВарианта;
		КонецЕсли;
		
		КлючСохраненияПоложенияОкна = Уникальность;
		
		НастройкиОтчета.ПараметрыПечатиПоУмолчанию.Вставить("КлючПараметровПечати", Уникальность);
		ЗаполнитьЗначенияСвойств(ОтчетТабличныйДокумент, НастройкиОтчета.ПараметрыПечатиПоУмолчанию);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ФоновоеЗаданиеЗагрузитьРезультат()
	
	ОтображениеСостояния = Элементы.ОтчетТабличныйДокумент.ОтображениеСостояния;
	ОтображениеСостояния.Видимость                      = Ложь;
	ОтображениеСостояния.ДополнительныйРежимОтображения = ДополнительныйРежимОтображения.НеИспользовать;
	ОтображениеСостояния.Картинка                       = Новый Картинка;
	ОтображениеСостояния.Текст                          = "";
	
	РезультатФормирования = ПолучитьИзВременногоХранилища(ФоновоеЗаданиеАдресХранилища);
	
	Если РезультатФормирования = Неопределено Тогда
		
		СформироватьНепосредственно();
		
	Иначе
		
		ПараметрыПечатиДоФормирования = НастройкиОтчета.ПараметрыПечатиПоУмолчанию;
		ЗаполнитьЗначенияСвойств(ПараметрыПечатиДоФормирования, ОтчетТабличныйДокумент);
		
		ОтчетТабличныйДокумент = РезультатФормирования.ОтчетТабличныйДокумент;
		
		ЗаполнитьЗначенияСвойств(ОтчетТабличныйДокумент, ПараметрыПечатиДоФормирования);
		
		Если ЗначениеЗаполнено(ОтчетДанныеРасшифровки) И ЭтоАдресВременногоХранилища(ОтчетДанныеРасшифровки) Тогда
			УдалитьИзВременногоХранилища(ОтчетДанныеРасшифровки);
		КонецЕсли;
		ОтчетДанныеРасшифровки = ПоместитьВоВременноеХранилище(РезультатФормирования.ОтчетРасшифровка, УникальныйИдентификатор);
		
		Если РезультатФормирования.ВариантМодифицирован
			Или РезультатФормирования.ПользовательскиеНастройкиМодифицированы Тогда
			РезультатФормирования.Вставить("Событие", Новый Структура);
			РезультатФормирования.Событие.Вставить("Имя", "ПослеФормирования");
			РезультатФормирования.Событие.Вставить("Непосредственно", Ложь);
			БыстрыеНастройкиЗаполнить(РезультатФормирования);
		КонецЕсли;
		
	КонецЕсли;
	
	УдалитьИзВременногоХранилища(ФоновоеЗаданиеАдресХранилища);
	ФоновоеЗаданиеАдресХранилища = Неопределено;
	ФоновоеЗаданиеИдентификатор = Неопределено;
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьОшибкиФормирования(ИнформацияОбОшибке)
	
	ОписаниеОшибки = ФункцииОтчетовКлиентСервер.КраткоеПредставлениеОшибкиФормированияОтчета(ИнформацияОбОшибке);
	
	ОтображениеСостояния = Элементы.ОтчетТабличныйДокумент.ОтображениеСостояния;
	ОтображениеСостояния.Видимость                      = Истина;
	ОтображениеСостояния.ДополнительныйРежимОтображения = ДополнительныйРежимОтображения.Неактуальность;
	ОтображениеСостояния.Картинка                       = Новый Картинка;
	ОтображениеСостояния.Текст                          = ?(ПустаяСтрока(ОписаниеОшибки), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке), ОписаниеОшибки);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПредставлениеПользовательскойНастройки(ПользовательскаяНастройка, ОбщаяНастройка, ТипОбщейНастройки)
	
	Если ТипОбщейНастройки = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
		ЭлементЗаголовок = Строка(ПользовательскаяНастройка.ТипГруппы);
	ИначеЕсли ТипОбщейНастройки = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
		ЭлементЗаголовок = Строка(ОбщаяНастройка.ЛевоеЗначение);
	ИначеЕсли ТипОбщейНастройки = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
		ЭлементЗаголовок = Строка(ОбщаяНастройка.Параметр);
	ИначеЕсли ТипОбщейНастройки = Тип("ЭлементУсловногоОформленияКомпоновкиДанных") Тогда
		ЭлементЗаголовок = НСтр("ru = 'Оформление'");
		Если НЕ ПустаяСтрока(Строка(ПользовательскаяНастройка.Оформление)) Тогда
			ЭлементЗаголовок = ЭлементЗаголовок +" ("+ НРег(Строка(ПользовательскаяНастройка.Оформление)) +")";
		КонецЕсли;
	ИначеЕсли ТипОбщейНастройки = Тип("ГруппировкаКомпоновкиДанных") Тогда
		Суффикс = СокрЛП(Строка(ОбщаяНастройка.ПоляГруппировки));
		ЭлементЗаголовок = НСтр("ru = 'Группировка'") + ?(Суффикс = "", "", " '" + Суффикс + "'");
	ИначеЕсли ТипОбщейНастройки = Тип("ГруппировкаДиаграммыКомпоновкиДанных") Тогда
		Суффикс = СокрЛП(Строка(ОбщаяНастройка.ПоляГруппировки));
		ЭлементЗаголовок = НСтр("ru = 'Группировка диаграммы'") + ?(Суффикс = "", "", " '" + Суффикс + "'");
	ИначеЕсли ТипОбщейНастройки = Тип("ВыбранныеПоляКомпоновкиДанных") Тогда
		ЭлементЗаголовок = НСтр("ru = 'Выбранные поля'");
	ИначеЕсли ТипОбщейНастройки = Тип("ПорядокКомпоновкиДанных") Тогда
		ЭлементЗаголовок = НСтр("ru = 'Сортировка'");
	ИначеЕсли ТипОбщейНастройки = Тип("ОтборКомпоновкиДанных") Тогда
		ЭлементЗаголовок = НСтр("ru = 'Отбор'");
	ИначеЕсли ТипОбщейНастройки = Тип("УсловноеОформлениеКомпоновкиДанных") Тогда
		ЭлементЗаголовок = НСтр("ru = 'Условное оформление'");
	ИначеЕсли ТипОбщейНастройки = Тип("СтруктураНастроекКомпоновкиДанных") Тогда
		ЭлементЗаголовок = НСтр("ru = 'Структура настроек'");
	ИначеЕсли ТипОбщейНастройки = Тип("ТаблицаКомпоновкиДанных") Тогда
		ЭлементЗаголовок = НСтр("ru = 'Таблица'");
	ИначеЕсли ТипОбщейНастройки = Тип("ГруппировкаТаблицыКомпоновкиДанных") Тогда
		ЭлементЗаголовок = НСтр("ru = 'Группировка таблицы'");
	ИначеЕсли ТипОбщейНастройки = Тип("ДиаграммаКомпоновкиДанных") Тогда
		ЭлементЗаголовок = НСтр("ru = 'Диаграмма'");
	ИначеЕсли ТипОбщейНастройки = Тип("ГруппировкаДиаграммыМакетаКомпоновкиДанных") Тогда
		ЭлементЗаголовок = НСтр("ru = 'Группировка диаграммы макета'");
	ИначеЕсли ТипОбщейНастройки = Тип("НастройкиВложенногоОбъектаКомпоновкиДанных") Тогда
		ЭлементЗаголовок = СтрЗаменить(НСтр("ru = 'Вложенная группировка ''%1'''"), "%1", Строка(ПользовательскаяНастройка));
	Иначе
		ЭлементЗаголовок = Строка(ПользовательскаяНастройка);
	КонецЕсли;
	
	Возврат СокрЛП(ЭлементЗаголовок);
КонецФункции

&НаСервереБезКонтекста
Процедура АнализТиповИСвязей(ИдентификаторЭлемента, ДоступнаяНастройка, ШаблонГенерации, ИменаОбъектовМетаданных, Связи)
	
	КоличествоИзвестныхТипов = 0;
	
	ИсходноеОписаниеТипов = ДоступнаяНастройка.ТипЗначения;
	ДобавляемыеТипы = Новый Массив;
	ВычитаемыеТипы = Новый Массив;
	
	ШаблонГенерации.Вставить("СодержитТипТип",        Ложь);
	ШаблонГенерации.Вставить("СодержитТипДата",       Ложь);
	ШаблонГенерации.Вставить("СодержитТипБулево",     Ложь);
	ШаблонГенерации.Вставить("СодержитТипЧисло",      Ложь);
	ШаблонГенерации.Вставить("СодержитТипПериод",     Ложь);
	ШаблонГенерации.Вставить("СодержитДругиеТипы",    Ложь);
	ШаблонГенерации.Вставить("СодержитСсылочныеТипы", Ложь);
	
	ДоступнаяНастройкаТипы = ИсходноеОписаниеТипов.Типы();
	Для Каждого Тип Из ДоступнаяНастройкаТипы Цикл
		Если Тип = Тип("ПолеКомпоновкиДанных") Тогда
			ВычитаемыеТипы.Добавить(Тип);
		ИначеЕсли Тип = Тип("Тип") Тогда
			ШаблонГенерации.СодержитТипТип = Истина;
			ДобавляемыеТипы.Добавить(Тип("ОписаниеТипов"));
			ВычитаемыеТипы.Добавить(Тип("Тип"));
		ИначеЕсли Тип = Тип("Дата") Тогда
			ШаблонГенерации.СодержитТипДата = Истина;
		ИначеЕсли Тип = Тип("Булево") Тогда
			ШаблонГенерации.СодержитТипБулево = Истина;
		ИначеЕсли Тип = Тип("Число") Тогда
			ШаблонГенерации.СодержитТипЧисло = Истина;
		ИначеЕсли Тип = Тип("СтандартныйПериод") Тогда
			ШаблонГенерации.СодержитТипПериод = Истина;
		Иначе
			ШаблонГенерации.СодержитДругиеТипы = Истина;
			
			ОбъектМетаданных = Метаданные.НайтиПоТипу(Тип);
			Если ОбъектМетаданных <> Неопределено Тогда
				ШаблонГенерации.СодержитСсылочныеТипы = Истина;
				
				ПолноеИмя = ОбъектМетаданных.ПолноеИмя();
				
				// Регистрация имени объекта метаданных.
				ИменаОбъектовМетаданных.Вставить(Тип, ПолноеИмя);
				
				// Поиск типа в глобальных связях среди подчиненных.
				Найденные = Связи.ОбъектовМетаданных.НайтиСтроки(Новый Структура("ПодчиненныйТип", Тип));
				Для Каждого СтрокаТаблицы Из Найденные Цикл // Регистрация настройки как подчиненной.
					СтрокаТаблицы.ЕстьПодчиненные = Истина;
					СтрокаТаблицы.ПодчиненныйПолноеИмя = ПолноеИмя;
					СтрокаТаблицы.ПодчиненныйМассивИдентификаторов.Добавить(ИдентификаторЭлемента);
				КонецЦикла;
				
				// Поиск типа в глобальных связях среди ведущих.
				Найденные = Связи.ОбъектовМетаданных.НайтиСтроки(Новый Структура("ВедущийТип", Тип));
				Для Каждого СтрокаТаблицы Из Найденные Цикл // Регистрация настройки как ведущей.
					СтрокаТаблицы.ЕстьВедущие = Истина;
					СтрокаТаблицы.ВедущийПолноеИмя = ПолноеИмя;
					СтрокаТаблицы.ВедущийМассивИдентификаторов.Добавить(ИдентификаторЭлемента);
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	ОписаниеТипов = Новый ОписаниеТипов(ИсходноеОписаниеТипов, ДобавляемыеТипы, ВычитаемыеТипы);
	
	ШаблонГенерации.Вставить("ОписаниеТипов", ОписаниеТипов);
	ШаблонГенерации.Вставить("КоличествоТипов", ОписаниеТипов.Типы().Количество());
	
	// Переменные цикла
	ПараметрыВыбора = Новый Массив;
	
	Если ЗначениеЗаполнено(ДоступнаяНастройка.СвязьПоТипу) Тогда
		
		СтрокаСвязи = Связи.ПоТипу.Добавить();
		СтрокаСвязи.ПодчиненныйИдентификатор = ИдентификаторЭлемента;
		СтрокаСвязи.ВедущийПолеКД            = ДоступнаяНастройка.СвязьПоТипу.Поле;
		СтрокаСвязи.НомерСубконто            = ДоступнаяНастройка.СвязьПоТипу.ЭлементСвязи;
		
	КонецЕсли;
	
	Для Каждого СтрокаСвязи Из ДоступнаяНастройка.ПолучитьСвязиПараметровВыбора() Цикл
		
		Если Не ПустаяСтрока(Строка(СтрокаСвязи.Поле)) Тогда
			СтрокаСвязиПараметров = Связи.ПараметровВыбора.Добавить();
			СтрокаСвязиПараметров.ПодчиненныйИдентификатор = ИдентификаторЭлемента;
			СтрокаСвязиПараметров.ПодчиненныйИмяПараметра  = СтрокаСвязи.Имя;
			СтрокаСвязиПараметров.ВедущийПолеКД            = СтрокаСвязи.Поле;
			СтрокаСвязиПараметров.Действие                 = СтрокаСвязи.ИзменениеЗначения;
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого ПараметрВыбораКД Из ДоступнаяНастройка.ПолучитьПараметрыВыбора() Цикл
		ПараметрыВыбора.Добавить(Новый ПараметрВыбора(ПараметрВыбораКД.Имя, ПараметрВыбораКД.Значение));
	КонецЦикла;
	
	Если ПараметрыВыбора.Количество() > 0 Тогда
		ШаблонГенерации.Вставить("ПараметрыВыбора", ПараметрыВыбора);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьСвязьПараметровВыбора(ПодчиненныйЭлемент, СтруктураСвязи, ВключитьСвязь)
	
	Если ПустаяСтрока(СтруктураСвязи.ПодчиненныйИмяПараметраФормы) Тогда
		Возврат;
	КонецЕсли;
	
	СвязьНайдена = Ложь;
	
	МассивЭлементов = Новый Массив;
	Для Каждого СвязьПараметра Из ПодчиненныйЭлемент.СвязиПараметровВыбора Цикл
		Если СвязьПараметра.ПутьКДанным = СтруктураСвязи.ВедущийИмяРеквизита
			И СвязьПараметра.Имя = СтруктураСвязи.ПодчиненныйИмяПараметраФормы
			И СвязьПараметра.ИзменениеЗначения = СтруктураСвязи.Действие Тогда
			Если ВключитьСвязь Тогда
				Возврат; // Включать не требуется - связь найдена.
			Иначе
				СвязьНайдена = Истина;
			КонецЕсли;
		Иначе
			МассивЭлементов.Добавить(СвязьПараметра);
		КонецЕсли;
	КонецЦикла;
	
	Если Не ВключитьСвязь И Не СвязьНайдена Тогда
		Возврат; // Отключать не требуется - связь не найдена.
	КонецЕсли;
	
	Если ВключитьСвязь Тогда
		МассивЭлементов.Добавить(Новый СвязьПараметраВыбора(СтруктураСвязи.ПодчиненныйИмяПараметраФормы, СтруктураСвязи.ВедущийИмяРеквизита, СтруктураСвязи.Действие));
	КонецЕсли;
	
	ПодчиненныйЭлемент.СвязиПараметровВыбора = Новый ФиксированныйМассив(МассивЭлементов);
	
КонецПроцедуры

&НаСервере
Функция СоздатьСтруктуруТаблицСвязей()
	
	Связи = Новый Структура;
	
	// Связи из СКД.
	ТаблицаСвязейПоТипу = Новый ТаблицаЗначений;
	ТаблицаСвязейПоТипу.Колонки.Добавить("ПодчиненныйИдентификатор");
	ТаблицаСвязейПоТипу.Колонки.Добавить("ВедущийПолеКД");
	ТаблицаСвязейПоТипу.Колонки.Добавить("НомерСубконто");
	Связи.Вставить("ПоТипу", ТаблицаСвязейПоТипу);
	
	ТаблицаСвязейПараметровВыбора = Новый ТаблицаЗначений;
	ТаблицаСвязейПараметровВыбора.Колонки.Добавить("ПодчиненныйИдентификатор");
	ТаблицаСвязейПараметровВыбора.Колонки.Добавить("ВедущийПолеКД");
	ТаблицаСвязейПараметровВыбора.Колонки.Добавить("ПодчиненныйИмяПараметра");
	ТаблицаСвязейПараметровВыбора.Колонки.Добавить("Действие");
	Связи.Вставить("ПараметровВыбора", ТаблицаСвязейПараметровВыбора);
	
	// Связи из метаданных.
	ТаблицаСвязейОбъектовМетаданных = Новый ТаблицаЗначений;
	ТаблицаСвязейОбъектовМетаданных.Колонки.Добавить("ПодчиненныйРеквизит", Новый ОписаниеТипов("Строка"));
	ТаблицаСвязейОбъектовМетаданных.Колонки.Добавить("ПодчиненныйТип",      Новый ОписаниеТипов("Тип"));
	ТаблицаСвязейОбъектовМетаданных.Колонки.Добавить("ВедущийТип",          Новый ОписаниеТипов("Тип"));
	
	ФормаОтчетаПереопределяемый.ДополнитьСвязиОбъектовМетаданных(ТаблицаСвязейОбъектовМетаданных);
	
	ТаблицаСвязейОбъектовМетаданных.Колонки.Добавить("ЕстьПодчиненные",                  Новый ОписаниеТипов("Булево"));
	ТаблицаСвязейОбъектовМетаданных.Колонки.Добавить("ПодчиненныйПолноеИмя",             Новый ОписаниеТипов("Строка"));
	ТаблицаСвязейОбъектовМетаданных.Колонки.Добавить("ПодчиненныйМассивИдентификаторов", Новый ОписаниеТипов("Массив"));
	ТаблицаСвязейОбъектовМетаданных.Колонки.Добавить("ЕстьВедущие",                      Новый ОписаниеТипов("Булево"));
	ТаблицаСвязейОбъектовМетаданных.Колонки.Добавить("ВедущийПолноеИмя",                 Новый ОписаниеТипов("Строка"));
	ТаблицаСвязейОбъектовМетаданных.Колонки.Добавить("ВедущийМассивИдентификаторов",     Новый ОписаниеТипов("Массив"));
	Связи.Вставить("ОбъектовМетаданных", ТаблицаСвязейОбъектовМетаданных);
	
	Возврат Связи;
	
КонецФункции

&НаСервере
Процедура РекурсивныйАнализНастроекКД(Коллекция, Соответствие)
	Если Коллекция = Неопределено Тогда
		РекурсивныйАнализНастроекКД(Отчет.КомпоновщикНастроек.Настройки.Отбор.Элементы, Соответствие);
		РекурсивныйАнализНастроекКД(Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы, Соответствие);
		РекурсивныйАнализНастроекКД(Отчет.КомпоновщикНастроек.ФиксированныеНастройки.Отбор.Элементы, Соответствие);
		РекурсивныйАнализНастроекКД(Отчет.КомпоновщикНастроек.ФиксированныеНастройки.ПараметрыДанных.Элементы, Соответствие);
	Иначе
		// Регистрация значений включенных отборов и параметров КД, не выведенных в быстрый доступ.
		Для Каждого ОбщаяНастройка Из Коллекция Цикл
			// ЭлементОтбораКомпоновкиДанных, ГруппаЭлементовОтбораКомпоновкиДанных,
			// ЗначениеПараметраКомпоновкиДанных, ЗначениеПараметраНастроекКомпоновкиДанных.
			Если ТипЗнч(ОбщаяНастройка) = Тип("ЗначениеПараметраКомпоновкиДанных") Тогда
				Значение = ОбщаяНастройка.Значение;
				Если ЗначениеИлиПолеКомпоновкиДанныхЗаполнено(Значение) Тогда
					ПолеКД = Новый ПолеКомпоновкиДанных("ПараметрыДанных." + Строка(ОбщаяНастройка.Параметр));
					Соответствие.Вставить(ПолеКД, Значение);
				КонецЕсли;
				РекурсивныйАнализНастроекКД(ОбщаяНастройка.ЗначенияВложенныхПараметров, Соответствие);
				Продолжить;
			КонецЕсли;
			
			Если ОбщаяНастройка.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ
				И ЗначениеЗаполнено(ОбщаяНастройка.ИдентификаторПользовательскойНастройки) Тогда
				Продолжить;
			КонецЕсли;
			Если ОбщаяНастройка.Использование <> Истина Тогда
				Продолжить;
			КонецЕсли;
			
			Если ТипЗнч(ОбщаяНастройка) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
				РекурсивныйАнализНастроекКД(ОбщаяНастройка.Элементы, Соответствие);
				Продолжить;
			КонецЕсли;
			
			Если ТипЗнч(ОбщаяНастройка) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
				Значение = ОбщаяНастройка.ПравоеЗначение;
				Если ЗначениеИлиПолеКомпоновкиДанныхЗаполнено(Значение) Тогда
					ПолеКД = ОбщаяНастройка.ЛевоеЗначение;
					Соответствие.Вставить(ПолеКД, Значение);
				КонецЕсли;
			ИначеЕсли ТипЗнч(ОбщаяНастройка) = Тип("ЗначениеПараметраКомпоновкиДанных") Тогда
				Значение = ОбщаяНастройка.ПравоеЗначение;
				Если ЗначениеИлиПолеКомпоновкиДанныхЗаполнено(Значение) Тогда
					ПолеКД = Новый ПолеКомпоновкиДанных("ПараметрыДанных." + Строка(ОбщаяНастройка.Параметр));
					Соответствие.Вставить(ПолеКД, Значение);
				КонецЕсли;
				РекурсивныйАнализНастроекКД(ОбщаяНастройка.ЗначенияВложенныхПараметров, Соответствие);
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗначениеИлиПолеКомпоновкиДанныхЗаполнено(Значение)
	Если ТипЗнч(Значение) = Тип("ПолеКомпоновкиДанных") Тогда
		Возврат ЗначениеЗаполнено(Строка(Значение));
	Иначе
		Возврат ЗначениеЗаполнено(Значение);
	КонецЕсли;
КонецФункции

&НаСервере
Процедура ЗаполнитьПанельОтчета()
	Если ВариантыПанелиКлючТекущегоВарианта = КлючТекущегоВарианта Тогда
		Возврат;
	КонецЕсли;
	ВариантыПанелиКлючТекущегоВарианта = КлючТекущегоВарианта;
	
	ТаблицаВывода = РеквизитФормыВЗначение("ВариантыПанели");
	ТаблицаВывода.Колонки.Добавить("ЭлементНадоДобавить", Новый ОписаниеТипов("Булево"));
	ТаблицаВывода.Колонки.Добавить("ЭлементНадоОставить", Новый ОписаниеТипов("Булево"));
	ТаблицаВывода.Колонки.Добавить("Группа");
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВариантыОтчетов.Ссылка,
	|	ВариантыОтчетов.Отчет,
	|	ВариантыОтчетов.КлючВарианта,
	|	ВариантыОтчетов.Наименование КАК Наименование,
	|	ВЫБОР
	|		КОГДА ПОДСТРОКА(ВариантыОтчетов.Описание, 1, 1) = """"
	|			ТОГДА ВЫРАЗИТЬ(ВариантыОтчетов.ПредопределенныйВариант.Описание КАК СТРОКА(1000))
	|		ИНАЧЕ ВЫРАЗИТЬ(ВариантыОтчетов.Описание КАК СТРОКА(1000))
	|	КОНЕЦ КАК Описание,
	|	ВариантыОтчетов.Автор,
	|	ВариантыОтчетов.ТипОтчета,
	|	ВариантыОтчетов.Отчет.Имя КАК ИмяОтчета
	|ИЗ
	|	Справочник.ВариантыОтчетов КАК ВариантыОтчетов
	|ГДЕ
	|	ВариантыОтчетов.Отчет = &Отчет
	|	И ВариантыОтчетов.ПометкаУдаления = ЛОЖЬ
	|	И (ВариантыОтчетов.ТолькоДляАвтора = ЛОЖЬ
	|			ИЛИ ВариантыОтчетов.Автор = &ТекущийПользователь)
	|	И НЕ ВариантыОтчетов.ПредопределенныйВариант В (&ОтключенныеВариантыПрограммы)
	|	И ВариантыОтчетов.КлючВарианта <> """"
	|
	|УПОРЯДОЧИТЬ ПО
	|	Наименование";
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Отчет", ФормаПараметры.ОтчетСсылка);
	Запрос.УстановитьПараметр("ТекущийПользователь", Пользователи.ТекущийПользователь());
	Запрос.УстановитьПараметр("ОтключенныеВариантыПрограммы", ВариантыОтчетовПовтИсп.ОтключенныеВариантыПрограммы());
	Запрос.Текст = ТекстЗапроса;
	
	ОбщиеНастройки = ВариантыОтчетов.ОбщиеНастройкиПанели();
	ПоказыватьПодсказки = ОбщиеНастройки.ПоказыватьПодсказки = 1;
	
	ВидимостьГруппыВариантыОтчета = Ложь;
	ТаблицаВариантов = Запрос.Выполнить().Выгрузить();
	Для Каждого СтрокаТаблицы Из ТаблицаВариантов Цикл
		// Только другие варианты.
		Если СтрокаТаблицы.Ссылка = ВариантСсылка Тогда
			Продолжить;
		КонецЕсли;
		ВидимостьГруппыВариантыОтчета = Истина;
		ВывестиГиперссылкуВПанель(ТаблицаВывода, СтрокаТаблицы, Элементы.ГруппаДругиеВариантыОтчета, ПоказыватьПодсказки);
	КонецЦикла;
	
	ВидимостьГруппыПохожиеОтчеты = Ложь;
	Если ЗначениеЗаполнено(ФормаПараметры.Подсистема) Тогда
		Элементы.ГруппаПохожиеОтчеты.Заголовок = ФормаПараметры.ПодсистемаПредставление;
		ПараметрыПоиска = Новый Структура;
		ПараметрыПоиска.Вставить("Подсистемы", Новый Массив);
		ПараметрыПоиска.Подсистемы.Добавить(ФормаПараметры.Подсистема);
		ПараметрыПоиска.Вставить("ТолькоВидимыеВПанелиОтчетов", Истина);
		ПараметрыПоиска.Вставить("ПолучатьИтоговуюТаблицу", Истина);
		ПараметрыПоиска.Вставить("ПометкаУдаления", Ложь);
		
		РезультатПоиска = ВариантыОтчетов.НайтиСсылки(ПараметрыПоиска);
		
		ТаблицаВариантов = РезультатПоиска.ТаблицаЗначений;
		ТаблицаВариантов.Колонки.НаименованиеВарианта.Имя = "Наименование";
		ТаблицаВариантов.Сортировать("Наименование");
		Для Каждого СтрокаТаблицы Из ТаблицаВариантов Цикл
			// Только другие варианты.
			Если СтрокаТаблицы.Ссылка = ВариантСсылка Тогда
				Продолжить;
			КонецЕсли;
			// Также не показывать вариант "по умолчанию" этого отчета.
			Если СтрокаТаблицы.КлючВарианта = "" И СтрокаТаблицы.Отчет = ФормаПараметры.ОтчетСсылка Тогда
				Продолжить;
			КонецЕсли;
			ВидимостьГруппыПохожиеОтчеты = Истина;
			ВывестиГиперссылкуВПанель(ТаблицаВывода, СтрокаТаблицы, Элементы.ГруппаПохожиеОтчеты, ПоказыватьПодсказки);
		КонецЦикла;
	КонецЕсли;
	
	Элементы.ГруппаДругиеВариантыОтчета.Видимость = ВидимостьГруппыВариантыОтчета;
	Элементы.ГруппаПохожиеОтчеты.Видимость        = ВидимостьГруппыПохожиеОтчеты;
	Элементы.РазделительГруппВариантов.Видимость  = ВидимостьГруппыВариантыОтчета И ВидимостьГруппыПохожиеОтчеты;
	
	НайденныеДляУдаления = ТаблицаВывода.НайтиСтроки(Новый Структура("ЭлементНадоОставить", Ложь)); //ВариантыПанелиНомерЭлемента
	Для Каждого СтрокаТаблицы Из НайденныеДляУдаления Цикл
		ЭлементВарианта = Элементы.Найти(СтрокаТаблицы.НадписьИмя);
		Если ЭлементВарианта <> Неопределено Тогда
			Элементы.Удалить(ЭлементВарианта);
		КонецЕсли;
		ТаблицаВывода.Удалить(СтрокаТаблицы);
	КонецЦикла;
	
	ЕстьДругиеОтчеты = ВидимостьГруппыВариантыОтчета Или ВидимостьГруппыПохожиеОтчеты;
	
	ТаблицаВывода.Колонки.Удалить("ЭлементНадоОставить");
	ТаблицаВывода.Колонки.Удалить("Группа");
	ЗначениеВРеквизитФормы(ТаблицаВывода, "ВариантыПанели");
КонецПроцедуры

&НаСервере
Процедура ВывестиГиперссылкуВПанель(ТаблицаВывода, Вариант, Группа, ПоказыватьПодсказки)
	
	Найденные = ТаблицаВывода.НайтиСтроки(Новый Структура("Ссылка", Вариант.Ссылка));
	Если Найденные.Количество() > 0 Тогда
		СтрокаВывода = Найденные[0];
		СтрокаВывода.ЭлементНадоОставить = Истина;
		Возврат;
	КонецЕсли;
	
	СтрокаВывода = ТаблицаВывода.Добавить();
	ЗаполнитьЗначенияСвойств(СтрокаВывода, Вариант);
	ВариантыПанелиНомерЭлемента = ВариантыПанелиНомерЭлемента + 1;
	СтрокаВывода.НадписьИмя = "Вариант" + Формат(ВариантыПанелиНомерЭлемента, "ЧГ=");
	СтрокаВывода.Дополнительный = (Вариант.ТипОтчета = Перечисления.ТипыОтчетов.Дополнительный);
	СтрокаВывода.ИмяГруппы = Группа.Имя;
	СтрокаВывода.ЭлементНадоОставить = Истина;
	СтрокаВывода.Группа = Группа;
	
	// Добавление надписи-гиперссылки варианта отчета
	Надпись = Элементы.Вставить(СтрокаВывода.НадписьИмя, Тип("ДекорацияФормы"), СтрокаВывода.Группа);
	Надпись.Вид = ВидДекорацииФормы.Надпись;
	Надпись.Гиперссылка = Истина;
	Надпись.РастягиватьПоГоризонтали = Истина;
	Надпись.РастягиватьПоВертикали = Ложь;
	Надпись.Высота = 1;
	Надпись.ЦветТекста = ЦветаСтиля.ВидимыйВариантОтчетаЦвет;
	Надпись.Заголовок = СокрЛП(Строка(Вариант.Ссылка));
	Если ЗначениеЗаполнено(Вариант.Описание) Тогда
		Надпись.Подсказка = СокрЛП(Вариант.Описание);
	КонецЕсли;
	Если ЗначениеЗаполнено(Вариант.Автор) Тогда
		Надпись.Подсказка = СокрЛ(Надпись.Подсказка + Символы.ПС) + НСтр("ru = 'Автор:'") + " " + СокрЛП(Строка(Вариант.Автор));
	КонецЕсли;
	Если ПоказыватьПодсказки Тогда
		Надпись.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСнизу;
		Надпись.РасширеннаяПодсказка.РастягиватьПоГоризонтали = Истина;
		Надпись.РасширеннаяПодсказка.ЦветТекста = ЦветаСтиля.ПоясняющийТекст;
	КонецЕсли;
	Надпись.УстановитьДействие("Нажатие", "ВариантНажатие");
	
КонецПроцедуры

#КонецОбласти
